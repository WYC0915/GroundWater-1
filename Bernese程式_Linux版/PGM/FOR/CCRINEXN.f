C*
      PROGRAM CCRINEXN
CC
CC NAME         : CCRINEXN
CC
CC PURPOSE      : CONCATENATION OF RINEX NAVIGATION FILES INTO
CC                1 NAVIGATION DATA FILE
CC
CC REMARKS      : ---
CC
CC AUTHOR       : W.GURTNER
CC
CC CREATED      : 28-OCT-90
CC
CC CHANGES      : 24-JUN-91: ERROR IN INPUT EPH FILENAME CREATION
CC                21-JAN-91: TIME WINDOW WITH START AND STOP DATE/TIME  1.2
CC                30-JUN-92: RINEX VERSION 2                            1.3.1
CC                13-AUG-92: CHAR4: BLANK = SAME AS FIRST RNX INP FILE  1.3.2
CC                01-SEP-93: LFNINP=5                                   1.3.3
CC                08-SEP-93: STATUS='UNKNOWN' INSTEAD OF 'NEW'          1.3.4
CC                10-SEP-93: EXTENSION OF NAV INPUT FILES IF DOS DIR    1.3.5
CC                20-SEP-93: SR R2RDNH: DON'T ABORT IF UNKNOWN TYPE     1.3.6
CC                24-AUG-94: SORT MESSAGES IN TIME AND PRN              1.4.1
CC                           FILE SEQUENCE NUMBER CAN BE INPUT IN
CC                           DAY FILE
CC                25-SEP-94: DEVOUT WAS MISSING IN CCRXCN CALL          1.4.2
CC                19-OCT-94: CLOSE FILES EXPLICITLY                     1.4.3
CC                13-FEB-95: TAKE ALMANAC DATA WHEN AVAILABLE           1.5.1
CC                14-JUN-96: REPLACED LAST OPEN WITH OPNFIL             1.5.2
CC                10-APR-97: INITIALIZATION OF IRXVRS                   1.5.3
CC                23-FEB-98: READ FILE LISTS GENERATED UNDER WIN NT DOS 1.5.4
CC                24-SEP-98: SET RETURN CODE, SKIP FILE WITH BAD HEADER 1.5.5
CC                13-OCT-98: WINDOWS NT VERSION DIRECTORY               1.5.6
CC                26-APR-99: DON'T ACCEPT SATELLITES WITH ZERO NUMBER   1.5.7
CC                28-JUN-99: CHECK GPS WEEK AGAINST APPROXIMATE WEEK    1.5.8
CC                           (SR RXCHKW)
CC                29-AUG-99: USE PROMP1 ROUTINE                         1.5.9
CC                13-FEB-01: CHECK MINIMUM NUMBER OF MESSAGES PER SVN   1.6.0
CC                16-NOV-01: INCLUDE CALLS FOR BERNESE VERSION 5.0      1.6.1
CC                11-JAN-02: HU: DO NOT WRITE TO STDOUT FOR BERNESE VERSION
CC                11-FEB-02: HU: IDNINT ADDED FOR WEEK NUMBER
CC                20-MAY-02: HU: FILSTM IBERN5 (SR CCRXOP) INITIALIZED
CC                18-FEB-03: HU: USE PREPROCESSOR COMMANDS
CC                19-MAY-03: RD: INIT TIME WINDOW TO (/0D0,1D20/)
CC                20-JUL-03: SS: MAXFIL FROM 200 TO 3000
CC                04-MAR-03: HU: MAXFIL FROM 3000 TO 4000
CC                23-AUG-06: SS: MAXFIL FROM 4000 TO 5000
CC                27-FEB-07: AG: CALL DEFCON
CC                23-SEP-10: RD: ENABLE CPU COUNTER
CC                28-MAR-12: RD: USE PROMP1 AS MODULE
CC                28-MAR-12: RD: USE INPCI4 AS MODULE
CC                29-OCT-12: RD: REMOVE #ifdef OS_VMS
CC
CC COPYRIGHT    : ASTRONOMICAL INSTITUTE
CC      1990      UNIVERSITY OF BERN
CC                SWITZERLAND
CC
C*
      USE m_cpu,    ONLY: cpu_start
      USE s_ccrxninp
      USE s_opnfil
      USE s_defcon
      USE s_r2rdnh
      USE s_r2rdnr
      USE s_r2wtnh
      USE s_r2wtnr
      USE s_rxchkw
      USE s_cordup
      USE s_radgms
      USE s_mjdgps
      USE s_clocks
      USE s_exitrc
      USE s_opnfil
      USE s_opnerr
      USE s_jmt
      USE s_promp1
      USE f_gpsmjd
      USE f_lengt0
      USE f_lengt1
      USE f_djul
      USE f_inpci4

      IMPLICIT INTEGER*4 (I-N)
      IMPLICIT REAL*8 (A-H,O-Z)
C
C  MAXIMUM NUMBER OF COMMENT LINES
      PARAMETER(MAXCOM=10)
C
C  MAXIMUM NUMBER OF NAVIGATION MESSAGES TO BE STORED
      PARAMETER(MAXMES=1000)
C
C  MAXIMUM NUMBER OF OBS.FILES IN LIST
      PARAMETER(MAXFIL=5000)
C
      PARAMETER(MAXSVN=32)
C
      REAL*8       EPHDAT(32),ALPHA(4),BETA(4),ALPHAI(4),BETAI(4)
      CHARACTER    FILEPH(MAXFIL)*64,FILOUT*64,FILINP*32,FILPRT*32
      CHARACTER    FILSTM*32,CHAR4*4,FILEXT*32,FILDAY*32,CHAR4I*4
      CHARACTER    DEVOUT*32,COMENI(MAXCOM)*60,RUNBI*20,PRGNAI*20
      CHARACTER    FILSEQ*1, COMENT(MAXCOM)*60,RUNBY*20,PRGNAM*20
      CHARACTER    CH1*1,CRDATE*9,CRTIME*5,MONTH(12)*3,FILOPT*32
      CHARACTER    CRDATI*9,CRTIMI*5,VRS*2,SORTLI(MAXMES)*16
      CHARACTER    LISTE(MAXMES)*40,DEVINP*32,FILHLP*64
      CHARACTER*80 STRG(1)
      INTEGER*4    M(8),INPT(4),INDEXS(MAXMES),MESSAT(MAXSVN)
      INTEGER*4    ITUTC
      LOGICAL      YES
      COMMON /TRRNX1/ LISTE,INDEXS
C
      DATA MONTH/'JAN','FEB','MAR','APR','MAY','JUN',
     1           'JUL','AUG','SEP','OCT','NOV','DEC'/
      DATA MESSAT/MAXSVN*0/,FILPRT/' '/,FILDAY/' '/,FILOUT/' '/
      DATA TCOLL1/0.D0/,TCOLL2/1.D20/,IBERN5/0/
C
#ifdef OS_WIN32
      DATA VRS/'LH'/
#endif
#ifdef OS_UNIX
      DATA VRS/'UX'/
#endif
C
      PRGNAM='CCRINEXN V1.6.0 '//VRS
C
C  LOGICAL FILE NUMBERS
      LFNINP=10
      LFNNAV=20
      LFNEPH=30
      LFNPRT=40
      LFNDAY=50
      LFNERR=6
      LFNSRT=60
      IRXVRS=1
      IRCODE=0
C
C  GET OPTIONS, DEFAULT VALUES ETC.
C  --------------------------------
C
C  --> "HARDWIRED" FILENAME FOR OPTIONS, DEFAULT VALUES ETC:
C
      FILOPT='CCRINEXN.OPT'
      FILDAY='CCRINEXN.DAY'
C
C_BEG_BERNESE
C
C DEFINE CONSTANTS
C ----------------
      CALL DEFCON(0)
C
C START CPU COUNTER
C -----------------
      CALL cpu_start(.TRUE.)
C
      IBERN5=1
      CALL CCRXNINP(LFNERR,LFNPRT,MAXFIL,MAXCOM,NCOM,COMENT,RUNBY,
     1              MINMES,IFLFLG,NFLEPH,FILEPH,CHAR4I,
     2              DEVOUT,TCOLL1,TCOLL2,FILSEQ)
C_END_BERNESE
C
      IF(IBERN5.EQ.0) THEN
        CALL CCRXOP(FILOPT,MAXCOM,NCOM,COMENT,RUNBY,MINMES,
     1              IFLFLG,DEVINP,FILINP,FILPRT,DEVOUT,CHAR4I,FILDAY)
        LDVI=LENGT0(DEVINP)
      END IF
C
C  PRINT-FILE
C  ----------
C
      IF(FILPRT.NE.' ') THEN
        CALL OPNFIL(LFNPRT,FILPRT,'UNKNOWN',' ',' ',' ',IOSTAT)
        CALL OPNERR(LFNERR,LFNPRT,IOSTAT,FILPRT,'CCRINEXN')
      END IF
      IF(FILPRT.NE.' '.OR.IBERN5.EQ.1) THEN
        WRITE(LFNPRT,21)
21      FORMAT(//,' CONCATENATION OF RINEX NAVIGATION FILES',
     1          /,' ---------------------------------------'/)
        IF(NCOM.NE.0) THEN
          DO 20 ICOM=1,NCOM
            WRITE(LFNPRT,22) COMENT(ICOM)
22          FORMAT(1X,A)
20        CONTINUE
          WRITE(LFNPRT,*)
        END IF
      ELSE
        LFNPRT=0
      END IF
C
      IF(IBERN5.EQ.1) GOTO 600
C
C  FILE WITH BROADCAST FILENAMES
C  -----------------------------
C
      IF(FILINP.NE.' ') THEN
        INQUIRE(FILE=FILINP,EXIST=YES)
        IF(.NOT.YES) THEN
          IF(IBERN5.NE.1)WRITE(*,11) FILINP(1:LENGT0(FILINP))
11        FORMAT(/,' FILE WITH INPUT FILE LIST ',A,' NOT FOUND.',/)
          IRCODE=2
          GOTO 999
        END IF
        CALL OPNFIL(LFNINP,FILINP,'OLD',' ',' ',' ',IOSTAT)
        CALL OPNERR(LFNERR,LFNINP,IOSTAT,FILINP,'CCRINEXN')
      ELSE
        LFNINP=5
      END IF
C
C  FILE WITH DAY TO COLLECT DATA
C  ------------------------------
C
      FILSEQ=' '
      IF(FILDAY.NE.' ') THEN
        INQUIRE(FILE=FILDAY,EXIST=YES)
        IF(.NOT.YES) THEN
          IF(IBERN5.NE.1)WRITE(*,12) FILDAY(1:LENGT0(FILDAY))
12        FORMAT(/,' FILE WITH COLLECT DAY ',A,' NOT FOUND.',/)
          IRCODE=2
          GOTO 999
        END IF
        JAN=1
        FIRST=1.D0
        NA=4
        NM=0
        CALL OPNFIL(LFNDAY,FILDAY,'OLD',' ',' ',' ',IOSTAT)
        CALL OPNERR(LFNERR,LFNDAY,IOSTAT,FILDAY,'CCRINEXN')
        INPT(3)=0
        INPT(4)=0
        ISTA=INPCI4(LFNDAY,NA,NM,CH1,INPT,CH1,M,IRC)
        IYEAR =INPT(1)
        IDAY  =INPT(2)
        IHOUR =INPT(3)
        MINUTE=INPT(4)
        IF(IYEAR.LT. 80) IYEAR=IYEAR+2000
        IF(IYEAR.LT.100) IYEAR=IYEAR+1900
        TCOLL1=DJUL(IYEAR,JAN,FIRST)+IDAY-1+IHOUR/24.D0+MINUTE/1440.D0
        INPT(3)=0
        INPT(4)=0
        ISTA=INPCI4(LFNDAY,NA,NM,CH1,INPT,CH1,M,IRC)
        IF(ISTA.GE.2) THEN
          IYEAR =INPT(1)
          IDAY  =INPT(2)
          IHOUR =INPT(3)
          MINUTE=INPT(4)
          IF(IYEAR.LT. 80) IYEAR=IYEAR+2000
          IF(IYEAR.LT.100) IYEAR=IYEAR+1900
          TCOLL2=DJUL(IYEAR,JAN,FIRST)+IDAY-1+IHOUR/24.D0+MINUTE/1440.D0
          READ(LFNDAY,'(A)',END=15) FILSEQ
15        CONTINUE
        ELSE
          TCOLL2=TCOLL1+1.D0
        END IF
        CLOSE(UNIT=LFNDAY)
      ELSE
        TCOLL1=0.D00
        TCOLL2=1.D20
      END IF
C
      NMESS=0
C
C  COLLECT RINEX NAV FILE NAMES
C  ----------------------------
C
      DO 500 IFL=1,MAXFIL
C
30      IF(LFNINP.EQ.5) THEN
          STRG(1)='RINEX NAVIGATION FILENAME        :'
          CALL PROMP1(1,STRG)
        END IF
        READ(LFNINP,32,END=510) FILHLP
32      FORMAT(A)
        IF(FILHLP(1:1).EQ.' '.AND.IFL.EQ.1.AND.LFNINP.NE.5) GOTO 30
        IF(FILHLP(1:1).EQ.' '.AND.IFL.NE.1.AND.LFNINP.NE.5) GOTO 510
        IF(FILHLP.EQ.' '.AND.LFNINP.EQ.5) GOTO 510
        IF(FILHLP(1:8).EQ.'Director') GOTO 30
        IF(FILHLP(1:8).EQ.'Total of') GOTO 30
C  WINDOWS NT DOS
        IF((FILHLP(3:3).EQ.'/'.AND.FILHLP(6:6).EQ.'/').OR.
     1     (FILHLP(3:3).EQ.'.'.AND.FILHLP(6:6).EQ.'.')) THEN
          DO 41 I=1,12
            FILHLP(I:I)=FILHLP(I+39:I+39)
41        CONTINUE
          FILHLP(13:)=' '
        END IF
        LHLP=LENGT0(FILHLP)
        DO 110 I=1,LHLP
          IF(FILHLP(I:I).EQ.' '.OR.FILHLP(I:I).EQ.'.') GOTO 115
110     CONTINUE
115     LSTM=I-1
        FILSTM=FILHLP(1:LSTM)
        LEXT=0
        DO 120 I=LSTM+2,LHLP
          IF(FILHLP(I:I).NE.' ') THEN
            FILEXT=FILHLP(I:LHLP)
            LEXT=LHLP-I+1
            GOTO 130
          END IF
120     CONTINUE
130     DO 140 I=1,LEXT
          IF(FILEXT(I:I).EQ.' '.OR.FILEXT(I:I).EQ.';') GOTO 150
140     CONTINUE
150     LEXT=I-1
        IF(LDVI.NE.0) THEN
          WRITE(FILEPH(IFL),131) DEVINP(1:LDVI),
     1                           FILSTM(1:LSTM),FILEXT(1:LEXT)
131       FORMAT(2A,'.',A)
        ELSE
          WRITE(FILEPH(IFL),132) FILSTM(1:LSTM),FILEXT(1:LEXT)
132       FORMAT(A,'.',A)
        END IF
C
40      INQUIRE(FILE=FILEPH(IFL),EXIST=YES)
        IF(.NOT.YES) THEN
          LF=LENGT0(FILEPH(IFL))
          IF(IBERN5.NE.1)WRITE(*,44) FILEPH(IFL)(1:LF)
44        FORMAT(/,' FILE ',A,' NOT FOUND.')
          IF(IFLFLG.NE.2) THEN
            IF(IBERN5.NE.1)WRITE(*,46)
46          FORMAT(/,' CHANGE DISKETTE AND TYPE <ENTER> OR',
     1             /,' ENTER <S> TO SKIP FILE OR')
            STRG(1)='ENTER <E> TO END PROGRAM            :'
            CALL PROMP1(1,STRG)
            READ(*,32) CH1
C_KEEP_LOWER_ON
            IF(CH1.EQ.'E'.OR.CH1.EQ.'e') GOTO 999
            IF(CH1.EQ.'S'.OR.CH1.EQ.'s') GOTO 30
C_KEEP_LOWER_OFF
            GOTO 40
          ELSE
            GOTO 30
          END IF
        END IF
500   CONTINUE
510   NFLEPH=IFL-1
C
C  READ AND INTERPRET NEXT RINEX FILE
C  ----------------------------------
C
600   DO 610 IFL=1,NFLEPH
        LE=LENGT0(FILEPH(IFL))
        IF(IBERN5.NE.1)WRITE(*,51) FILEPH(IFL)(1:LE)
51      FORMAT(' PROCESSING FILE ',A)
        CALL OPNFIL(LFNEPH,FILEPH(IFL),'OLD',' ',' ',' ',IOSTAT)
        CALL OPNERR(LFNERR,LFNEPH,IOSTAT,FILEPH(IFL),'CCRINEXN')
        CALL R2RDNH(LFNEPH,LFNERR,MAXCOM,PRGNAI,RUNBI,
     1                  CRDATI,CRTIMI,NCOMI,COMENI,
     2                  ALPHAI,BETAI,A0UTCI,A1UTCI,ITUTCI,NWKUTI,LEAPI,
     3                  IRXVRI,IRC)
        IF(IRC.NE.0) THEN
          IF(IBERN5.NE.1)WRITE(*,13) FILEPH(IFL)(1:LE)
13        FORMAT(' FILE ',A,' SKIPPED',/)
          CLOSE(UNIT=LFNEPH)
          GOTO 610
        ENDIF
        CALL RXCHKW(2,DUMMY,NWKUTI)
C
        DO 250 K=1,4
          IF(ALPHAI(K).NE.0.D0.OR.BETAI(K).NE.0.D0) THEN
             DO 230 J=1,4
                ALPHA(J)=ALPHAI(J)
                BETA (J)=BETAI (J)
230          CONTINUE
             GOTO 240
          END IF
250     CONTINUE
240     IF(NWKUTI.NE.0) THEN
          A0UTC=A0UTCI
          A1UTC=A1UTCI
          ITUTC=ITUTCI
          NWKUTC=NWKUTI
        END IF
        IF(IRXVRI.NE.1) IRXVRS=IRXVRI
        IF(LEAPI.NE.0) LEAP=LEAPI
C
        IFPRNT=1
50        CALL R2RDNR(LFNEPH,LFNERR,IRXVRI,ISVN,EPHDAT,IRC)
          IF(IRC.NE.0) THEN
            CLOSE(UNIT=LFNEPH)
            GOTO 610
          END IF
          CALL RXCHKW(1,EPHDAT(1),IDUMMY)
          NWEEK=IDNINT(EPHDAT(23))
          CALL RXCHKW(2,DUMMY,NWEEK)
          EPHDAT(23)=NWEEK
C
C  DON'T ACCEPT SATELLITES WITH NUMBER ZERO!
          IF(ISVN.EQ.0) GOTO 50
C
          NWEEK=DNINT(EPHDAT(23))
          SECOND=EPHDAT(13)
          TMJD=GPSMJD(SECOND,NWEEK)
C
C  MESSAGE OF CORRECT DAY?
          IF((TCOLL1.NE.0.D0.AND.TMJD.LT.TCOLL1).OR.
     1       (TCOLL2.NE.1D20.AND.TMJD.GE.TCOLL2)) GOTO 50
C
          CALL JMT(TMJD,IY,IMO,DAY)
          ITYP=3
          CALL RADGMS(ITYP,DAY,CH1,IH,MIN,SECOND)
          WRITE(LISTE(NMESS+1),61) ISVN,MOD(IY,100),IMO,IDINT(DAY),
     1                             IH,MIN,IDNINT(SECOND)
61        FORMAT(' SVN :',I3,'  TOE :',I5.2,2('-',I2.2),
     1                             I4.2,2(':',I2.2))
C
C  PREPARE HEADER TO BE WRITTEN BEFORE FIRST NAVIGATION RECORD
C  -----------------------------------------------------------
C
          IF(NMESS.EQ.0) THEN
C
C  GET RINEX FILENAME, OPEN FILE
            IF(TCOLL1.NE.0.D0) TMJD=TCOLL1
            IF(CHAR4I.NE.' ') THEN
              CHAR4=CHAR4I
            ELSE
              CHAR4=FILSTM(1:4)
            END IF
            CALL CCRXCN(TMJD,IFLFLG,CHAR4,FILSEQ,DEVOUT,FILOUT)
C
C  WRITE PROTOCOL
C
            IF(LFNPRT.NE.0) THEN
              LF=LENGT0(FILOUT)
              WRITE(LFNPRT,73) FILOUT(1:LF)
73            FORMAT(' RINEX NAVIGATION OUTPUT FILE : ',A)
            END IF
C
          END IF
C
          IF(LFNPRT.NE.0) THEN
            IF(IFPRNT.EQ.1) THEN
              LE=LENGT0(FILEPH(IFL))
              WRITE(LFNPRT,81) FILEPH(IFL)(1:LE),IRXVRI
81            FORMAT(/,' RINEX NAVIGATION  INPUT FILE : ',A,
     1                 ' VERSION',I2)
              IFPRNT=0
            END IF
          END IF
C
C  CHECK IF MESSAGE ALREADY STORED
C  IF YES: READ NEW MESSAGE
C  (NMESS: CURRENT TOTAL NUMBER OF MESSAGES)
          DO 80 I=1,NMESS
            IF(LISTE(I).EQ.LISTE(NMESS+1)) GOTO 50
80        CONTINUE
          NMESS=NMESS+1
C
C  WRITE EPHEMERIS-INFORMATION
C  ---------------------------
C
          IF(IRXVRI.EQ.1) THEN
            DO 85 K=29,32
              EPHDAT(K)=0.D0
85          CONTINUE
          END IF
C
C  WRITE PROTOCOL
C
          IF(IBERN5.NE.1)WRITE(*,*) LISTE(NMESS)
          IF(LFNPRT.NE.0) THEN
            WRITE(LFNPRT,82) LISTE(NMESS)
82          FORMAT(1X,A40)
          END IF
C
          IF(NMESS.EQ.MAXMES) THEN
            IF(IBERN5.NE.1)
     1         WRITE(*,*) 'MAXIMUM NUMBER OF MESSAGES REACHED:',MAXMES
            GOTO 210
          END IF
C
C  CREATE STRING WITH DATE AND SATELLITE TO BE USED FOR SORTING
          WRITE(SORTLI(NMESS),83) EPHDAT(1),ISVN
83        FORMAT(F13.6,1X,I2.2)
          IF(NMESS.EQ.1) THEN
            OPEN(UNIT=LFNSRT,FILE='CCRINEXN.SRT',ACCESS='DIRECT',
     1           RECL=260,STATUS='UNKNOWN')
          END IF
          WRITE(LFNSRT,REC=NMESS) EPHDAT,ISVN
          MESSAT(ISVN)=MESSAT(ISVN)+1
C
C  NEXT MESSAGE
        GOTO 50
C
610   CONTINUE
210   IF (NMESS.EQ.0) THEN
        WRITE(LFNERR,"(/,' ### PG CCRINEXN: ',
     1                   'NO MESSAGES FOUND WITHIN TIME WINDOW',/)")
        IF(IBERN5.NE.1)WRITE(*,"(/,' ### PG CCRINEXN: ',
     1                   'NO MESSAGES FOUND WITHIN TIME WINDOW',/)")
        CALL EXITRC(0)
      ENDIF
C
C  SORT MESSAGES
C
      CALL CORDUP(SORTLI,NMESS,1,16,INDEXS)
C
C  GET SYSTEM DATE/TIME TO BE STORED IN THE FIRST OUTPUT RECORD
      CALL CLOCKS(M)
      WRITE(CRDATE,71) M(3),MONTH(M(2)),MOD(M(1),100)
71    FORMAT(I2.2,'-',A3,'-',I2.2)
      WRITE(CRTIME,72) M(5),M(6)
72    FORMAT(I2.2,':',I2.2)
C
      CALL OPNFIL(LFNNAV,FILOUT,'UNKNOWN',' ',' ',' ',IOSTAT)
      CALL OPNERR(LFNERR,LFNNAV,IOSTAT,FILOUT,'CCRINEXN')
C
C  WRITE RINEX HEADER
C
      CALL R2WTNH(LFNNAV,LFNERR,PRGNAM,RUNBY,
     1            CRDATE,CRTIME,NCOM,COMENT,
     2            ALPHA,BETA,A0UTC,A1UTC,ITUTC,NWKUTC,LEAP,
     3            IRXVRS,IRC)
C
C  REWRITE MESSAGES TO RINEX FILE
C
      DO 220 I=1,NMESS
        READ(LFNSRT,REC=INDEXS(I)) EPHDAT,ISVN
        IF(MESSAT(ISVN).GE.MINMES) THEN
          CALL R2WTNR(LFNNAV,LFNERR,IRXVRS,ISVN,EPHDAT,IRC)
        END IF
220   CONTINUE
      CLOSE(UNIT=LFNSRT,STATUS='DELETE')
      IF(IBERN5.NE.1)WRITE(*,272) (K,K= 1,16),
     1             (MESSAT(K),K= 1,16),
     2             (K,K=17,32),
     1             (MESSAT(K),K=17,32)
272   FORMAT(/,2(1X,16I3.2,/,1X,16I3,/))
      WRITE(LFNPRT,272) (K,K= 1,16),
     1             (MESSAT(K),K= 1,16),
     2             (K,K=17,32),
     1             (MESSAT(K),K=17,32)
      DO 270 I=1,MAXSVN
        IF(MESSAT(I).NE.0.AND.MESSAT(I).LT.MINMES
     1                           .AND.IBERN5.NE.1) WRITE(*,271) I
        IF(MESSAT(I).NE.0.AND.MESSAT(I).LT.MINMES) WRITE(LFNPRT,271) I
271     FORMAT('  SVN :',I3,'  NOT ENOUGH MESSAGES')
270   CONTINUE
C
999   IF(LFNPRT.NE.0) CLOSE(UNIT=LFNPRT)
      CLOSE(UNIT=LFNNAV)
      CALL EXITRC(IRCODE)
      END
C*
      SUBROUTINE CCRXOP(FILOPT,MAXCOM,NCOM,COMENT,RUNBY,MINMES,
     1                  IFLFLG,DEVINP,FILINP,FILPRT,DEVOUT,CHAR4,FILDAY)
CC
CC NAME       :  CCRXOP
CC
CC PURPOSE    :  READ OPTION INPUT FILE FOR PROGRAM CCRINEXN
CC
CC PARAMETERS :
CC         IN :  FILOPT : FILENAME OF OPTION FILE             CH*32
CC               MAXCOM : MAXIMUM COMMENT LINES                I*4
CC        OUT :  NCOM   : NUMBER OF COMMENT LINES              I*4
CC               COMENT : COMMENT LINES                       CH*60(*)
CC               RUNBY  : AGENCY RUNNING PROGRAM              CH*20
CC               MINMES : MINIMUM NUMBER OF MESSAGES PER SAT   I*4
CC               IFLFLG : FLAG FOR RINEX FILENAME CREATION     I*4
CC               DEVINP : DEVICE/PATH FOR INPUT RINEX FILES   CH*32
CC               FILINP : NAME OF FILE WITH LIST OF RNX.FILES CH*32
CC               FILPRT : PROTOCOL FILENAME                   CH*32
CC               DEVOUT : DEVICE/PATH FOR RINEX OUTPUT FILES  CH*32
CC               CHAR4  : FIRST 4 CHAR FOR RINEX OUTPUT FILE  CH*32
CC               FILDAY : NAME OF FILE WITH DAY TO COLLECT    CH*32
CC
CC REMARKS    :  ---
CC
CC AUTHOR     :  W. GURTNER
CC
CC VERSION    :  3.4  (JAN 93)
CC
CC CREATED    :  28-OCT-90
CC
CC CHANGES    :  13-FEB-01 : ADD MINIMUM NUMBER OF MESSAGES PER SATELLITE
CC
CC COPYRIGHT  :  ASTRONOMICAL INSTITUTE
CC      1990     UNIVERSITY OF BERN
CC               SWITZERLAND
CC
C*
      USE s_exitrc
      USE s_opnfil
      USE s_opnerr
      IMPLICIT REAL*8 (A-H,O-Z)
      IMPLICIT INTEGER*4 (I-N)
      CHARACTER    COMENT(MAXCOM)*60,RUNBY*20,DEVINP*32,FILDAY*32
      CHARACTER    FILPRT*32,FILINP*32,FILOPT*32,DEVOUT*32,CHAR4*4
      DATA IBERN5/0/
C
C  LOCAL DECLARATIONS
      CHARACTER STRING*80
      LOGICAL      YES
C
C OPEN INPUT FILE
C
      INQUIRE(FILE=FILOPT,EXIST=YES)
      IF(.NOT.YES) THEN
        IF(IBERN5.NE.1)WRITE(*,901) FILOPT
901     FORMAT(/,' *** SR CCRXOP: OPTIONS FILE NOT FOUND',
     1         /,'     FILENAME : ',A,/)
        CALL EXITRC(2)
      END IF
      LFNLOC=99
      CALL OPNFIL(LFNLOC,FILOPT,'OLD',' ',' ',' ',IOSTAT)
      CALL OPNERR(LFNERR,LFNLOC,IOSTAT,FILOPT,'CCRXOP')
C
C READ COMENT LINES
C
      READ(LFNLOC,1)
1     FORMAT(///////)
      DO 10 I=1,MAXCOM+1
        READ(LFNLOC,11) STRING
11      FORMAT(A)
        IF(STRING.EQ.' ') GOTO 20
        IF(I.GT.MAXCOM) THEN
          IF(IBERN5.NE.1)WRITE(*,902) I
902       FORMAT(/,' *** SR CCRXOP: TOO MANY COMENT LINES:',I5,/)
          CALL EXITRC(2)
        END IF
        COMENT(I)=STRING(7:66)
10    CONTINUE
20    NCOM=I-1
      IF(NCOM.EQ.1.AND.COMENT(1).EQ.' ') NCOM=0
C
C READ ADDITIONAL INFORMATION
C
      READ(LFNLOC,21)
21    FORMAT(////)
      READ(LFNLOC,22) RUNBY
22    FORMAT(36X,A)
C
      READ(LFNLOC,24)
24    FORMAT(//)
      READ(LFNLOC,23) IFLFLG
23    FORMAT(36X,I5)
      READ(LFNLOC,23) MINMES
C
      READ(LFNLOC,*)
      READ(LFNLOC,*)
      READ(LFNLOC,22) DEVINP
      READ(LFNLOC,22) FILINP
      READ(LFNLOC,22) FILPRT
      READ(LFNLOC,22) DEVOUT
      READ(LFNLOC,22) CHAR4
      READ(LFNLOC,22) FILDAY
C
      CLOSE(UNIT=LFNLOC)
      RETURN
      END
C*
      SUBROUTINE CCRXCN(TJUL,IFLFLG,CHAR4,FILSEQ,DEVOUT,FILOUT)
CC
CC NAME       :  CCRXCN
CC
CC PURPOSE    :  CHANGE GIVEN DATA (PROGRAM CCRINEXN)
CC
CC PARAMETERS :
CC         IN :  TJUL   : CURRENT JULIAN DATE                  R*8
CC               IFLFLG : OUTPUT FILENAME FLAG                 I*4
CC               CHAR4  : 4 CHARACTERS FOR FILENAME           CH*4
CC               FILSEQ : FILE SEQUENCE # FOR OUTPUT FILE     CH*1
CC               DEVOUT : DEVICE/PATH FOR OUTPUT FILES        CH*32
CC        OUT :  FILOUT : RINEX OUTPUT FILENAME               CH*64
CC
CC REMARKS    :  ---
CC
CC AUTHOR     :  W. GURTNER
CC
CC VERSION    :  3.4  (JAN 93)
CC
CC CREATED    :  28-OCT-90
CC
CC COPYRIGHT  :  ASTRONOMICAL INSTITUTE
CC      1990     UNIVERSITY OF BERN
CC               SWITZERLAND
CC
C*
      USE s_jmt
      USE s_promp1
      USE f_lengt0
      USE f_djul
      IMPLICIT REAL*8 (A-H,O-Z)
      IMPLICIT INTEGER*4 (I-N)
      CHARACTER FILOUT*64,DEVOUT*32,CHAR4*4
      CHARACTER FILHLP*64,FILSEQ*1,ISESS*1
      CHARACTER*80 STRING(1)
C
C  RINEX OUTPUT FILENAME
C
      IF(IFLFLG.NE.0) THEN
C
C  CREATE FILENAME USING DATE AND SESSION NUMBER
        IF(FILSEQ.NE.' ') THEN
          ISESS=FILSEQ
        ELSE
          ISESS='0'
        END IF
        JAN=1
        FIRST=1.D0
        CALL JMT(TJUL,IYEAR,IMON,DAY)
        IDYEAR=IDINT(TJUL-DJUL(IYEAR,JAN,FIRST)+1)
        WRITE(FILOUT,61) CHAR4(1:4),IDYEAR,ISESS,
     1                   MOD(IYEAR,100),'N'
61      FORMAT(A,I3.3,A1,'.',I2.2,A1)
      ELSE
        FILOUT=' '
      END IF
      IF(IFLFLG.NE.2) THEN
60      WRITE(STRING(1),62) FILOUT
62      FORMAT('RINEX FILENAME : ',A32,' :')
        CALL PROMP1(1,STRING)
        READ(*,63) FILHLP
63      FORMAT(A)
        IF(FILHLP.NE.' ') FILOUT=FILHLP
        IF(FILOUT.EQ.' ') GOTO 60
      END IF
      LD=LENGT0(DEVOUT)
      LF=LENGT0(FILOUT)
      IF(LD.NE.0) THEN
        WRITE(FILHLP,64) DEVOUT(1:LD),FILOUT(1:LF)
64      FORMAT(2A)
      ELSE
        WRITE(FILHLP,64) FILOUT(1:LF)
      END IF
      FILOUT=FILHLP
C
      RETURN
      END
