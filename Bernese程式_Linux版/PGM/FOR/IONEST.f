C*
      PROGRAM IONEST
CC
CC NAME       :  IONEST
CC
CC PURPOSE    :  THE PROGRAM IONEST ESTIMATES IONOSPHERE MODELS
CC               (SINGLE LAYER MODEL) USING DUAL FREQUENCY OBSERVATIONS
CC               FROM ONE OR MORE GPS RECEIVERS
CC
CC REMARKS    :  ---
CC
CC AUTHOR     :  U. WILD
CC
CC CREATED    :  88/11/02 15:48
CC
CC CHANGES    :  20-JUN-91 : ??: MAXAMB CHANGED FROM 15 TO 50
CC               21-JUN-91 : ??: MAXAMP,MAXPAR CHANGED FROM 300 TO 400
CC               23-APR-92 : ??: NEW INTERNAL FILE NAME "RESIDRS"
CC               13-JUL-92 : ??: USE "MAXFRQ" FOR DIMENS. OF "FILFRQ"
CC               09-DEC-92 : EB: NEW COMMON MCMSTA
CC               11-JAN-93 : ??: ADD STOP STATEMENT AT END OF PROGRAM
CC               23-NOV-93 : SF: SET MAXSAT TO 30
CC               10-AUG-94 : MR: CALL EXITRC
CC               14-AUG-94 : MR: FORMAT 4: SESSION AS CHARACTER*4
CC               17-SEP-95 : JJ: INCREASE MAXSTA TO 200
CC               05-JUN-96 : SS: UNUSED VARIABLE "SESSAV" REMOVED
CC               24-SEP-97 : DI: USE MAXSAT.inc
CC               30-MAR-98 : TS: CHANGED CALL OF DEFSES
CC               06-NOV-00 : CU: SWITCH TO THE NEW MENU SYSTEM
CC               27-AUG-02 : RD: HANDLE NEW FORMATTED RESIDUAL FILES
CC               28-JAN-03 : RS: IONCOE,IONSIG REAL*4 -> REAL*8
CC               17-FEB-03 : LM: USE M_MAXDIM
CC               03-MAR-03 : SC: ADD TITLE SECTION
CC               16-MAY-03 : HB: INITIALIZE STRUCTURE
CC               28-MAY-03 : RD: CORRECT SETTING OF RESHED - ARRAY
CC               08-SEP-03 : HU: ANTNAM, RECNAM, OPRNAM CHR16 -> CHR20
CC               19-NOV-03 : RD: READ INP-FILENAME IN READINPF
CC               28-JUN-04 : RD: USE MAXSTA FROM M_MAXDIM
CC               11-OCT-04 : RD: NEW CALL OF DEFSES
CC               16-JUN-05 : MM: UNUSED COMCONST.inc REMOVED
CC               21-JUN-05 : MM: (COM)LFNUM.inc REMOVED, m_bern ADDED
CC               23-JUN-05 : MM: IMPLICIT NONE AND DECLARATIONS ADDED
CC               27-Feb-07 : AG: CALL DEFCON WITH PARAMETER
CC               25-MAY-10 : MF: CALL SR init_filhead
CC               23-SEP-10 : RD: ENABLE CPU COUNTER
CC               26-JAN-11 : LP: CALL TO RDHEAD CHANGED
CC               17-FEB-11 : RD: COMMON MCMSTA NOT NEEDED ANYMORE
CC               18-FEB-11 : RD: USE MODULE GETSTA NOW
CC               14-NOV-11 : SL: NEW TITLE STRING FOR PRITIT, M_BERN WITH ONLY
CC               05-MAR-12 : RD: USE LISTI4 AS MODULE NOW
CC               10-JUL-12 : RD: REMOVE UNUSED VARIABLES FROM IONSLM
CC
CC COPYRIGHT  :  ASTRONOMICAL INSTITUTE
CC      1988     UNIVERSITY OF BERN
CC               SWITZERLAND
CC
C*
      USE m_bern,   ONLY: lfnErr, lfnOrb, lfnRes, lfn001
      USE m_cpu,    ONLY: cpu_start
      USE d_inpkey, ONLY: inpKey, init_inpkey
      USE d_resfil, ONLY: t_reshead, reshed_IONEST, init_resHead,
     &                    init_filhead
      USE m_maxdim, ONLY: maxsat, maxsta
C
      USE s_ionres
      USE s_alcerr
      USE s_prflna
      USE s_ionslm
      USE s_ionopt
      USE s_gtfile
      USE s_defses
      USE s_pritit
      USE s_readinpf
      USE s_ionsav
      USE s_ionses
      USE s_rdhead
      USE s_getsta
      USE s_defcon
      USE s_exitrc
      USE s_opnsys
      USE s_wtresh2
      USE s_ionout
      USE s_gtflna
      USE f_listi4
      IMPLICIT NONE
C
C DECLARATIONS INSTEAD OF IMPLICIT
C --------------------------------
      INTEGER*4 I     , ICARR , IDEVHA, IDEVLA, IDEVMI, IFIL  , IMOD  ,
     1          INTMAX, IQ    , IRC   , IRESID, IRMARK, IRSOUT, ISASYS,
     2          ISAT  , ISES  , LFNION, LFNRSD, MAXAMB, MAXAMP, MAXARC,
     3          MAXCMP, MAXEPO, MAXFIL, MAXFRQ, MAXLCQ, MAXMOD, MAXPAR,
     4          MAXTRM, MELWUB, MINELV, MOBS  , MODNR , MODTYP, MXCAMB,
     5          MXCAMP, MXCARC, MXCCLK, MXCEPO, MXCFIL, MXCFRQ, MXCLCQ,
     6          MXCMOD, MXCPAR, MXCSAT, MXCTRM, NCENTR, NDIFF ,
     7          NEPFLG, NFIL  , NFLCOL, NFREQ , NFTOT , NIONP , NMXFLS,
     8          NMXFRS, NMXSAS, NSESS , NSTAT , NUMAMB, IDUMMY, NSAT  ,
     9          ISATEL
C
      REAL*8    AELL  , AOI   , BELL  , HION  , RMS   , RMSOBS, SCELL
C
CCC       IMPLICIT REAL*8 (A-H,O-Z)
C
C MAXIMAL DIMENSIONS
C ------------------
      PARAMETER (MAXAMB=600,MAXFRQ=2,MAXFIL=10,MAXARC=10,
     1           MAXPAR=200,MAXAMP=200,MAXMOD=10,MAXTRM=20,
     2           MAXLCQ=  7,MAXEPO=5000,MAXCMP=1)
C
C
C MAXSAT: MAXIMUM NUMBER OF SATELLITES
C MAXAMB: MAXIMUM NUMBER OF AMBIGUITIES FOR ONE SATELLITE IN A FILE
C MAXFRQ: MAXIMUM NUMBER OF DIFFERENT FREQ. TO BE PROCESSED PER FILE
C MAXFIL: MAXIMUM NUMBER OF FILES TO BE PROCESSED
C MAXARC: MAXIMUM NUMBER OF SATELLITE ARCS
C MAXPAR: MAXIMUM NUMBER OF PARAMETERS SIMULTANEOUSLY PROCESSED
C MAXAMP: MAXIMUM NUMBER OF AMBIGUITY PARAMETERS SIMULTAN. PROCESSED
C MAXMOD: MAXIMUM NUMBER OF IONOSPHERE MODELS
C MAXTRM: MAXIMUM NUMBER OF TERMS IN IONOSPHERE MODEL
C MAXLCQ: MAXIMUM NUMBER OF ITEMS PER PARAMETER IN ARRAY "LOCQ"
C MAXEPO: MAXIMUM NUMBER OF EPOCHS
C MAXCMP: MAXIMUM NUMBER OF CAMPAIGNS
C MAXSTA: MAXIMUM NUMBER OF STATIONS
C
C DECLARATIONS
C -------------
      TYPE(t_resHead) reshed
C
      REAL*8        IONDEV(8,MAXMOD),OBSERV(MAXSAT,MAXFRQ)
      REAL*8        POSECC(3,2),AMBIGU(MAXAMB,3)
      REAL*8        DPHI(MAXEPO,MAXSAT,2)
      REAL*8        TIMOBS(MAXEPO),TIMREF(MAXFIL)
      REAL*8        XSTAT(3,2,MAXFIL),XSTELL(3,2,MAXFIL)
      REAL*8        XSTECC(3,2)
      REAL*8        XSTAS(3,2,MAXFIL),XSTELS(3,2,MAXFIL)
      REAL*8        DXELL(3),DRELL(3)
      REAL*8        ANOR(MAXPAR*(MAXPAR+1)/2),BNOR(MAXPAR)
      REAL*8        AOBS(MAXPAR),BOBS(MAXPAR)
      REAL*8        A0I(MAXPAR*MAXSAT),AII(MAXAMP*(MAXAMP+1)/2)
      REAL*8        XX(MAXPAR),VSIG(MAXPAR),OFFSET(MAXAMP,MAXSAT)
      REAL*8        OFFS(MAXSAT),RMSIP(MAXPAR),TIMRES(MAXFIL)
      REAL*8        RESIDU(MAXSAT)
      REAL*8        L4SUM(MAXAMP,MAXSAT),C4(MAXAMP,MAXSAT)
C
      REAL*8        IONCOE(MAXTRM,MAXMOD),IONSIG(MAXTRM,MAXMOD)
C
      INTEGER*4     IRUNIT(2),SVNLST(MAXSAT)
      INTEGER*4     ICLOCK(2),NUMOBS(MAXSAT,2),NUMMRK(MAXSAT,2)
      INTEGER*4     NUMSAT(MAXSAT,MAXFIL)
      INTEGER*4     NUMSAS(MAXSAT,MAXFIL),IANTEN(2)
      INTEGER*4     AMBNUM(MAXSAT),AMBEPO(MAXAMP,MAXSAT)
      INTEGER*4     AMBEP1(2*MAXAMP+1,MAXSAT),NFRFIL(MAXFIL)
      INTEGER*4     AMBIEP(MAXAMB),NRSAT(MAXSAT)
      INTEGER*4     AMBSAT(MAXAMB),AMBWLF(MAXAMB,2),AMBCLS(MAXAMB,3)
      INTEGER*4     LOCQ(MAXLCQ,MAXPAR),PRTLEV,IFRQS(MAXFRQ)
      INTEGER*4     STANUM(2,MAXFIL),ICENTR(MAXFIL),INUMOB(MAXFIL)
      INTEGER*4     FILNUM(MAXFIL),IDEL(MAXPAR),INDP(MAXPAR)
      INTEGER*4     MEATYP(MAXFIL),NEPOCH(MAXFIL),NSATEL(MAXFIL)
      INTEGER*4     IDELTT(MAXFIL)
      INTEGER*4     MTYPS(MAXFIL),NEPOS(MAXFIL),NSATS(MAXFIL)
      INTEGER*4     IDELTS(MAXFIL)
      INTEGER*4     INDFIL(MAXFIL),IFRMAT(MAXFIL),IFRMAS(MAXFIL)
      INTEGER*4     STNUM(2,MAXFIL),FILFRQ(MAXFRQ,MAXFIL),CORSTR
      INTEGER*4     OBSNUM(MAXAMP,MAXSAT)
C
      INTEGER*4     IONREQ(4,MAXMOD),NTERM(MAXMOD),NM(MAXTRM,2,MAXMOD)
      INTEGER*4     INDA(MAXPAR)
C
      INTEGER*4     meatype,READGEOS
C
      CHARACTER*80  TITLE
      CHARACTER*53  HTITLE
      CHARACTER*32  FILNAM(2,MAXFIL)
      CHARACTER*32  HEDFIS(MAXFIL),OBSFIS(MAXFIL)
      CHARACTER*32  RESFIL
      CHARACTER*16  CAMPGN(MAXCMP),STANAM(2,MAXFIL)
      CHARACTER*16  DATUM,STNAMS(2,MAXFIL),STNAME(MAXFIL)
      CHARACTER*20  ANTTYP(2),RECTYP(2),OPRNAM(2)
      CHARACTER*9   CRDATE(2)
      CHARACTER*6   MXNSAT,MXNCLK,MXNAMB,MXNFRQ,MXNFIL,MXNARC,MXNPAR
      CHARACTER*6   MXNAMP,MXNMOD,MXNTRM,MXNLCQ,MXNEPO
      CHARACTER*5   CRTIME(2)
      CHARACTER*4   CSESS(2,MAXFIL),SESSID(MAXFIL)
      CHARACTER*1   OBSFLG(MAXSAT,MAXFRQ)
      CHARACTER*1   FLAG(MAXEPO,MAXSAT,2)
C
C COMMON BLOCKS
C -------------
      COMMON/LARGE1/IONDEV,OBSERV,AMBIGU,DPHI,TIMOBS,
     1              XSTAT,XSTELL,XSTECC,XSTAS,XSTELS,
     2              ANOR,BNOR,AOI,AII,
     3              XX,VSIG,OFFSET,RMSIP,L4SUM,C4,IONCOE,IONSIG
      COMMON/LARGE2/NUMSAT,NUMSAS,AMBEPO,AMBEP1,AMBIEP,LOCQ
      COMMON/LARGE3/FILNAM,HEDFIS,OBSFIS,STANAM,STNAMS,OBSNUM
      COMMON/MCMSAT/MXCSAT,MXNSAT
      COMMON/MCMCLK/MXCCLK,MXNCLK
      COMMON/MCMAMB/MXCAMB,MXNAMB
      COMMON/MCMFRQ/MXCFRQ,MXNFRQ
      COMMON/MCMFIL/MXCFIL,MXNFIL
      COMMON/MCMARC/MXCARC,MXNARC
      COMMON/MCMPAR/MXCPAR,MXNPAR
      COMMON/MCMAMP/MXCAMP,MXNAMP
      COMMON/MCMMOD/MXCMOD,MXNMOD
      COMMON/MCMTRM/MXCTRM,MXNTRM
      COMMON/MCMLCQ/MXCLCQ,MXNLCQ
      COMMON/MCMEPO/MXCEPO,MXNEPO
C
C START CPU COUNTER
C -----------------
      CALL cpu_start(.TRUE.)
C
C INITIALIZE COMMON BLOCKS FOR MAXIMAL DIMENSIONS
C -----------------------------------------------
      MXCSAT=MAXSAT
      MXNSAT='MAXSAT'
      MXCAMB=MAXAMB
      MXNAMB='MAXAMB'
      MXCFRQ=MAXFRQ
      MXNFRQ='MAXFRQ'
      MXCFIL=MAXFIL
      MXNFIL='MAXFIL'
      MXCARC=MAXARC
      MXNARC='MAXARC'
      MXCPAR=MAXPAR
      MXNPAR='MAXPAR'
      MXCAMP=MAXAMP
      MXNAMP='MAXAMP'
      MXCMOD=MAXMOD
      MXNMOD='MAXMOD'
      MXCTRM=MAXTRM
      MXNTRM='MAXTRM'
      MXCLCQ=MAXLCQ
      MXNLCQ='MAXLCQ'
      MXCEPO=MAXEPO
      MXNEPO='MAXEPO'
C
C DEFINE LOGICAL FILE NUMBERS
C ---------------------------
C
      LFNION = LFNRES
      LFNRSD = LFN001+10
C
C NULLIFY POINTER
C ---------------
      CALL INIT_RESHEAD(resHed)
      CALL INIT_INPKEY(inpKey)
C
C GET THE NAME OF THE INPUT FILE
C ------------------------------
      CALL readinpf(' ',inpKey)
C
C DEFINE SYSTEM FILES
C -------------------
      CALL OPNSYS
C
C PRINT TITLE
C -----------
      CALL pritit('IONEST','Local ionosphere model estimation')
      CALL prflna
C
C DEFINE CONSTANTS
C ----------------
      CALL DEFCON(1)
C
C INITIALIZE RESIDUAL ARRAY
C -------------------------
      DO 5 ISAT = 1,MAXSAT
        RESIDU(ISAT) = 0.D0
5     CONTINUE
C
C INITIALIZE ARRAY IONREQ
C -----------------------
      DO 7 IMOD = 1,MAXMOD
        NTERM(IMOD) = 0
        DO 6 I = 1,4
          IONREQ(I,IMOD) = 0
6       CONTINUE
7     CONTINUE
C
C READ INPUT OPTION FILE
C ----------------------
      CALL ionopt(title,prtlev,icarr,iq,intmax,rmsobs,modtyp,
     1            minelv,iresid,idevla,idevha,idevmi,hion,meatype)
C
C READ INPUT FILENAMES (HEADER- AND OBSERVATION FILES)
C ----------------------------------------------------
      NFLCOL=2
      IF (meatype == 1) THEN
        CALL GTFILE('PHASFIL ',NFLCOL,MAXFIL,NFTOT,FILNAM)
      ELSE IF (meatype == 2) THEN
        CALL GTFILE('CODEFIL ',NFLCOL,MAXFIL,NFTOT,FILNAM)
      END IF
C
C READ INFORMATION FROM FILE HEADERS
C ----------------------------------
      READGEOS=0
      DO 10 IFIL = 1,NFTOT
C
        CALL RDHEAD(FILNAM(1,IFIL),MEATYP(IFIL),NDIFF,NFREQ,
     1              NEPOCH(IFIL),NSATEL(IFIL),
     2              CSESS(1,IFIL),IDELTT(IFIL),
     3              TIMREF(IFIL),CAMPGN(1),HTITLE,CRDATE,CRTIME,IRMARK,
     4              NEPFLG,IFRMAT(IFIL),STANAM(1,IFIL),RECTYP,
     5              ANTTYP,IRUNIT,IANTEN,OPRNAM,POSECC,ICLOCK,
     6              NUMSAT(1,IFIL),NUMOBS,NUMMRK,NUMAMB,AMBSAT,AMBIEP,
     7              AMBWLF,AMBIGU,AMBCLS,READGEOS)
C
C GENERATE THE LIST OF SATELLITES
C -------------------------------
        NSAT=0
        DO ISATEL=1,NSATEL(IFIL)
          IDUMMY=LISTI4(1,MAXSAT,SVNLST,NUMSAT(ISATEL,IFIL),NSAT)
        ENDDO
C
C CHECK NUMBER OF FREQUENCIES
C ---------------------------
        IF (NFREQ .NE. 2) THEN
          WRITE(LFNERR,8)
8         FORMAT(/,' *** PGM IONEST: SINGLE FREQUENCY FILES',/,
     1           16X,' NOT ALLOWED FOR PGM IONEST',/)
          CALL EXITRC(2)
        ENDIF
        FILFRQ(1,IFIL) = 4
        NFRFIL(IFIL) = 1
C
C CHECK MAXIMUM NUMBER OF EPOCHS
C ------------------------------
        IF(NEPOCH(IFIL) .GT. MAXEPO) THEN
          WRITE(LFNERR,11) NEPOCH(IFIL),MXCEPO
11        FORMAT(/,' *** PGM IONEST: TOO MANY EPOCHS ON FILE',/,
     1                             16X,'NUMBER OF EPOCHS:',I6,/,
     2                             16X,'MAXIMUM NUMBER  :',I6,/)
          CALL EXITRC(2)
        END IF
C
C GET STATION COORDINATES
C -----------------------
        NSTAT = 1
        CALL GETSTA(NSTAT,STANAM(:,IFIL),STANUM(:,IFIL),NCENTR,ICENTR,
     1              XSTAT(:,:,IFIL),XSTELL(:,:,IFIL),XSTECC,
     2              DATUM,AELL,BELL,DXELL,DRELL,SCELL)
C
        STNAME(IFIL) = STANAM(1,IFIL)
        STNUM(1,IFIL) = IFIL
C
10    CONTINUE
C
C DEFINE NUMBER OF SESSIONS AND SESSION NUMBERS
C ---------------------------------------------
      CORSTR = 0
      MELWUB = 0
      ISASYS = 0
      CALL DEFSES(NFTOT,CSESS,MELWUB,0,CORSTR,NSATEL,NUMSAT,(/1/),
     1            NSAT,SVNLST,ISASYS,NSESS,SESSID,NMXFLS,NMXSAS,NMXFRS)
C
C SORT ARRAY WITH SESSION NUMBERS
C -------------------------------
C      DO 20 JJ = 1,NSESS-1
C        DO 20 KK = JJ+1,NSESS
C          IF (SESSID(KK) .LT. SESSID(JJ)) THEN
C            SESSAV = SESSID(KK)
C            SESSID(KK) = SESSID(JJ)
C            SESSID(JJ) = SESSAV
C          ENDIF
C20    CONTINUE
C
C WRITE HEADER OF RESIDUAL FILE
C -----------------------------
      CALL GTFLNA(0,'RESIDRS',RESFIL,IRC)
      IF (IRC .EQ. 0) THEN
        IRSOUT = 2
      ELSE
        IRSOUT = 1
      ENDIF
      IF (IRESID .EQ. 1 .AND. IRC .EQ. 0) THEN
C
C INIT RESIDUAL HEADER RECORD
C ---------------------------
        RESHED%TITLE = TITLE
        RESHED%DSC   = resHed_IONEST
        RESHED%NFIL  = NFTOT
        ALLOCATE(RESHED%FILHEAD(NFTOT),STAT=irc)
        CALL ALCERR(IRC,'RESHED%FILHEAD',(/NFTOT/),'IONEST')
        DO IFIL=1,NFTOT
          CALL init_filhead(RESHED%FILHEAD(IFIL))
          RESHED%FILHEAD(IFIL)%MEATYP = MEATYP(IFIL)
          RESHED%FILHEAD(IFIL)%NFRFIL = NFRFIL(IFIL)
          RESHED%FILHEAD(IFIL)%ICARR(1:NFRFIL(IFIL)) =
     1                                     FILFRQ(1:NFRFIL(IFIL),IFIL)
          RESHED%FILHEAD(IFIL)%STANAM(1) = STNAME(STNUM(1,IFIL))
          RESHED%FILHEAD(IFIL)%CSESS(:) = CSESS(:,IFIL)
          RESHED%FILHEAD(IFIL)%IDELTT = IDELTT(IFIL)
          RESHED%FILHEAD(IFIL)%TIMREF = TIMREF(IFIL)
          RESHED%FILHEAD(IFIL)%NSATEL = NSATEL(IFIL)
          ALLOCATE(RESHED%FILHEAD(IFIL)%NUMSAT(NSATEL(IFIL)),STAT=IRC)
          CALL ALCERR(IRC,'RESHED%FILHEAD(IFIL)%NUMSAT',
     1               (/NSATEL(IFIL)/),'IONEST')
          RESHED%FILHEAD(IFIL)%NUMSAT(:) = NUMSAT(1:NSATEL(IFIL),IFIL)
        ENDDO
C
C WRITE THE RESIDUAL FILE HEADER
C ------------------------------
        CALL WTRESH2(LFNRSD,RESHED)
C
        DO IFIL=1,NFTOT
          DEALLOCATE(RESHED%FILHEAD(IFIL)%NUMSAT,STAT=IRC)
        ENDDO
        DEALLOCATE(RESHED%FILHEAD,STAT=irc)
      ENDIF
C
C LOOP OVER ALL SESSIONS
C ----------------------
      DO 1000 ISES = 1,NSESS
C
C SELECT FILES OF ONE SESSION
C ---------------------------
        CALL IONSES(CSESS,SESSID,NFTOT,NDIFF,FILNAM,STANAM,
     1              MEATYP,NEPOCH,NSATEL,IDELTT,TIMREF,NUMSAT,
     2              XSTAT,XSTELL,IFRMAT,NFIL,INDFIL,HEDFIS,OBSFIS,
     3              STNAMS,MTYPS,NEPOS,NSATS,IDELTS,
     4              TIMRES,NUMSAS,XSTAS,XSTELS,IFRMAS)
C
C COMPUTATION OF IONOSPHERE MODELS
C --------------------------------
C
        IF (MODTYP .EQ. 1) THEN
C
C SINGLE LAYER MODEL
C ------------------
          CALL IONSLM(NFIL  ,HEDFIS,OBSFIS,MTYPS ,NEPOS ,NSATS ,
     1                IDELTS,TIMRES,NUMSAS,XSTAS ,XSTELS,PRTLEV,
     2                ICARR ,IQ    ,INTMAX,RMSOBS,NIONP ,MINELV,
     3                IDEVLA,IDEVHA,IDEVMI,HION  ,LOCQ  ,IFRMAS,
     4                INUMOB,MOBS  ,XX    ,RMS   ,RMSIP ,FLAG  ,
     5                OBSFLG,INDA  ,INDP  ,IDEL  ,FILNUM,NRSAT ,
     6                IFRQS ,AMBEPO,AMBNUM,OFFS  ,OFFSET,VSIG  ,
     7                AII   ,A0I   ,AOBS  ,BOBS  ,ANOR  ,BNOR  ,
     8                TIMOBS,DPHI  ,OBSERV,IONDEV,MODNR)
C
C COMPUTE RESIDUALS
C -----------------
          IF (IRESID .EQ. 1) THEN
C
            CALL IONRES(NFIL,INDFIL,HEDFIS,OBSFIS,MTYPS,NEPOS,NSATS,
     1                  IDELTS,TIMRES,NUMSAS,XSTAS,XSTELS,PRTLEV,
     2                  ICARR,IQ,INTMAX,RMSOBS,NIONP,MINELV,IDEVLA,
     3                  IDEVHA,IDEVMI,HION,LOCQ,IRSOUT,IFRMAS,
     4                  XX,FLAG,OBSFLG,NRSAT,IFRQS,AMBEPO,AMBNUM,OFFS,
     5                  OFFSET,TIMOBS,DPHI,OBSERV,IONDEV,RESIDU,AMBEP1,
     6                  L4SUM,C4,MODNR,OBSNUM)
C
          ENDIF
C
        ENDIF
C
C OUTPUT OF RESULTS
C -----------------
        CALL IONOUT(MODTYP,CAMPGN(1),NFIL,MTYPS,TIMRES,INUMOB,
     1              STNAMS,NFTOT,MOBS,NIONP,LOCQ,XX,RMS,RMSIP)
C
C SAVE RESULTS ON IONOSPHERE MODEL FILE
C -------------------------------------
        CALL IONSAV(MODTYP,TITLE,NIONP,RMS,LOCQ,XX,ANOR,IONDEV,
     1              IONREQ,NTERM,NM,IONCOE,IONSIG,HION)
C
C NEXT SESSION
C ------------
1000  CONTINUE
C
C END
C ---
      CLOSE(UNIT=LFNION)
      CLOSE(UNIT=LFNORB)
C
      CALL EXITRC(0)
      END
