package R2S_SUM;
@ISA = ("RUNBPE");
# ============================================================================
#
# Name    :  R2S_SUM
#
# Purpose :  Create RNX2SNX BPE processing summary file
#
# PARAMs  :
#
# Author  :  M. Meindl, S. Schaer
# Created :  20-Oct-2003
#
# Changes :  11-Aug-2011 RD: Updated for version 5.2
#
# ============================================================================
use strict;

use File::Basename;

use lib $ENV{BPE};
use bpe_util qw(timprt isHourly appFile);

sub run {
  my $bpe = shift;

# Get variables
# -------------
  my ($yyssss, $yyyy, $yy, $ssss,
      $b, $c, $e, $f,
      $repro,  $obsSel, $rnxDir, $satInf, $pcvInf, $pcv,
      $satcrx, $refDir, $refInf, $crdInf, $blqInf, $atlInf,
      $crxInf, $hoiFil, $sampl, $updAtx,
      $satsys, $HOURLY, $sav,    $result, $clu, $cluEdt,
      $gnssar, $bl_amb, $bl_qif, $bl_l53, $bl_l12,
      $dirOut, $dirDel, $dirSmc, $dirSum, $dirLst, $dirClb,
      $extOut, $extDel, $extSmc, $extSum, $extLst, $extClb,
      $dirCrd, $dirVel, $dirSta,          $dirBlq, $dirAtl,
      $extCrd, $extVel, $extSta, $extCrx, $extBlq, $extAtl) =
    $bpe->getKeys(
      '$YSS+0', '$Y+0', '$Y', '$S+0',
      'V_B', 'V_C', 'V_E', 'V_F',
      'V_REPRO',  'V_OBSSEL', 'V_RNXDIR', 'V_SATINF', 'V_PCVINF', 'V_PCV',
      'V_SATCRX', 'V_REFDIR', 'V_REFINF', 'V_CRDINF', 'V_BLQINF', 'V_ATLINF',
      'V_CRXINF', 'V_HOIFIL', 'V_SAMPL',  'V_MYATX',
      'V_SATSYS', 'V_HOURLY', 'V_SAV',    'V_RESULT', 'V_CLU', 'V_CLUEDT',
      'V_GNSSAR', 'V_BL_AMB', 'V_BL_QIF', 'V_BL_L53', 'V_BL_L12',
      'DIR_OUT', 'DIR_DEL', 'DIR_SMC', 'DIR_SUM', 'DIR_LST', 'DIR_CLB',
      'EXT_OUT', 'EXT_DEL', 'EXT_SMC', 'EXT_SUM', 'EXT_LST', 'EXT_CLB',
      'DIR_CRD', 'DIR_VEL', 'DIR_STA',            'DIR_BLQ', 'DIR_ATL',
      'EXT_CRD', 'EXT_VEL', 'EXT_STA', 'EXT_CRX', 'EXT_BLQ', 'EXT_ATL');

  $refDir = '${D}/' . $refDir . '/';

# Initialize processing summary file
# ----------------------------------
  my $prcFil = "${dirOut}R2S${yyssss}.PRC";
  unlink $prcFil;
  appFile("RNX2SNX BPE PROCESSING SUMMARY FOR YEAR-SESSION $yy-$ssss",
    1,"",$prcFil,0);

# Report the BPE settings
# -----------------------
  open(PRC,">> $prcFil");
  print PRC "Summary file generated at ",timprt()," by R2S_SUM\n\n";
  print PRC "General files:\n";
  print PRC "   Antenna phase center eccentricity file:   ${pcvInf}.${pcv}\n";
  print PRC "   Satellite information file:               ${satInf}.${pcv}\n";
  print PRC "   Satellite problem file:                   ${satcrx}.${extCrx}\n";
  print PRC "   Orbit, ERP and clock products used from:  \${D}/$b\n";
  print PRC "\nObservation file selection:\n";
  print PRC "   RINEX files copied from:                  \${D}/$rnxDir/\n";
  print PRC "   Station selection:                        ${obsSel}\n";
  print PRC "\nReference frame and station related files:\n";
  print PRC "   Station related files used from:          ${refDir}\n";
  print PRC "   External reference frame file series:     ",
            $refInf ne "" ? "${refInf}_R.(${extCrd}|${extVel})" : "" ,"\n";
  print PRC "   Project specific station file series:     ${crdInf}\n";
  print PRC "      Station information file:              ",
            $crdInf ne "" ? basename($dirSta)."/${crdInf}.${extSta}" : "", "\n";
  print PRC "      RINEX inconsistency file:              ",
            $crxInf ne "" ? basename($dirSta)."/${crxInf}.${extCrx}" : "", "\n";
  print PRC "      Ocean tidal loading table:             ",
            $blqInf ne "" ? basename($dirBlq)."/${blqInf}.${extBlq}" : "", "\n";
  print PRC "      Atmosphere tidal loading table:        ",
            $atlInf ne "" ? basename($dirAtl)."/${atlInf}.${extAtl}" : "", "\n";
  print PRC "\nOther options from PCF:\n";
  if ( ${updAtx} ne "" ) {
    print PRC "   Antenna phase center model updated with:  ",basename(${updAtx}),"\n";
  } else {
    print PRC "   Antenna phase center model was not updated.\n";
  }
  print PRC "   Satellite system(s) included:             ${satsys}\n";
  print PRC "   Sampling rate for final solution:         ${sampl} seconds\n";
  if ( ${hoiFil} ne "" ) {
    print PRC "   Higher order ionosphere based on:         ",basename(${hoiFil}),"\n";
  } else {
    print PRC "   No higher order ionosphere corrections applied\n";
  }
  if ( isHourly($ssss) ) {
    print PRC "   Session scheme:                           hourly (including ${HOURLY} hours)\n";
  } else {
    print PRC "   Session scheme:                           daily\n";
  }
  print PRC "   Maximum number of files per cluster:      ${clu}\n";
  print PRC "   Max. number of files per GPSEDT cluster:  ",
                ${cluEdt} ne "" ? ${cluEdt} : ${clu},"\n";

  print PRC "\nAmbiguity resolution options from PCF:\n";
  print PRC "   GNSS to be used for ambiguity resolution: ${gnssar}\n";
  print PRC "   Maximum baseline length for MW/L3 AR:     ${bl_amb} km\n";
  print PRC "   Maximum baseline length for QIF AR:       ${bl_qif} km\n";
  print PRC "   Maximum baseline length for L5/L3 AR:     ${bl_l53} km\n";
  print PRC "   Maximum baseline length for L1&L2 AR:     ${bl_l12} km\n";

  print PRC "\nStoring the results:\n";
  if ( uc $sav eq "Y" ) {
    my $dirSav = '${S}/' . $result . "/$yyyy/";
    print PRC "   Results files saved to:                   ${dirSav}\n";
    print PRC "      Preliminary (ambiguity-float) results: ${c}${yyssss}\n";
    print PRC "      Final (ambiguity-fixed) results:       ${e}${yyssss}\n";
    print PRC "      Size-reduced NEQ information:          ${f}${yyssss}\n";
  } else {
    print PRC "   The results have not been copied into the product database.\n";
  }

  print PRC"\n\n";
  close PRC;


# Append protocol files
# ---------------------
# - Error/warning messages concerning RINEX inconsistencies
  my $sumFil = "${dirOut}RNX${yyssss}.ERR";
  if (-s $sumFil) {
    appFile("PART 0: RINEX INCONSISTENCIES",2,$sumFil,$prcFil,1) }

# - RINEX pseudo-graphics
  $sumFil = "${dirSmc}GRA${yyssss}.${extSmc}";
  appFile("PART 1: RINEX PSEUDO-GRAPHICS",2,$sumFil,$prcFil,1);

  $sumFil = "${dirDel}GRA${yyssss}.${extDel}";
  if (-s $sumFil) {
    appFile("LIST OF REJECTED RINEX DATA FILES",2,$sumFil,$prcFil,1) }

# - Orbit generation summary
  $sumFil = "${dirLst}ORB${yyssss}.${extLst}";
  appFile("PART 2: ORBIT GENERATION SUMMARY",2,$sumFil,$prcFil,1);

# - Single-point-positioning summary
  $sumFil = "${dirOut}SPP${yyssss}.${extOut}";
  appFile("PART 3: SINGLE-POINT-POSITIONING SUMMARY",2,$sumFil,$prcFil,1);

  $sumFil = "${dirDel}SPP${yyssss}.${extDel}";
  if (-s $sumFil) {
    appFile("LIST OF REJECTED BINARY DATA FILES",2,$sumFil,$prcFil,1) }

# - Single-point-positioning summary
  $sumFil = "${dirOut}MPR${yyssss}.${extSum}";
  appFile("PART 4: PREPROCESS PHASE OBSERVATIONS",2,$sumFil,$prcFil,1);

# - Data preprocessing/screening summary
  $sumFil = "${dirOut}RES${yyssss}.PRC";
  appFile("PART 5: DATA PREPROCESSING/SCREENING SUMMARY",2,$sumFil,$prcFil,1);

# - QIF ambiguity resolution summary
  $sumFil = "${dirSum}AMB${yyssss}.${extSum}";
  appFile("PART 6: AMBIGUITY RESOLUTION SUMMARY",2,$sumFil,$prcFil,1);

# - Preliminary/final coordinate/troposphere solutions
  appFile("PART 7: GNSS COORDINATE/TROPOSPHERE SOLUTION STATISTICS",
    1,"",$prcFil,0);

  $sumFil = "${dirSum}${c}${yyssss}.${extSum}";
  appFile("Preliminary (ambiguity-float) results",1,$sumFil,$prcFil,1);

  appFile("Final (ambiguity-fixed) results",0,"",$prcFil,0);
  open(PRC,">> $prcFil");
  print PRC "Station have been processed in clusters:\n";
  foreach my $clbFil ( glob("${dirClb}${yyssss}??.${extClb}") ) {
    my $lines = 0;
    open(CLB,$clbFil);
    map { /$ssss/ and ++$lines } <CLB>;
    close CLB;
    print PRC "  Cluster: ",basename($clbFil)," with $lines baselines\n"
  }
  print PRC "\n";
  close PRC;
  $sumFil = "${dirSum}${e}${yyssss}.${extSum}";
  appFile("",1,$sumFil,$prcFil,1);

  $sumFil = "${dirSum}${f}${yyssss}.${extSum}";
  appFile("Size-reduced NEQ information",1,$sumFil,$prcFil,1);

  appFile("",2,"",$prcFil,0);

# - Verification of datum definition
  $sumFil = "${dirOut}HLM${yyssss}.${extOut}";
  appFile("PART 8: VERIFICATION OF DATUM DEFINITION",2,$sumFil,$prcFil,1);

  $sumFil = "${dirLst}HLM${yyssss}.${extLst}";
  if (-s $sumFil) {
    appFile("",2,$sumFil,$prcFil,1) }

# - Sliding 7-session comparison of station coordinates
  $sumFil = "${dirSum}CMP${yyssss}.${extSum}";
  if ($repro eq "" && -s $sumFil) {
    appFile("PART 9: SLIDING 7-SESSION COMPARISON OF STATION COORDINATES",
      2,$sumFil,$prcFil,1);
    $sumFil = "${dirOut}CMP${yyssss}.${extOut}";
    appFile("",2,$sumFil,$prcFil,1) }

}
