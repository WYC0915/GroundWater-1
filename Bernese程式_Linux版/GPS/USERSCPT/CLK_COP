package CLK_COP;
@ISA = ("RUNBPE");
# ============================================================================
#
# Name    :  CLK_COP
#
# Purpose :  Copy required files for CLKDET.PCF
#
# PARAMs  :
#
# Author  :  R. Dach
# Created :  11-Aug-2011
#
# Changes :  13-Feb-2013 SL: Path for crxInf corrected
#
# ============================================================================
use strict;

use File::Basename;
use File::Copy qw(copy move);

use lib $ENV{BPE};
use bpe_util qw(isHourly copyRef appFile);
use lib "$ENV{X}/EXE";
use Gps_Date qw(gps_date);

sub run{
  my $bpe = shift;

# Get variables
# -------------
  my ($wwwwd,$wwww,$yymm,$yyssss,$ssss,$yyyy,
      $refDir,   $crdInf,   $refInf,   $blqInf,   $atlInf,   $crxInf,
      $hoiFil,   $satcrx,   $gnss,
      $v_b,  $v_g, $hourly,
      $fixPth, $fixCrd, $fixTrp,
      $dirCrd,  $dirVel,  $dirAbb,  $dirSta,  $dirCrx,  $dirBlq,  $dirAtl,
      $extCrd,  $extVel,  $extAbb,  $extSta,  $extCrx,  $extBlq,  $extAtl,
      $dirClu,  $dirFix,  $dirSig,  $dirPld,  $dirPre,  $dirErp,  $dirIep,
      $extClu,  $extFix,  $extSig,  $extPld,  $extPre,  $extErp,  $extIep,
      $dirClk,  $dirDcb,  $dirIon,  $dirTrp,  $dirOut,  $pthGen,
      $extClk,  $extDcb,  $extIon,  $extTrp,  $extOut,  $extCrx) =
  $bpe->getKeys
     ('$WD+0','$W+0','$M+0','$YSS+0','$S+0','$Y+0',
      'V_REFDIR','V_CRDINF','V_REFINF','V_BLQINF','V_ATLINF','V_CRXINF',
      'V_HOIFIL','V_SATCRX','V_SATSYS',
      'V_B','V_G','V_HOURLY',
      'V_FIXPTH','V_FIXCRD','V_FIXTRP',
      'DIR_CRD','DIR_VEL','DIR_ABB','DIR_STA','DIR_STA','DIR_BLQ','DIR_ATL',
      'EXT_CRD','EXT_VEL','EXT_ABB','EXT_STA','EXT_CRX','EXT_BLQ','EXT_ATL',
      'DIR_CLU','DIR_FIX','DIR_SIG','DIR_PLD','DIR_PRE','DIR_ERP','DIR_IEP',
      'EXT_CLU','EXT_FIX','EXT_SIG','EXT_PLD','EXT_PRE','EXT_ERP','EXT_IEP',
      'DIR_RXC','DIR_DCB','DIR_ION','DIR_TRP','DIR_OUT','PTH_GEN',
      'EXT_RXC','EXT_DCB','EXT_ION','EXT_TRP','EXT_OUT','EXT_CRX');


# Generate a list of WWWWD and WWWW to cover all days/weeks for
# an hourly processing
# -------------------------------------------------------------
  my @lstWWWWD = ( "$wwwwd" );
  my @lstWWWW  = ( "$wwww" );

  if (isHourly($ssss)) {
    for (my $nn = -1; $nn >= -abs($hourly); $nn--) {
      my ($myWWWWD,$myWWWW) = gps_date("-yd $yyyy $ssss -h $nn","-o %W%w %W");
      push @lstWWWWD,"$myWWWWD" if ( join(" ",@lstWWWWD) !~ /$myWWWWD/ );
      push @lstWWWW ,"$myWWWW"  if ( join(" ",@lstWWWW)  !~ /$myWWWW/  );
    }
  }

# Construct list of reference files
# ---------------------------------
  my @filLst = ();

  # Station related input files:
  # Reference frame stations:
  push @filLst,"${dirCrd}${refInf}_R.${extCrd} 1";
  push @filLst,"${dirVel}${refInf}_R.${extVel} 1";
  push @filLst,"${dirCrd}${crdInf}.${extCrd} 1";
  push @filLst,"${dirVel}${crdInf}.${extVel} 1";
  push @filLst,"${dirAbb}${crdInf}.${extAbb} 1";
  push @filLst,"${dirSta}${crdInf}.${extSta} 1";
  push @filLst,"${dirCrx}${crxInf}.${extCrx} 1" if (${crxInf} ne "");
  push @filLst,"${dirBlq}${blqInf}.${extBlq} 1" if (${blqInf} ne "");
  push @filLst,"${dirAtl}${atlInf}.${extAtl} 1" if (${atlInf} ne "");

# Copy Reference Files
# --------------------
  $refDir = "$ENV{D}/" . $refDir;
  copyRef($refDir,@filLst);

# Construct list of external product files
# ----------------------------------------
  @filLst = ();
  foreach my $myWWWWD ( @lstWWWWD ) {
    push @filLst,"${dirPre}${v_b}${myWWWWD}.${extPre} 1";
    push @filLst,"${dirPre}IGL${myWWWWD}.${extPre} 1"   if ( $gnss =~ /GLO/ &&
                                                         $v_b eq "IGS" );
    push @filLst,"${dirClk}${v_b}${myWWWWD}.${extClk} 1";
    push @filLst,"${dirClk}${v_b}${myWWWWD}.${extClk}_30S 0";
  }
  foreach my $myWWWW ( @lstWWWW ) {
    push @filLst,"${dirIep}${v_b}${myWWWW}7.${extIep} 1";
  }

# Copy External Product Files
# ---------------------------
  $refDir = "$ENV{D}/" . ${v_b};
  copyRef($refDir,@filLst);

# Use the 30s file if available
  foreach my $file (@filLst) {
    next unless ( $file =~ /${extClk}_30S/ );  # It is not a 30s-clock file
    $file = (split(" ",$file))[0];             # Remove the mandatory flag
    if (-s "$file") {
      my $help = substr($file,0,-4);
      move("$file","$help");
    }
  }


# Construct list of BSW-formatted input files
# -------------------------------------------
  @filLst = ();
  push @filLst,"${dirDcb}P1C1${yymm}.${extDcb} 1";
  push @filLst,"${dirDcb}P1P2${yymm}.${extDcb} 0";
  my @ionLst = ();
  if ( $hoiFil ne "" ) {
    foreach my $myWWWWD ( @lstWWWWD ) {
      push @filLst,"${dirIon}COD${myWWWWD}.${extIon} 1";
      push @ionLst,"${dirIon}COD${myWWWWD}.${extIon}";
    }
  }

# Copy BSW-formatted input Files
# ------------------------------
  $refDir = "$ENV{D}/BSW52";
  copyRef($refDir,@filLst);

# Copy/concatenate the ionosphere files
# -------------------------------------
  if ( -e $ionLst[0] ) { # ionosphere model for HOI is available
    $hoiFil = "${dirIon}${hoiFil}.${extIon}";

    unlink "$hoiFil";
    map { appFile("",0,$_,$hoiFil,1); } reverse @ionLst;
    unlink @ionLst;
  }

# Copy the CRD/TRP files from an external solution to be introuced
# ----------------------------------------------------------------
  if ( $fixCrd ne "" ) {
    unless ( $fixPth ne "" ) {
      die("If you want to introduce CRD/TRP files from an external solution\n".
          "you must also specify the path where the shall taken from.\n".
          "(\"V_FIXCRD\" was specified but not \"V_FIXPTH\")");
    }

    # Copy coordinate file
    @filLst = ();
    push @filLst,"${dirCrd}${fixCrd}.${extCrd} 1";

    $refDir = "${fixPth}/".basename($dirCrd);
    copyRef($refDir,@filLst);

    # Copy troposphere file
    unless ( $fixTrp eq "" ) {
      @filLst = ();
      push @filLst,"${dirTrp}${fixTrp}.${extTrp} 1";

      $refDir = "${fixPth}/".basename($dirTrp);
      copyRef($refDir,@filLst);
    }
  }

# Make a copy of the satellite problem file
# -----------------------------------------
  print("${pthGen}/${satcrx}.${extCrx}","${pthGen}/CLK${yyssss}.${extCrx}");
  copy("${pthGen}/${satcrx}.${extCrx}","${pthGen}/CLK${yyssss}.${extCrx}");
}
