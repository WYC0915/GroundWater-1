      MODULE s_R2WTOR
      CONTAINS

C*
      SUBROUTINE R2WTOR(LFNOBS,LFNERR,MAXSAT,IRXVRS,
     1                  NUMTYP,ISNMIN,ISNTHR,ISNMAX,
     1                  EPOCH,IFLAG,NSATEP,SATEPO,
     2                  OBSEPO,SIGNAL,LLI,IRCODE)
CC
CC NAME       :  R2WTOR
CC
CC PURPOSE    :  WRITE OBSERVATION RECORDS OF A
CC               RINEX OBSERVATION FILE (VERSIONS 1 AND 2)
CC
CC PARAMETERS :
CC         IN :  LFNOBS : LOGICAL FILE NUMBER                  I*4
CC               LFNERR : LFN FOR ERROR MESSAGES               I*4
CC               MAXSAT : MAXIMUM NUMBER OF SATELLITES         I*4
CC                        -> CORRESPONDS TO ROW DECLARATIONS OF
CC                           ARRAYS OBSEPO,SIGNAL,LLI
CC               IRXVRS : RINEX VERSION NUMBER                 I*4
CC               NUMTYP : NUMBER OF DIFFERENT OBS.TYPES        I*4
CC               ISNMIN : MINIMUM S/N RATIO FOR ALL OBS.TYPES  I*4(*)
CC               ISNTHR : THRESHOLD FOR GOOD S/N RATIO         I*4(*)
CC               ISNMAX : MAXIMUM S/N RATIO FOR ALL OBS.TYPES  I*4(*)
CC               EPOCH  : OBSERVATION EPOCH (RECEIVER TIME)    R*8
CC               IFLAG  : FLAG FOR CURRENT RECORD              I*4
CC               NSATEP : NUMBER OF SATELLITES                 I*4
CC                        <0: EPOCH(2) CONTAINS DELTA-T
CC               SATEPO : LIST OF SATELLITE NUMBERS            I*4(*)
CC               OBSEPO : LIST OF OBSERVATIONS                 R*8(*,*)
CC                        OBSEPO(I,J), I: SATELLITE, J: OBS.TYPE
CC               SIGNAL : S/N RATIOS                           R*8(*,*)
CC                        SIGNAL(I,J), I: SATELLITE, J: OBS.TYPE
CC               LLI    : LOSS OF LOCK INDICATORS              I*4(*,*)
CC                        LLI   (I,J), I: SATELLITE, J: OBS.TYPE
CC         OUT : IRCODE : RETURN CODE                          I*4
CC                        0: OK
CC
CC REMARKS    :  ALLOWS FOR GLONASS AND TRANSIT SATELLITES:
CC                  SATEPO :   1 -  99 : GPS
CC                  SATEPO : 101 - 199 : GLONASS
CC                  SATEPO : 201 - 299 : TRANSIT
CC                  SATEPO : 301 - 399 : GEO EGNOS/WAAS/MSAS
CC                  SATEPO : 900 - 999 : LEO
CC
CC               NSATEP<0: EPOCH(2) CONTAINS DELTA-T
CC
CC AUTHOR     :  W. GURTNER
CC
CC CREATED    :  21-SEP-92
CC
CC CHANGES    :  29-AUG-93 : ??: SVN >= 100: GLONASS SATELLITEN
CC                                ERROR MESSAGE FOR EPOCH RECORD
CC               24-AUG-94 : ??: DEAL WITH MORE THAN 12 SATELLITES PER EPOCH
CC               17-OCT-95 : ??: OPTION TO STORE DELTA T
CC               10-APR-97 : ??: CONTINUATION LINE WITH FORMAT 32X,12(A1,I2.2)
CC                                BLANK IDENTIFIER DEFAULTS FOR PURE GPS FILES
CC                                ONLY
CC               11-JUL-97 : ??: REMOVE ALL TRAILING BLANKS IN OBS RECORDS
CC               23-FEB-98 : ??: SORT SATELLITES
CC               16-JUN-99 : TS: ROUND TO 1D-6 SECONDS (COSMETICS)
CC               27-MAR-02 : DS: LEO NUMBERS
CC               30-JAN-03 : WG: GEO ADDED. SKIP SATELLITES OF WRONG SYSTEM
CC               22-JAN-04 : HB: WRITE(*,*)-->WRITE(LFNERR,*)
CC               23-JUN-05 : MM: IMPLICIT NONE AND DECLARATIONS ADDED
CC               28-MAY-07 : AG: E INSTEAD OF T
CC               29-Feb-12 : RD: USE R2WTOR AS MODULE, REMOVE UNUSED LABEL
CC               26-APR-12 : LP: SATSYS 'C' ADDED
CC
CC COPYRIGHT  :  ASTRONOMICAL INSTITUTE
CC      1992     UNIVERSITY OF BERN
CC               SWITZERLAND
CC
C*
      USE l_basfun, ONLY: dmod
      USE s_jmt
      USE s_radgms
      USE f_lengt0
      USE s_cordup
      IMPLICIT NONE
C
C DECLARATIONS INSTEAD OF IMPLICIT
C --------------------------------
      INTEGER*4 I     , I1    , IDAY  , IFLAG , IHOUR , IRCODE, IREC  ,
     1          IRXVRS, ISN   , ITYP  , IYEAR , J     , K     , L     ,
     2          LFNERR, LFNOBS, MAXSAT, MINUTE, MONTH , NSAT  ,
     3          NSATEP, NUMREC, NUMTYP
C
      REAL*8    DAY   , OBS   , SEC
C
CCC       IMPLICIT REAL*8 (A-H,O-Z)
C
C GLOBAL DECLARATIONS
C -------------------
      INTEGER*4    SATEPO(*),LLI(MAXSAT,*)
      INTEGER*4    ISNMIN(*),ISNTHR(*),ISNMAX(*)
      REAL*8       SIGNAL(MAXSAT,*),OBSEPO(MAXSAT,*),EPOCH(*)
C
C  LOCAL DECLARATIONS
C  ------------------
      CHARACTER    STRING(5)*16,CHR*1,LINE*80,SATTYP(50)*1
      CHARACTER    SATLOC(50)*3
      INTEGER*4    INDEXS(50)
C
      DATA ITYP/3/
C
      IF(NSATEP.EQ.0) GOTO 900
      NSAT=IABS(NSATEP)
C
C RECORD 1        EPOCH/SAT
      NSAT=0
      DO 10 I=1,IABS(NSATEP)
C MARKER FOR SATELLITES TO SKIP (HIGH IN ALPHABET FOR SORT ROUTINE)
        SATLOC(I)='ZZZ'
        SATTYP(I)=' '
        IF(SATEPO(I)/100.EQ.0) THEN
          IF(IRXVRS/100.EQ.5) THEN
            SATTYP(I)='G'
          ELSEIF(IRXVRS/100.NE.0) THEN
            GOTO 10
          ENDIF
          WRITE(SATLOC(I),'(A,I2.2)') SATTYP(I),SATEPO(I)
        ELSEIF(SATEPO(I)/100.EQ.1) THEN
          IF(IRXVRS.EQ.1) GOTO 920
          IF(IRXVRS/100.NE.1.AND.IRXVRS/100.NE.5) GOTO 10
          SATTYP(I)='R'
          WRITE(SATLOC(I),'(A,I2.2)') SATTYP(I),SATEPO(I)-100
        ELSEIF(SATEPO(I)/100.EQ.2) THEN
          IF(IRXVRS.EQ.1) GOTO 920
          IF(IRXVRS/100.NE.2.AND.IRXVRS/100.NE.5) GOTO 10
          SATTYP(I)='E'
          WRITE(SATLOC(I),'(A,I2.2)') SATTYP(I),SATEPO(I)-200
        ELSEIF(SATEPO(I)/100.EQ.3) THEN
          IF(IRXVRS.EQ.1) GOTO 920
          IF(IRXVRS/100.NE.3.AND.IRXVRS/100.NE.5) GOTO 10
          SATTYP(I)='S'
          WRITE(SATLOC(I),'(A,I2.2)') SATTYP(I),SATEPO(I)-300
        ELSEIF(SATEPO(I)/100.EQ.4) THEN
          IF(IRXVRS.EQ.1) GOTO 920
          IF(IRXVRS/100.NE.4.AND.IRXVRS/100.NE.5) GOTO 10
          SATTYP(I)='C'
          WRITE(SATLOC(I),'(A,I2.2)') SATTYP(I),SATEPO(I)-400
C         ADD_GNSS_HERE   conflict between 'M' and 'J' in IRXVRS!
        ELSEIF(SATEPO(I)/100.EQ.9) THEN
          IF(IRXVRS.EQ.1) GOTO 920
          IF(IRXVRS/100.NE.9.AND.IRXVRS/100.NE.5) GOTO 10
          SATTYP(I)='L'
          WRITE(SATLOC(I),'(A,I2.2)') SATTYP(I),SATEPO(I)-900
        ELSE
          GOTO 950
        END IF
        NSAT=NSAT+1
10    CONTINUE
C  NSAT: ACTUAL NUMBER OF SATELLITES
      IF(NSAT.EQ.0) GOTO 900
C  SORT SATELLITES ALPHABETICALLY (SKIPPED SATELLITES "ZZZ" ARE LAST)
      CALL CORDUP(SATLOC,IABS(NSATEP),1,3,INDEXS)
      CALL JMT(EPOCH(1),IYEAR,MONTH,DAY)
      CALL RADGMS(ITYP,DAY,CHR,IHOUR,MINUTE,SEC)
      SEC=DNINT(SEC*1.D6)/1.D6
      IDAY=IDINT(DAY)
      IYEAR=MOD(IYEAR,100)
      WRITE(LINE,1,ERR=930) IYEAR,MONTH,IDAY,IHOUR,MINUTE,SEC,IFLAG,
     1              NSAT,(SATLOC(INDEXS(K)),K=1,MIN(NSAT,12))
1     FORMAT(I3.2,4I3,F11.7,2I3,12A3)
      IF(IRXVRS.NE.1.AND.NSATEP.LT.0) WRITE(LINE(69:80),2) EPOCH(2)
2     FORMAT(F12.9)
      WRITE(LFNOBS,3) LINE(1:LENGT0(LINE))
3     FORMAT(A)
      IF(NSAT.GT.12) THEN
        WRITE(LFNOBS,11) (SATLOC(INDEXS(K)),K=13,MIN(NSAT,24))
11      FORMAT(32X,12A3)
      END IF
      IF(NSAT.GT.24) THEN
        WRITE(LFNOBS,11) (SATLOC(INDEXS(K)),K=25,MIN(NSAT,36))
      END IF
      IF(NSAT.GT.36) THEN
        WRITE(LFNOBS,11) (SATLOC(INDEXS(K)),K=37,MIN(NSAT,48))
      END IF
C
C RECORD 2 FF     OBSERVATIONS
C
C LOOP OVER ALL SATELLITES
      DO 100 I1=1,NSAT
        I=INDEXS(I1)
C
C LOOP OVER NECESSARY NUMBER OF RECORDS
        NUMREC=INT(NUMTYP-1)/5+1
        K=0
        DO 120 IREC=1,NUMREC
C
C LOOP OVER OBSERVATION TYPES WITHIN ONE RECORD
          DO 110 L=1,5
            K=K+1
            IF(K.GT.NUMTYP) GOTO 130
            IF(OBSEPO(I,K).NE.0.D0) THEN
C KEEP OBS IN INTERVAL (-1.D9,+1.D9)
              OBS=DMOD(OBSEPO(I,K),1.D9)
C PROJECT S/N RATIO INTO INTERVAL (1,9)
C (5 = THRESHOLD)
              IF(SIGNAL(I,K).NE.0.D0.AND.
     1             ISNMIN(K).NE.ISNTHR(K).AND.
     2             ISNTHR(K).NE.ISNMAX(K)) THEN
                IF(SIGNAL(I,K).LE.ISNTHR(K)) THEN
                  ISN=IDNINT((SIGNAL(I,K)-ISNTHR(K))/
     1                       (ISNMIN( K )-ISNTHR(K))*(1-5)+5)
                ELSE
                  ISN=IDNINT((SIGNAL(I,K)-ISNTHR(K))/
     1                       (ISNMAX( K )-ISNTHR(K))*(9-5)+5)
                END IF
                IF(ISN.LT.1) ISN=1
                IF(ISN.GT.9) ISN=9
              ELSE
                ISN=0
              END IF
              WRITE(STRING(L),111) OBS,LLI(I,K),ISN
111           FORMAT(F14.3,2I1)
              IF(STRING(L)(15:15).EQ.'0') STRING(L)(15:15)=' '
              IF(STRING(L)(16:16).EQ.'0') STRING(L)(16:16)=' '
            ELSE
              STRING(L)=' '
            END IF
110       CONTINUE
C
C  ACTUAL NUMBER OF OBS.TYPES IN CURRENT RECORD
130       L=L-1
C  REMOVE TRAILING BLANKS
          LINE=' '
          DO 140 J=1,L
            LINE((J-1)*16+1:16*J)=STRING(J)
140       CONTINUE
C
C  WRITE RECORD
          WRITE(LFNOBS,3) LINE(1:LENGT0(LINE))
120     CONTINUE
100   CONTINUE
C
900   IRCODE=0
      GOTO 999
C
C  VERSION NUMBER TOO LOW FOR GLONASS
920   IRCODE=2
      WRITE(LFNERR,921) IRXVRS
921   FORMAT(' SR R2WTOR: VERSION NUMBER TOO LOW FOR NON-GPS:',I5)
      GOTO 999
C
C  WRITE ERROR
930   IRCODE=0
      WRITE(LFNERR,*)
      WRITE(LFNERR,*) ' SR R2WTOR: WRITE ERROR ON EPOCH RECORD.'
      WRITE(LFNERR,*) ' DATE (Y,M,D,H,M,S) :',
     1            IYEAR,MONTH,IDAY,IHOUR,MINUTE,SEC
      WRITE(LFNERR,*) '  #   OF SATELLITES :',NSAT
      WRITE(LFNERR,*) ' LIST OF SATELLITES : ',(SATLOC(K),' ',K=1,NSAT)
      WRITE(LFNERR,*)
      GOTO 999
C
C  WRONG FILETYPE
      IRCODE=2
      WRITE(LFNERR,941) IRXVRS/100
941   FORMAT(' SR R2WTOR: WRONG FILETYPE FOR OBSERVATION:',I5)
      GOTO 999
C
C  WRONG FILETYPE
950   IRCODE=2
      WRITE(LFNERR,951) SATEPO(I)
951   FORMAT(' SR R2WTOR: ILLEGAL SATELLITE NUMBER:',I5)
      GOTO 999
C
999   RETURN
      END SUBROUTINE

      END MODULE
