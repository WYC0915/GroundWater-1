      MODULE s_SDGCHN
      CONTAINS

C*
      SUBROUTINE SDGCHN(ISITE,NPAIR,LINDEF,NCHAIN,LCHAIN,IRCODE)
CC
CC NAME        : SDGCHN
CC
CC PURPOSE     : GET ALL SITES THAT ARE DIRECTLY OR INDIRECTLY CHAINED
CC               BY BASELINES WITH GIVEN SITE NUMBER
CC
CC PARAMETERS  :
CC          IN : ISITE : GIVEN SITE NUMBER                     I*4
CC               NPAIR  : NUMBER OF BASELINES                   I*4
CC               LINDEF : LIST OF BASELINES                     I*4(2,*)
CC                        LINDEF(1,I): SITE # 1 OF BASELINE I
CC                        LINDEF(2,I): SITE # 2 OF BASELINE I
CC                                     I=1,2,...,NSITE-1
CC         OUT : NCHAIN : NUMBER OF SITES CHAINED TOGETHER      I*4
CC               LCHAIN : LIST OF CHAINED SITES                 I*4(*)
CC               IRCODE : RETURN CODE                           I*4
CC                        1: TOO LARGE SITE NUMBER
CC
CC AUTHOR      : W. GURTNER
CC
CC CREATED     : 09-JAN-1992
CC
CC CHANGES     : 27-AUG-94 : MR: NO PROMP1 FOR NON-INTERACTIVE MODE
CC               24-OCT-02 : MM: MODIFIED FOR NEW MENU (NOINTR = 1)
CC               23-JUN-05 : MM: IMPLICIT NONE AND DECLARATIONS ADDED
CC               27-MAR-12 : RD: USE PROMP1 AS MODULE NOW
CC
CC COPYRIGHT  :  ASTRONOMICAL INSTITUTE
CC      1989     UNIVERSITY OF BERN
CC               SWITZERLAND
C*
      USE s_promp1
      IMPLICIT NONE
C
C DECLARATIONS INSTEAD OF IMPLICIT
C --------------------------------
      INTEGER*4 I     , ICHAIN, IFST  , IL1   , IL2   , ILST  , IPAIR ,
     1          IRCODE, ISITE , MAXSIT, NCHAIN, NOINTR, NPAIR
C
      PARAMETER (MAXSIT=500)
      INTEGER*4 LINDEF(2,*),LCHAIN(*),SITE(MAXSIT)
      CHARACTER*80 STRG(1)
      CHARACTER CHR1*1
C
C GET INTERACTION MODE
CC      CALL GTINTM(NOINTR)
      NOINTR = 1
C
      DO 10 I=1,MAXSIT
        SITE(I)=0
10    CONTINUE
      LCHAIN(1)=ISITE
      NCHAIN=1
      ILST=0
C
20    IFST=ILST+1
      ILST=NCHAIN
C
C  LOOK FOR ALL BASELINES WITH SITE 'LCHAIN(I)'.
C  ADD ALL ITS PARTNERS INTO 'LCHAIN'
C
      DO 30 ICHAIN=IFST,ILST
C
        DO 40 IPAIR=1,NPAIR
          IL1=LINDEF(1,IPAIR)
          IL2=LINDEF(2,IPAIR)
C
          IF(IL1.GT.MAXSIT.OR.IL2.GT.MAXSIT) GOTO 910
C
C  FIRST SITE OF BASELINE IPAIR
          IF(IL1.EQ.LCHAIN(ICHAIN)) THEN
            IF(SITE(IL2).EQ.0) THEN
              SITE(IL2)=1
              IF(IL2.NE.LCHAIN(1)) THEN
                NCHAIN=NCHAIN+1
                LCHAIN(NCHAIN)=IL2
              END IF
            END IF
          END IF
C  SECOND SITE OF BASELINE IAPIR
          IF(IL2.EQ.LCHAIN(ICHAIN)) THEN
            IF(SITE(IL1).EQ.0) THEN
              SITE(IL1)=1
              IF(IL1.NE.LCHAIN(1)) THEN
                NCHAIN=NCHAIN+1
                LCHAIN(NCHAIN)=IL1
              END IF
            END IF
          END IF
40      CONTINUE
30    CONTINUE
C
C HAVE NEW SITES BEEN ADDED IN THIS RUN?
      IF(NCHAIN.NE.ILST) GOTO 20
C
      IRCODE=0
      GOTO 999
C
910   WRITE(*,911) MAXSIT
911   FORMAT(/,' *** SR SDGCHN: TOO MANY STATIONS',
     1       /,16X,'INCREASE PARAMETER "MAXSIT" IN THIS ROUTINEN',
     2       /,16X,'MAX. NUMBER OF STATIONS ALLOWED:',I5,/)
      IF (NOINTR.NE.1) THEN
        STRG(1)='PRESS <ENTER> TO CONTINUE'
        CALL PROMP1(1,STRG)
        READ(*,'(A)') CHR1
      ENDIF
      IRCODE=1
C
999   RETURN
      END SUBROUTINE

      END MODULE
