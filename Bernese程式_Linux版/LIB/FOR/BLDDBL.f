      MODULE s_BLDDBL
      CONTAINS

C*
      SUBROUTINE BLDDBL(IEQ0  ,NEW   ,MAXEQN,MAXSNG,AAUX  ,INDAUX,
     1                  MELWUB,AOBS  ,INDA  ,BOBS  ,MXESNG)
CC
CC NAME       :  BLDDBL
CC
CC PURPOSE    :  FORM DOUBLE DIFFERENCE EQUATIONS FROM SINGLE DIFFE-
CC               RENCE EQUATIONS.
CC               SPECIALITY: ONLY NON-ZERO ELEMENTS IN AOBS, INDEX
CC               IN ORIGINAL EQUATIONS DEFINED BY INDA.
CC
CC PARAMETERS :
CC         IN :  IEQ0   : NUMBER OF FIRST EQUATION MINUS 1    I*4
CC               NEW    : NUMBER OF NEW SINGLE DIFFERENCE     I*4
CC                        EQUATIONS
CC               MAXEQN : MAXIMUM NUMBER OF EQUATIONS
CC               MAXSNG : MAXIMUM NUMBER OF ROWS IN ARRAY AOBS I*4
CC               AAUX   : AUXILIARY ARRAY (DIM MAXSNG)        R*8
CC               INDAUX : AUXILIARY INDEX ARRAY (DIM MAXSNG)  I*2
CC               MELWUB : MELBOURNE-WUEBBENA LC               I*4
CC                        =0: NO
CC                        =1: YES
CC                        =2: DTEC LC
CC     IN/OUT :  AOBS   : LINEARIZED OBSERVATION EQN. MATRIX  R*8
CC               INDA   : LINEARIZED ARRAY OF INDICES FOR     I*2
CC                        MATRIX AOBS
CC               BOBS   : ARRAY WITH TERMS "OBSERVED-COMPUTED"R*8
CC               MXESNG : MAXIMUM ACTUAL NUMBER OF NON-ZERO   I*4
CC                        ELEMENTS IN ARRAY AOBS
CC
CC REMARKS    :  ---
CC
CC AUTHOR     :  G.BEUTLER, M.ROTHACHER
CC
CC VERSION    :  3.4  (JAN 93)
CC
CC CREATED    :  87/10/22 07:52
CC
CC CHANGES    :  19-JUL-92 : ADD CHECK FOR "MAXSNG" ON THE DOUBLE
CC                           DIFF. LEVEL
CC               10-AUG-94 : MR: CALL EXITRC
CC               29-APR-98 : SS: DTEC LC
CC               23-JUN-04 : RD: CORRECT COMP. OF MXESNG
CC               21-JUN-05 : MM: COMLFNUM.inc REMOVED, m_bern ADDED
CC               23-JUN-05 : MM: IMPLICIT NONE AND DECLARATIONS ADDED
CC
CC COPYRIGHT  :  ASTRONOMICAL INSTITUTE
CC      1987     UNIVERSITY OF BERN
CC               SWITZERLAND
CC
C*
      USE m_bern
      USE s_exitrc
      IMPLICIT NONE
C
C DECLARATIONS INSTEAD OF IMPLICIT
C --------------------------------
      INTEGER*4 IDBL  , IEQ0  , IND   , IND1  , IPAR  , IPAR1 , KL    ,
     1          KL1   , KROW  , LINE  , LINE1 , MAXEQN, MAXSNG, MELWUB,
     2          MXESNG, NEW
C
      REAL*8    XSGN
C
CCC       IMPLICIT REAL*8 (A-H,O-Z)
CCC       IMPLICIT INTEGER*4 (I-N)
C
      INTEGER*4 INDA(*),INDAUX(*)
      REAL*8    AOBS(*),BOBS(*),AAUX(*)
C
C
C ADD PARTIAL DERIVATIVES WHEN ANALYZIN DTEC LC
      IF (MELWUB.EQ.2) THEN
        XSGN= 1.D0
      ELSE
        XSGN=-1.D0
      ENDIF
C
C LOOP OVER NEW DOUBLE DIFFERENCES
      DO 100 IDBL=1,NEW-1
C
C INITIALIZE INDEX ARRAYS OF CURRENT DOUBLE DIFFERENCE
        DO 10 KROW=1,MAXSNG
          INDAUX(KROW)=0
          AAUX(KROW)=0
10      CONTINUE
C
C INDEX OF CURRENT LINE, LINE+1
        LINE=IEQ0+IDBL
        LINE1=LINE+1
C
C TERM "OBSERVED-COMPUTED"
        BOBS(LINE)=BOBS(LINE)-BOBS(LINE1)
C
C SQUARE O-C TERM, IF DTEC LC ANALYZED
        IF (MELWUB.EQ.2) BOBS(LINE)=BOBS(LINE)**2
C
        KL=1
        KL1=1
C
C CURRENT LINE OF MATRIX AOBS, ARRAY INDAUX
        DO 20 KROW=1,2*MAXSNG
          IND=LINE+(KL-1)*MAXEQN
          IND1=LINE1+(KL1-1)*MAXEQN
          IPAR=INDA(IND)
          IPAR1=INDA(IND1)
          IF(IPAR.EQ.0.AND.IPAR1.EQ.0) THEN
            IF(KROW.GT.MXESNG) MXESNG=KROW
            GOTO 30
          ENDIF
C
          IF(KROW.GT.MAXSNG) THEN
            WRITE(LFNERR,901) MAXSNG
901         FORMAT(/,' *** SR BLDDBL: TOO MANY NON-ZERO PARAMETER FOR',
     1             /,16X,'ONE DOUBLE DIFF. OBSERVATION.',/,
     2               16X,'INCREASE MAX. DIMENSION "MAXSNG"',/,
     3               16X,'MAX. NUMBER OF NON-ZERO PARAM.:',I4,/)
            CALL EXITRC(2)
          ENDIF
C
          IF(IPAR.EQ.0)IPAR=1000000
          IF(IPAR1.EQ.0)IPAR1=1000000
          IF(IPAR.LT.IPAR1)THEN
            INDAUX(KROW)=IPAR
            AAUX(KROW)=AOBS(IND)
            KL=KL+1
          ELSE IF(IPAR.GT.IPAR1)THEN
            INDAUX(KROW)=IPAR1
            AAUX(KROW)=XSGN*AOBS(IND1)
            KL1=KL1+1
          ELSE
            INDAUX(KROW)=IPAR
            AAUX(KROW)=AOBS(IND)+XSGN*AOBS(IND1)
            KL1=KL1+1
            KL=KL+1
          END IF
20      CONTINUE
C
C COPY CURRENT LINE INTO RESULT MATRICES
30      CONTINUE
        DO 40 KROW=1,MAXSNG
          IND=LINE+(KROW-1)*MAXEQN
          AOBS(IND)=AAUX(KROW)
          INDA(IND)=INDAUX(KROW)
          IF(IDBL.EQ.NEW-1)THEN
            INDA(IND+1)=0
            AOBS(IND+1)=0.D0
          END IF
40      CONTINUE
100   CONTINUE
      RETURN
      END SUBROUTINE

      END MODULE
