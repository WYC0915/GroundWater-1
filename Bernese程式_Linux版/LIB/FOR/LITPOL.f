      MODULE s_LITPOL
      CONTAINS

C*
      SUBROUTINE LITPOL(NEP,NFCT,TTAB,TAB,T,TOL,FCT)
CC
CC NAME       :  LITPOL
CC
CC PURPOSE    :  LINEARLY INTERPOLATE IN A TABLE WITH NFCT FUNCTIONS
CC               AND NEP TABULAR EPOCHS.
CC
CC PARAMETERS :
CC         IN :  NEP    : NUMBER OF EPOCHS                    I*4
CC               NFCT   : NUMBER OF FUNCTIONS TO BE INTER-    I*4
CC                        POLATED
CC               TTAB(I),I=1,2,...,NEP: TABULAR EPOCHS        R*8
CC               TAB(I),I=1,2,...,NEP*NFCT: TABULAR VALUES    R*8
CC                        THE FIRST NFCT VALUES REFER TO EPOCH TTAB(1),
CC                        THE SECOND NFCT VALUES TO TTAB(2) AND SO ON.
CC               T      : INTERPOLATION TIME                  R*8
CC        OUT :  FCT(I),I=1,NFCT: INTERPOL. FUNCTION VALUES   R*8
CC
CC REMARKS    :  THE TABULAR INTERVAL MAY BE POSITIVE OR NEGATIVE
CC
CC AUTHOR     :  G.BEUTLER
CC
CC VERSION    :  3.4  (JAN 93)
CC
CC CREATED    :  87/11/02 15:27
CC
CC CHANGES    :  10-AUG-94 : MR: CALL EXITRC
CC               21-JUN-05 : MM: COMLFNUM.inc REMOVED, m_bern ADDED
CC               23-JUN-05 : MM: IMPLICIT NONE AND DECLARATIONS ADDED
CC               29-JAN-07 : AG: EXTRAPOLATION TOLERANCE ADDED
CC
CC COPYRIGHT  :  ASTRONOMICAL INSTITUTE
CC      1987     UNIVERSITY OF BERN
CC               SWITZERLAND
CC
C*
      USE m_bern
      USE s_exitrc
      IMPLICIT NONE
C
C DECLARATIONS INSTEAD OF IMPLICIT
C --------------------------------
      INTEGER*4 I0  , I1  , IFCT, INT , NEP , NFCT
C
      REAL*8    T   , TREL, TOL
C
CCC       IMPLICIT REAL*8 (A-H,O-Z)
      REAL*8 TTAB(*),TAB(*),FCT(*)
C
      IF(TTAB(1).GT.TTAB(NEP)) GO TO 100
      IF(T.GE.TTAB(1)-TOL.AND.T.LE.TTAB(NEP)+TOL) GO TO 20
      WRITE(LFNERR,10) T,TTAB(1),TTAB(NEP),NEP
10    FORMAT(/,' *** SR LITPOL: ARGUMENT OUT OF RANGE',/,
     1                     16X,'ARGUMENT    :',F9.2,/,
     2                     16X,'LOWER RANGE :',F9.2,/,
     3                     16X,'UPPER RANGE :',F9.2,/,
     4                     16X,'EPOCH NUMBER:',I4,/)
      CALL EXITRC(2)
20    CONTINUE
      DO 30 INT=2,NEP
        IF(T.LE.TTAB(INT)+TOL)GO TO 200
30    CONTINUE
100   IF(T.LE.TTAB(1)+TOL.AND.T.GE.TTAB(NEP)-TOL)GO TO 110
      WRITE(LFNERR,10)T,TTAB(1),TTAB(NEP),NEP
      CALL EXITRC(2)
110   CONTINUE
      DO 120 INT=2,NEP
        IF(T.GE.TTAB(INT)-TOL)GO TO 200
120   CONTINUE
200   TREL=(T-TTAB(INT-1))/(TTAB(INT)-TTAB(INT-1))
      DO 210 IFCT=1,NFCT
        I0=(INT-2)*NFCT
        I1=I0+NFCT
        FCT(IFCT)=TAB(I0+IFCT)+TREL*(TAB(I1+IFCT)-TAB(I0+IFCT))
210   CONTINUE
      RETURN
      END SUBROUTINE

      END MODULE
