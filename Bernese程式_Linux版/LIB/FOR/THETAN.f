      MODULE f_thetan
      CONTAINS

C*
      FUNCTION THETAN(T)
CC
CC NAME       :  THETAN
CC
CC PURPOSE    :  COMPUTATION OF MEAN SIDERAL TIME GREENWICH
CC               AT EPOCH T (MODIFIED JULIAN DATE)
CC               SEE "USNO CIRCULAR NO. 163", 1981
CC               (IAU RESOLUTIONS)
CC
CC PARAMETERS :
CC         IN :  T      : EPOCH IN MODIFIED JULIAN DATE       R*8
CC        OUT :  THETAN : MEAN SIDERAL GREENWICH TIME AT      R*8
CC                        EPOCH T
CC
CC REMARKS    :  ---
CC
CC AUTHOR     :  G.BEUTLER
CC
CC VERSION    :  3.4  (JAN 93)
CC
CC CREATED    :  87/10/29 10:03
CC
CC CHANGES    :  14-MAY-92 : ??: TWO STEPS: CALCULATE FIRST GMST AT 0 UT
CC               23-JUN-05 : MM: IMPLICIT NONE AND DECLARATIONS ADDED
CC               28-FEB-07 : AG: USE PI FROM DEFCON
CC               04-MAY-12 : RD: USE DMOD FROM MODULE
CC
CC COPYRIGHT  :  ASTRONOMICAL INSTITUTE
CC      1987     UNIVERSITY OF BERN
CC               SWITZERLAND
CC
C*
      USE d_const,  ONLY: pi
      USE l_basfun, ONLY: dmod
      IMPLICIT NONE
C
C DECLARATIONS INSTEAD OF IMPLICIT
C --------------------------------
      REAL*8    T     , THETA0, THETAN, TU
C
CCC       IMPLICIT REAL*8 (A-H,O-Z)
C      PI= 3.141592653589793D0
C
C  CENTURIES ELAPSED SINCE 2000,1,1.5
      TU=(T-51544.5D0)/36525.D0
C
C  GMST IN TIME-SECONDS AT 0 UT
      THETAN=24110.54841D0+8640184.812866D0*TU
     1       +0.093104D0*TU*TU-6.2D-6*TU*TU*TU
C
C  CORRECTION FOR OTHER TIME THAN 0 UT MODULO ROTATIONS
      THETA0=43200.D0+3155760000.D0*TU
      THETAN=THETAN+DMOD(THETA0,86400.D0)
C
C  GMST IN RADIANS
      THETAN=DMOD(THETAN/43200.D0*PI,2.D0*PI)
      IF (THETAN.LT.0.D0) THETAN=THETAN+2.D0*PI
C
      RETURN
      END FUNCTION

      END MODULE
