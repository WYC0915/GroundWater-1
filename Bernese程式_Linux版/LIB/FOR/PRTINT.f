      MODULE s_PRTINT
      CONTAINS

C*
      SUBROUTINE PRTINT(DTH,TB1,TB2,DTABD,NINTV,TB12,TOSC)
CC
CC NAME       :  PRTINT
CC
CC PURPOSE    :  SUBDIVISION OF INTEGRATION INTERVAL (TB1,TB2) INTO
CC               SUBINTERVALS OF APPROXIMATELY DTH HOURS. THE SUB-
CC               DIVISION BOUNDARIES ARE ELEMENTS OF
CC               (TB1 + K*DTABD, K=0,1,2,3,......)
CC
CC PARAMETERS :
CC         IN :  DTH    : TABULAR INTERVAL IN HOURS           R*8
CC               TB1, TB2: INTERVAL BOUNDARIES                R*8
CC               DTABD  : TIME INTERVAL
CC        OUT :  NINTV  : NUMBER OF INTERVALS                 I*4
CC               TB12(K),K=1,2,..,NINTV+1: PARTIAL INTERVAL   R*8
CC                        BOUNDARIES
CC               TOSC  : DEFINITION OF AN OSCULATION EPOCH    R*8
CC                       (IGNORED IN VERSION 3.0 OF SOFTWARE)
CC
CC REMARKS    :  ---
CC
CC AUTHOR     :  G.BEUTLER, M.ROTHACHER
CC
CC VERSION    :  3.4  (JAN 93)
CC
CC CREATED    :  87/11/30 08:11
CC
CC CHANGES    :  18-FEB-93 : NINT INTO NINTV RENAMED, INT INTO INTV
CC               10-AUG-94 : MR: CALL EXITRC
CC               13-MAY-98 : MR: REPLACE "DFLOAT" BY "DBLE"
CC               21-JUN-05 : MM: COMLFNUM.inc REMOVED, m_bern ADDED
CC               23-JUN-05 : MM: IMPLICIT NONE AND DECLARATIONS ADDED
CC               15-JUL-10 : DT: COMPUTE INTERVALS IN UNITS OF DTABD
CC               07-SEP-11 : RD: FIX THE LAST BOUNDARY TO THE END OF INTERVAL
CC
CC COPYRIGHT  :  ASTRONOMICAL INSTITUTE
CC      1987     UNIVERSITY OF BERN
CC                SWITZERLAND
CC
C*
      USE m_bern
      USE s_exitrc
      IMPLICIT NONE
C
C DECLARATIONS INSTEAD OF IMPLICIT
C --------------------------------
      INTEGER*4 INTV  , K0    , MXCINT, NINTV
C
      REAL*8    DTABD , DTAPPR, DTH   , TB1   , TB2   , TOSC
C
CCC       IMPLICIT REAL*8 (A-H,O-Z)
      REAL*8 TB12(*)
      CHARACTER*6 MXNINT
      COMMON/MCMINT/MXCINT,MXNINT
C
C OLD:
CCC      NINTV=DABS(TB2-TB1)/(DTH/24)+.5D0
C NEW:
      NINTV=NINT(DABS(TB2-TB1+1d0/86400d0)/DTABD)/NINT(DTH/24d0/DTABD)
C
      K0=1
      IF (TB1.GT.TB2) K0=-1
      IF (NINTV.EQ.0) NINTV=1
C
C CHECK MAXIMUM NUMBER OF PARTIAL INTERVALS
      IF (NINTV.GT.MXCINT) THEN
        WRITE(LFNERR,5) NINTV,MXCINT
5       FORMAT(/,' *** SR PRTINT: TOO MANY INTEGRATION INTERVALS',/,
     1                       16X,'NUMBER OF INTERVALS     :',I8,/,
     2                       16X,'MAX. NUMBER OF INTERVALS:',I8,/)
        CALL EXITRC(2)
      END IF
C
C PARTITION OF (TB1,TB2)
      TB12(1)=TB1
      DO 20 INTV=1,NINTV-1
C OLD:
CCC        DTAPPR=DABS(DBLE(INTV)*(TB2-TB1)/DBLE(NINTV))
CCC        TB12(INTV+1)=TB1+K0*DNINT(DTAPPR/DTABD)*DTABD
C NEW:
        DTAPPR = DABS(DBLE(INTV) * DNINT((TB2-TB1)/DTABD/DBLE(NINTV)))
        TB12(INTV+1) = TB1 + K0*DTAPPR*DTABD
20    CONTINUE
C
C FIX THE LAST BOUNDARY TO THE END OF INTERVAL,
C BUT REMOVE THE LAST INTERVAL IF IT IS VERY SHORT
C ------------------------------------------------
      TB12(NINTV+1)=TB2
      IF (DABS(TB12(NINTV+1)-TB12(NINTV)).LT.DTH/24d0/2d0) THEN
        TB12(NINTV)=TB2
        NINTV=NINTV-1
      ENDIF
C
      TOSC=TB12(2)
      IF(NINTV.EQ.1)TOSC=(TB12(1)+TB12(2))/2.D0
      RETURN
      END SUBROUTINE

      END MODULE
