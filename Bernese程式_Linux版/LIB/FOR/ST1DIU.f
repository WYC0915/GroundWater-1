      MODULE s_ST1DIU
      CONTAINS

C*
      SUBROUTINE ST1DIU(XSTA,XSUN,XMON,F2SUN,F2MON,XCOSTA)
CC
CC  PURPOSE   :  THIS SUBROUTINE GIVES THE OUT-OF-PHASE CORRECTIONS INDUCED BY
CC               MANTLE INELASTICITY IN THE DIURNAL BAND
CC
CC PARAMETERS :
CC         IN :  XSTA,XSUN,XMON,F2SUN,F2MON
CC     IN/OUT :  XCOSTA
CC
CC REMARKS    :  ---
CC
CC AUTHOR     :  V. DEHANT, S. MATHEWS AND J. GIPSON
CC
CC VERSION    :  4.1
CC
CC CREATED    :  06-OCT-97
CC
CC CHANGES    :  06-OCT-97 : TS: ADDEPTED FOR THE BERNESE SOFTWARE
CC               23-JUN-05 : MM: IMPLICIT NONE AND DECLARATIONS ADDED
CC
CC COPYRIGHT  :  ASTRONOMICAL INSTITUTE
CC      1997     UNIVERSITY OF BERN
CC               SWITZERLAND
CC
C*
      IMPLICIT NONE
C
C DECLARATIONS INSTEAD OF IMPLICIT
C --------------------------------
      REAL*8    COS2PHI, COSLA  , COSPHI , DE     , DEMON  , DESUN  ,
     1          DHI    , DLI    , DN     , DNMON  , DNSUN  , DR     ,
     2          DRMON  , DRSUN  , F2MON  , F2SUN  , RMON   , RSTA   ,
     3          RSUN   , SINLA  , SINPHI
C
CCC       IMPLICIT REAL*8 (A-H,O-Z)
C
      REAL*8 XSTA(*),XSUN(*),XMON(*),XCOSTA(*)
C
      DATA DHI/-0.0025D0/,DLI/-0.0007D0/
C
      RSTA=DSQRT(XSTA(1)**2+XSTA(2)**2+XSTA(3)**2)
      SINPHI=XSTA(3)/RSTA
      COSPHI=DSQRT(XSTA(1)**2+XSTA(2)**2)/RSTA
      COS2PHI=COSPHI**2-SINPHI**2
      SINLA=XSTA(2)/COSPHI/RSTA
      COSLA=XSTA(1)/COSPHI/RSTA
      RMON=XMON(4)
      RSUN=XSUN(4)
      DRSUN=-3.D0*DHI*SINPHI*COSPHI*F2SUN*XSUN(3)*(XSUN(1)*
     1            SINLA-XSUN(2)*COSLA)/RSUN**2
      DRMON=-3.D0*DHI*SINPHI*COSPHI*F2MON*XMON(3)*(XMON(1)*
     1            SINLA-XMON(2)*COSLA)/RMON**2
      DNSUN=-3.D0*DLI*COS2PHI*F2SUN*XSUN(3)*(XSUN(1)*SINLA-
     1            XSUN(2)*COSLA)/RSUN**2
      DNMON=-3.D0*DLI*COS2PHI*F2MON*XMON(3)*(XMON(1)*SINLA-
     1            XMON(2)*COSLA)/RMON**2
      DESUN=-3.D0*DLI*SINPHI*F2SUN*XSUN(3)*
     1           (XSUN(1)*COSLA+XSUN(2)*SINLA)/RSUN**2
      DEMON=-3.D0*DLI*SINPHI*F2MON*XMON(3)*
     1           (XMON(1)*COSLA+XMON(2)*SINLA)/RMON**2
      DR=DRSUN+DRMON
      DN=DNSUN+DNMON
      DE=DESUN+DEMON
      XCOSTA(1)=DR*COSLA*COSPHI-DE*SINLA-DN*SINPHI*COSLA
      XCOSTA(2)=DR*SINLA*COSPHI+DE*COSLA-DN*SINPHI*SINLA
      XCOSTA(3)=DR*SINPHI+DN*COSPHI
C
      RETURN
      END SUBROUTINE

      END MODULE
