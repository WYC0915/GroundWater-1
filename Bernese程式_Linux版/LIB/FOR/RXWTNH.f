      MODULE s_RXWTNH
      CONTAINS

C*
      SUBROUTINE RXWTNH(LFNNAV,LFNERR,PRGNAM,RUNBY,CRDATE,CRTIME,
     1                  NCOM,COMENT,IRCODE)
CC
CC NAME       :  RXWTNH
CC
CC PURPOSE    :  WRITE THE ENTIRE HEADER INFORMATION OF A
CC               RINEX NAVIGATION FILE
CC
CC PARAMETERS :
CC         IN :  LFNNAV : LOGICAL FILE NUMBER                  I*4
CC               LFNERR : LFN FOR ERROR MESSAGES               I*4
CC               PRGNAM : PROGRAM NAME                        CH*20
CC               RUNBY  : NAME OF AGENCY CREATING RINEX FILE  CH*20
CC               CRDATE : CREATION DATE                       CH*9
CC               CRTIME : CREATION TIME                       CH*5
CC               NCOM   : NUMBER OF COMMENT LINES              I*4
CC               COMENT : COMMENT LINES                       CH*60(*)
CC               IRCODE : RETURN CODE (0=OK)                   I*4
CC
CC REMARKS    :  ---
CC
CC AUTHOR     :  W. GURTNER
CC
CC VERSION    :  3.4  (JAN 93)
CC
CC CREATED    :  89/04/05 11:45
CC
CC CHANGES    :  23-JUN-05 : MM: IMPLICIT NONE AND DECLARATIONS ADDED
CC
CC COPYRIGHT  :  ASTRONOMICAL INSTITUTE
CC      1989     UNIVERSITY OF BERN
CC               SWITZERLAND
CC
C*
      IMPLICIT NONE
C
C DECLARATIONS INSTEAD OF IMPLICIT
C --------------------------------
      INTEGER*4 I     , IRCODE, IRVERS, LFNERR, LFNNAV, NCOM
C
CCC       IMPLICIT REAL*8 (A-H,O-Z)
C
C GLOBAL DECLARATIONS
C -------------------
      CHARACTER*20 PRGNAM,RUNBY
      CHARACTER    COMENT(*)*60,CRDATE*9,CRTIME*5
C
C  LOCAL DECLARATIONS
C  ------------------
      CHARACTER    HEADRC(3)*20,RXTYPE*20
C
      DATA HEADRC/'RINEX VERSION / TYPE',
     2            'PGM / RUN BY / DATE ',
     3            'COMMENT             '/
C
C RINEX FILE TYPE
      DATA RXTYPE/'NAVIGATION DATA     '/
C
C RINEX FORMAT VERSION
      DATA IRVERS/1/
C
C RECORD 1        RINEX VERSION / TYPE
      WRITE(LFNNAV,1) IRVERS,RXTYPE,HEADRC(1)
1     FORMAT(I6,14X,A20,20X,A20)
C
C RECORD 2        PGM / RUN BY / DATE
      WRITE(LFNNAV,2) PRGNAM,RUNBY,CRDATE,CRTIME,HEADRC(2)
2     FORMAT(A20,A20,A9,1X,A5,5X,A20)
C
C RECORD 3        COMMENT
      DO 300 I=1,NCOM
        WRITE(LFNNAV,3) COMENT(I),HEADRC(3)
3       FORMAT(A60,A20)
300   CONTINUE
C
C  WRITE BLANK LINE
      WRITE(LFNNAV,*)
C
900   IRCODE=0
      RETURN
      END SUBROUTINE

      END MODULE
