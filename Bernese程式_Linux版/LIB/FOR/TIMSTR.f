      MODULE s_TIMSTR
      CONTAINS

C*
      SUBROUTINE TIMSTR(NTIM,TMJD,TSTRNG)
CC
CC NAME       :  TIMSTR
CC
CC PURPOSE    :  WRITE ONE OR TWO DATES WITH TIME ON A STRING WITH
CC               THE FOLLOWING FORMAT:
CC                     90-12-24 20:13:34    OR
CC                     90-12-24 20:13:34  90-12-25 21:13:13
cc
CC               *** PLEASE USE SR TIMST2 PROVIDING 4-DIGIT YEAR ***
CC
CC PARAMETERS :
CC         IN :  NTIM   : NUMBER OF DATES (1 OR 2)            I*4
CC               TMJD(I),I=1,2: MODIFIED JULIAN DATE(S)       R*8
CC        OUT :  TSTRNG : STRING CONTAINING THE DATE(S)       CH
CC
CC REMARKS    :  ---
CC
CC AUTHOR     :  M.ROTHACHER
CC
CC VERSION    :  3.4  (JAN 93)
CC
CC CREATED    :  90/06/29 17:42
CC
CC CHANGES    :  11-JUN-92 : ??: EPOCHS TMJD(I) WERE CHANGED BY 1/10 SEC !
CC               04-MAY-98 : SS: RECOMMEND SR TIMST2
CC               20-MAR-03 : SS: 2-DIGIT HOUR
CC               21-JUN-05 : MM: COMLFNUM.inc REMOVED
CC               23-JUN-05 : MM: IMPLICIT NONE AND DECLARATIONS ADDED
CC               03-JUL-09 : SL: DECLARATION OF TMJD: (2)->(*)
CC               26-MAR-12 : RD: USE TIMSTR AS MODULE NOW
CC
CC COPYRIGHT  :  ASTRONOMICAL INSTITUTE
CC      1990     UNIVERSITY OF BERN
CC               SWITZERLAND
CC
C*
      USE s_jmt
      USE s_radgms
      IMPLICIT NONE
C
C DECLARATIONS INSTEAD OF IMPLICIT
C --------------------------------
      INTEGER*4 ICH1  , ICH2  , IDAY  , IHOUR , IMIN  , IMONTH, ISEC  ,
     1          ITIM  , IYEAR , NTIM
C
      REAL*8    DAY   , SEC   , TMJDI
C
CCC       IMPLICIT REAL*8 (A-H,O-Z)
C
      CHARACTER TSTRNG*(*),VORZ*1
      REAL*8    TMJD(*)
C
C
C LOOP OVER DATES
C ---------------
      TSTRNG=' '
      DO 100 ITIM=1,NTIM
C
C CONVERT MODIFIED JULIAN DATE TO DATE AND TIME
        TMJDI=TMJD(ITIM)+0.1/86400.D0
        CALL JMT(TMJDI,IYEAR,IMONTH,DAY)
        IDAY=IDINT(DAY)
        CALL RADGMS(3,DAY,VORZ,IHOUR,IMIN,SEC)
        ISEC=IDNINT(SEC)
C
C ADJUST YEAR
        IYEAR=MOD(IYEAR,100)
C
C WRITE ON STRING
        ICH1=(ITIM-1)*19+1
        ICH2=(ITIM-1)*19+17
        WRITE(TSTRNG(ICH1:ICH2),1) IYEAR,IMONTH,IDAY,
     1                             IHOUR,IMIN,ISEC
1       FORMAT(I2.2,2('-',I2.2),I3.2,2(':',I2.2))
C
100   CONTINUE
C
C END
C ---
      RETURN
      END SUBROUTINE

      END MODULE
