      MODULE s_GETBIM
      CONTAINS

C*
      SUBROUTINE GETBIM(BIMFIL,BIMCOE,IRCODE)
CC
CC NAME       :  GETBIM
CC
CC PURPOSE    :  GET BROADCAST IONOSPHERE MODEL (BIM) COEFFICIENTS
CC               FROM RINEX NAVIGATION DATA FILE
CC
CC PARAMETERS :
CC         IN :  BIMFIL : EXTERNAL RINEX NAV DATA FILE NAME   CH*32
CC        OUT :  BIMCOE(I=1,2,J=1,..,4): BIM COEFFICIENTS     R*8(2,4)
CC                        I=1: ION ALPHAS
CC                        I=2: ION BETAS
CC               IRCODE : RETURN CODE                         I*4
CC                        =0: ERROR OCCURRED
CC                        =1: OK
CC
CC REMARKS    :  ---
CC
CC AUTHOR     :  S.SCHAER
CC
CC VERSION    :  4.3
CC
CC CREATED    :  21-JUN-00
CC
CC CHANGES    :  21-JUN-05 : MM: COMLFNUM.inc REMOVED, m_bern ADDED
CC               23-JUN-05 : MM: IMPLICIT NONE AND DECLARATIONS ADDED
CC
CC COPYRIGHT  :  ASTRONOMICAL INSTITUTE
CC      2000     UNIVERSITY OF BERN
CC               SWITZERLAND
CC
C*
      USE m_bern
      USE s_opnfil
      USE s_opnerr
      USE s_findln
      IMPLICIT NONE
C
C DECLARATIONS INSTEAD OF IMPLICIT
C --------------------------------
      INTEGER*4 I     , IERR  , IOSTAT, IRCODE, J
C
CCC       IMPLICIT REAL*8 (A-H,O-Z)
C
      CHARACTER*80  LINE,LOOK
      CHARACTER*32  BIMFIL
C
      REAL*8        BIMCOE(2,4)
C
C
      DO I=1,2
        DO J=1,4
          BIMCOE(I,J)=0.D0
        ENDDO
      ENDDO
C
C OPEN NAV DATA INPUT FILE
C ------------------------
      CALL OPNFIL(LFNLOC,BIMFIL,'OLD','FORMATTED',
     1  'READONLY',' ',IOSTAT)
      CALL OPNERR(LFNERR,LFNLOC,IOSTAT,BIMFIL,'GETBIM')
C
C SEARCH FOR KEY STRINGS
C ----------------------
      LOOK='RINEX VERSION / TYPE'
      CALL FINDLN(LFNLOC,0,1,'GETBIM',BIMFIL,LOOK,61,LINE,IERR)
      IF (IERR.NE.0) THEN
        WRITE(LFNERR,910)
910     FORMAT(/,' ### SR GETBIM: NO RINEX FILE',/)
        GOTO 900
      ENDIF
C
      IF (LINE(21:21).NE.'N') THEN
        WRITE(LFNERR,920)
920     FORMAT(/,' ### SR GETBIM: NO NAVIGATION DATA FILE',/)
        GOTO 900
      ENDIF
C
      LOOK='ION ALPHA'
      CALL FINDLN(LFNLOC,0,0,'GETBIM',BIMFIL,LOOK,61,LINE,IERR)
      IF (IERR.NE.0) THEN
        WRITE(LFNERR,931)
931     FORMAT(/,' ### SR GETBIM: NO ION ALPHAS FOUND',/)
        GOTO 900
      ENDIF
C
      READ(LINE,'(2X,4D12.4)',ERR=900) (BIMCOE(1,J),J=1,4)
C
      LOOK='ION BETA'
      CALL FINDLN(LFNLOC,0,0,'GETBIM',BIMFIL,LOOK,61,LINE,IERR)
      IF (IERR.NE.0) THEN
        WRITE(LFNERR,932)
932     FORMAT(/,' ### SR GETBIM: NO ION BETAS FOUND',/)
        GOTO 900
      ENDIF
C
      READ(LINE,'(2X,4D12.4)',ERR=900) (BIMCOE(2,J),J=1,4)
C
      IRCODE=1
      GOTO 999
C
C ERROR OCCURED
C -------------
900   CONTINUE
      WRITE(LFNERR,940) BIMFIL
940   FORMAT(/,' ### SR GETBIM: ERROR READING BIM DATA',
     1  /,16X,'FILE NAME: ',A32,/)
C
      IRCODE=0
      GOTO 999
C
C CLOSE INPUT FILE
C ----------------
999   CONTINUE
      CLOSE (UNIT=LFNLOC)
      RETURN
      END SUBROUTINE

      END MODULE
