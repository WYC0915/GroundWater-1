      MODULE s_PRENEW
      CONTAINS

C*
      SUBROUTINE PRENEW(XMJD,PRAEZ)
CC
CC NAME       :  PRENEW
CC
CC PURPOSE    :  COMPUTATION OF PRECESSION-MATRIX FROM 1950.0
CC               TO EPOCH XMJD
CC               SEE "USNO CIRCULAR NO 163",1981
CC               (IAU RESOLUTIONS)
CC
CC PARAMETERS :
CC         IN :  XMJD   : EPOCH IN MODIFIED JULIAN DATE       R*8
CC        OUT :  PRAEZ  : PRECESSION - MATRIX                 R*8(3,3)
CC
CC REMARKS    :  ---
CC
CC AUTHOR     :  G.BEUTLER
CC
CC VERSION    :  3.4  (JAN 93)
CC
CC CREATED    :  87/10/29 10:00
CC
CC CHANGES    :  23-JUN-05 : MM: IMPLICIT NONE AND DECLARATIONS ADDED
CC               28-FEB-07 : AG: USE 206264... FROM DEFCON
CC
CC COPYRIGHT  :  ASTRONOMICAL INSTITUTE
CC      1987     UNIVERSITY OF BERN
CC               SWITZERLAND
CC
C*
      USE d_const, ONLY: ars
      IMPLICIT NONE
C
C DECLARATIONS INSTEAD OF IMPLICIT
C --------------------------------
      REAL*8    COSTA, COSXA, COSZA, SINTA, SINXA, SINZA, TA   , TL   ,
     1          TU   , XA   , XMJD , ZA
C
CCC       IMPLICIT REAL*8 (A-H,O-Z)
      REAL*8 PRAEZ(3,3)
C
C  TIME INTERVAL (IN JUL. CENTURIES) BETWEEN EPOCH AND 1950.0
      TL=(XMJD-33281.9234D0)/36525.D0
C
C  TIME INTERVAL (IN JUL. CENTURIES) BETWEEN 1950.0 AND J2000.0
      TU=(33281.9234D0-51544.5)/36525.D0
C
C  ROTATION ANGLES XI,Z AND THETA
      XA=(2306.2181  D0+1.39656 D0*TU-0.000139D0*TU**2)*TL
     1     +(0.30188 D0-0.000344D0*TU)*TL**2
     2     + 0.017998D0*TL**3
      ZA=(2306.2181  D0+1.39656 D0*TU-0.000139D0*TU**2)*TL
     1     +(1.09468 D0+0.000066D0*TU)*TL**2
     2     + 0.018203D0*TL**3
      TA=(2004.3109  D0-0.85330 D0*TU-0.000217D0*TU**2)*TL
     1     -(0.42665 D0+0.000217D0*TU)*TL**2
     2     - 0.041833D0*TL**3
      XA=XA/ars
      ZA=ZA/ars
      TA=TA/ars
      COSXA=DCOS(XA)
      SINXA=DSIN(XA)
      COSZA=DCOS(ZA)
      SINZA=DSIN(ZA)
      COSTA=DCOS(TA)
      SINTA=DSIN(TA)
C
C  ROTATION MATRIX
      PRAEZ(1,1)= COSXA*COSZA*COSTA-SINXA*SINZA
      PRAEZ(2,1)= COSXA*SINZA*COSTA+SINXA*COSZA
      PRAEZ(3,1)= COSXA*SINTA
      PRAEZ(1,2)=-SINXA*COSZA*COSTA-COSXA*SINZA
      PRAEZ(2,2)=-SINXA*SINZA*COSTA+COSXA*COSZA
      PRAEZ(3,2)=-SINXA*SINTA
      PRAEZ(1,3)=-COSZA*SINTA
      PRAEZ(2,3)=-SINZA*SINTA
      PRAEZ(3,3)= COSTA
C
      RETURN
      END SUBROUTINE

      END MODULE
