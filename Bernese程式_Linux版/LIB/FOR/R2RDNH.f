      MODULE s_R2RDNH
      CONTAINS

C*
      SUBROUTINE R2RDNH(LFNNAV,LFNERR,MAXCOM,PRGNAM,RUNBY,
     1                  CRDATE,CRTIME,NCOM,COMENT,
     2                  ALPHA,BETA,A0UTC,A1UTC,ITUTC,NWKUTC,LEAP,
     3                  IRXVRS,IRCODE)
CC
CC NAME       :  R2RDNH
CC
CC PURPOSE    :  READ  THE ENTIRE HEADER INFORMATION OF A
CC               RINEX NAVIGATION FILE
CC
CC PARAMETERS :
CC         IN :  LFNNAV : LOGICAL FILE NUMBER                  I*4
CC               LFNERR : LFN FOR ERROR MESSAGES               I*4
CC        OUT :  PRGNAM : PROGRAM NAME                        CH*20
CC               RUNBY  : NAME OF AGENCY CREATING RINEX FILE  CH*20
CC               CRDATE : CREATION DATE                       CH*9
CC               CRTIME : CREATION TIME                       CH*5
CC               NCOM   : NUMBER OF COMMENT LINES              I*4
CC               COMENT : COMMENT LINES                       CH*60(*)
CC               ALPHA  : IONOSPHERE PARAMETERS A0-A3          R*8(4)
CC               BETA   : IONOSPHERE PARAMETERS B0-B3          R*8(4)
CC               A0UTC  : UTC POLYNOMIALS                      R*8
CC               A1UTC  :                                      R*8
CC               ITUTC  : REF.TIME FOR UTC                     I*4
CC               NWKUTC : REF.WEEK FOR UTC                     I*4
CC               LEAP   : LEAP SECONDS                         I*4
CC               IRXVRS : RINEX VERSION NUMBER                 I*4
CC               IRCODE : RETURN CODE                          I*4
CC                        0: OK
CC                        1: WRONG FILE TYPE
CC                        2: NOT ANTICIPATED VERSION NUMBER
CC                        3: END OF FILE WITHIN HEADER
CC                        4: READ ERROR
CC                        5: FIRST LINE NOT VERSION LINE
CC                        9: END OF FILE AT BEGINNING OF HEADER
CC
CC REMARKS    :  ---
CC
CC AUTHOR     :  W. GURTNER
CC
CC VERSION    :  3.3
CC
CC CREATED    :   1-MAY-91
CC
CC CHANGES    :  20-SEP-93 : ??: DON'T ABORT IF UNKNOWN RECORD TYPE FOUND
CC               04-JAN-02 : HU: REMOVE ^M
CC               01-MAR-02 : DI: ADD ERR= FOR ALL READ STATEMENTS
CC               23-JUN-05 : MM: IMPLICIT NONE AND DECLARATIONS ADDED
CC
CC COPYRIGHT  :  ASTRONOMICAL INSTITUTE
CC      1989     UNIVERSITY OF BERN
CC               SWITZERLAND
CC
C*
      USE s_upperc
      IMPLICIT NONE
C
C DECLARATIONS INSTEAD OF IMPLICIT
C --------------------------------
      INTEGER*4 I     , IRCODE, IRVERS, IRXVRS, ITEST , ITUTC , JPGM  ,
     1          LEAP  , LFNERR, LFNNAV, MAXCOM, NCOM  , NWKUTC
C
      REAL*8    A0UTC , A1UTC
C
CCC       IMPLICIT REAL*8 (A-H,O-Z)
C
C GLOBAL DECLARATIONS
C -------------------
      CHARACTER*20 PRGNAM,RUNBY
      CHARACTER    COMENT(*)*60,CRDATE*9,CRTIME*5
      REAL*8       ALPHA(*),BETA(*)
C
C  LOCAL DECLARATIONS
C  ------------------
      CHARACTER    HEAD*20,RXTYPE*1,STRING*60,RXT*1
C
C RINEX FILE TYPE
      DATA RXTYPE/'N'/
C
C MAXIMUM RINEX FORMAT VERSION
      DATA IRVERS/2/
C
      NCOM=0
      JPGM=0
      DO 10 I=1,4
        ALPHA(I)=0.D0
        BETA(I) =0.D0
10    CONTINUE
      A0UTC=0.D0
      A1UTC=0.D0
      ITUTC=0
      NWKUTC=0
      LEAP=0
C
C RECORD 1        RINEX VERSION / TYPE
101   READ(LFNNAV,222,END=990,ERR=940) STRING,HEAD
222   FORMAT(A60,A20)
      CALL UPPERC(STRING)
      CALL UPPERC(HEAD)
C REMOVE ^M
      ITEST=INDEX(STRING,CHAR(13))
      IF(ITEST.GT.0)STRING(ITEST:ITEST)=' '
      ITEST=INDEX(HEAD,CHAR(13))
      IF(ITEST.GT.0)HEAD(ITEST:ITEST)=' '
C
      IF(HEAD.NE.'RINEX VERSION / TYPE') GOTO 950
      READ(STRING,1,ERR=940) IRXVRS,RXT
1     FORMAT(I6,14X,A1)
      IF(RXT.NE.RXTYPE) GOTO 910
      IF(IRXVRS.LE.0.OR.IRXVRS.GT.IRVERS) GOTO 920
C
C  LOOP OVER ALL REMAINING LINES
200   READ(LFNNAV,222,END=930,ERR=940) STRING,HEAD
      CALL UPPERC(STRING)
      CALL UPPERC(HEAD)
C REMOVE ^M
      ITEST=INDEX(STRING,CHAR(13))
      IF(ITEST.GT.0)STRING(ITEST:ITEST)=' '
      ITEST=INDEX(HEAD,CHAR(13))
      IF(ITEST.GT.0)HEAD(ITEST:ITEST)=' '
C
      IF(HEAD.EQ.'COMMENT') THEN
        IF(NCOM.EQ.MAXCOM) THEN
           WRITE(LFNERR,201)
201        FORMAT(' SR R2RDNH: TOO MANY COMMENT LINES')
        ELSE
          NCOM=NCOM+1
          COMENT(NCOM)=STRING
        END IF
C
C   PGM / RUN BY / DATE
      ELSEIF(HEAD.EQ.'PGM / RUN BY / DATE '.OR.
     1       HEAD.EQ.'PGM / RUN BY /DATE  ') THEN
        READ(STRING,2,ERR=940) PRGNAM,RUNBY,CRDATE,CRTIME
2       FORMAT(A20,A20,A9,1X,A5)
        JPGM=1
C
C  ION ALPHA
      ELSEIF (HEAD.EQ.'ION ALPHA           ') THEN
        READ(STRING,3,ERR=940) (ALPHA(I),I=1,4)
3       FORMAT(2X,4D12.4)
C
C  ION BETA
      ELSEIF (HEAD.EQ.'ION BETA            ') THEN
        READ(STRING,3,ERR=940) (BETA(I),I=1,4)
C
C  DELTA-UTC
      ELSEIF (HEAD.EQ.'DELTA-UTC: A0,A1,T,W') THEN
        READ(STRING,4,ERR=940) A0UTC,A1UTC,ITUTC,NWKUTC
4       FORMAT(3X,2D19.12,2I9)
C
C  LEAP SECONDS
      ELSEIF (HEAD.EQ.'LEAP SECONDS        ') THEN
        READ(STRING,6,ERR=940) LEAP
6       FORMAT(I6)
C
C  BLANK LINE OR END OF HEADER
      ELSEIF ((HEAD.EQ.'END OF HEADER').OR.
     1        (IRXVRS.EQ.1.AND.STRING.EQ.' ')) THEN
        GOTO 300
      ELSE
C  UNKNOWN RECORD TYPE
        WRITE(LFNERR,941) STRING,HEAD
941     FORMAT(' SR R2RDNH: UNKNOWN HEADER RECORD TYPE FOUND:',
     1       /,1X,2A)
      END IF
C
C  READ NEXT RECORD
      GOTO 200
C
C  ARE ALL NECESSARY RECORDS READ?
300   IF(JPGM.EQ.0) WRITE(LFNERR,301) '"PGM / RUN BY / DATE "'
301   FORMAT(' SR R2RDNH: ',A,' RECORD MISSING')
C
      IRCODE=0
      GOTO 999
C
C  WRONG FILE TYPE
910   IRCODE=1
      WRITE(LFNERR,911) RXT
911   FORMAT(' SR R2RDNH: WRONG FILE TYPE: "',A,'"')
      BACKSPACE LFNNAV
      GOTO 999
C
C  NOT ANTICIPATED VERSION NUMBER
920   IRCODE=2
      WRITE(LFNERR,921) IRXVRS
921   FORMAT(' SR R2RDNH: NOT ANTICIPATED VERSION NUMBER:',I5)
      GOTO 999
C
C  END OF FILE WITHIN HEADER
930   IRCODE=3
      WRITE(LFNERR,931)
931   FORMAT(' SR R2RDNH: END OF FILE WITHIN HEADER')
      GOTO 999
C
C  ERROR DECODING DATA
940   IRCODE=4
      WRITE(LFNERR,942) STRING,HEAD
942   FORMAT(' SR R2RDNH: ERROR DECODING DATA ON LINE:',
     1       /,1X,2A)
      GOTO 999
C
C  FIRST LINE NOT VERSION LINE
950   IRCODE=5
      WRITE(LFNERR,951) STRING,HEAD
951   FORMAT(' SR R2RDNH: FIRST LINE NOT "VERSION"-LINE:',
     1       /,1X,2A)
      GOTO 999
C
C  END OF FILE AT FIRST RECORD
990   IRCODE=9
      GOTO 999
C
999   RETURN
      END SUBROUTINE

      END MODULE
