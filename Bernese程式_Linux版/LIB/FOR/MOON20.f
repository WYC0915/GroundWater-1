      MODULE s_MOON20
      CONTAINS

C*
      SUBROUTINE MOON20(XMJD,X,R,L,B,P)
CC
CC NAME       :  MOON20
CC
CC PURPOSE    :  RADIUS VECTOR AND DISTANCE OF THE MOON
CC               IN SYSTEM J2000.0 IN METERS.
CC               COMPUTE LONGITUDE L, LATITUDE B (REFERRED TO ECLIPTIC
CC               AND MEAN SYSTEM OF EPOCH FK4) PARALLAX P
CC
CC PARAMETERS :
CC         IN :  XMJD   : TIME IN MOD. JULIAN DATE IN TDB     R*8
CC                        BARYCENTRIC DYNAMICAL TIME
CC                        (FORMER ET)
CC        OUT :  X      : RADIUS VECTOR TO MOON (M) J2000.0   R*8(3)
CC               R      : DISTANCE (M)                        R*8
CC               L      : ECL. LONGITUDE                      R*8
CC               B      : ECL. LATITUDE                       R*8
CC               P      : PARALLAX (RADIAN)                   R*8
CC
CC REMARKS    :  NEW CALL  [OLD CALL: CALL MOON(T,L,B,P,RA,DE)]
CC
CC REMARKS    :  ---
CC
CC AUTHOR     :  G.BEUTLER, M.ROTHACHER, U.HUGENTOBLER
CC
CC VERSION    :  3.4  (JAN 93)
CC
CC CREATED    :  87/12/11 11:24
CC
CC CHANGES    :  13-NOV-96 : MR: CHANGE ALL REAL*4 TO REAL*8 (NUMERICAL
CC                               PROBLEM WITH LAHEY)
CC               18-DEZ-97 : DI: CALCULATE DP IN 3 STEPS (NUMERICAL
CC                               PROBLEM WITH LAHEY COMPILER)
CC               23-JUN-05 : MM: IMPLICIT NONE AND DECLARATIONS ADDED
CC               28-FEB-07 : AG: USE PI FROM DEFCON
CC               04-MAY-12 : RD: USE DMOD FROM MODULE
CC               04-MAY-12 : RD: USE RHO INSTEAD OF PI FROM D_CONST
CC
CC COPYRIGHT  :  ASTRONOMICAL INSTITUTE
CC      1987     UNIVERSITY OF BERN
CC               SWITZERLAND
CC
C*
      USE d_const,  ONLY: RHO
      USE l_basfun, ONLY: dmod
      USE s_dmlmtv
      USE s_monsto
      USE s_fk4fk5
      USE s_prae
      IMPLICIT NONE
C
C DECLARATIONS INSTEAD OF IMPLICIT
C --------------------------------
      INTEGER*4 I
C
      REAL*8    AD    , ARCMOD, B     , D     , DB    , DEPOCH, DL    ,
     1          DP    , E     , E2    , EPS   , F     , O     , O1    ,
     2          O2    , P     , PR    , R     , SO    , SW    ,
     3          TT    , V     , VR    , XKLAM , XMJD  , Y
C
CCC       IMPLICIT REAL*8 (A-H,O-Z)
C
      REAL*8 L,K,M,M1,L1,J
      REAL*8 X(3),PRAEZ(3,3),W(3)
C
      ARCMOD(Y)=DMOD(Y,360.D0)/RHO
C
C      PI=3.141592653589793D0
      TT=(XMJD-15019.5D0)/36525.D0
C
C MEAN LONGITUDE OF THE MOON
C
      L1=ARCMOD(270.434164D0+481267.8831D0*TT-.001133D0*TT*TT
     1          +1.9D-6*TT**3)
C
C MEAN ANOMALY OF THE SUN
C
      M=ARCMOD(358.475833D0+35999.0498D0*TT-1.5D-4*TT**2
     1         -3.3D-6*TT**3)
C
C MEAN ANOMALY OF THE MOON
C
      M1=ARCMOD(296.104608D0+477198.8491D0*TT+9.192D-3*TT**2
     1          +1.44D-5*TT**3)
C
C MEAN ELONGATION OF THE MOON
C
      D=ARCMOD(350.737486D0+445267.1142D0*TT-1.436D-3*TT**2
     1         +1.9D-6*TT**3)
C
C MEAN DISTANCE OF THE MOON FROM THE ASCENDING NODE
C
      F=ARCMOD(11.250889D0+483202.0251D0*TT-3.211D-3*TT**2
     1         -3.D-7*TT**3)
C
C LONGITUDE OF ASCENDING NODE OF THE MOON
C
      O=ARCMOD(259.183275D0-1934.142D0*TT+2.078D-3*TT**2
     1         +2.2D-6*TT**3)
C
C MEAN LONGITUDES OF VENUS AND JUPITER, MEAN LONGITUDE OF THE EARTH +180
C DEGREES FOR JUPITER
C
      V=ARCMOD(63.07037D0+22518.442986D0*TT)
      J=ARCMOD(221.64742D0+32964.466939D0*TT)
C
C "ADDITIVE" TERMS
C
      K=1D0/RHO
      SW=K*DSIN((51.2D0+20.2D0*TT)*K)
C
      L1=L1+2.33D-4*SW
      M =M-1.778D-3*SW
      M1=M1+8.17D-4*SW
      D =D+2.011D-3*SW
C
      AD=K*3.964D-3*DSIN((346.56D0+132.87D0*TT-9.1731D-3*TT**2)*K)
C
      L1=L1+AD
      M1=M1+AD
      D = D+AD
      F = F+AD
C
      SO=K*DSIN(O)
C
      L1=L1+1.964D-3*SO
      M1=M1+2.541D-3*SO
      D = D+1.964D-3*SO
      F =F-2.4691D-2*SO
      F =F-K*4.328D-3*DSIN(O+(275.05D0-2.3D0*TT)*K)
C
C
      E=1.D0-2.495D-3*TT-7.52D-6*TT**2
      E2=E**2
C
C PERTURBATIONS IN LONGITUDE AND LATITUDE: SUBROUTINE MONSTO
C
      CALL MONSTO(M,M1,F,D,V,J,E,E2,K,DL,DB)
      L=L1+DL
      O1=4.664D-4*DCOS(O)
      O2=7.54D-5*DCOS(O+(275.05D0-2.3D0*TT)*K)
      XKLAM=1-O1-O2
      B=DB*XKLAM
C
C PARALLAXIS OF THE MOON
C
      DP=51818.D0*DCOS(M1)+9531.D0*DCOS(2*D-M1)+7843.D0*DCOS(2*D)
     *   +2824.D0*DCOS(2*M1)+857.D0*DCOS(2*D+M1)+533.D0*E*DCOS(2*D-M)
     *   +401.D0*E*DCOS(2*D-M-M1)+320.D0*E*DCOS(M1-M)-271.D0*DCOS(D)
     *   -264.D0*E*DCOS(M+M1)-198.D0*DCOS(2*F-M1)+173.D0*DCOS(3*M1)
      DP=DP+
     *   167.D0*DCOS(4*D-M1)-111.D0*E*DCOS(M)+103.D0*DCOS(4*D-2*M1)
     *   -84.D0*DCOS(2*M1-2*D)-83.D0*E*DCOS(2*D+M)+79.D0*DCOS(2*D+2*M1)
     *   +72.D0*DCOS(4*D)+64.D0*E*DCOS(2*D-M+M1)-63.D0*E*DCOS(2*D+M-M1)
      DP=DP+
     *   41.D0*E*DCOS(M+D)+35.D0*E*DCOS(2*M1-M)-33.D0*DCOS(3*M1-2*D)
     *   -30.D0*DCOS(M1+D)-29.D0*DCOS(2*F-2*D)-29.D0*E*DCOS(2*M1+M)
     *   +26.D0*E2*DCOS(2*D-2*M)-23.D0*DCOS(2*F-2*D+M1)
     *   +19.D0*E*DCOS(4*D-M-M1)
      P=(.950724D0+1.D-6*DP)*K
C
C OBLIQUITY OF ECLIPTIC
C
      EPS=(23.452294D0-.0130125D0*TT-1.64D-6*TT**2+5.03D-7*TT**3)*K
CX      EPS=23.452294D0-.0130125D0*TT-1.64D-6*TT**2+5.03D-7*TT**3
CX      DEPS=((9.21+.00091*TT)*DCOS(O)+.552*DCOS(2*F-2*D+2*O))/3600.
CX      EPS=(EPS+DEPS)*K
C
C NUTATION IN LONGITUDE (FIRST 4 TERMS) (NOT APPLIED TO STAY IN MEAN SYSTEM)
C
CX      PSI=(-17.2327*DSIN(O)-1.2730*DSIN(2*F-2*D+2*O)+0.2088*DSIN(2*O)
CX     *     -0.2037*DSIN(2*F+2*O))/3600.D0*K
CX      L=L+PSI
C
C CARTESIAN UNIT VECTOR IN MEAN SYSTEM OF EPOCH
C
      X(1)=DCOS(B)*DCOS(L)
      X(2)=DCOS(B)*DSIN(L)*DCOS(EPS)-DSIN(B)*DSIN(EPS)
      X(3)=DCOS(B)*DSIN(L)*DSIN(EPS)+DSIN(B)*DCOS(EPS)
C
C DISTANCE IN KILOMETERS
C
      R=6378137.D0/DSIN(P)
C
C TRANSFORMATION FROM B1950.0 TO J2000.0
C
      CALL PRAE(XMJD,PRAEZ)
      CALL DMLMTV(X,PRAEZ,X)
      W(1)=0.D0
      W(2)=0.D0
      W(3)=0.D0
      CALL FK4FK5(X,W,0.D0,0.D0,X,W,PR,VR)
      DEPOCH=(XMJD-51544.5D0)/36525.D0
      DO 79 I=1,3
        X(I)=(X(I)+W(I)*DEPOCH)*R
79    CONTINUE
C
      RETURN
      END SUBROUTINE

      END MODULE
