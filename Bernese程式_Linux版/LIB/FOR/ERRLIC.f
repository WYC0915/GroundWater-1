      MODULE s_ERRLIC
      CONTAINS

C*
      SUBROUTINE ERRLIC(RMS,Q,I1,I2,COE1,COE2,ERRLC)
CC
CC NAME       :  ERRLIC
CC
CC PURPOSE    :  COMPUTE THE ERROR OF A LINEAR COMBINATION
CC               C1*X(I1) + C2*X(I2)
CC               OF TWO ELEMENTS OF A PARAMETER VECTOR X.
CC               RMS**2*Q IS THE (UPPER TRIANGULAR PART OF) THE
CC               VARIANCE-COVARIANCE MATRIX.
CC
CC PARAMETERS :
CC         IN :  RMS    : ROOT MEAN SQUARE ERROR              R*8
CC               Q(I),I=1,2,..: UPPER TRIANGULAR PART OF      R*8
CC                        VARIANCE-COVARIANCE MATRIX/RMS**2   R*8
CC               I1     : INDEX OF FIRST ELEMENT OF LC        I*4
CC               I2     : INDEX OF SECOND ELEMENT             I*4
CC               COE1   : COEFFICIENT OF FIRST ELEMENT        R*8
CC               COE2   : COEFFICIENT OF SECOND ELEMENT       R*8
CC        OUT :  ERRLC  : MEAN ERROR OF LINEAR COMBINATION    R*8
CC
CC REMARKS    :  ---
CC
CC AUTHOR     :  G. BEUTLER
CC
CC VERSION    :  3.4  (JAN 94)
CC
CC CREATED    :  89/07/03 14:48
CC
CC CHANGES    :  23-JUN-05 : MM: IMPLICIT NONE AND DECLARATIONS ADDED
CC
CC COPYRIGHT  :  ASTRONOMICAL INSTITUTE
CC      1989     UNIVERSITY OF BERN
CC               SWITZERLAND
CC
C*
      IMPLICIT NONE
C
C DECLARATIONS INSTEAD OF IMPLICIT
C --------------------------------
      INTEGER*4 I1   , I2   , IND1 , IND2 , IND3
C
      REAL*8    COE1 , COE2 , ERR2 , ERRLC, RMS
C
CCC       IMPLICIT REAL*8 (A-H,O-Z)
CCC       IMPLICIT INTEGER*4 (I-N)
C
      REAL*8 Q(*)
C
C COMPUTE INDICES IN MATRIX Q
C ---------------------------
      IND1=I1*(I1+1)/2
      IND2=I2*(I2+1)/2
      IF (I1.LE.I2) THEN
        IND3=I1+I2*(I2-1)/2
      ELSE
        IND3=I2+I1*(I1-1)/2
      END IF
C
C ERROR OF LINEAR COMBINATION
C ---------------------------
      ERR2=RMS**2*(COE1**2*Q(IND1)+2*COE1*COE2*Q(IND3)+COE2**2*Q(IND2))
      ERRLC=DSQRT(ERR2)
C
      RETURN
      END SUBROUTINE

      END MODULE
