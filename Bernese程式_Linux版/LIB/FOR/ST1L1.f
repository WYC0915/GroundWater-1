      MODULE s_ST1L1
      CONTAINS

C*
      SUBROUTINE ST1L1(XSTA,XSUN,XMON,F2SUN,F2MON,XCOSTA)
CC
CC  PURPOSE   :  .
CC THIS SUBROUTINE GIVES THE CORRECTIONS INDUCED BY THE LATITUDE DEPENDENCE
CC GIVEN BY L^(1) IN MAHTEWS ET AL (1991)
CC
CC PARAMETERS :
CC         IN :  XSTA,XSUN,XMON,F3SUN,F3MON
CC     IN/OUT :  XCOSTA
CC
CC REMARKS    :  ---
CC
CC AUTHOR     :  V. DEHANT, S. MATHEWS AND J. GIPSON
CC
CC VERSION    :  4.1
CC
CC CREATED    :  06-OCT-97
CC
CC CHANGES    :  06-OCT-97 : TS: ADDEPTED FOR THE BERNESE SOFTWARE
CC               23-JUN-05 : MM: IMPLICIT NONE AND DECLARATIONS ADDED
CC
CC COPYRIGHT  :  ASTRONOMICAL INSTITUTE
CC      1997     UNIVERSITY OF BERN
CC               SWITZERLAND
CC
C*
      IMPLICIT NONE
C
C DECLARATIONS INSTEAD OF IMPLICIT
C --------------------------------
      REAL*8    COSLA , COSPHI, CTWOLA, DE    , DEMON , DESUN , DN    ,
     1          DNMON , DNSUN , F2MON , F2SUN , RMON  , RSTA  , RSUN  ,
     2          SINLA , SINPHI, STWOLA
C
CCC       IMPLICIT REAL*8 (A-H,O-Z)
C
      REAL*8 XSTA(*),XSUN(*),XMON(*),XCOSTA(*)
      REAL*8 L1,L1D,L1SD
C
      DATA L1D/0.0012D0/,L1SD/0.0024D0/
C
      RSTA=DSQRT(XSTA(1)**2+XSTA(2)**2+XSTA(3)**2)
      SINPHI=XSTA(3)/RSTA
      COSPHI=DSQRT(XSTA(1)**2+XSTA(2)**2)/RSTA
      SINLA=XSTA(2)/COSPHI/RSTA
      COSLA=XSTA(1)/COSPHI/RSTA
      RMON=XMON(4)
      RSUN=XSUN(4)
C
C FOR THE DIURNAL BAND
C --------------------
      L1=L1D
      DNSUN=-L1*SINPHI**2*F2SUN*XSUN(3)*(XSUN(1)*COSLA+XSUN(2)*SINLA)
     1            /RSUN**2
      DNMON=-L1*SINPHI**2*F2MON*XMON(3)*(XMON(1)*COSLA+XMON(2)*SINLA)
     1            /RMON**2
      DESUN=L1*SINPHI*(COSPHI**2-SINPHI**2)*F2SUN*XSUN(3)*
     1      (XSUN(1)*SINLA-XSUN(2)*COSLA)/RSUN**2
      DEMON=L1*SINPHI*(COSPHI**2-SINPHI**2)*F2MON*XMON(3)*
     1      (XMON(1)*SINLA-XMON(2)*COSLA)/RMON**2
      DE=3.D0*(DESUN+DEMON)
      DN=3.D0*(DNSUN+DNMON)
      XCOSTA(1)=-DE*SINLA-DN*SINPHI*COSLA
      XCOSTA(2)=DE*COSLA-DN*SINPHI*SINLA
      XCOSTA(3)=DN*COSPHI
C
C FOR THE SEMI-DIURNAL BAND
C -------------------------
      L1=L1SD
      CTWOLA=COSLA**2-SINLA**2
      STWOLA=2.D0*COSLA*SINLA
      DNSUN=-L1/2.D0*SINPHI*COSPHI*F2SUN*((XSUN(1)**2-XSUN(2)**2)*
     1       CTWOLA+2.D0*XSUN(1)*XSUN(2)*STWOLA)/RSUN**2
      DNMON=-L1/2.D0*SINPHI*COSPHI*F2MON*((XMON(1)**2-XMON(2)**2)*
     1       CTWOLA+2.D0*XMON(1)*XMON(2)*STWOLA)/RMON**2
      DESUN=-L1/2.D0*SINPHI**2*COSPHI*F2SUN*((XSUN(1)**2-XSUN(2)**2)*
     1       STWOLA-2.D0*XSUN(1)*XSUN(2)*CTWOLA)/RSUN**2
      DEMON=-L1/2.D0*SINPHI**2*COSPHI*F2MON*((XMON(1)**2-XMON(2)**2)*
     1       STWOLA-2.D0*XMON(1)*XMON(2)*CTWOLA)/RMON**2
      DE=3.D0*(DESUN+DEMON)
      DN=3.D0*(DNSUN+DNMON)
      XCOSTA(1)=XCOSTA(1)-DE*SINLA-DN*SINPHI*COSLA
      XCOSTA(2)=XCOSTA(2)+DE*COSLA-DN*SINPHI*SINLA
      XCOSTA(3)=XCOSTA(3)+DN*COSPHI
C
      RETURN
      END SUBROUTINE

      END MODULE
