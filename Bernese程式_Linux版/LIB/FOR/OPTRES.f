      MODULE s_OPTRES
      CONTAINS

C*
      SUBROUTINE OPTRES(NFRCHK,IWLSCR)
CC
CC NAME       :  OPTRES
CC
CC PURPOSE    :  DISPLAY RESIDUALS WITH SPECIFICATIONS TO BE ENTERED
CC               AT THE KEYBOARD
CC
CC PARAMETERS :
CC         IN :  NFRCHK : FREQUENCY(IES) CHECKED              I*4
CC                         =1 : L1
CC                         =2 : L2
CC                         =3 : L1&L2 TOGETHER (VIA L5 AND L3)
CC                         =4 : L1 AND L2 SEPARATELY
CC               IWLSCR(I),I=1,2: WAVELENGTH FACTORS (L1/L2)  I*4
CC                         =1 : INTEGER CYCLES
CC                         =2 : HALF    CYCLES
CC
CC REMARKS    :  ---
CC
CC AUTHOR     :  M.ROTHACHER
CC
CC VERSION    :  3.4  (JAN 93)
CC
CC CREATED    :  89/08/22 10:06
CC
CC CHANGES    :  05-JUN-92 : ??: CHANGES FOR THE NEW MAUPRP VERSION
CC               11-JAN-94 : SF:INTERNAL READ WITH FORMAT "*" REPLACED
CC                              BY INPCI4,INPCR8
CC               22-MAR-94 : SF:READING 'FROMTO' WITH INPCR8 INSTEAD OF
CC                              INPCI4
CC               21-JUN-05 : MM: COMLFNUM.inc REMOVED, m_bern ADDED
CC               23-JUN-05 : MM: IMPLICIT NONE AND DECLARATIONS ADDED
CC               04-NOV-05 : HB: INPCR8 IS INTEGER*4 AND NOT REAL*8
CC               03-JUL-09 : SL: INPCI4(.,1,...) CALLS CORRECTED
CC               29-FEB-12 : RD: CORRECT ARRAY DIMENSIONS OF INPCxx ROUTINES
CC               27-MAR-12 : RD: USE PROMP1 AS MODULE NOW
CC               27-MAR-12 : RD: USE INPCI4 AS MODULE NOW
CC               27-MAR-12 : RD: USE INPCR8 AS MODULE NOW
CC
CC COPYRIGHT  :  ASTRONOMICAL INSTITUTE
CC      1989     UNIVERSITY OF BERN
CC               SWITZERLAND
CC
C*
      USE m_bern, ONLY: lfnkbd, lfnprt
      USE s_dspres
      USE s_promp1
      USE f_inpcr8
      USE s_upperc
      USE f_inpci4
      IMPLICIT NONE
C
C DECLARATIONS INSTEAD OF IMPLICIT
C --------------------------------
      INTEGER*4 ICARR , ICARR1, ICYC34, ICYCLE, IFILE ,
     1          IRC   , IRCODE, IUNIT , IVAL  , IWLFAC, NFRCHK
C
CCC       IMPLICIT REAL*8 (A-H,O-Z)
C
      CHARACTER*72 INPUT,STRING(2)
      CHARACTER*1  CDUMMY
      INTEGER*4    IWLSCR(2),FROMTO(2),IDUMMY(1)
      REAL*8       RESSIZ(1)
C
C
      DATA ICARR/3/
      DATA IUNIT/1/
      DATA ICYC34/1/
C
      FROMTO(1)=1
      FROMTO(2)=99999
      STRING(1)=' '
C
C ENTER DISPLAY UNITS
C -------------------
100   WRITE(STRING(2),101) IUNIT
101   FORMAT('ENTER UNITS (CYCLES=1,MM=2,M=3,DF:',I1,') :')
      CALL PROMP1(2,STRING)
      READ(LFNKBD,302) INPUT
      IF (INPUT.NE.' ') THEN
        IVAL=INPCI4(-72,1,0,INPUT,(/IUNIT/),CDUMMY,IDUMMY,IRCODE)
        IF (IRCODE.NE.0) GOTO 100
      ENDIF
      IF(IUNIT.LT.1.OR.IUNIT.GT.3) THEN
        WRITE(LFNPRT,102) IUNIT
102     FORMAT(/,' INVALID UNIT SPECIFICATION:',I3,/)
        IUNIT=1
        GOTO 100
      ENDIF
C
C ENTER FREQUENCY
C ---------------
300   IF(NFRCHK.LT.3) THEN
        ICARR=NFRCHK
      ELSE
        WRITE(STRING(2),301) ICARR
301     FORMAT('ENTER FREQUENCY (1,2,3,4,5,END=E,DF:',I1,') :')
        CALL PROMP1(2,STRING)
        READ(LFNKBD,302) INPUT
302     FORMAT(A)
        CALL UPPERC(INPUT)
        IF(INPUT.EQ.'E') GOTO 999
        IF(INPUT.NE.' ') THEN
          IVAL=INPCI4(-72,1,0,INPUT,(/ICARR1/),CDUMMY,IDUMMY,IRCODE)
          IF (IRCODE.NE.0) GOTO 300
          IF(ICARR1.LT.1.OR.ICARR1.GT.5) THEN
            WRITE(LFNPRT,303) ICARR1
303         FORMAT(/,' INVALID FREQUENCY:',I4)
            GOTO 300
          ENDIF
          ICARR=ICARR1
        ENDIF
      ENDIF
C
C SPECIFY TYPE OF CYCLES TO BE DISPLAYED (FOR L3,L4)
C --------------------------------------------------
      IF(ICARR.LE.2) THEN
        ICYCLE=ICARR
      ELSE IF(ICARR.LE.4) THEN
305     WRITE(STRING(2),306) ICYC34
306     FORMAT('ENTER CYCLE TYPE (L1=1,L2=2,COMB. L1/L2=3,DF:',
     1           I1,') :')
        CALL PROMP1(2,STRING)
        READ(LFNKBD,302) INPUT
        IF (INPUT.NE.' ') THEN
          IVAL=INPCI4(-72,1,0,INPUT,(/ICYC34/),CDUMMY,IDUMMY,IRCODE)
          IF (IRCODE.NE.0) GOTO 305
        ENDIF
        IF(ICYC34.LT.1.OR.ICYC34.GT.3) THEN
          WRITE(LFNPRT,307) ICYC34
307       FORMAT(/,' INVALID CYCLE TYPE:',I3,/)
          ICYC34=1
          GOTO 305
        ENDIF
        ICYCLE=ICYC34
      ELSE
        ICYCLE=1
      ENDIF
C
C WAVELENGTH FACTOR
C -----------------
      IF(ICARR.LE.4) THEN
        IF(ICYCLE.NE.3) THEN
          IWLFAC=IWLSCR(ICYCLE)
        ELSE
          IWLFAC=MIN0(IWLSCR(1),IWLSCR(2))
        ENDIF
      ELSE
        IWLFAC=MAX0(IWLSCR(1),IWLSCR(2))
      ENDIF
C
C RANGE OF EPOCHS TO BE DISPLAYED
C -------------------------------
310   WRITE(STRING(2),311) FROMTO
311   FORMAT('ENTER FIRST AND LAST EPOCH NUMBER (END=E,',
     1         'DF:',I5,',',I5,') :')
      CALL PROMP1(2,STRING)
      READ(LFNKBD,302) INPUT
      CALL UPPERC(INPUT)
      IF(INPUT.EQ.'E'.AND.NFRCHK.LT.3) GOTO 999
      IF(INPUT.EQ.'E'.AND.NFRCHK.GE.3) GOTO 300
      IF(INPUT.NE.' ') THEN
        IVAL=INPCI4(-72,2,0,INPUT,FROMTO,CDUMMY,IDUMMY,IRCODE)
        IF (IRCODE.EQ.8)THEN
          FROMTO(2)=99999
        ELSE IF (IRCODE.NE.0) THEN
          GOTO 310
        ENDIF
      ENDIF
C
C MINIMUM RESIDUAL SIZE
C ---------------------
320   WRITE(STRING(2),321)
321   FORMAT('ENTER MIN. SIZE OF RESIDUALS IN SPECIFIED UNITS ',
     1         '(DF:0.000) :')
      CALL PROMP1(2,STRING)
      READ(LFNKBD,302) INPUT
      IF(INPUT.EQ.' ') THEN
        RESSIZ=0.D0
      ELSE
        IVAL=INPCR8(-72,1,0,INPUT,RESSIZ,CDUMMY,IDUMMY,IRCODE)
        IF (IRCODE.NE.0) GOTO 320
      ENDIF
C
C DISPLAY RESIDUALS
C -----------------
      IFILE=1
      CALL DSPRES(LFNPRT,IFILE,FROMTO,RESSIZ(1),ICARR,IWLFAC,ICYCLE,
     1            IUNIT,IRC)
      GOTO 300
C
C RETURN
C ------
999   RETURN
      END SUBROUTINE

      END MODULE
