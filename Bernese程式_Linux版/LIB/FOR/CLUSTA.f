      MODULE s_CLUSTA
      CONTAINS

C*
      SUBROUTINE CLUSTA(NPAR,NPARN,LOCQ,NSING,IPAMB,ICLUST,OBSCLS)
CC
CC NAME       :  CLUSTA
CC
CC PURPOSE    :  FIND THE OBSERVATION CLUSTER NUMBERS
CC
CC PARAMETERS :
CC         IN :  NPAR ... NUMBER OF PARAMETERS                I*4
CC               NPARN ... NUMBER OF NON AMBIGUITY PARAMETERS I*4
CC               LOCQ(I,K),K=1,...,NPAR,I=1,..,MAXLCQ:        I*4
CC                        DEFINITION OF EACH PARAMETER
CC               NSING ... NUMBER OF SINGLE DIFF. OBS.        I*4
CC               IPAMB(I),I=1,..NSING : AMBIGUITY INDEX IN    I*4
CC                                      ARRAY OBSCLS
CC     IN/OUT :  ICLUST ... MAXIMUM CLUSTER NUMBER IN FILE    I*4
CC               OBSCLS(I),I=1,...NAMB  CLUSTER NUMBERS       I*4
CC
CC REMARKS    :  ---
CC
CC AUTHOR     :  M.ROTHACHER, L.MERVART
CC
CC VERSION    :  3.4
CC
CC CREATED    :  14-AUG-92
CC
CC CHANGES    :  19-AUG-94 : MR: CORRECT LOGIC OF OBS.CLUSTERS
CC               22-AUG-94 : MR: CORRECT LOGIC OF OBS.CLUSTERS !!!
CC               21-JUN-05 : MM: COMLFNUM.inc REMOVED, m_bern ADDED
CC               23-JUN-05 : MM: IMPLICIT NONE AND DECLARATIONS ADDED
CC
CC COPYRIGHT  :  ASTRONOMICAL INSTITUTE
CC      1992     UNIVERSITY OF BERN
CC               SWITZERLAND
CC
C*
      USE m_bern
      USE s_exitrc
      IMPLICIT NONE
C
C DECLARATIONS INSTEAD OF IMPLICIT
C --------------------------------
      INTEGER*4 IAMB  , IAMB1 , IAMB2 , ICLUST, IOBCLS, IPAR1 , IPAR2 ,
     1          ISING , MINCLS, MXCLCQ, NAMB  , NEWCLS, NPAR  , NPARN ,
     2          NSING
C
CCC       IMPLICIT REAL*8 (A-H,O-Z)
C
      CHARACTER*6 MXNLCQ
      INTEGER*4   IPAMB(*),OBSCLS(*),LOCQ(MXCLCQ,*)
C
      COMMON/MCMLCQ/MXCLCQ,MXNLCQ
C
C
C NUMBER OF AMBIGUITY PARAMETERS
      NAMB=NPAR-NPARN
C
C SET NEW CLUSTER NUMBER (IF NECESSARY) AND FIND THE SMALLEST
C CLUSTER AVAILABLE IN CURRENT EPOCH
C -----------------------------------------------------------
      NEWCLS=ICLUST+1
      MINCLS=999999
      DO 100 ISING=1,NSING
        IAMB=IPAMB(ISING)
C
C IPAMB=0 SHOULD NOT APPEAR IN ANY CASE (?)
        IF (IAMB.EQ.0) THEN
          WRITE(LFNERR,901)
901       FORMAT(/,' *** SR CLUSTA: UNEXPECTED "IPAMB=0" OCCURED',/,
     1             16X,'OBSERVATION CLUSTER LOGIC DOES NOT WORK !',/)
          CALL EXITRC(2)
        ENDIF
C
        IF (OBSCLS(IAMB).EQ.-1) OBSCLS(IAMB)=NEWCLS
        IF (OBSCLS(IAMB).LT.MINCLS) MINCLS=OBSCLS(IAMB)
100   CONTINUE
C
C SET ALL CONNECTED CLUSTER NUMBERS TO "MINCLS"
C ---------------------------------------------
      DO 610 ISING=1,NSING
        IAMB1 = IPAMB(ISING)
        IPAR1 = NPARN+IAMB1
        IOBCLS=OBSCLS(IAMB1)
C
C SET OBSERVATION CLUSTER NUMBER FOR CURRENT AMBIGUITY PARAMETERS
        OBSCLS(IAMB1)=MINCLS
C
C SET OBSERVATION CLUSTER FOR ALL AMBIGUITIES BELONGING INTO THE SAME
C CLUSTER AS THE CURRENT AMBIGUITY PARAMETER
        DO 620 IAMB2=1,NAMB
          IPAR2=NPARN+IAMB2
          IF (LOCQ(2,IPAR2).NE.LOCQ(2,IPAR1)) GOTO 620
          IF (OBSCLS(IAMB2).EQ.IOBCLS) OBSCLS(IAMB2)=MINCLS
620     CONTINUE
C
610   CONTINUE
C
C MAXIMUM CLUSTER NUMBER (NOT EQUAL TO THE NUMBER OF CLUSTERS !)
      ICLUST=0
      DO 630 IAMB=1,NAMB
        IF (OBSCLS(IAMB).GT.ICLUST) ICLUST=OBSCLS(IAMB)
630   CONTINUE
C
      RETURN
      END SUBROUTINE

      END MODULE
