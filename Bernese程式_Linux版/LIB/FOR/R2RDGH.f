      MODULE s_R2RDGH
      CONTAINS

C*
      SUBROUTINE R2RDGH(LFNNAV,LFNERR,MAXCOM,PRGNAM,RUNBY,
     1                  CRDATE,CRTIME,NCOM,COMENT,
     2                  CORSYS,CORDAT,LEAP,IRXVRS,IRCODE)
CC
CC NAME       :  R2RDGH
CC
CC PURPOSE    :  READ  THE ENTIRE HEADER INFORMATION OF A
CC               RINEX GLONASS NAVIGATION FILE
CC
CC PARAMETERS :
CC         IN :  LFNNAV : LOGICAL FILE NUMBER                  I*4
CC               LFNERR : LFN FOR ERROR MESSAGES               I*4
CC               MAXCOM : MAXIMUM NUMBER OF COMMENT LINES
CC        OUT :  PRGNAM : PROGRAM NAME                        CH*20
CC               RUNBY  : NAME OF AGENCY CREATING RINEX FILE  CH*20
CC               CRDATE : CREATION DATE                       CH*9
CC               CRTIME : CREATION TIME                       CH*5
CC               NCOM   : NUMBER OF COMMENT LINES              I*4
CC               COMENT : COMMENT LINES                       CH*60(*)
CC               CORSYS : CORRECTION TO SYSTEM TIME (s)        R*8
CC               CORDAT : DATE FOR CORSYS (MJD)                R*8
CC               IRXVRS : RINEX VERSION NUMBER                 I*4
CC               IRCODE : RETURN CODE                          I*4
CC                        0: OK
CC                        1: WRONG FILE TYPE
CC                        2: NOT ANTICIPATED VERSION NUMBER
CC                        3: END OF FILE WITHIN HEADER
CC                        4: READ ERROR
CC                        5: FIRST LINE NOT VERSION LINE
CC                        9: END OF FILE AT BEGINNING OF HEADER
CC
CC REMARKS    :  READ VERSION 1 HEADERS
CC
CC AUTHOR     :  W. GURTNER, H. HABRICH
CC
CC VERSION    :  4.1
CC
CC CREATED    :  26-FEB-97
CC
CC CHANGES    :   4-AUG-97 : ??: INCLUDE LEAP SECONDS
CC               30-DEC-97 : ??: FORMAT ERROR READING CORSYS
CC               21-JAN-98 : WG: ALLOW FOR "FLOATING" VERSION NUMBER
CC               10-AUG-98 : DI: READ 'SPECIAL' LANDAU RINEX FORMAT
CC               17-AUG-00 : TS: IRVERS INCREASED TO 210
CC               04-JAN-02 : HU: REMOVE ^M
CC               01-MAR-02 : DI: ADD ERR= TO ALL READ STATEMENTS
CC               23-JUN-05 : MM: IMPLICIT NONE AND DECLARATIONS ADDED
CC               07-JUL-08 : SS: IRVERS INCREASED TO 211
CC
CC COPYRIGHT  :  ASTRONOMICAL INSTITUTE
CC      1997     UNIVERSITY OF BERN
CC               SWITZERLAND
CC
C*
      USE f_djul
      USE s_upperc
      IMPLICIT NONE
C
C DECLARATIONS INSTEAD OF IMPLICIT
C --------------------------------
      INTEGER*4 IDAY  , IRCODE, IRVERS, IRX100, IRXVRS, ITEST , IYEAR ,
     1          JPGM  , LEAP  , LFNERR, LFNNAV, MAXCOM, MONTH , NCOM
C
      REAL*8    CORDAT, CORSYS, DAY
C
CCC       IMPLICIT REAL*8 (A-H,O-Z)
C
C GLOBAL DECLARATIONS
C -------------------
      CHARACTER*20 PRGNAM,RUNBY
      CHARACTER    COMENT(*)*60,CRDATE*9,CRTIME*5
C
C  LOCAL DECLARATIONS
C  ------------------
      CHARACTER    HEAD*20,RXTYPE*1,STRING*60,RXT*1
C
C RINEX FILE TYPE
      DATA RXTYPE/'G'/
C
C MAXIMUM RINEX FORMAT VERSION
C **** CHANGED TO 210 BECAUSE OF (UNALLOWED!) JAVAD VERSION NUMBER CHANGE ****
C **** CHANGED TO 211 BECAUSE OF (UNALLOWED!) TPS   VERSION NUMBER CHANGE ****
      DATA IRVERS/211/
C
      NCOM=0
      JPGM=0
C
C RECORD 1        RINEX VERSION / TYPE
101   READ(LFNNAV,222,END=990,ERR=940) STRING,HEAD
222   FORMAT(A60,A20)
      CALL UPPERC(STRING)
      CALL UPPERC(HEAD)
C REMOVE ^M
      ITEST=INDEX(STRING,CHAR(13))
      IF(ITEST.GT.0)STRING(ITEST:ITEST)=' '
      ITEST=INDEX(HEAD,CHAR(13))
      IF(ITEST.GT.0)HEAD(ITEST:ITEST)=' '
CCC   IF(HEAD.NE.'RINEX VERSION / TYPE') GOTO 950
      IF(INDEX(HEAD,'RINEX VERSION').EQ.0) GOTO 950
      IF(INDEX(STRING,'.').EQ.6) THEN
C SPECIAL 'LANDAU' RINEX FORMAT
        READ(STRING,10,ERR=940) IRXVRS,IRX100,RXT
10      FORMAT(I5,1X,I2,12X,A1)
      ELSE
C CORRECT RINEX FORMAT
        READ(STRING,1,ERR=940) IRXVRS,IRX100,RXT
1       FORMAT(I6,1X,I2,11X,A1)
      ENDIF
      IF(RXT.NE.RXTYPE) GOTO 910
      IRXVRS=IRXVRS*100+IRX100
      IF(IRXVRS.LE.0.OR.IRXVRS.GT.IRVERS) GOTO 920
C
C  LOOP OVER ALL REMAINING LINES
200   READ(LFNNAV,222,END=930,ERR=940) STRING,HEAD
      CALL UPPERC(STRING)
      CALL UPPERC(HEAD)
C REMOVE ^M
      ITEST=INDEX(STRING,CHAR(13))
      IF(ITEST.GT.0)STRING(ITEST:ITEST)=' '
      ITEST=INDEX(HEAD,CHAR(13))
      IF(ITEST.GT.0)HEAD(ITEST:ITEST)=' '
C
      IF(HEAD.EQ.'COMMENT') THEN
        IF(NCOM.EQ.MAXCOM) THEN
           WRITE(LFNERR,201)
201        FORMAT(' SR R2RDGH: TOO MANY COMMENT LINES')
        ELSE
          NCOM=NCOM+1
          COMENT(NCOM)=STRING
        END IF
C
C   PGM / RUN BY / DATE
      ELSEIF(HEAD.EQ.'PGM / RUN BY / DATE '.OR.
     1       HEAD.EQ.'PGM / RUN BY /DATE  ') THEN
        READ(STRING,2,ERR=940) PRGNAM,RUNBY,CRDATE,CRTIME
2       FORMAT(A20,A20,A9,1X,A5)
        JPGM=1
C
C  CORR TO SYSTEM TIME
      ELSEIF(HEAD.EQ.'CORR TO SYSTEM TIME')THEN
        READ(STRING,3,ERR=940) IYEAR,MONTH,IDAY,CORSYS
3       FORMAT(3I6,3X,D19.12)
        DAY=IDAY
        CORDAT=DJUL(IYEAR,MONTH,DAY)
        IF(IRXVRS.LE.200) CORSYS=-CORSYS
C
C  LEAP SECONDS
      ELSEIF(HEAD.EQ.'LEAP SECONDS')THEN
        READ(STRING,5,ERR=940) LEAP
5       FORMAT(I6)
C
C  BLANK LINE OR END OF HEADER
      ELSEIF ((HEAD.EQ.'END OF HEADER').OR.
     1        (IRXVRS.EQ.1.AND.STRING.EQ.' ')) THEN
        GOTO 300
      ELSE
C  UNKNOWN RECORD TYPE
        WRITE(LFNERR,941) STRING,HEAD
941     FORMAT(' SR R2RDGH: UNKNOWN HEADER RECORD TYPE FOUND:',
     1       /,1X,2A)
      END IF
C
C  READ NEXT RECORD
      GOTO 200
C
C  ARE ALL NECESSARY RECORDS READ?
300   IF(JPGM.EQ.0) WRITE(LFNERR,301) '"PGM / RUN BY / DATE "'
301   FORMAT(' SR R2RDGH: ',A,' RECORD MISSING')
C
      IRCODE=0
      GOTO 999
C
C  WRONG FILE TYPE
910   IRCODE=1
      WRITE(LFNERR,911) RXT
911   FORMAT(' SR R2RDGH: WRONG FILE TYPE: "',A,'"')
      BACKSPACE LFNNAV
      GOTO 999
C
C  NOT ANTICIPATED VERSION NUMBER
920   IRCODE=2
      WRITE(LFNERR,921) IRXVRS
921   FORMAT(' SR R2RDGH: NOT ANTICIPATED VERSION NUMBER:',I5)
      GOTO 999
C
C  END OF FILE WITHIN HEADER
930   IRCODE=3
      WRITE(LFNERR,931)
931   FORMAT(' SR R2RDGH: END OF FILE WITHIN HEADER')
      GOTO 999
C
C  ERROR DECODING DATA
940   IRCODE=4
      WRITE(LFNERR,942) STRING,HEAD
942   FORMAT(' SR R2RDGH: ERROR DECODING DATA ON LINE:',
     1       /,1X,2A)
      GOTO 999
C
C  FIRST LINE NOT VERSION LINE
950   IRCODE=5
      WRITE(LFNERR,951) STRING,HEAD
951   FORMAT(' SR R2RDGH: FIRST LINE NOT "VERSION"-LINE:',
     1       /,1X,2A)
      GOTO 999
C
C  END OF FILE AT FIRST RECORD
990   IRCODE=9
      GOTO 999
C
999   RETURN
      END SUBROUTINE

      END MODULE
