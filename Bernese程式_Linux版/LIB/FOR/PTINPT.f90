MODULE s_PTINPT
CONTAINS


! -------------------------------------------------------------------------
! Bernese GPS Software Version 5.1
! -------------------------------------------------------------------------

SUBROUTINE ptinpt(maxclk, title, iorsys, smin, ideg, &
                  rmBad, useAC, minAC, maxAC,cmcyn)

! -------------------------------------------------------------------------
! Purpose:    This is a new version of the old subroutine PTINPT.f that
!             reads the input options of the program PRETAB
!
! Author:     L. Mervart
!
! Created:    13-Apr-2000
! Last mod.:  03-Dec-2010
!
! Changes:    05-Nov-2001 MM: Included new options (use accuracy codes)
!                             Use check-routines
!             26-Oct-2002 HU: Remove bad satellites
!             23-Apr-2003 AJ: Nullify local pointers
!             14-Jan-2004 RD: Empty title (for SATCLK file)
!             18-Jul-2006 AG: CMCYN added
!             03-Dec-2010 RD: CMC for ATL added
!
! Copyright:  Astronomical Institute
!              University of Bern
!                  Switzerland
! -------------------------------------------------------------------------

  USE m_bern

  USE s_exitrc
  USE s_ckoptr
  USE s_ckoptb
  USE s_ckoptc
  USE s_readkeys
  USE s_ckopti
  USE s_ckoptl
  USE s_gtflna
  IMPLICIT NONE

! List of Parameters
! ------------------
  INTEGER(i4b)          :: maxclk  ! maximum number of clock coeff.
  CHARACTER(LEN=*)      :: title
  INTEGER(i4b)          :: iorsys  ! ref. system (=1: B1950.0, =2: J2000.0)
  REAL(r8b)             :: smin    ! time interval in minutes
  INTEGER(i4b)          :: ideg    ! polynomial degree
  LOGICAL               :: rmBad   ! Remove bad satellites
  LOGICAL               :: useAC   ! Flag for using accuracy codes
  INTEGER(i4b)          :: minAC   ! minimum accuracy code
  INTEGER(i4b)          :: maxAC   ! maximum accuracy code
  LOGICAL, DIMENSION(2) :: cmcyn   ! apply cmc
                                   ! 1: for OTL, 2. for ATL


! Local Variables
! ---------------
  CHARACTER(LEN=keyValueLength), DIMENSION(:), POINTER :: keyValue
  CHARACTER(LEN=fileNameLength)                        :: help
  LOGICAL                                              :: exclZ
  INTEGER(i4b)                                         :: irc, ircSum

  NULLIFY(keyValue)

  ircSum = 0
  title  = ' '
  sMin   = 0
  iDeg   = 0
  minAC  = -1
  maxAC  = 99


! Read all Options
! ----------------

! Reference system
  CALL readkeys('SYSTEM',keyValue,irc)
  CALL ckoptc(1,'SYSTEM',keyValue,(/'B1950','J2000'/),'ptinpt',           &
              'Reference system',irc,ircSum,                              &
              maxVal=1,valList=(/1,2/),result1=iorSys)

! Check if clock options should be read
  CALL readkeys('SATCLRS',keyValue,irc)
  CALL ckoptl(1,'SATCLRS',keyValue,'ptinpt',                              &
              'Satellite clock file',irc,ircSum,                          &
              empty='',result1=help)

  IF (help /= '') THEN
! Title for sat clock file
    CALL readkeys('TITLE',keyValue,irc)
    CALL ckoptl(1,'TITLE',keyValue,'ptinpt',                              &
                'Title line for satellite clock file',irc,ircSum,         &
                empty='Satellite file generated by PRETAB',result1=title)

! Interval
    CALL readkeys('INTERVAL',keyValue,irc)
    CALL ckoptr(1,'INTERVAL',keyValue,'ptinpt',                            &
                'Interval for polynomials',irc,ircSum,                     &
                result1=sMin)
    sMin = sMin*60d0

! Degree
    CALL readkeys('POLYDEGR',keyValue,irc)
    CALL ckopti(1,'POLYDEGR',keyValue,'ptinpt',                            &
                'Polynomial degree',irc,ircSum,                            &
                le=maxclk-1,result1=iDeg)
  ENDIF

! Set flag for applying CMC
  cmcyn = .FALSE.
  CALL gtflna(0,'OCNLOAD',help,irc)
  IF (irc == 0 .AND. LEN_TRIM(help) > 0) THEN
    CALL ckoptb(1,(/'CMCYN_O'/),'ptinpt',                                  &
                'Apply OTL CMC correction',ircSum,                         &
                resultL=cmcyn(1))
  ENDIF

! Set flag for applying CMC
  CALL gtflna(0,'ATMLOAD',help,irc)
  IF (irc == 0 .AND. LEN_TRIM(help) > 0) THEN
    CALL ckoptb(1,(/'CMCYN_A'/),'ptinpt',                                  &
                'Apply ATL CMC correction',ircSum,                         &
                resultL=cmcyn(2))
  ENDIF

! Set flag for removing of bad satellites
  CALL ckoptb(1,(/'RMBAD'/),'ptinpt',                                      &
              'Remove bad satellites',ircSum,                              &
              resultL=rmBad)

! Set flag for accuracy codes
  CALL ckoptb(1,(/'USEAC'/),'ptinpt',                                      &
              'Use accuracy codes from precise orbit',ircSum,              &
              resultL=useAC)

  IF (useAC) THEN

! Exclude satellites witch AC 0
    CALL ckoptb(1,(/'MINAC'/),'ptinpt',                                    &
                'Exclude satellites with AC 0',ircSum,                     &
                resultL=exclZ)
    IF (exclZ) minAC = 1

!Maximum accuracy code
    CALL readKeys('MAXAC',keyValue,irc)
    CALL ckopti(1,'MAXAC',keyValue,'ptinpt',                               &
                'Maximum accuracy code allowed',irc,ircSum,                &
                ge=0,result1=maxAC)
  ENDIF


! On error exit
! -------------
  IF (ircSum /= 0) CALL exitrc(2)

  DEALLOCATE(keyValue,stat=irc)

! END
! ---
END SUBROUTINE ptinpt

END MODULE
