      MODULE s_REDEPO
      CONTAINS

C*
      SUBROUTINE REDEPO(SIGAPR,SIGDI1,OPTDIP,NFLSES,FILNUM,NPAR  ,
     1                  NPAEPO,NPARMS,PARLST,ANOR  ,BNOR  ,LOCQ  ,
     2                  PARTYP,RMS   ,RMSSES,PARFLG)
CC
CC NAME       :  REDEPO
CC
CC PURPOSE    :  ELIMINATE DIFF. ION. PARAMETERS OF CURRENT EPOCH
CC               THAT HAVE NOT BEEN OBSERVED, ADD THE A PRIORI
CC               WEIGHTS TO THE NEQ MATRIX, AND PRE-ELIMINATE
CC               DIFF. ION. PARAMETERS OF CURRENT EPOCH
CC
CC PARAMETERS :
CC         IN :  SIGAPR : A PRIORI SIGMA                      R*8
CC               SIGDI1 : A PRIORI SIGMA FOR DIFF. ION. PAR.  R*8
CC               OPTDIP : OPTIONS FOR DIFF. ION. PARAMETERS   I*4(3)
CC                        (1): =0: NO DIFF. ION. PARAMETERS
CC                             =1: ONE PAR. PER EPOCH AND SAT.
CC                             =2: PARAMETERS EPOCH-WISE PRE-
CC                                 ELIMINATED
CC                        (2): ELIMINATION OF REF. ION. PAR.
CC                        (3): ELEV.-DEP. PAR. CONSTRAINING
CC               NFLSES : TOTAL NUMBER OF FILES (IN SESSION)  I*4
CC               FILNUM(I),I=1,..,NFLSES: FILE NUMBERS        I*4
CC     IN/OUT :  NPAR   : TOTAL NUMBER OF PARAMETERS NOT YET  I*4
CC                        ELIMINATED
CC               NPAEPO : NUMBER OF EPOCH SPECIFIC PARAMETERS I*4
CC               NPARMS : NUMBER OF PARAMETERS TO CALCULATE   I*4
CC                        RMS ERROR
CC               PARLST(I,K), I=1,..,5,K=1,..,MAXTYP: NUMBER  I*4
CC                        OF PARAMETERS:
CC                        I=1: #PARAMETERS OF TYPE I (NPARMS)
CC                        I=2: #SET-UP
CC                        I=3: #NO-OBS
CC                        I=4: #REF. PARAMETERS
CC                        I=5: #SINGULAR
CC               ANOR(I),I=1,..,NPAR*(NPAR+1)/2               R*8
CC               BNOR(I),I=1,..,NPAR                          R*8
CC               LOCQ(K,I),K=1,..,MAXLCQ,I=1,..,NPAR:         I*4
CC                       PARAMETER CHARACTERIZATION
CC               PARTYP: PARAMETER DESCRIPTION                t_partyp(*)
CC               RMS   : SUM OF RESIDUALS SQUARES             R*8
CC               RMSSES: SESSION SPECIFIC SUM OF (O-C)**2     R*8
CC               PARFLG(K),K=1,..NPAR: FLAG FOR SINGULAR PAR. I*4
CC                        =0 : PARAMETER NOT SINGULAR
CC                        =1 : PARAMETER SINGULAR
CC
CC REMARKS    :  ---
CC
CC AUTHOR     :  S.SCHAER, L.MERVART
CC
CC VERSION    :  3.5
CC
CC CREATED    :  11-APR-94
CC
CC CHANGES    :  14-APR-94 : SS: ELIMINATION OF REF. ION. PARAMETERS
CC               01-JUL-94 : SS: SELECT REFERENCES WITH OBSERV. ONLY
CC               25-JUL-94 : MR: PRE-ELIMINATION STATISTICS SIMPLIFIED
CC               27-JUL-94 : MR: SYMING INSTEAD OF SYMIN8, "PARFLG"
CC               05-AUG-94 : MR: UPDATE "NPARMS" FOR SINGULAR PARAM.
CC               10-AUG-94 : MR: CALL EXITRC
CC               04-MAY-95 : GB,MR: TURBO VERSION BASED ON SR REDTRB
CC               08-JUN-95 : LM: KINEMATIC COORDINATES
CC               22-AUG-95 : SS: REMOVAL OF ALL NO-OBS PARAMETERS
CC               04-SEP-95 : SS: "SIGDI1" GIVEN ON SINGLE DIFF. LEVEL
CC               31-OCT-95 : SS: USE EXTERNAL FILE NUMBERS "FILNUM"
CC               27-MAR-96 : TS: CHANGES FOR EPOCH WISE CLOCK SOLUTIONS
CC               19-JUL-96 : TS: IMPROVED "###" OUTPUTS
CC               29-AUG-96 : TS: OUTPUT OF "REAL" TIME FOR THE CLOCKS
CC               05-AUG-97 : SS: ELEV.-DEP. SIP CONSTRAINING
CC               12-AUG-97 : TS: SMALL CHANGES FOR CLOCKS
CC               08-SEP-97 : SS: ESTIMATE ZENITH SIP PARAMETERS
CC               25-JAN-00 : TS: REMOVED OUTPUT OF CLOCK ESTIMATES
CC               23-AUG-01 : DS: CONSTRAINTS FOR KIN. COORDINATES
CC               01-DEC-01 : MR/DS: CHECK A0I DIMENSION
CC               28-MAY-03 : HU: USE M_MAXDIM
CC               22-JUL-03 : RD: MOVE CONSTRAINING OF KIN.POS. TO SR WGTKIN
CC               21-OCT-04 : RD: ALLOCATE A0I, AII, IDEL LOCALLY
CC               16-JUN-05 : MM: UNUSED COMCONST.inc REMOVED
CC               21-JUN-05 : MM: COMLFNUM.inc REMOVED
CC               23-JUN-05 : MM: IMPLICIT NONE AND DECLARATIONS ADDED
CC               16-NOV-10 : RD: RED_PARTYP ADDED
CC               10-JUL-12 : RD: USE SYMINVG INSTEAD OF SYMING
CC               10-JUL-12 : RD: USE M_BERN WITH ONLY
CC
CC COPYRIGHT  :  ASTRONOMICAL INSTITUTE
CC      1994     UNIVERSITY OF BERN
CC               SWITZERLAND
CC
C*
      USE M_BERN,   ONLY: r8b, LFNERR
      USE P_GPSEST, ONLY: MAXLCQ, T_PARTYP, RED_PARTYP
      USE f_ikf
      USE s_alcerr
      USE s_syminvg
      USE s_updpar
      USE s_redtrb
      USE s_exitrc
      USE s_imatrd
      USE s_dmatrd
      IMPLICIT NONE
C
C DECLARATIONS INSTEAD OF IMPLICIT
C --------------------------------
      INTEGER*4 I     , IAC   , IFIL  , IFNUM , II    , IK    , IK1   ,
     1          IK2   , IONE  , IP    , IPAR  , IPIP  , IRIP  ,
     2          ISING , ISIX  , ITYP  , K     , MAXRIP, MZDMAX, MZDMIN,
     3          MZDTST, NAMEFF, NDEL  , NFLSES, NPAEPO, NPAR  , NPARMS,
     4          NPARNE, NPNEW , NUMRIP
C
      REAL*8    RMS   , RMSOLD, RMSSES, SIGAPR, SIGDI1, WGTABS
C
CCC       IMPLICIT REAL*8 (A-H,O-Z)
C
      PARAMETER (MAXRIP=250)
C
      TYPE(T_PARTYP), DIMENSION(:) :: partyp
C
      CHARACTER(LEN=6), PARAMETER  :: SRNAME = 'REDEPO'
C
      INTEGER*4    LOCQ(MAXLCQ,*),IDEL(NPAR),OPTDIP(*)
      INTEGER*4    REFDIP(MAXRIP),PARLST(5,*),PARFLG(*)
      INTEGER*4    FILNUM(*)
C
      REAL*8       ANOR(*),BNOR(*)
      REAL(r8b),   DIMENSION(:), ALLOCATABLE :: A0I,AII
C
C
C NO EPOCH PARAMETERS
C -------------------
      IF (NPAEPO.EQ.0) GOTO 999
C
      NPARNE=NPAR-NPAEPO
C
C PART 1: ELIMINATE DIFF. ION. PARAMETERS OR KINEMATIC COORDINATES
C         WHICH HAVE NOT BEEN OBSERVED IN CURRENT EPOCH
C ----------------------------------------------------------------
      NDEL=0
      NAMEFF=0
      DO 100 IPAR=1,NPAR
        IDEL(IPAR)=0
100   CONTINUE
C
      DO 200 IPAR=NPARNE+1,NPAR
        ITYP=LOCQ(1,IPAR)
        II=IKF(IPAR,IPAR)
        IF(ANOR(II).NE.0.D0)THEN
          NAMEFF=NAMEFF+1
        ELSE
          NDEL=NDEL+1
          IDEL(IPAR)=1
          PARLST(1,ITYP)=PARLST(1,ITYP)-1
          PARLST(3,ITYP)=PARLST(3,ITYP)+1
        END IF
200   CONTINUE
      NPARMS=NPARMS-NDEL
C
C PART 2: ELIMINATION OF REFERENCE IONOSPHERE PARAMETERS
C ------------------------------------------------------
C
      IF (OPTDIP(1) .EQ. 2) THEN
C
C SELECT REFERENCE IONOSPHERE PARAMETERS
        NUMRIP=0
        MZDMAX=324000
        DO 805 IFIL=1,NFLSES
          IFNUM=FILNUM(IFIL)
          MZDMIN=MZDMAX
          DO 815 IPAR=NPARNE+1,NPAR
            IF (LOCQ(1,IPAR).EQ.17 .AND. LOCQ(2,IPAR).EQ.IFNUM) THEN
              MZDTST=LOCQ(6,IPAR)
              IF (MZDTST.LT.MZDMIN .AND. IDEL(IPAR).EQ.0) THEN
                MZDMIN=MZDTST
                IRIP=IPAR
              ENDIF
            ENDIF
815       CONTINUE
          IF (MZDMIN.LT.MZDMAX) THEN
            NUMRIP=NUMRIP+1
            REFDIP(NUMRIP)=IRIP
          ENDIF
805     CONTINUE
C
C INCREASE DEGREE OF FREEDOM (NPARMS) IF DIFF. IONOSPHERE PARAMETERS
C ARE ESTIMATED ON THE SINGLE-DIFF. LEVEL (REFERENCES KEPT)
        NPARMS=NPARMS-NUMRIP
        IF (OPTDIP(2).EQ.0) NUMRIP=0
C
C UPDATE NUMBER OF PARAMETERS
        PARLST(1,17)=PARLST(1,17)-NUMRIP
        PARLST(4,17)=PARLST(4,17)+NUMRIP
C
C CHECK MAXIMUM NUMBER OF REFERENCE IONOSPHERE PARAMETERS
        IF(NUMRIP.GT.MAXRIP)THEN
          WRITE(LFNERR,906) NUMRIP,MAXRIP
906       FORMAT(/,' *** SR REDEPO: TOO MANY REFERENCE IONOSPHERE ',
     1           'PARAMETERS',/,
     2           16X,'NUMBER OF PARAMETERS:',I5,/,
     3           16X,'MAXIMUM NUMBER      :',I5,/)
          CALL EXITRC(2)
        ENDIF
C
C ELIMINATE REFERENCE IONOSPHERE PARAMETERS
        IF (OPTDIP(2).EQ.1) THEN
          DO 820 IRIP=1,NUMRIP
            DO 825 IPAR=NPARNE+1,NPAR
              IF (LOCQ(1,IPAR).EQ.17                   .AND.
     1            LOCQ(2,IPAR).EQ.LOCQ(2,REFDIP(IRIP)) .AND.
     2            LOCQ(3,IPAR).EQ.LOCQ(3,REFDIP(IRIP))) THEN
                IDEL(IPAR)=1
                NDEL=NDEL+1
                NAMEFF=NAMEFF-1
              ENDIF
825         CONTINUE
820       CONTINUE
        ENDIF
      END IF
C
C REMOVAL OF REFERENCES AND PARAMETERS WITH NO OBSERVATIONS
      IF (NDEL.GT.0) THEN
        CALL DMATRD('S',NPAR,NPAR,IDEL,ANOR,NPNEW,NPNEW)
        CALL DMATRD('R',NPAR,1,IDEL,BNOR,NPNEW,IONE)
        CALL IMATRD('C',MAXLCQ,NPAR,IDEL,LOCQ,ISIX,NPNEW)
C
        CALL red_parTyp(NPAR,IDEL,PARTYP)
C
        NPAR=NPAR-NDEL
        NPAEPO=NAMEFF
      END IF
C
C PART 3: ADD A PRIORI WEIGHTS TO NEQ MATRIX
C ------------------------------------------
C
C ABSOLUTE CONSTRAINTS FOR DIFFERENTIAL IONOSPHERE PARAMETERS
      IF (OPTDIP(1).EQ.2 .AND. SIGDI1.NE.0.D0) THEN
        WGTABS=(SIGAPR/SIGDI1)**2
C
C COMPUTE ABSOLUTE WEIGHT ON DOUBLE DIFFERENCE LEVEL
        IF (OPTDIP(2).EQ.1) WGTABS=WGTABS/2.D0
C
        DO 110 IP=NPARNE+1,NPAR
          IF (LOCQ(1,IP).EQ.17) THEN
            IPIP=IKF(IP,IP)
            ANOR(IPIP)=ANOR(IPIP)+WGTABS
          ENDIF
110     CONTINUE
      ENDIF
C
C PART 4: PRE-ELIMINATE EPOCH-SPECIFIC PARAMETERS OF CURRENT EPOCH
C ----------------------------------------------------------------
      ALLOCATE(AII(IKF(NPAEPO,NPAEPO)),STAT=IAC)
      CALL ALCERR(IAC,'AII',(/IKF(NPAEPO,NPAEPO)/),SRNAME)
      AII=0D0
C
      ALLOCATE(A0I(NPAEPO*(NPAR-NPAEPO)),STAT=IAC)
      CALL ALCERR(IAC,'A0I',(/NPAEPO*(NPAR-NPAEPO)/),SRNAME)
      A0I=0D0
C
C GENERATE TWO AUXILIARY MATRICES
      IK1=0
      IK2=0
      DO 300 K=NPARNE+1,NPAR
        DO 350 I=1,K
          IK=IKF(I,K)
          IF (I.LE.NPARNE) THEN
            IK1=IK1+1
            A0I(IK1)=ANOR(IK)
          ELSE
            IK2=IK2+1
            AII(IK2)=ANOR(IK)
          END IF
350     CONTINUE
300   CONTINUE
C
C INVERT AII
C ----------
      CALL SYMINVG(NPAEPO,AII,1,ISING,PARFLG)
C
C UPDATE NUMBER OF SINGULAR PARAMETERS IN "PARLST"
C ------------------------------------------------
      CALL UPDPAR(0,NPAEPO,LOCQ(1,NPARNE+1),IDEL,PARFLG,NPARMS,PARLST)
C
C RE-DEFINE IDEL
C --------------
      DO 310 IPAR=1,NPAR
        IF(IPAR.LE.NPARNE)THEN
          IDEL(IPAR)=0
        ELSE
          IDEL(IPAR)=1
        END IF
310   CONTINUE
C
C REDUCE RMS, BNOR, REDPAR USING SR REDTRB
C ----------------------------------------
      RMSOLD=RMS
      CALL REDTRB(NPAR,A0I,AII,IDEL,NPARNE,ANOR,BNOR,RMS)
      RMSSES=RMSSES+(RMS-RMSOLD)
C
      NPAR=NPAR-NPAEPO
C
      DEALLOCATE(AII,STAT=IAC)
      DEALLOCATE(A0I,STAT=IAC)
C
999   RETURN
      END SUBROUTINE

      END MODULE
