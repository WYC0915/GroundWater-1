      MODULE s_R2WTGH
      CONTAINS

C*
      SUBROUTINE R2WTGH(LFNNAV,LFNERR,PRGNAM,RUNBY,
     1                  CRDATE,CRTIME,NCOM,COMENT,
     3                  CORSYS,CORDAT,LEAP,IRXVRS,IRCODE)
CC
CC NAME       :  R2WTGH
CC
CC PURPOSE    :  WRITE THE ENTIRE HEADER INFORMATION OF A
CC               RINEX GLONASS NAVIGATION FILE
CC
CC PARAMETERS :
CC         IN :  LFNNAV : LOGICAL FILE NUMBER                  I*4
CC               LFNERR : LFN FOR ERROR MESSAGES               I*4
CC               PRGNAM : PROGRAM NAME                        CH*20
CC               RUNBY  : NAME OF AGENCY CREATING RINEX FILE  CH*20
CC               CRDATE : CREATION DATE                       CH*9
CC               CRTIME : CREATION TIME                       CH*5
CC               NCOM   : NUMBER OF COMMENT LINES              I*4
CC               COMENT : COMMENT LINES                       CH*60(*)
CC               CORSYS : CORRECTION TO SYSTEM TIME (S)        R*8
CC               CORDAT : DATE FOR CORSYS  (MJD)               R*8
CC               LEAP   : LEAP SECONDS                         I*4
CC               IRXVRS : RINEX VERSION NUMBER                 I*4
CC               IRCODE : RETURN CODE (0=OK)                   I*4
CC
CC REMARKS    :  WRITES VERSION 2 HEADERS
CC
CC AUTHOR     :  W. GURTNER
CC
CC VERSION    :  3.4
CC
CC CREATED    :  13-MAR-93
CC
CC CHANGES    :  30-AUG-93 : ??: INCLUDE CORSYS,CORDAT INTO HEADER
CC               23-FEB-97 : ??: INCLUDE LEAP SECONDS  INTO HEADER
CC               21-JAN-97 : ??: ALLOW FOR "FLOATING NUMBER" RINEX VERSION
CC                               IF IRXVRS > 100 THEN FLTVRS = 0.01*IRXVRS
CC               23-JUN-05 : MM: IMPLICIT NONE AND DECLARATIONS ADDED
CC
CC COPYRIGHT  :  ASTRONOMICAL INSTITUTE
CC      1993     UNIVERSITY OF BERN
CC                SWITZERLAND
CC
C*
      USE s_jmt
      IMPLICIT NONE
C
C DECLARATIONS INSTEAD OF IMPLICIT
C --------------------------------
      INTEGER*4 I     , IDAY  , IRCODE, IRXVRS, IYEAR , LEAP  , LFNERR,
     1          LFNNAV, MONTH , NCOM
C
      REAL*8    CORDAT, CORSYS, DAY
C
CCC       IMPLICIT REAL*8 (A-H,O-Z)
C
C GLOBAL DECLARATIONS
C -------------------
      CHARACTER*20 PRGNAM,RUNBY
      CHARACTER    COMENT(*)*60,CRDATE*9,CRTIME*5
C
C  LOCAL DECLARATIONS
C  ------------------
      CHARACTER    HEADRC(6)*20,RXTYPE*20
C
      DATA HEADRC/'RINEX VERSION / TYPE',
     2            'PGM / RUN BY / DATE ',
     3            'COMMENT             ',
     4            'CORR TO SYSTEM TIME ',
     5            'LEAP SECONDS        ',
     6            'END OF HEADER       '/
C
C RINEX FILE TYPE
      DATA RXTYPE/'GLONASS NAV DATA    '/
C
C RECORD 1        RINEX VERSION / TYPE
      IF(IRXVRS.LT.100) THEN
        WRITE(LFNNAV,1) IRXVRS,RXTYPE,HEADRC(1)
1       FORMAT(I6,14X,A20,20X,A20)
      ELSE
        WRITE(LFNNAV,11) 0.01*IRXVRS,RXTYPE,HEADRC(1)
11       FORMAT(F9.2,11X,A20,20X,A20)
      END IF
C
C RECORD 2        PGM / RUN BY / DATE
      WRITE(LFNNAV,2) PRGNAM,RUNBY,CRDATE,CRTIME,HEADRC(2)
2     FORMAT(A20,A20,A9,1X,A5,5X,A20)
C
C RECORD 3        COMMENT
      IF(NCOM.GT.1.OR.COMENT(1).NE.' ') THEN
        DO 300 I=1,NCOM
          WRITE(LFNNAV,3) COMENT(I),HEADRC(3)
3         FORMAT(A60,A20)
300     CONTINUE
      END IF
C
C RECORD 4        CORRECTION TO SYSTEM TIME
      IF(CORDAT.NE.0.D0) THEN
        CALL JMT(CORDAT+1.D-6,IYEAR,MONTH,DAY)
        IDAY=IDINT(DAY)
        WRITE(LFNNAV,4) IYEAR,MONTH,IDAY,CORSYS,HEADRC(4)
4       FORMAT(3I6,3X,D19.12,20X,A20)
      END IF
C
C RECORD 5        LEAP SECONDS
      IF(LEAP.NE.0) THEN
        WRITE(LFNNAV,5) LEAP,HEADRC(5)
5       FORMAT(I6,54X,A20)
      END IF
C
C END OF HEADER LINE
      WRITE(LFNNAV,6) HEADRC(6)
6     FORMAT(60X,A20)
C
900   IRCODE=0
      RETURN
      END SUBROUTINE

      END MODULE
