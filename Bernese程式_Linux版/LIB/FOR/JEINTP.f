      MODULE s_JEINTP
      CONTAINS

C*
      SUBROUTINE JEINTP(BUF,T,NCF,NCM,NA,FL,PV)
CC
CC NAME       :  JEINTP
CC
CC PURPOSE    :  THIS SUBROUTINE DIFFERENTIATES AND INTERPOLATES
CC               A SET OF CHEBYSHEV COEFFICIENTS TO GIVE POSITION
CC               AND VELOCITY
CC
CC PARAMETERS :
CC        IN  :  BUF(NCF,NCM,*) : 1ST LOCATION OF ARRAY OF     R*8
CC                        CHEBYSHEV COEFFICIENTS OF POSITION
CC               VAL    : ARRAY OF VALUES OF CONSTANTS         R*8
CC               T      : T(1) IS DP FRACTIONAL TIME IN        R*8(2)
CC                        INTERVAL COVERED BY COEFFICIENTS AT
CC                        WHICH INTERPOLATION IS WANTED
CC                        (0 .LE. T(1) .LE. 1).
CC                        T(2) IS DP LENGTH OF WHOLE INTERVAL
CC                        IN INPUT TIME UNITS.
CC               NCF    : # OF COEFFICIENTS PER COMPONENT      I*4
CC               NCM    : # OF COMPONENTS PER SET OF           I*4
CC                        COEFFICIENTS
CC               NA     : # OF SETS OF COEFFICIENTS IN FULL    I*4
CC                        ARRAY (I.E., # OF SUB-INTERVALS IN
CC                        FULL INTERVAL)
CC               FL     : FLAG: =1 FOR POSITIONS ONLY          I*4
CC                              =2 FOR POS AND VEL
CC        OUT :  PV     : INTERPOLATED QUANTITIES REQUESTED.   R*8
CC                        DIMENSION EXPECTED IS PV(NCM,FL).
CC
CC
CC REMARKS    :  ---
CC
CC AUTHOR     :  JPL
CC
CC VERSION    :  3.4
CC
CC CREATED    :  22-OCT-92
CC
CC CHANGES    :  23-JUN-05 : MM: IMPLICIT NONE AND DECLARATIONS ADDED
CC               04-MAY-12 : RD: USE DMOD FROM MODULE
CC
CC COPYRIGHT  :  NONE
CC      1992
CC
C*
      USE l_basfun, ONLY: dmod
      IMPLICIT NONE
C
C DECLARATIONS INSTEAD OF IMPLICIT
C --------------------------------
      INTEGER*4 I   , J   , L
C
      REAL*8    DNA , DT1 , TC  , TEMP, VFAC
C
CCC       IMPLICIT REAL*8 (A-H,O-Z)
C
C     DECLARATIONS OF SUBROUTINE ARGUMENTS
C
      INTEGER*4 NCF,NCM,NA,FL
      REAL*8    BUF(NCF,NCM,*),T(2),PV(NCM,*)
C
      INTEGER*4 NP,NV
      REAL*8    TWOT,PC(18),VC(18)
C
      DATA NP/2/
      DATA NV/3/
      DATA TWOT/0.D0/
      DATA PC(1),PC(2)/1.D0,0.D0/
      DATA VC(2)/1.D0/
C
C     ENTRY POINT. GET CORRECT SUB-INTERVAL NUMBER FOR THIS SET
C     OF COEFFICIENTS AND THEN GET NORMALIZED CHEBYSHEV TIME
C     WITHIN THAT SUBINTERVAL.
C
      DNA=DBLE(NA)
      DT1=DINT(T(1))
      TEMP=DNA*T(1)
      L=IDINT(TEMP-DT1)+1
C
C     TC IS THE NORMALIZED CHEBYSHEV TIME (-1 .LE. TC .LE. 1)
C
      TC=2.D0*(DMOD(TEMP,1.D0)+DT1)-1.D0
C
C     CHECK TO SEE WHETHER CHEBYSHEV TIME HAS CHANGED,
C     AND COMPUTE NEW POLYNOMIAL VALUES IF IT HAS.
C     (THE ELEMENT PC(2) IS THE VALUE OF T1(TC) AND HENCE
C     CONTAINS THE VALUE OF TC ON THE PREVIOUS CALL.)
C
      IF(TC.NE.PC(2)) THEN
        NP=2
        NV=3
        PC(2)=TC
        TWOT=TC+TC
      ENDIF
C
C     BE SURE THAT AT LEAST 'NCF' POLYNOMIALS HAVE BEEN EVALUATED
C     AND ARE STORED IN THE ARRAY 'PC'.
C
      IF(NP.LT.NCF) THEN
        DO 1 I=NP+1,NCF
          PC(I)=TWOT*PC(I-1)-PC(I-2)
    1   CONTINUE
        NP=NCF
      ENDIF
C
C     INTERPOLATE TO GET POSITION FOR EACH COMPONENT
C
      DO 2 I=1,NCM
        PV(I,1)=0.D0
        DO 3 J=NCF,1,-1
          PV(I,1)=PV(I,1)+PC(J)*BUF(J,I,L)
    3   CONTINUE
    2 CONTINUE
      IF(FL.LE.1) RETURN
C
C     IF VELOCITY INTERPOLATION IS WANTED, BE SURE ENOUGH
C     DERIVATIVE POLYNOMIALS HAVE BEEN GENERATED AND STORED.
C
      VFAC=(DNA+DNA)/T(2)
      VC(3)=TWOT+TWOT
      IF(NV.LT.NCF) THEN
        DO 4 I=NV+1,NCF
          VC(I)=TWOT*VC(I-1)+PC(I-1)+PC(I-1)-VC(I-2)
    4   CONTINUE
        NV=NCF
      ENDIF
C
C     INTERPOLATE TO GET VELOCITY FOR EACH COMPONENT
C
      DO 5 I=1,NCM
        PV(I,2)=0.D0
        DO 6 J=NCF,2,-1
          PV(I,2)=PV(I,2)+VC(J)*BUF(J,I,L)
    6   CONTINUE
        PV(I,2)=PV(I,2)*VFAC
    5 CONTINUE
C
      RETURN
C
      END SUBROUTINE

      END MODULE
