      MODULE s_NMFWET
      CONTAINS

C*
      SUBROUTINE NMFWET(LAT,ZEN,WMF)
CC
CC NAME       :  NMFWET
CC
CC PURPOSE    :  ROUTINE TO COMPUTE THE NIELL MAPPING FUNCTION (NMF)
CC               FOR THE WET COMPONENT. SEE ALSO SR NMFDRY FOR THE
CC               HYDROSTATIC (DRY) COMPONENT.
CC
CC PARAMETERS :
CC         IN :  LAT    : LATITUDE OF STATION IN RAD          R*8
CC               ZEN    : ZENITH DISTANCE IN RAD              R*8
CC        OUT :  WMF    : MAPPING FUNCTION FOR WET DELAY      R*8(2)
CC                        (1): MAPPING FACTOR
CC                        (2): DERIVATIVE WITH RESPECT TO
CC                             ZENITH DISTANCE (IN 1/RAD)
CC
CC REMARKS    :  THIS SUBROUTINE IS A SLIGHTLY MODIFIED VERSION OF
CC               NIELL'S NMFH2 ROUTINE. REFERENCE: A.E.NIELL, GLOBAL
CC               MAPPING FUNCTIONS FOR THE ATMOSPHERE DELAY AT RADIO
CC               WAVELENGTHS, JGR, VOL. 101, NO. B2, PP. 3227-3246,
CC               1996.
CC
CC AUTHOR     :  S.SCHAER
CC
CC VERSION    :  4.1
CC
CC CREATED    :  31-JAN-97
CC
CC CHANGES    :  11-FEB-97 : SS: SIGN OF DERIVATIVE
CC               28-FEB-07 : AG: USE PI FROM DEFCON
CC
CC COPYRIGHT  :  ASTRONOMICAL INSTITUTE
CC      1997     UNIVERSITY OF BERN
CC               SWITZERLAND
CC
C*
      USE d_const, ONLY: pi
      IMPLICIT NONE
      REAL*8 LAT,ZEN
C
C NEW AEN 930517 ROUTINE TO COMPUTE THE NEW WMF2.0 MAPPING FUNCTION WHICH
C                DEPENDS ONLY ON LATITUDE.
C
      INTEGER*4 I
C
C   A,B,C       - THE A,B,AND C COEFFICIENTS IN THE CONTINUED FRACTION
C                 FORM OF MARINI
C   BETA        - INTERMEDIATE TERM IN CALCULATION
C   GAMMA       - INTERMEDIATE TERM IN CALCULATION
C   SINE        - SINE OF ELEVATION ANGLE
C   COSE        - COS OF ELEVATION ANGLE
C   WMF(1)      - WET DELAY MAPPING FUNCTION
C   WMF(2)      - D_WET_MAPPING_FUNCTION/D_ELEVATION
C   TOPCON      - CONSTANT OF TOP OF MAPPING FUNCTION TO ENSURE
C                 THAT VALUE IS 1.0000 AT ZENITH
C
      REAL*8 A,B,C, BETA, COSE, WMF(2), GAMMA, SINE, TOPCON
C
C   LATITUDE   - LATITUDE (DEGREES)
C   L          - ABSOLUTE LATITUDE
C   DL         - INCREMENTAL LATITUDE FROM LAST LAT_WMF
C   ELEV       - ELEVATION (DEGREES)
C   DL,DA,DB,DC  - USED FOR INTERPOLATION
C
      REAL*8 LAT_WMF(5), ABC_W2P0(5,3)
      REAL*8 DL, DA, DB, DC
      REAL*8 LATITUDE, L, ELEV, DEG2RAD
C
C   DEFINE PARAMETERS USED FOR CALCULATING COEFFICIENTS.
C
      DATA LAT_WMF / 15.D0, 30.D0, 45.D0, 60.D0, 75.D0 /
C
C   COEFFICIENTS ARE FROM FITS TO RAYTRACES OF THE STANDARD ATMOSPHERES
C   FOR JULY FOR LATITUDES 15, 45, 60, AND 75 DEGREES LATITUDE AND FOR
C   JANUARY FOR 30 DEGREES LATITUDE (930517).
C
      DATA ABC_W2P0 /
     . 5.8021897D-4,5.6794847D-4,5.8118019D-4,5.9727542D-4,6.1641693D-4,
     . 1.4275268D-3,1.5138625D-3,1.4572752D-3,1.5007428D-3,1.7599082D-3,
     . 4.3472961D-2,4.6729510D-2,4.3908931D-2,4.4626982D-2,5.4736038D-2/
C
      DEG2RAD = PI/180.D0
C
      LATITUDE=LAT/DEG2RAD
      ELEV=90.D0-ZEN/DEG2RAD
C
      L = DABS(LATITUDE)
C
C   COEFFICIENTS FOR THE CONTINUED FRACTION EXPANSION FOR EACH LATITUDE.
C
C   FOR LATITUDES LESS THAN 15 DEGREES:
C
      IF (L .LE. LAT_WMF(1)) THEN
         A = ABC_W2P0(1,1)
         B = ABC_W2P0(1,2)
         C = ABC_W2P0(1,3)
      ENDIF
C
C   FOR LATITUDES BETWEEN 15 AND 75  DEGREES:
C
      DO I = 1,4
          IF (L .GT. LAT_WMF(I) .AND. L .LE. LAT_WMF(I+1)) THEN
             DL = (L-LAT_WMF(I))/(LAT_WMF(I+1)-LAT_WMF(I))
             DA  =   ABC_W2P0(I+1,1)-ABC_W2P0(I,1)
             A   =   ABC_W2P0(I,1) + DL*DA
C
             DB  =   ABC_W2P0(I+1,2)-ABC_W2P0(I,2)
             B   =   ABC_W2P0(I,2) + DL*DB
C
             DC  =   ABC_W2P0(I+1,3)-ABC_W2P0(I,3)
             C   =   ABC_W2P0(I,3) + DL*DC
          ENDIF
      END DO
C
C   FOR LATITUDES GREATER THAN 75 DEGREES:
C
      IF (L .GE. LAT_WMF(5)) THEN
         A = ABC_W2P0(5,1)
         B = ABC_W2P0(5,2)
         C = ABC_W2P0(5,3)
      ENDIF
C
C   NOW THE COEFFICIENTS EXIST; CALCULATE THE MAPPING FUNCTION, WMF(1),
C       AND THE CHANGE OF MAPPING FUNCTION WITH ELEVATION,
C       DWMF/D_EL =WMF(2).
C   TO CALCULATE THE DELAY-RATE CORRECTION, D_TAU/DT:
C       D_TAU/DT = D_TAU_ZEN/DT * WMF(1) + TAU_ZEN * DWMF/D_EL * D_EL/DT
C
      SINE  = DSIN( ELEV * DEG2RAD)
      COSE  = DCOS( ELEV * DEG2RAD)
      BETA  = B/( SINE + C )
      GAMMA = A/( SINE + BETA)
      TOPCON = (1.D0 + A/(1.D0 + B/(1.D0 + C)))
C
      WMF(1) = TOPCON / ( SINE + GAMMA )
C
      WMF(2) = -TOPCON / ( SINE + GAMMA )**2 *
     .         ( COSE - A/( SINE + BETA)**2 * COSE *
     .         ( 1.D0 - B/( SINE + C )**2 ) )
C
      WMF(2)=-WMF(2)
C
      RETURN
      END SUBROUTINE

      END MODULE
