      MODULE s_GTCOVO
      CONTAINS

C*
      SUBROUTINE GTCOVO(FILCOV,MAXLCQ,MAXPAR,SIGMA,NPAR,LOCQ,QMAT)
CC
CC NAME       :  GTCOVO
CC
CC PURPOSE    :  READ ALL THE VARIANCE-COVARIANCE ELEMENTS OF THE
CC               ORBIT PARAMETERS FROM A COVARIANCE FILE
CC
CC PARAMETERS :
CC         IN :  FILCOV : FILE NAME OF COVARIANCE FILE        CH*32
CC               MAXLCQ : MAXIMUM NUMBER OF CHARACTERISTICS    I*4
CC                        PER PARAMETER
CC               MAXPAR : MAXIMUM NUMBER OF ORBIT PARAMETERS   I*4
CC               SIGMA  : MEAN ERROR OF UNIT WEIGHT            R*8
CC               NPAR   : ACTUAL NUMBER OF PARAMETERS          I*4
CC               LOCQ(I,K),I=1,..,MAXLCQ,K=1,..,NPAR: PARA-    I*4
CC                        CHARACTERIZATION LIST
CC               QMAT   : VARIANCE-COVARIANCE MATRIX FOR       R*8
CC                        ORBIT PARAMETERS (UPPER TRIANGULAR
CC                        MATRIX ONLY)
CC
CC REMARKS    :  ---
CC
CC AUTHOR     :  M.ROTHACHER
CC
CC VERSION    :  3.4  (JAN 93)
CC
CC CREATED    :  91/04/15 13:17
CC
CC CHANGES    :  28-DEC-92 : ??: USE OF SR "OPNFIL" TO OPEN FILES
CC               10-AUG-94 : MR: CALL EXITRC
CC               21-JUN-05 : MM: COMLFNUM.inc REMOVED, m_bern ADDED
CC               23-JUN-05 : MM: IMPLICIT NONE AND DECLARATIONS ADDED
CC
CC COPYRIGHT  :  ASTRONOMICAL INSTITUTE
CC      1991     UNIVERSITY OF BERN
CC               SWITZERLAND
CC
C*
      USE m_bern
      USE f_ikf
      USE s_exitrc
      USE s_opnfil
      USE s_opnerr
      IMPLICIT NONE
C
C DECLARATIONS INSTEAD OF IMPLICIT
C --------------------------------
      INTEGER*4 I     , ICOV  , IELE  , INDORB, IOSTAT, IPA1  ,
     1          IPAR  , IPAR1 , IPAR2 , ITYP  , JPAR  , KPA1  , KPAR  ,
     2          MAXLCQ, MAXPAR, MAXTYP, NPAR  , NPASUM, NTYP
C
      REAL*8    COVARI, SIGMA
C
CCC       IMPLICIT REAL*8 (A-H,O-Z)
C
      PARAMETER (MAXTYP=12)
C
      CHARACTER*32 FILCOV
      CHARACTER*24 PATYPE
      CHARACTER*4  ELETYP(8),ELTYPE
      REAL*8       QMAT(MAXPAR*(MAXPAR+1)/2)
      INTEGER*4    LOCQ(MAXLCQ,MAXPAR),NUMPAR(MAXTYP+1)
C
C
      DATA ELETYP/'A','E','I','NODE','PER','U0','P0','P2'/
C
C OPEN VARIANCE-COVARIANCE FILE
C -----------------------------
      CALL OPNFIL(LFNLOC,FILCOV,'OLD','FORMATTED',
     1            'READONLY',' ',IOSTAT)
      CALL OPNERR(LFNERR,LFNLOC,IOSTAT,FILCOV,'GTCOVO')
C
C READ NUMBER OF PARAMETERS OF DIFFERENT TYPES
C --------------------------------------------
      INDORB=0
      NPASUM=0
      READ(LFNLOC,'(/////)')
      DO 10 ITYP=1,1000
        READ(LFNLOC,1) PATYPE,NUMPAR(ITYP)
1       FORMAT(2X,A24,2X,I8)
        IF (PATYPE.EQ.' ') GOTO 20
        IF (ITYP.GT.MAXTYP) THEN
          WRITE(LFNERR,901) MAXTYP
901       FORMAT(/,' *** SR GTCOVO: TOO MANY PARAMETER TYPES',/,
     1                   16X,'MAX. NUMBER OF PARAM.TYPES:',I3,/)
          CALL EXITRC(2)
        ENDIF
        NPASUM=NPASUM+NUMPAR(ITYP)
        IF (PATYPE.EQ.'ORBITAL ELEMENTS') INDORB=ITYP
10    CONTINUE
20    NTYP=ITYP-1
C
C NO ORBIT PARAMETERS FOUND
      IF (INDORB.EQ.0) THEN
        WRITE(LFNERR,902)
902     FORMAT(/,' *** SR GTCOVO: NO ORBITAL ELEMENTS FOUND IN',/,
     1                       16X,'COVARIANCE FILE',/)
        CALL EXITRC(2)
      ENDIF
      NPAR=NUMPAR(INDORB)
C
C CHECK MAXIMUM NUMBER OF PARAMETERS
      IF (NPAR.GT.MAXPAR) THEN
        WRITE(LFNERR,903) NPAR,MAXPAR
903     FORMAT(/,' *** SR GTCOVO: TOO MANY ORBIT PARAMETERS',/,
     1                       16X,'NUMBER OF ORB.PARAMETERS:',I5,/,
     2                       16X,'MAX.NUMBER OF ORB.PARAM.:',I5,/)
        CALL EXITRC(2)
      ENDIF
C
C SKIP 5 LINES
C ------------
      READ(LFNLOC,'(////)')
C
C LOOP OVER ALL PARAMETER TYPES: READ PARAM. CHARACTERISTICS
C ----------------------------------------------------------
      DO 100 ITYP=1,NTYP
        READ(LFNLOC,'(//)')
        DO 30 IPAR=1,NUMPAR(ITYP)
          IF (ITYP.NE.INDORB) THEN
            READ(LFNLOC,'( )')
          ELSE
            READ(LFNLOC,3) JPAR,(LOCQ(I,IPAR),I=1,3),ELTYPE
3           FORMAT(I4,I6,I5,I6,5X,A)
            IF (IPAR.EQ.1) IPAR1=JPAR
            DO 40 IELE=1,8
              IF (ELETYP(IELE).EQ.ELTYPE) GOTO 45
40          CONTINUE
45          LOCQ(4,IPAR)=IELE
          ENDIF
30      CONTINUE
100   CONTINUE
C
C READ RMS AND SKIP LINES
C -----------------------
      READ(LFNLOC,4) SIGMA
4     FORMAT(/////,19X,F8.4,////)
C
C LOOP OVER ALL COVARIANCE ELEMENTS
C ---------------------------------
      IPAR2=IPAR1+NPAR-1
      DO 200 ICOV=1,NPASUM*(NPASUM+1)/2
        READ(LFNLOC,5) IPAR,KPAR,COVARI
5       FORMAT(2I5,D22.10)
        IF(IPAR.GE.IPAR1.AND.IPAR.LE.IPAR2 .AND.
     1     KPAR.GE.IPAR1.AND.KPAR.LE.IPAR2) THEN
          IPA1=IPAR-IPAR1+1
          KPA1=KPAR-IPAR1+1
          QMAT(IKF(IPA1,KPA1))=COVARI
        ENDIF
200   CONTINUE
C
C CLOSE COVARIANCE FILE
C ---------------------
      CLOSE (UNIT=LFNLOC)
C
      RETURN
      END SUBROUTINE

      END MODULE
