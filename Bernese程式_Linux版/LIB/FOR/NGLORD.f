      MODULE s_NGLORD
      CONTAINS

C*
      SUBROUTINE NGLORD(NPAR,NPNEW,INDOLD,LOCQ,ANOR,BNOR,SOL,
     1                  IAMB1,LOCLOC,HELP)
CC
CC NAME       :  NGLORD
CC
CC PURPOSE    :  RE-ARRANGE MATRICES LOCQ, ANOR, BNOR FROM NORMAL
CC               EQUATION SYSTEM. THE NEW ORDER IS DEFINED BY
CC               THE ARRAY INDOLD(K),K=1,..,NPAR, WHERE
CC               INDOLD(K) = INDEX OF OLD ELEMENT CORRESPONDING
CC                           NEW ELELMENT NUMBER K
CC
CC PARAMETERS :
CC         IN :  NPAR   : NUMBER OF PARAMETERS                I*4
CC               NPNEW  : NEW NUMBER OF PARAMETERS (ONLY      I*4
CC                        FIRST NPNEW ELEMENTS ARE COPIED)
CC               INDOLD : INDEX CONTAINING OLD ELEMENT IND.   I*4(*)
CC   IN / OUT :  LOCQ   : ELEMENT DESCRIPTOR                  I*4(*,*)
CC               ANOR   : LINEARIZED NEQ MATRIX               R*8(*)
CC               BNOR   : RIGHT HAND SIDE OF NEQ - SYSTEM     R*8(*)
CC               SOL    : SOLUTION VECTOR                     R*8
CC               IAMB1  : ARRAY SHOWING SUCCESS/FAILURE OF    I*4
CC                        AMBIGUITY RESOLUTION
CC               LOCLOC : LOCAL ELEMENT DESCRIPTOR            I*4(*,*)
CC               HELP   : AUXILIARY ARRAY (DIMENSION AS A-MAT R*8(*)
CC
CC REMARKS    :  ---
CC
CC AUTHOR     :  G.BEUTLER
CC
CC VERSION    :  3.4  (JAN 94)
CC
CC CREATED    :  92/02/07
CC
CC CHANGES    :  23-JUN-05 : MM: IMPLICIT NONE AND DECLARATIONS ADDED
CC
CC COPYRIGHT  :  ASTRONOMICAL INSTITUTE
CC      1990     UNIVERSITY OF BERN
CC               SWITZERLAND
CC
C*
      USE f_ikf
      IMPLICIT NONE
C
C DECLARATIONS INSTEAD OF IMPLICIT
C --------------------------------
      INTEGER*4 IKNEW , IKOLD , IOLD  , IPAR  , K     , KOLD  ,
     1          KPAR  , MXCLCQ, NPAR  , NPNEW , NTOT
C
CCC       IMPLICIT REAL*8 (A-H,O-Z)
C
      COMMON/MCMLCQ/MXCLCQ,MXNLCQ
C
      CHARACTER*6 MXNLCQ
C
      INTEGER*4   INDOLD(*),LOCQ(MXCLCQ,*),LOCLOC(MXCLCQ,*),IAMB1(*)
C
      REAL*8      ANOR(*),BNOR(*),HELP(*),SOL(*)
C
C 1. MATRIX BNOR
C --------------
      DO 110 IPAR=1,NPAR
        HELP(IPAR)=BNOR(IPAR)
110   CONTINUE
      DO 120 IPAR=1,NPNEW
        IOLD=INDOLD(IPAR)
        BNOR(IPAR)=HELP(IOLD)
120   CONTINUE
C
C 1A. MATRIX SOL
C --------------
      DO 111 IPAR=1,NPAR
        HELP(IPAR)=SOL(IPAR)
111   CONTINUE
      DO 121 IPAR=1,NPNEW
        IOLD=INDOLD(IPAR)
        SOL(IPAR)=HELP(IOLD)
121   CONTINUE
C
C 1B. MATRIX IAMB1
C ----------------
      DO 112 IPAR=1,NPAR
        HELP(IPAR)=IAMB1(IPAR)
112   CONTINUE
      DO 122 IPAR=1,NPNEW
        IOLD=INDOLD(IPAR)
        IAMB1(IPAR)=HELP(IOLD)
122   CONTINUE
C
C 2. MATRIX LOCQ
C --------------
      DO 130 IPAR=1,NPAR
      DO 130 K=1,MXCLCQ
        LOCLOC(K,IPAR)=LOCQ(K,IPAR)
130   CONTINUE
      DO 140 IPAR=1,NPNEW
        IOLD=INDOLD(IPAR)
        DO 140 K=1,MXCLCQ
          LOCQ(K,IPAR)=LOCLOC(K,IOLD)
140   CONTINUE
C
C 3. MATRIX ANOR
C --------------
      NTOT=NPAR*(NPAR+1)/2
      DO 210 K=1,NTOT
        HELP(K)=ANOR(K)
210   CONTINUE
      DO 220 IPAR=1,NPNEW
        IOLD=INDOLD(IPAR)
        DO 220 KPAR=IPAR,NPNEW
          KOLD=INDOLD(KPAR)
          IKNEW=IKF(IPAR,KPAR)
          IKOLD=IKF(IOLD,KOLD)
          ANOR(IKNEW)=HELP(IKOLD)
220   CONTINUE
C
      RETURN
      END SUBROUTINE

      END MODULE
