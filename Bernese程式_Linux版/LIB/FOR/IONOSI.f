      MODULE s_IONOSI
      CONTAINS

C*
      SUBROUTINE IONOSI(ALL   ,NMODEL,IONREQ,IONDEV,NTERM ,NM    ,
     1                  IONCOE,IONSIG)
CC
CC NAME       :  IONOSI
CC
CC PURPOSE    :  EXTRACT ESSENTIAL INFORMATION FROM IONOSPHERE FILE
CC               CONTAINING LOCAL IONOSPHERE MODELS
CC
CC PARAMETERS :
CC         IN :  ALL    : PROGRAM SWITCH                      I*4
CC               ALL= 0 : NO COEFFICIENTS, NO SIGMAS
CC                  = 1 : WITH COEFFICIENTS, SIGMAS
CC        OUT :  NMODEL : NUMBER OF MODELS                    I*4
CC               IONREQ(K,I),K=1,2,3,4,I=1,2,...,NMODEL       I*4
CC                     (1,I): DEGREE OF DEVELOPMENT IN LATITUDE
CC                     (2,I): DEGREE OF DEVELOPMENT IN TIME
CC                     (3,I): MAXIMUM DEGREE FOR MIXED COEFFICIENTS
CC                     (4,I): HEIGHT OF SINGLE LAYER (KM)
CC               IONDEV(K,I),K=1,2,3,4,I=1,2,...,NMODEL       R*8
CC                     (1,I): LATITUDE ORIGIN OF DEVELOPMENT
CC                     (2,I): TIME ORIGIN OF DEVELOPMENT
CC                     (3,I): LONGITUDE ORIGIN OF DEVELOPMENT
CC                     (4,I): MINIMUM TIME OF VALIDITY FOR MODEL I
CC                     (5,I): MAXIMUM TIME OF VALIDITY FOR MODEL I
CC                     (6,I): NORMALIZATION FACTOR FOR ELECTRON NUMBER
CC                     (7,I): NORMALIZATION FACTOR FOR LATITUDE (DEG)
CC                     (8,I): NORMALIZATION FACTOR FOR TIME (HOURS)
CC               NTERM(I),I=1,2,...,NMODEL: NUMBER OF TERMS GIVEN
CC                            A PRIORI FOR MODEL I                I*4
CC               NM(K,J,I),K=1,2,...,NTERM,J=1,2,I=1,2,...,NMODEL I*4
CC                 (K,1,I): LATITUDE DEGREE OF TERM K OF MODEL I
CC                 (K,2,I): TIME DEGREE OF TERM K OF MODEL I
CC               IONCOE(K,I),K=1,2,...,NTERM,I=1,2,...,NMODEL:    R*8
CC                          COEFFICIENT K
CC               IONSIG(K,I),K=1,2,...,NTERM,I=1,2,...,NMODEL     R*8
CC                          A PRIORI SIGMA
CC
CC REMARKS    :  ---
CC
CC AUTHOR     :  G.BEUTLER
CC
CC VERSION    :  3.4  (JAN 93)
CC
CC CREATED    :  87/11/02 15:15
CC
CC CHANGES    :  28-DEC-92 : ??: USE OF SR "OPNFIL" TO OPEN FILES
CC               10-AUG-94 : MR: CALL EXITRC
CC               05-DEC-95 : SS: FORMATS MODIFIED
CC               26-JAN-98 : SS: INCLUDE FILES "MAXGIT" AND "MAXGIM"
CC               26-MAY-98 : SS: "IONDEV" REDIMENSIONED
CC               17-FEB-03 : LM: USE M_MAXDIM
CC               16-JUN-05 : MM: COMCONST.inc REPLACED BY d_const
CC               21-JUN-05 : MM: COMLFNUM.inc REMOVED, m_bern ADDED
CC               23-JUN-05 : MM: IMPLICIT NONE AND DECLARATIONS ADDED
CC
CC COPYRIGHT  :  ASTRONOMICAL INSTITUTE
CC      1987     UNIVERSITY OF BERN
CC               SWITZERLAND
CC
C*
      USE m_bern
      USE m_maxdim, ONLY: MAXGIT, MAXGIM
      USE d_const, ONLY: PI
      USE s_dimtst
      USE s_opnfil
      USE f_djul
      USE s_opnerr
      USE s_gtflna
      IMPLICIT NONE
C
C DECLARATIONS INSTEAD OF IMPLICIT
C --------------------------------
      INTEGER*4 I     , ID0   , ID1   , ID2   , ILAT  , IMODEL, IOSTAT,
     1          IRC   , ITIME , J0    , J1    , J2    , M0    , M1    ,
     2          M2    , NMOD  , NMODEL
C
      REAL*8    COE   , H0    , H1    , H2    , SIGMA , XLAT  ,
     1          XLONG
C
CCC       IMPLICIT REAL*8 (A-H,O-Z)
C
C
      INTEGER*4 ALL
      INTEGER*4 IONREQ(6,*),NM(MAXGIT,2,*),NTERM(*)
      REAL*8 IONCOE(MAXGIT,*),IONSIG(MAXGIT,*)
      REAL*8 IONDEV(10,*)
      CHARACTER*10 COMENT
      CHARACTER*32 FILION
C
C OPEN IONOSPHERE FILE
      CLOSE(UNIT=LFNLOC)
      CALL GTFLNA(1,'IONOS  ',FILION,IRC)
      CALL OPNFIL(LFNLOC,FILION,'OLD','FORMATTED',
     1            'READONLY',' ',IOSTAT)
      CALL OPNERR(LFNERR,LFNLOC,IOSTAT,FILION,'IONOSI')
C
C SKIP TITLE LINES
      READ(LFNLOC,10)COMENT
10    FORMAT(A10,/)
C
C READ ALL MODELS
      DO 100 IMODEL=1,MAXGIM
        READ(LFNLOC,20,END=110)J0,M0,ID0,H0,XLAT,XLONG,IONREQ(4,IMODEL),
     1            IONREQ(2,IMODEL),IONREQ(1,IMODEL),IONREQ(3,IMODEL),
     2            IONDEV(7,IMODEL),IONDEV(8,IMODEL),
     3            IONDEV(6,IMODEL),J1,M1,ID1,H1,J2,M2,ID2,H2
        IONDEV(1,IMODEL)=XLAT/180*PI
        IONDEV(3,IMODEL)=XLONG/180*PI
        IONDEV(2,IMODEL)=DJUL(J0,M0,ID0+H0/24.D0)
        IONDEV(4,IMODEL)=DJUL(J1,M1,ID1+H1/24.D0)
        IONDEV(5,IMODEL)=DJUL(J2,M2,ID2+H2/24.D0)
20      FORMAT(//,48X,I5,2I3,F5.1,/,2(48X,F8.2,/),4(48X,I5,/),
     1         2(48X,F7.2,/),48X,D15.2,/,2(48X,I5,2I3,F5.1,/),/)
C
C READ COEFFICIENTS AND ASSOCIATED SIGMAS
        IF(ALL.NE.0)THEN
          NTERM(IMODEL)=0
        END IF
        DO 40 I=1,MAXGIT
          READ(LFNLOC,30)ILAT,ITIME,COE,SIGMA
30        FORMAT(I5,I10,5X,2E17.8)
          IF(ILAT.EQ.-1)GO TO 100
          IF(ALL.NE.0)THEN
            NTERM(IMODEL)=NTERM(IMODEL)+1
C
            CALL DIMTST(1,1,2,'IONOSI','MAXGIT',
     1        'TERMS PER LOCAL IONOSPHERE MODEL','INCLUDE FILE USED',
     2        NTERM(IMODEL),MAXGIT,IRC)
C
            NM(NTERM(IMODEL),1,IMODEL)=ILAT
            NM(NTERM(IMODEL),2,IMODEL)=ITIME
            IONCOE(NTERM(IMODEL),IMODEL)=COE
            IONSIG(NTERM(IMODEL),IMODEL)=SIGMA
          END IF
40      CONTINUE
100   CONTINUE
C
      NMOD=MAXGIM+1
      CALL DIMTST(1,2,2,'IONOSI','MAXGIM',
     1  'LOCAL IONOSPHERE MODELS','INCLUDE FILE USED',
     2  NMOD,MAXGIM,IRC)
C
110   NMODEL=IMODEL-1
      CLOSE (UNIT=LFNLOC)
      RETURN
      END SUBROUTINE


      END MODULE
