      MODULE s_RXWTOH
      CONTAINS

C*
      SUBROUTINE RXWTOH(LFNOBS,LFNERR,MAXSAT,
     1                  PRGNAM,RUNBY,CRDATE,CRTIME,NCOM,COMENT,
     2                  STANAM,OPRNAM,AGENCY,IRUNIT,RECTYP,RCVERS,
     3                  IANTEN,ANTTYP,POSXYZ,POSECC,IWLFAC,
     4                  NUMTYP,OBSTYP,IDELTT,TFIRST,TLAST,
     5                  NSATEL,NUMSAT,NUMOBS,IRCODE)
CC
CC NAME       :  RXWTOH
CC
CC PURPOSE    :  WRITE THE ENTIRE HEADER INFORMATION OF A
CC               RINEX OBSERVATION FILE
CC
CC PARAMETERS :
CC         IN :  LFNOBS : LOGICAL FILE NUMBER OF RINEX FILE    I*4
CC               LFNERR : LFN OF ERROR MESSAGES                I*4
CC               MAXSAT : MAXIMUM NUMBER OF SATELLITES         I*4
CC                        CORRESPONDS TO THE ROW DECLARATION OF
CC                        ARRAY NUMOBS
CC               PRGNAM : PROGRAM NAME                        CH*20
CC               RUNBY  : NAME OF AGENCY CREATING RINEX FILE  CH*20
CC               CRDATE : CREATION DATE                       CH*9
CC               CRTIME : CREATION TIME                       CH*5
CC               NCOM   : NUMBER OF COMMENT LINES              I*4
CC               COMENT : COMMENT LINES                       CH*60(*)
CC               STANAM : STATION NAME                        CH*60
CC               OPRNAM : OPERATOR NAME                       CH*20
CC               AGENCY : OPERATOR AGENCY                     CH*40
CC               IRUNIT : RECEIVER UNIT NUMBER                 I*4
CC               RECTYP : RECEIVER TYPE                       CH*20
CC               RCVERS : RECEIVER VERSION                    CH*20
CC               IANTEN : ANTENNA NUMBER                       I*4
CC               ANTTYP : ANTENNA  TYPE                       CH*20
CC               POSXYZ : APPROXIMATE MARKER POSITION (WGS 84) R*8(3)
CC               POSECC : POSITIONING ECCENTRICITIES IN LOCAL  R*8(3)
CC                          SYSTEM IN METERS
CC                          (ANTENNA BOTTOM PLANE - MARKER)
CC                          POSECC(1): ECC. IN LATITUDE
CC                          POSECC(2): ECC. IN LONGITUDE
CC                          POSECC(3): ECC. IN HEIGHT
CC               IWLFAC : WAVELENGTH FACTORS (L1/L2)           I*4
CC                          1: CYCLE AMBIGUITIES
CC                          2: HALF-CYCLE AMBIGUITIES
CC               NUMTYP : NUMBER OF DIFFERENT OBS.TYPES        I*4
CC               OBSTYP : LIST OF OBSERVATION TYPES           CH*2(*)
CC               IDELTT : OBSERVATION INTERVAL (SEC)           I*4
CC               TFIRST : FIRST OBSERVATION EPOCH              R*8
CC               TLAST  : LAST  OBSERVATION EPOCH              R*8
CC                        IF TLAST=0: NO EXTENDED HEADER
CC               NSATEL : NUMBER OF SATELLITES                 I*4
CC               NUMSAT : LIST OF SATELLITE NUMBERS            I*4(*)
CC               NUMOBS : NUMBER OF OBSERVATIONS USED          I*4(*,*)
CC                          NUMOBS(I,J): SATELLITE I, OBSTYP J
CC        OUT :  IRCODE : RETURN CODE (0: OK)                  I*4
CC
CC REMARKS    :  EXTENDED HEADER IS WRITTEN IF TLAST NOT EQUAL 0.D0
CC
CC AUTHOR     :  W. GURTNER
CC
CC VERSION    :  3.4  (JAN 93)
CC
CC CREATED    :  89/04/05 08:25
CC
CC CHANGES    :  23-JUN-05 : MM: IMPLICIT NONE AND DECLARATIONS ADDED
CC
CC COPYRIGHT  :  ASTRONOMICAL INSTITUTE
CC      1989     UNIVERSITY OF BERN
CC               SWITZERLAND
CC
C*
      USE s_jmt
      USE s_radgms
      IMPLICIT NONE
C
C DECLARATIONS INSTEAD OF IMPLICIT
C --------------------------------
      INTEGER*4 I     , IANTEN, IDAY  , IDELTT, IHOUR , IRCODE, IRUNIT,
     1          IRVERS, ITYP  , IYEAR , K     , LFNERR, LFNOBS, MAXSAT,
     2          MINUTE, MONTH , NCOM  , NSATEL, NUMTYP
C
      REAL*8    DAY   , SEC   , TFIRST, TLAST
C
CCC       IMPLICIT REAL*8 (A-H,O-Z)
C
C GLOBAL DECLARATIONS
C -------------------
      CHARACTER    STANAM*60,RECTYP*20,OPRNAM*20,AGENCY*40
      CHARACTER*20 PRGNAM,RUNBY,RCVERS,ANTTYP
      CHARACTER    COMENT(*)*60,CRDATE*9,CRTIME*5
      CHARACTER    OBSTYP(*)*2
      REAL*8       POSECC(3),POSXYZ(3)
      INTEGER*4    IWLFAC(2)
      INTEGER*4    NUMSAT(*),NUMOBS(MAXSAT,*)
C
C  LOCAL DECLARATIONS
C  ------------------
      CHARACTER    HEADRC(16)*20,RXTYPE*20,STRING*60,CHR*1
C
      DATA HEADRC/'RINEX VERSION / TYPE',
     2            'PGM / RUN BY / DATE ',
     3            'COMMENT             ',
     4            'MARKER NAME         ',
     5            'OBSERVER / AGENCY   ',
     6            'REC # / TYPE / VERS ',
     7            'ANT # / TYPE        ',
     8            'APPROX POSITION XYZ ',
     9            'ANTENNA: DELTA H/E/N',
     *            'WAVELENGTH FACT L1/2',
     1            '# / TYPES OF OBSERV ',
     2            'INTERVAL            ',
     3            'TIME OF FIRST OBS   ',
     4            'TIME OF LAST OBS    ',
     5            '# OF SATELLITES     ',
     6            'PRN / # OF OBS      '/
      DATA ITYP/3/
C
C RINEX FILE TYPE
      DATA RXTYPE/'OBSERVATION DATA    '/
C
C RINEX FORMAT VERSION
      DATA IRVERS/1/
C
C RECORD 1        RINEX VERSION / TYPE
      WRITE(LFNOBS,1) IRVERS,RXTYPE,HEADRC(1)
1     FORMAT(I6,14X,A20,20X,A20)
C
C RECORD 2        PGM / RUN BY / DATE
      WRITE(LFNOBS,2) PRGNAM,RUNBY,CRDATE,CRTIME,HEADRC(2)
2     FORMAT(A20,A20,A9,1X,A5,5X,A20)
C
C RECORD 3        COMMENT
      DO 300 I=1,NCOM
        WRITE(LFNOBS,3) COMENT(I),HEADRC(3)
3       FORMAT(A60,A20)
300   CONTINUE
C
C RECORD 4        MARKER NAME
      WRITE(LFNOBS,4) STANAM,HEADRC(4)
4     FORMAT(A60,A20)
C
C RECORD 5        OBSERVER / AGENCY
      WRITE(LFNOBS,5) OPRNAM,AGENCY,HEADRC(5)
5     FORMAT(A20,A40,A20)
C
C RECORD 6        REC # / TYPE / VERS
      WRITE(LFNOBS,6) IRUNIT,RECTYP,RCVERS,HEADRC(6)
6     FORMAT(I6,14X,A20,A20,A20)
C
C RECORD 7        ANT # / TYPE
      WRITE(LFNOBS,7) IANTEN,ANTTYP,HEADRC(7)
7     FORMAT(I6,14X,A20,20X,A20)
C
C RECORD 8        APPROX POSITION XYZ
      WRITE(LFNOBS,8) (POSXYZ(K),K=1,3),HEADRC(8)
8     FORMAT(3F14.4,18X,A20)
C
C RECORD 9        ANTENNA: DELTA H/E/N
      WRITE(LFNOBS,9) POSECC(3),POSECC(2),POSECC(1),HEADRC(9)
9     FORMAT(3F14.4,18X,A20)
C
C RECORD 10       WAVELENGTH FACT L1/2
      WRITE(LFNOBS,10) IWLFAC(1),IWLFAC(2),HEADRC(10)
10    FORMAT(2I6,48X,A20)
C
C RECORD 11       # / TYPES OF OBSERV
      WRITE(STRING,111) NUMTYP,(OBSTYP(K),K=1,NUMTYP)
111   FORMAT(I6,9(4X,A2))
      WRITE(LFNOBS,112) STRING,HEADRC(11)
112   FORMAT(A60,A20)
C
C RECORD 12       INTERVAL
      WRITE(LFNOBS,12) IDELTT,HEADRC(12)
12    FORMAT(I6,54X,A20)
C
C RECORD 13       TIME OF FIRST OBS
      CALL JMT(TFIRST,IYEAR,MONTH,DAY)
      CALL RADGMS(ITYP,DAY,CHR,IHOUR,MINUTE,SEC)
      SEC=DNINT(SEC*1.D6)/1.D6
      IDAY=IDINT(DAY)
      WRITE(LFNOBS,13) IYEAR,MONTH,IDAY,IHOUR,MINUTE,SEC,HEADRC(13)
13    FORMAT(5I6,F12.6,18X,A20)
C
      IF(TLAST.NE.0.D0) THEN
C
C RECORD 14       TIME OF LAST OBS
        CALL JMT(TLAST ,IYEAR,MONTH,DAY)
        CALL RADGMS(ITYP,DAY,CHR,IHOUR,MINUTE,SEC)
        SEC=DNINT(SEC*1.D7)/1.D7
        IDAY=IDINT(DAY)
        WRITE(LFNOBS,14) IYEAR,MONTH,IDAY,IHOUR,MINUTE,SEC,HEADRC(14)
14      FORMAT(5I6,F12.6,18X,A20)
C
C RECORD 15       # OF SATELLITES
        WRITE(LFNOBS,15) NSATEL,HEADRC(15)
15      FORMAT(I6,54X,A20)
C
C RECORD 16       PRN / # OF OBS
        DO 100 I=1,NSATEL
          WRITE(STRING,161) NUMSAT(I),(NUMOBS(I,K),K=1,NUMTYP)
161       FORMAT(I6,9I6)
          WRITE(LFNOBS,162) STRING,HEADRC(16)
162       FORMAT(A60,A20)
100     CONTINUE
      END IF
C
C  WRITE BLANK LINE
      WRITE(LFNOBS,*)
C
      IRCODE=0
      RETURN
      END SUBROUTINE

      END MODULE
