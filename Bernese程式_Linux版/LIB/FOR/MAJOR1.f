      MODULE s_MAJOR1
      CONTAINS

C*
      SUBROUTINE MAJOR1(N,ARRAY,IREF,DISTM)
CC
CC NAME       :  MAJOR1
CC
CC PURPOSE    :  FIND THE ARRAY ELEMENT WHICH HAS THE SMALLEST
CC               SUM OF DISTANCES TO ALL THE OTHER ELEMENTS.
CC               IF AN ARRAY ELEMENT HAS THE VALUE 1.D20,
CC               IT IS NOT CONSIDERED
CC
CC PARAMETERS :
CC         IN :  N      : NUMBER OF ELEMENTS                  I*4
CC               ARRAY(I),I=1,2,..,N: ARRAY ELEMENTS          R*8
CC        OUT :  IREF   : REFERENCE ELEMENT                   I*4
CC               DISTM  : MEAN DISTANCE OF REFERENCE ELEMENT  R*8
CC                        FROM ALL OTHER ELEMENTS
CC
CC REMARKS    :  ---
CC
CC AUTHOR     :  M.ROTHACHER
CC
CC VERSION    :  3.4  (JAN 93)
CC
CC CREATED    :  91/03/02 14:40
CC
CC CHANGES    :  23-JUN-05 : MM: IMPLICIT NONE AND DECLARATIONS ADDED
CC
CC COPYRIGHT  :  ASTRONOMICAL INSTITUTE
CC      1991     UNIVERSITY OF BERN
CC               SWITZERLAND
CC
C*
      IMPLICIT NONE
C
C DECLARATIONS INSTEAD OF IMPLICIT
C --------------------------------
      INTEGER*4 I     , IR    , IREF  , N     , NADD
C
      REAL*8    ADDIST, DISTM , VALUE , VALUER
C
CCC       IMPLICIT REAL*8 (A-H,O-Z)
CCC       IMPLICIT INTEGER*4 (I-N)
      REAL*8 ARRAY(*)
C
C FIND BEST ELEMENT TO BE USED AS A REFERENCE ELEMENT
C ---------------------------------------------------
      IF (N.EQ.0) THEN
        IREF=0
        DISTM=0.D0
      ELSE
        DISTM=1.D20
C
C LOOP OVER ALL POSSIBLE REFERENCE ELEMENTS
        DO 20 IR=1,N
          VALUER=ARRAY(IR)
          IF (VALUER.EQ.1.D20) GOTO 20
          NADD=0
          ADDIST=0.D0
C
C LOOP OVER ALL ELEMENTS TO SUM DISTANCES TO THE REFERENCE ELEMENT
          DO 10 I=1,N
            IF (I.EQ.IR) GOTO 10
            VALUE=ARRAY(I)
            IF (VALUE.EQ.1.D20) GOTO 10
            NADD=NADD+1
            ADDIST=ADDIST+DABS(VALUER-VALUE)
10        CONTINUE
C
C SELECT ELEMENT WITH SMALLEST DISTANCE AS REFERENCE ELEMENT
          IF (NADD.NE.0) ADDIST=ADDIST/NADD
          IF (ADDIST.LT.DISTM) THEN
            IREF=IR
            DISTM=ADDIST
          ENDIF
20      CONTINUE
      ENDIF
C
      RETURN
      END SUBROUTINE

      END MODULE
