      MODULE s_RXRDOH
      CONTAINS

C*
      SUBROUTINE RXRDOH(LFNOBS,LFNERR,MAXSAT,
     1                  PRGNAM,RUNBY,CRDATE,CRTIME,NCOM,COMENT,
     2                  STANAM,OPRNAM,AGENCY,IRUNIT,RECTYP,RCVERS,
     3                  IANTEN,ANTTYP,POSXYZ,POSECC,IWLFAC,
     4                  NUMTYP,OBSTYP,IDELTT,TFIRST,TLAST,
     5                  NSATEL,NUMSAT,NUMOBS,IRCODE)
CC
CC NAME       :  RXRDOH
CC
CC PURPOSE    :  READ  THE ENTIRE HEADER INFORMATION OF A
CC               RINEX OBSERVATION FILE
CC
CC PARAMETERS :
CC         IN :  LFNOBS : LOGICAL FILE NUMBER OF RINEX FILE    I*4
CC               LFNERR : LFN FOR ERROR MESSAGES               I*4
CC               MAXSAT : MAXIMUM NUMBER OF SATELLITES         I*4
CC                        CORRESPONDS TO ROW DECLARATION OF
CC                        ARRAY NUMOBS
CC        OUT :  PRGNAM : PROGRAM NAME                        CH*20
CC               RUNBY  : NAME OF AGENCY CREATING RINEX FILE  CH*20
CC               CRDATE : CREATION DATE                       CH*9
CC               CRTIME : CREATION TIME                       CH*5
CC               NCOM   : NUMBER OF COMMENT LINES              I*4
CC               COMENT : COMENT LINES                        CH*60(*)
CC               STANAM : STATION NAMES                       CH*20
CC               OPRNAM : OPERATOR NAME                       CH*20
CC               AGENCY : OPERATOR AGENCY                     CH*40
CC               IRUNIT : RECEIVER UNIT NUMBER                 I*4
CC               RECTYP : RECEIVER TYPE                       CH*20
CC               RCVERS : RECEIVER VERSION                    CH*20
CC               IANTEN : ANTENNA NUMBER                       I*4
CC               ANTTYP : ANTENNA  TYPE                       CH*20
CC               POSXYZ : APPROXIMATE MARKER POSITION (WGS 84) R*8(3)
CC               POSECC : POSITIONING ECCENTRICITIES IN LOCAL  R*8(3)
CC                          SYSTEM IN METERS
CC                          (ANTENNA BOTTOM PLANE - MARKER)
CC                          POSECC(1): ECC. IN LATITUDE
CC                          POSECC(2): ECC. IN LONGITUDE
CC                          POSECC(3): ECC. IN HEIGHT
CC               IWLFAC : WAVELENGTH FACTORS (L1/L2)           I*4
CC                          1: CYCLE AMBIGUITIES
CC                          2: HALF-CYCLE AMBIGUITIES
CC               NUMTYP : NUMBER OF DIFFERENT OBS.TYPES        I*4
CC               OBSTYP : LIST OF OBSERVATION TYPES           CH*2(*)
CC               IDELTT : OBSERVATION INTERVAL (SEC)           I*4
CC               TFIRST : FIRST OBSERVATION EPOCH              R*8
CC               TLAST  : LAST  OBSERVATION EPOCH              R*8
CC                        IF TLAST=0: NO EXTENDED HEADER
CC               NSATEL : NUMBER OF SATELLITES                 I*4
CC               NUMSAT : LIST OF SATELLITE NUMBERS            I*4(*)
CC               NUMOBS : NUMBER OF OBSERVATIONS USED          I*4(*,*)
CC                          NUMOBS(I,J): SATELLITE I, OBSTYP J
CC               IRCODE : RETURN CODE                          I*4
CC                        0: OK
CC                        1: WRONG FILE TYPE
CC                        2: NOT ANTICIPATED VERSION NUMBER
CC                        3: END OF FILE WITHIN HEADER
CC                        4: ERROR DECODING DATA
CC                        9: END OF FILE AT BEGINNING OF HEADER
CC
CC REMARKS    :  ---
CC
CC AUTHOR     :  W. GURTNER
CC
CC VERSION    :  3.4  (JAN 93)
CC
CC CREATED    :  89/04/06 17:25
CC
CC CHANGES    :  01-JUL-99 : PF: CALL IYEAR4 FOR CONVERSION YY->YYYY
CC               23-JUN-05 : MM: IMPLICIT NONE AND DECLARATIONS ADDED
CC
CC COPYRIGHT  :  ASTRONOMICAL INSTITUTE
CC      1989     UNIVERSITY OF BERN
CC               SWITZERLAND
CC
C*
      USE f_djul
      USE f_iyear4
      IMPLICIT NONE
C
C DECLARATIONS INSTEAD OF IMPLICIT
C --------------------------------
      INTEGER*4 I     , IANTEN, IDAY  , IDELTT, IHOUR , IRCODE, IRUNIT,
     1          IRV   , IRVERS, IYEAR , K     , LFNERR, LFNOBS,
     2          MAXSAT, MINUTE, MONTH , NCOM  , NSATEL, NUMTYP
C
      REAL*8    DAY   , SEC   , TFIRST, TLAST
C
CCC       IMPLICIT REAL*8 (A-H,O-Z)
C
C GLOBAL DECLARATIONS
C -------------------
      CHARACTER    STANAM*60,AGENCY*40
      CHARACTER*20 PRGNAM,RUNBY,RCVERS,ANTTYP,RECTYP,OPRNAM
      CHARACTER    COMENT(*)*60,CRDATE*9,CRTIME*5
      CHARACTER    OBSTYP(*)*2
      REAL*8       POSECC(3),POSXYZ(3)
      INTEGER*4    IWLFAC(2)
      INTEGER*4    NUMSAT(*),NUMOBS(MAXSAT,*)
C
C  LOCAL DECLARATIONS
C  ------------------
      CHARACTER    HEAD*20,RXTYPE*1,STRING*60,RXT*1
C
C RINEX FILE TYPE
      DATA RXTYPE/'O'/
C
C MAXIMUM RINEX FORMAT VERSION
      DATA IRVERS/1/
C
      NCOM=0
C
C RECORD 1        RINEX VERSION / TYPE
101   READ(LFNOBS,222,END=990) STRING,HEAD
222   FORMAT(A60,A20)
      IF(HEAD(1:7).EQ.'COMMENT') THEN
        NCOM=NCOM+1
        COMENT(NCOM)=STRING
        GOTO 101
      END IF
      READ(STRING,1,ERR=940) IRV,RXT
1     FORMAT(I6,14X,A1)
      IF(RXT.NE.RXTYPE) GOTO 910
      IF(IRV.LE.0.OR.IRV.GT.IRVERS) GOTO 920
C
C RECORD 2        PGM / RUN BY / DATE
102   READ(LFNOBS,222,END=930) STRING,HEAD
      IF(HEAD(1:7).EQ.'COMMENT') THEN
        NCOM=NCOM+1
        COMENT(NCOM)=STRING
        GOTO 102
      END IF
      READ(STRING,2,ERR=940) PRGNAM,RUNBY,CRDATE,CRTIME
2     FORMAT(A20,A20,A9,1X,A5)
C
C RECORD 4        MARKER NAME
104   READ(LFNOBS,222,END=930) STRING,HEAD
      IF(HEAD(1:7).EQ.'COMMENT') THEN
        NCOM=NCOM+1
        COMENT(NCOM)=STRING
        GOTO 104
      END IF
      READ(STRING,4,ERR=940) STANAM
4     FORMAT(A60)
C
C RECORD 5        OBSERVER / AGENCY
105   READ(LFNOBS,222,END=930) STRING,HEAD
      IF(HEAD(1:7).EQ.'COMMENT') THEN
        NCOM=NCOM+1
        COMENT(NCOM)=STRING
        GOTO 105
      END IF
      READ(STRING,5,ERR=940) OPRNAM,AGENCY
5     FORMAT(A20,A40)
C
C RECORD 6        REC # / TYPE / VERS
106   READ(LFNOBS,222,END=930) STRING,HEAD
      IF(HEAD(1:7).EQ.'COMMENT') THEN
        NCOM=NCOM+1
        COMENT(NCOM)=STRING
        GOTO 106
      END IF
      IRUNIT=0
      READ(STRING,1006,ERR=1026) IRUNIT
1006  FORMAT(I6)
1026  READ(STRING,1016,ERR=940) RECTYP,RCVERS
1016  FORMAT(20X,A20,A20)
C
C RECORD 7        ANT # TYPE
107   READ(LFNOBS,222,END=930) STRING,HEAD
      IF(HEAD(1:7).EQ.'COMMENT') THEN
        NCOM=NCOM+1
        COMENT(NCOM)=STRING
        GOTO 107
      END IF
      IANTEN=0
      READ(STRING,1007,ERR=1027) IANTEN
1007  FORMAT(I6)
1027  READ(STRING,1017,ERR=940) ANTTYP
1017  FORMAT(20X,A20)
C
C RECORD 8        APPROX POSITION XYZ
108   READ(LFNOBS,222,END=930) STRING,HEAD
      IF(HEAD(1:7).EQ.'COMMENT') THEN
        NCOM=NCOM+1
        COMENT(NCOM)=STRING
        GOTO 108
      END IF
      READ(STRING,8,ERR=940) (POSXYZ(K),K=1,3)
8     FORMAT(3F14.4)
C
C RECORD 9        ANTENNA: DELTA H/E/N
109   READ(LFNOBS,222,END=930) STRING,HEAD
      IF(HEAD(1:7).EQ.'COMMENT') THEN
        NCOM=NCOM+1
        COMENT(NCOM)=STRING
        GOTO 109
      END IF
      READ(STRING,9,ERR=940) POSECC(3),POSECC(2),POSECC(1)
9     FORMAT(3F14.4)
C
C RECORD 10       WAVELENGTH FACT L1/2
110   READ(LFNOBS,222,END=930) STRING,HEAD
      IF(HEAD(1:7).EQ.'COMMENT') THEN
        NCOM=NCOM+1
        COMENT(NCOM)=STRING
        GOTO 110
      END IF
      READ(STRING,10,ERR=940) IWLFAC(1),IWLFAC(2)
10    FORMAT(2I6)
C
C RECORD 11       # / TYPES OF OBSERV
111   READ(LFNOBS,222,END=930) STRING,HEAD
      IF(HEAD(1:7).EQ.'COMMENT') THEN
        NCOM=NCOM+1
        COMENT(NCOM)=STRING
        GOTO 111
      END IF
      READ(STRING,11,ERR=940) NUMTYP,(OBSTYP(K),K=1,NUMTYP)
11    FORMAT(I6,9(4X,A2))
C
C RECORD 12       INTERVAL
112   READ(LFNOBS,222,END=930) STRING,HEAD
      IF(HEAD(1:7).EQ.'COMMENT') THEN
        NCOM=NCOM+1
        COMENT(NCOM)=STRING
        GOTO 112
      END IF
      READ(STRING,12,ERR=940) IDELTT
12    FORMAT(I6)
C
C RECORD 13       TIME OF FIRST OBS
113   READ(LFNOBS,222,END=930) STRING,HEAD
      IF(HEAD(1:7).EQ.'COMMENT') THEN
        NCOM=NCOM+1
        COMENT(NCOM)=STRING
        GOTO 113
      END IF
      READ(STRING,13,ERR=940) IYEAR,MONTH,IDAY,IHOUR,MINUTE,SEC
13    FORMAT(5I6,F12.6)
      IYEAR = IYEAR4(IYEAR)
      DAY=IDAY+IHOUR/24.D0+MINUTE/1440.D0+SEC/86400.D0
      TFIRST=DJUL(IYEAR,MONTH,DAY)
C
C RECORD 14       TIME OF LAST OBS
114   READ(LFNOBS,222,END=930) STRING,HEAD
      IF(HEAD(1:7).EQ.'COMMENT') THEN
        NCOM=NCOM+1
        COMENT(NCOM)=STRING
        GOTO 114
      END IF
C SHORT HEADER
      IF(STRING.EQ.' ') THEN
        TLAST=0.D0
        NSATEL=0
      ELSE
C  EXTENDED HEADER
        READ(STRING,14,ERR=940) IYEAR,MONTH,IDAY,IHOUR,MINUTE,SEC
14      FORMAT(5I6,F12.6)
        IYEAR = IYEAR4(IYEAR)
        DAY=IDAY+IHOUR/24.D0+MINUTE/1440.D0+SEC/86400.D0
        TLAST =DJUL(IYEAR,MONTH,DAY)
C
C RECORD 15       # OF SATELLITES
115     READ(LFNOBS,222,END=930) STRING,HEAD
        IF(HEAD(1:7).EQ.'COMMENT') THEN
          NCOM=NCOM+1
          COMENT(NCOM)=STRING
          GOTO 115
        END IF
        READ(STRING,15,ERR=940) NSATEL
15      FORMAT(I6)
C
C RECORD 16       PRN / # OF OBS
        DO 100 I=1,NSATEL
116       READ(LFNOBS,222,END=930) STRING,HEAD
          IF(HEAD(1:7).EQ.'COMMENT') THEN
            NCOM=NCOM+1
            COMENT(NCOM)=STRING
            GOTO 116
          END IF
          READ(STRING,16,ERR=940) NUMSAT(I),(NUMOBS(I,K),K=1,NUMTYP)
16        FORMAT(I6,9I6)
100     CONTINUE
C
C READ BLANK LINE
117     READ(LFNOBS,222,END=940) STRING,HEAD
        IF(HEAD(1:7).EQ.'COMMENT') THEN
          NCOM=NCOM+1
          COMENT(NCOM)=STRING
          GOTO 117
        END IF
        IF(STRING.NE.' ') GOTO 940
      END IF
      IRCODE=0
      GOTO 999
C
C  WRONG FILE TYPE
910   IRCODE=1
      WRITE(LFNERR,911) RXT
911   FORMAT(' SR RXRDOH: WRONG FILE TYPE: "',A,'"')
      BACKSPACE LFNOBS
      GOTO 999
C
C  NOT ANTICIPATED VERSION NUMBER
920   IRCODE=2
      WRITE(LFNERR,921) IRV
921   FORMAT(' SR RXRDOH: NOT ANTICIPATED VERSION NUMBER:',I5)
      GOTO 999
C
C  END OF FILE WITHIN HEADER
930   IRCODE=3
      WRITE(LFNERR,931)
931   FORMAT(' SR RXRDOH: END OF FILE WITHIN HEADER')
      GOTO 999
C
C  ERROR DECODING DATA
940   IRCODE=4
      WRITE(LFNERR,941) STRING,HEAD(1:19)
941   FORMAT(' SR RXRDOH: ERROR DECODING DATA ON THE FOLLOWING LINE:',
     1       /,1X,2A)
      GOTO 999
C
C  END OF FILE AT FIRST RECORD
990   IRCODE=9
      GOTO 999
C
999   RETURN
      END SUBROUTINE

      END MODULE
