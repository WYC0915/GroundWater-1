      MODULE s_COVDIA
      CONTAINS

C*
      SUBROUTINE COVDIA(AELL,BELL,DXELL,DRELL,SCELL,XSTAT,COVM3,
     1                  RMS,DIAG1,REULER,DIAG2,ALPHA)
CC
CC NAME       :  COVDIA
CC
CC PURPOSE    :  COMPUTE MAIN AXIS TRANSFORMATION OF A COORDINATE
CC               ESTIMATION:
CC               DIAG CONTAINS THE MAIN AXIS OF THE ERROR ELLIPSOID:
CC               1 SMALLEST, 3 LARGEST ONE
CC               REULER CONTAINS THE ANGELS:
CC               1: AZIMUT OF THE MINOR AXIS
CC                  IN THE HORIZONTAL LOKAL PLANE
CC               2: 3-D ANGLE OF THE MAIN AXIS DIAG1(3)
CC               3: ELEVATION ANGLE OF THE SECOND MAIN AXIS
CC                  (DIAG1(2)) IN THE HORIZONTAL LOKAL PLANE
CC
CC PARAMETERS :
CC         IN :  AELL   : SEMI-MAJOR AXIS OF THE REFERENCE    R*8
CC                        ELLIPSOID IN METERS
CC               BELL   : SEMI-MINOR AXIS OF THE REFERENCE    R*8
CC                        ELLIPSOID IN METERS
CC               DXELL(3): TRANSLATION COMPONENTS FROM THE    R*8
CC                        ORIGIN OF THE CART. COORD. SYSTEM
CC                        (X,Y,Z) TO THE CENTER OF THE REF.
CC                        ELLIPSOID (IN METRES)
CC               DRELL(3): ROTATIONS TO WGS-84 (RADIAN)       R*8
CC               SCELL  : SCALE FACTOR BETWEEN REF. ELLIPSOID R*8
CC                        AND WGS-84
CC               XSTAT(3): CARTESIAN COORDINATES (M)          R*8
CC               COVM3(3,3): COVARIANCE MATRIX OF COORDINATES R*8
CC                        IN X,Y,Z (NOT YET MULTIPLIED WITH
CC                        RMS !!!)
CC               RMS:     RMS OF GPS OBSERVATIONS             R*8
CC        OUT :  DIAG1(3): MAIN AXIS OF ERROR ELLIPSOID IN M  R*8
CC                        (THEOR. 2 TIMES LARGER THAN 'NORMAL'
CC                        RMS ERRORS: REASON:F-DISTRIBUTION
CC                        OF 95 % CONFIDENCE INTERVAL)
CC               REULER(3): ANGLES IN DEG                     R*8
CC                        1: AZIMUT OF THE MINOR AXIS
CC                           IN THE HORIZONTAL LOKAL PLANE
CC                        2: 3-D ANGLE OF THE MAIN AXIS DIAG1(3)
CC                        3: ELEVATION ANGLE OF THE SECOND MAIN AXIS
CC                        (DIAG1(2)) IN THE HORIZONTAL LOKAL PLANE
CC               DIAG2(2):MAIN AXIS FOR 2-D ELLIPSE IN LOKAL  R*8
CC                        HORIZONTAL PLANE
CC               ALPHA   :AZIMUT OF MINOR AXIS OF 2-D         R*8
CC                        ELLIPSE IN LOKAL PLANE
CC
CC REMARKS    :  ---
CC
CC AUTHOR     :  E. BROCKMANN
CC
CC VERSION    :  3.4  (JAN 93)
CC
CC CREATED    :  5-MAR-93
CC
CC CHANGES    :  12-NOV-93 : ??: USE CORRECT SUBROUTINES TRIDIA AND EIGENV
CC                               AND /ZERO HANDLING
CC               22-DEC-93 : ??: ADD SOME COMMENT LINES
CC               08-APR-94 : ??: LOGIC TO ALIGN 2D AND 3D ANGLES
CC               25-MAI-95 : EB: CHANGE COMPUTATION OF AZIMUT IN LOCAL
CC                               DIRECTION TO STANDARD GEODETICAL
CC                               FORMULAS (OLD ONE IS STILL INCLUDED,
CC                               BUT COMMENTED OUT)
CC               17-JUN-96 : JJ: CHECK FOR NUMERICAL PROBLEM (COS>1)
CC                2-SEP-96 : EB: REPLACE DATAN BY DATAN2
CC               16-FEB-04 : RD: RETURN IF COVM3 IS SINGULAR
CC               23-JUN-05 : MM: IMPLICIT NONE AND DECLARATIONS ADDED
CC               28-FEB-07 : AG: use rho from defcon
CC
CC COPYRIGHT  :  ASTRONOMICAL INSTITUTE
CC      1993     UNIVERSITY OF BERN
CC               SWITZERLAND
CC
C*
      USE d_const, ONLY: rho
      USE s_tridia
      USE s_err3d
      USE s_eigenv
      USE s_xyzell
      IMPLICIT NONE
C
C DECLARATIONS INSTEAD OF IMPLICIT
C --------------------------------
      INTEGER*4 I     , J     , NP
C
      REAL*8    AELL  , ALPHA , BELL  , HH    , PHI   , RADIUS, RHLP  ,
     1          RMS   , SCELL , VCOS  , VSIN  , XLONG
C
CCC       IMPLICIT REAL*8 (A-H,O-Z)
C
      REAL*8       XSTAT(*),DXELL(3),DRELL(3)
      REAL*8       COVM3(3,*),COVH1(3,3)
      REAL*8       REULER(*)
      REAL*8       XPELL(3)
      REAL*8       DIAG1(*),OFFDIA(3),DIAG2(2),DIAG(3)
      REAL*8       EIGVEC(3,3)
C
C      DATA PI/3.141592653589793D0/
C
C      RHO=180.D0/PI
C
C CHECK FOR A VALID COV MATRIX
      IF (COVM3(1,1).LE.0d0 .OR. COVM3(2,2).LE.0d0 .OR.
     1    COVM3(3,3).LE.0d0) THEN
        DIAG1(1:3) =0D0
        REULER(1:3)=0d0
        DIAG2(1:2) =0d0
        ALPHA      =0d0
        RETURN
      ENDIF
C
C ELLIPSOIDAL COORDINATES OF RECEIVER (NEW)
      CALL XYZELL(AELL,BELL,DXELL,DRELL,SCELL,XSTAT,XPELL)
      PHI  =XPELL(1)
      XLONG=XPELL(2)
      HH   =XPELL(3)
      CALL ERR3D(PHI,XLONG,HH,AELL,BELL,-1,COVM3,EIGVEC)
C PHI,LAMDA IN M
      RADIUS=DSQRT(XSTAT(1)**2+XSTAT(2)**2+XSTAT(3)**2)
      EIGVEC(1,1)=EIGVEC(1,1)*RADIUS**2
      EIGVEC(2,2)=EIGVEC(2,2)*RADIUS**2*(DCOS(PHI))**2
      EIGVEC(1,2)=EIGVEC(1,2)*RADIUS**2*DCOS(PHI)
      EIGVEC(2,1)=EIGVEC(1,2)
      EIGVEC(1,3)=EIGVEC(1,3)*RADIUS
      EIGVEC(3,1)=EIGVEC(1,3)
      EIGVEC(2,3)=EIGVEC(2,3)*RADIUS*DCOS(PHI)
      EIGVEC(3,2)=EIGVEC(2,3)
      EIGVEC(3,3)=EIGVEC(3,3)
C
C DIMENSION
      NP=3
C
C SAVE MATRIX
      DO 80 I=1,NP
        DO 80 J=1,NP
          COVH1(I,J)=EIGVEC(I,J)
80    CONTINUE
C
C EIGEN-VALUES
C
      CALL TRIDIA(NP,NP,EIGVEC,DIAG,OFFDIA)
C
      CALL EIGENV(NP,NP,DIAG,OFFDIA,EIGVEC)
C
C COMPUTE ANGLES OF ERROR ELLIPSOID
C ---------------------------------
C
C DEVIATION OF THE ZENIT
      IF (EIGVEC(3,3).GT.1.D0) EIGVEC(3,3)=1.D0
      REULER(2)=RHO*DACOS(EIGVEC(3,3))
C AZIMUTH (PROJECTION OF EIGENVECTOR OF LAMDA 1 IN HORIZONTAL PLANE)
C ANGLE DIRECTION: FROM AXIS 1 TO AXIS 2 CLOCKWISE
      REULER(1)=RHO*DATAN2(EIGVEC(2,1),EIGVEC(1,1))
      IF (REULER(1).LT.0) REULER(1)=REULER(1)+360.D0
C MAXIMUM OF 180
      IF (REULER(1).GT.180.D0) REULER(1)=REULER(1)-180.D0
C PROJECTION ANGLE OF EIGENVECTOR OF LAMDA 1
      REULER(3)=RHO*DASIN(EIGVEC(3,1))
C
C MAJOR AXIS: C(I)= 2*RMS**2*EIGENV(I)*F(1-ALPHA;3,INFINITY)
C -------------------------------------|---  = 2  ----------|
      DO 130 I=1,NP
        IF (DIAG(I).GT.0.D0) THEN
C SAME SCALE AS PRINTED RMS ERRORS OF ONE COORDINATE
          DIAG1(I)=DSQRT(RMS**2*DIAG(I))
CC          DIAG1(I)=DSQRT(2.D0*RMS**2*DIAG(I)*2.D0)
        ELSE
          DIAG1(I)=0.D0
        ENDIF
130   CONTINUE
C
C COMPUTE THE ERROR ELLIPSE ONLY IN LB PLANE (USING DIRECT FORMULA)
C -----------------------------------------------------------------
C      IF (DIAG1(1).GT.0.D0.AND.DIAG1(2).GT.0.D0.AND.DIAG1(3).GT.0.D0)
C     1THEN
C        DIAG2(1)=DSQRT(RMS**2*(0.5D0*(COVH1(1,1)+COVH1(2,2))-
C     1         DSQRT(.25D0*(COVH1(1,1)+COVH1(2,2))**2-
C     2         COVH1(1,1)*COVH1(2,2)+COVH1(1,2)**2)))
C        DIAG2(2)=DSQRT(RMS**2*(0.5D0*(COVH1(1,1)+COVH1(2,2))+
C     1         DSQRT(.25D0*(COVH1(1,1)+COVH1(2,2))**2-
C     2         COVH1(1,1)*COVH1(2,2)+COVH1(1,2)**2)))
C        WRITE(*,*)'diff',DIAG2(1)**2/RMS,DIAG2(1),COVH1(2,2)
C        IF (DIAG2(1)**2/RMS-COVH1(2,2).NE.0.D0) THEN
C          ALPHA=RHO*DATAN(COVH1(1,2)/(DIAG2(1)**2/RMS-COVH1(2,2)))
C        ELSE
C          ALPHA=90.D0
C        ENDIF
C        IF (DIAG2(1)**2/RMS-COVH1(2,2).LT.0) ALPHA=ALPHA+180.D0
C        IF (COVH1(1,2).LT.0.AND.DIAG2(1)**2/RMS-COVH1(1,1).GE.0)
C     1          ALPHA=ALPHA+360.D0
CC MAXIMUM OF 180
C        IF (ALPHA.GT.180.D0) ALPHA=ALPHA-180.D0
C      ELSE
C        ALPHA=0.D0
C        DIAG2(1)=0.D0
C        DIAG2(2)=0.D0
C      ENDIF
C
C COMPUTE THE ERROR ELLIPSE ONLY IN LB PLANE (USING DIRECT FORMULA)
C -----------------------------------------------------------------
      IF (DIAG1(1).GT.0.D0.AND.DIAG1(2).GT.0.D0.AND.DIAG1(3).GT.0.D0)
     1THEN
        DIAG2(1)=RMS*DSQRT((0.5D0*(COVH1(1,1)+COVH1(2,2))-
     1         DSQRT(.25D0*(COVH1(1,1)-COVH1(2,2))**2+COVH1(1,2)**2)))
        DIAG2(2)=RMS*DSQRT((0.5D0*(COVH1(1,1)+COVH1(2,2))+
     1         DSQRT(.25D0*(COVH1(1,1)-COVH1(2,2))**2+COVH1(1,2)**2)))
C
C  STANDARD GEODETIC FORMULA FOR AZIMUTH OF MAJOR SEMIAXIS
C  AZ= RHO/2*DATAN(2*COV12/(COV11-COV22)
C  CALCULATION OF ARCTAN ONLY (WITH TAN=SI/CO) IN FULL 360 DEGREES
C
        VSIN=2.0D0*COVH1(1,2)
        VCOS=COVH1(1,1)-COVH1(2,2)
        IF (VCOS.NE.0.D0) THEN
          ALPHA=RHO*DATAN(VSIN/VCOS)
          IF (VCOS.LT.0) ALPHA=ALPHA+180.D0
          IF (ALPHA.LT.0) ALPHA=ALPHA+360.D0
        ELSE
          IF (VSIN.LT.0) THEN
            ALPHA=270.D0
          ELSE
            ALPHA=90.D0
          ENDIF
        ENDIF
C END OF ARCTAN: AZIMUTH IN FULL 360 DEGREES
C FACTOR 2 OF NEW FORMULA (SEE ABOVE)
        ALPHA=ALPHA/2
C CHANGE TO AZIMUTH OF MINOR SEMIAXIS
        ALPHA=ALPHA+90.D0
        IF (ALPHA.GT.360.D0) ALPHA=ALPHA-360.D0
C CUT TO AZIMUTH TO ANGLE BETWEEN 0 AND 180 DEGREES
        IF (ALPHA.GT.180.D0) ALPHA=ALPHA-180.D0
C END OF AZIMUTH CALCULATION IF ELLIPSE<>0
C
      ELSE
C IF ELLIPSIOD = 0
        ALPHA=0.D0
        DIAG2(1)=0.D0
        DIAG2(2)=0.D0
      ENDIF
C
C AZIMUT CONSISTENT FOR 2-DIM AND 3-DIM
C -------------------------------------
      IF (DABS(DIAG1(1)-DIAG(1)).GT.DABS(DIAG1(1)-DIAG(2))) THEN
        REULER(1)=REULER(1)-90.D0
        IF (REULER(1).LE.0.D0) REULER(1)=REULER(1)+180.D0
        RHLP=DIAG1(1)
        DIAG1(1)=DIAG1(2)
        DIAG1(2)=RHLP
      ENDIF
C
      RETURN
      END SUBROUTINE

      END MODULE
