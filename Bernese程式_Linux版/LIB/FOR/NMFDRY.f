      MODULE s_NMFDRY
      CONTAINS

C*
      SUBROUTINE NMFDRY(EPOCH,LAT,HEIGHT,ZEN,HMF)
CC
CC NAME       :  NMFDRY
CC
CC PURPOSE    :  ROUTINE TO COMPUTE THE NIELL MAPPING FUNCTION (NMF)
CC               FOR THE HYDROSTATIC (DRY) COMPONENT. SEE ALSO SR
CC               NMFWET FOR THE WET COMPONENT.
CC
CC PARAMETERS :
CC         IN :  EPOCH  : OBSERVATION EPOCH IN MJD            R*8
CC               LAT    : LATITUDE OF STATION IN RAD          R*8
CC               HEIGHT : HEIGHT OF STATION IN M              R*8
CC               ZEN    : ZENITH DISTANCE IN RAD              R*8
CC        OUT :  HMF    : MAPPING FUNCTION FOR DRY DELAY      R*8(2)
CC                        (1): MAPPING FACTOR
CC                        (2): DERIVATIVE WITH RESPECT TO
CC                             ZENITH DISTANCE (IN 1/RAD)
CC
CC REMARKS    :  THIS SUBROUTINE IS A SLIGHTLY MODIFIED VERSION OF
CC               NIELL'S NMFW2 ROUTINE. REFERENCE: A.E.NIELL, GLOBAL
CC               MAPPING FUNCTIONS FOR THE ATMOSPHERE DELAY AT RADIO
CC               WAVELENGTHS, JGR, VOL. 101, NO. B2, PP. 3227-3246,
CC               1996.
CC
CC AUTHOR     :  S.SCHAER
CC
CC VERSION    :  4.1
CC
CC CREATED    :  31-JAN-97
CC
CC CHANGES    :  11-FEB-97 : SS: SIGN OF DERIVATIVE
CC               28-FEB-07 : AG: USE PI FROM DEFCON
CC
CC COPYRIGHT  :  ASTRONOMICAL INSTITUTE
CC      1997     UNIVERSITY OF BERN
CC               SWITZERLAND
CC
C*
      USE d_const, ONLY: pi
      IMPLICIT NONE
      REAL*8 EPOCH,LAT,ZEN
C
C     ROUTINE TO COMPUTE THE HYDROSTATIC MAPPING FUNCTION NHMF2 WHICH
C     DEPENDS ON DOY (DAY OF YEAR) AND STATION POSITION (LATITUDE
C     AND HEIGHT ABOVE GEOID; USE ELLIPSOID HEIGHT FOR NOW).
C
C 931007 AEN NEW NHMF2
C 951129 AEN MOD NMFH2P1 ADD DERIVATIVE OF HEIGHT CORRECTION WRT ELEVATION TO HMF(2).
C                NOTE CHANGE IN SPELLING OF SUBROUTINE FROM NHMF TO NMFH.
C 960530 AEN MOD CORRECTION: ADD DOY TERM FOR |ELEVATIONS| .GE. 75 DEGREES.
C 960814 AEN MOD CHANGE CORRECTION FOR SOUTHERN LATITUDE SO DAY NUMBER (DOY) ISN'T CHANGED.
C
      INTEGER*4 I
C
C   A,B,C       - THE A,B,AND C COEFFICIENTS IN THE CONTINUED FRACTION
C                 FORM OF MARINI
C   BETA        - INTERMEDIATE TERM IN CALCULATION
C   GAMMA       - INTERMEDIATE TERM IN CALCULATION
C   SINE        - SINE OF ELEVATION ANGLE
C   COSE        - COS OF ELEVATION ANGLE
C   HMF(1)      - DELAY MAPPING FUNCTION
C   HMF(2)      - D_MAPPING_FUNCTION/D_ELEVATION (DHMF2/D_EL)
C   TOPCON      - CONSTANT OF TOP OF MAPPING FUNCTION TO ENSURE
C                 THAT VALUE IS 1.0000 AT ZENITH
C
      REAL*8 A,B,C, BETA, COSE, HMF(2), GAMMA, SINE, TOPCON
C
C   HEIGHT     - HEIGHT OF SITE ABOVE GEOID (METERS)
C   HS_KM      - HEIGHT OF SITE IN KMS.
C   LATITUDE   - LATITUDE (DEGREES)
C   L          - ABSOLUTE LATITUDE
C   DL         - INCREMENTAL LATITUDE FROM LAST LAT_HMF
C   ELEV       - ELEVATION (DEGREES)
C   EPOCH      - IF JULIAN DATE OF OBSERVATION IS KNOWN FOR THE OBSERVATION,
C              - THEN EPOCH CAN BE USED TO GET DAY OF YEAR.
C              - (IF EPOCH  IS PASSED AS ARGUMENT, THEN UN-COMMENT THE
C                 LINE CONVERTING EPOCH TO DOY.)
C   DOY        - DAYS SINCE DEC 31
C   DOY_ATM    - DOY FOR ATMOSPHERE RELATIVE TO JAN 28.
C   DOYR_ATM   - DOY_ATM IN RADIANS;
C   COST       - COSINE(DAY OF YEAR)
C   DOY2RAD    - CONVERT DOY TO RADIANS
C
      REAL*8 DOY, LATITUDE, HEIGHT, ELEV
      REAL*8 HS_KM, L, DL, DOY_ATM, DOYR_ATM, COST
      REAL*8 DOY2RAD, DEG2RAD
C
C   LAT_HMF     - LATITUDES AT WHICH COEFFICIENTS ARE DEFINED (5).
C   ABC_AVG     - CONTINUED FRACTION COEFFICIENTS AT LATITUDES LAT_HMF
C   ABC_AMP     - AMPLITUDE OF ANNUAL VARIATION OF ABC_AVG
C   DAAVG, DAAMP, ETC - INCREMENTAL VALUES FOR INTERPOLATION
C   AAVG,  AAMP,  ETC - AVERAGE AND AMPLITUDE AT LATITUDE
C
      REAL*8 LAT_HMF(5)
      REAL*8 ABC_AVG(5,3), ABC_AMP(5,3)
      REAL*8 DAAVG, DAAMP, DBAVG, DBAMP, DCAVG, DCAMP
      REAL*8 AAVG,  AAMP,  BAVG,  BAMP,  CAVG,  CAMP
C
C   A_HT, B_HT, C_HT - PARAMETERS FOR CONTINUED FRACTION FOR HEIGHT CORR'N.
C   DHCC_DEL    - DERIVATIVE OF HEIGHT CORRECTION COEFFICIENT WITH ELEVATION
C   DHT_CORR_DEL - DERIVATIVE OF HEIGHT CORRECTION WITH ELEVATION
C
      REAL*8 A_HT, B_HT, C_HT, HT_CORR_COEF, HT_CORR
      REAL*8 DHCC_DEL, DHT_CORR_DEL
C
C   DEFINE PARAMETERS USED FOR CALCULATING COEFFICIENTS.
C
      DATA LAT_HMF / 15.D0, 30.D0, 45.D0, 60.D0, 75.D0 /
C
      DATA ABC_AVG /
     .1.2769934D-3,1.2683230D-3,1.2465397D-3,1.2196049D-3,1.2045996D-3,
     .2.9153695D-3,2.9152299D-3,2.9288445D-3,2.9022565D-3,2.9024912D-3,
     .62.610505D-3,62.837393D-3,63.721774D-3,63.824265D-3,64.258455D-3/
C
      DATA ABC_AMP /
     .  0.D0,   1.2709626D-5, 2.6523662D-5, 3.4000452D-5, 4.1202191D-5,
     .  0.D0,   2.1414979D-5, 3.0160779D-5, 7.2562722D-5, 11.723375D-5,
     .  0.D0,   9.0128400D-5, 4.3497037D-5, 84.795348D-5, 170.37206D-5/
C
      DATA A_HT / 2.53D-5/
     .     B_HT / 5.49D-3/
     .     C_HT / 1.14D-3/
C
C   CONVERSIONS:
C
      DOY2RAD = 2.D0*PI/365.25D0
      DEG2RAD = PI/180.D0
C
      LATITUDE=LAT/DEG2RAD
      ELEV=90.D0-ZEN/DEG2RAD
C
C   CONVERT HEIGHT IN METERS TO KILOMETERS
C
      HS_KM  = HEIGHT/1000.D0
C
C   IF JULIAN DATE IS USED FOR EPOCH, THEN CALCULATE DAY OF YEAR;
C      USE 1980 JAN 0 AS REFERENCE EPOCH.
C
      DOY = EPOCH - 44238.D0
C
C MOD AEN 930517 USE PHASE OF 28 DAYS (WINTER EXTREMUM CORRESPONDS TO JAN 28)
C                BASED ON LEAST-SQUARE FIT TO
C                RAYTRACE OF RADIOSONDE DATA FOR DRT, ELP, ALB, CHH, FAI,
C                MUN, AND LIH.
C
      DOY_ATM  = DOY - 28.D0
C
C   TO ACCOUNT FOR THE SIX MONTH DIFFERENCE IN SEASONS BETWEEN HEMISPHERES,
C   ADD 365.25/2 DAYS TO DOY_ATM IF STATION IS IN THE SOUTHERN HEMISPHERE.
C
      L = DABS(LATITUDE)
      IF (LATITUDE .LT. 0.D0) DOY_ATM = DOY_ATM + 365.25D0/2.D0
C
      DOYR_ATM = DOY_ATM * DOY2RAD
C
      COST = DCOS(DOYR_ATM)
C
C   COEFFICIENTS FOR THE CONTINUED FRACTION EXPANSION FOR EACH LATITUDE.
C
C   FOR LATITUDES LESS THAN 15 DEGREES:
C
      IF (L .LE. LAT_HMF(1)) THEN
         A = ABC_AVG(1,1)
         B = ABC_AVG(1,2)
         C = ABC_AVG(1,3)
      ENDIF
C
C   FOR LATITUDES BETWEEN 15 AND 75  DEGREES:
C
      DO I = 1,4
          IF (L .GT. LAT_HMF(I) .AND. L .LE. LAT_HMF(I+1)) THEN
             DL = (L-LAT_HMF(I))/(LAT_HMF(I+1)-LAT_HMF(I))
             DAAVG =   ABC_AVG(I+1,1)-ABC_AVG(I,1)
             DAAMP =   ABC_AMP(I+1,1)-ABC_AMP(I,1)
             AAVG  =   ABC_AVG(I,1) + DL*DAAVG
             AAMP  =   ABC_AMP(I,1) + DL*DAAMP
             A     = AAVG - AAMP*COST
C
             DBAVG =   ABC_AVG(I+1,2)-ABC_AVG(I,2)
             DBAMP =   ABC_AMP(I+1,2)-ABC_AMP(I,2)
             BAVG  =   ABC_AVG(I,2) + DL*DBAVG
             BAMP  =   ABC_AMP(I,2) + DL*DBAMP
             B     = BAVG - BAMP*COST
C
             DCAVG =   ABC_AVG(I+1,3)-ABC_AVG(I,3)
             DCAMP =   ABC_AMP(I+1,3)-ABC_AMP(I,3)
             CAVG  =   ABC_AVG(I,3) + DL*DCAVG
             CAMP  =   ABC_AMP(I,3) + DL*DCAMP
             C     = CAVG - CAMP*COST
          ENDIF
      END DO
C
C   FOR LATITUDES GREATER THAN 75 DEGREES:
C
      IF (L .GE. LAT_HMF(5)) THEN
         A = ABC_AVG(5,1) - ABC_AMP(5,1)*COST
         B = ABC_AVG(5,2) - ABC_AMP(5,2)*COST
         C = ABC_AVG(5,3) - ABC_AMP(5,3)*COST
      ENDIF
C
C   NOW THE COEFFICIENTS EXIST; CALCULATE FOR THE SEA LEVEL PART
C   THE MAPPING FUNCTION, HMF(1), AND THE DERIVATIVE WRT ELEVATION
C   DHMF/D_EL = HMF(2).
C
C   TO GET DELAY-RATE CORRECTION D_TAU/DT:
C      D_TAU/DT = D_TAU-ZEN/DT*HMF(1) + TAU-ZEN*HMF(2)*D_EL/DT
C      WHERE  HMF(2)=DHMF/D_EL
C
      SINE   = DSIN(ELEV * DEG2RAD)
      COSE   = DCOS(ELEV * DEG2RAD)
      BETA   = B/( SINE + C )
      GAMMA  = A/( SINE + BETA)
      TOPCON = (1.D0 + A/(1.D0 + B/(1.D0 + C)))
C
      HMF(1) =     TOPCON / ( SINE + GAMMA )
C
      HMF(2) =     -TOPCON*COSE / ( SINE + GAMMA )**2 *
     .            ( 1.D0 - A/ ( SINE + BETA)**2 *
     .            ( 1.D0 - B/ ( SINE + C   )**2 ) )
C
C   APPLY HEIGHT CORRECTION TO MAPPING FUNCTION AND DERIVATIVE WRT ELEVATION:
C
C      1) HEIGHT CORRECTION COEFFICIENT IS
C         1/SINE(ELEV) - CONTINUED FRACTION(A_HT,B_HT,C_HT).
C      2) HEIGHT CORRECTION IS HT_CORR_COEF TIMES HEIGHT IN KM.
C      3) HEIGHT CORRECTION TO DERIVATIVE WRT ELEVATION IS (DERIVATIVE OF
C         HEIGHT CORRECTION COEFFICIENT WRT ELEVATION)*HEIGHT IN KM.
C
      BETA   = B_HT/( SINE + C_HT )
      GAMMA  = A_HT/( SINE + BETA)
      TOPCON = (1.D0 + A_HT/(1.D0 + B_HT/(1.D0 + C_HT)))
      HT_CORR_COEF = 1.D0/SINE - TOPCON/(SINE + GAMMA)
      HT_CORR      = HT_CORR_COEF * HS_KM
      HMF(1)       = HMF(1)     + HT_CORR
C
C    951129 THE DERIVATIVE OF THE HEIGHT CORRECTION WRT ELEVATION IS ADDED
C    TO HMF(2) AFTER CHRIS JACOBS POINTED OUT THE MAGNITUDE OF THE TERM.
C
      DHCC_DEL   = -COSE/SINE**2
     .             +TOPCON*COSE / ( SINE + GAMMA)**2 *
     .            ( 1.D0 - A_HT/ ( SINE + BETA)**2 *
     .            ( 1.D0 - B_HT/ ( SINE + C_HT)**2) )
      DHT_CORR_DEL = DHCC_DEL * HS_KM
      HMF(2)       = HMF(2)     + DHT_CORR_DEL
C
      HMF(2)=-HMF(2)
C
      RETURN
      END SUBROUTINE

      END MODULE
