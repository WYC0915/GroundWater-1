      MODULE s_RXRDMR
      CONTAINS

C*
      SUBROUTINE RXRDMR(LFNMET,LFNERR,NUMTYP,EPOCH,DATMET,IRCODE)
CC
CC NAME       :  RXRDMR
CC
CC PURPOSE    :  READ  OBSERVATION RECORDS OF A
CC               RINEX METEOROLOGICAL DATA FILE
CC
CC PARAMETERS :
CC         IN :  LFNMET : LOGICAL FILE NUMBER                  I*4
CC               LFNERR : LFN FOR ERROR MESSAGES               I*4
CC               NUMTYP : NUMBER OF DIFFERENT MET.TYPES        I*4
CC        OUT :  EPOCH  : OBSERVATION EPOCH (RECEIVER TIME)    R*8
CC               DATMET : LIST OF MET.VALUES                   R*8(*)
CC                        DATMET(J), J: OBS.TYPE
CC               IRCODE : RETURN CODE                          I*4
CC                        0: OK
CC                        4: ERROR DECODING DATA
CC                        5: START OF NEW HEADER FOUND
CC                        9: END OF FILE
CC
CC REMARKS    :  ---
CC
CC AUTHOR     :  W. GURTNER
CC
CC VERSION    :  3.4  (JAN 93)
CC
CC CREATED    :  89/04/07 09:35
CC
CC CHANGES    :  01-JUL-99 : PF: CALL IYEAR4 FOR CONVERSION YY->YYYY
CC               01-MAR-02 : DI: ADD ERR= TO ALL READ STATEMENTS
CC               23-JUN-05 : MM: IMPLICIT NONE AND DECLARATIONS ADDED
CC
CC COPYRIGHT  :  ASTRONOMICAL INSTITUTE
CC      1989     UNIVERSITY OF BERN
CC               SWITZERLAND
CC
C*
      USE f_djul
      USE f_iyear4
      IMPLICIT NONE
C
C DECLARATIONS INSTEAD OF IMPLICIT
C --------------------------------
      INTEGER*4 IDAY  , IHOUR , IRCODE, ISEC  , IYEAR , K     ,
     1          LFNERR, LFNMET, MINUTE, MONTH , NUMTYP
C
      REAL*8    DAY   , EPOCH
C
CCC       IMPLICIT REAL*8 (A-H,O-Z)
C
C GLOBAL DECLARATIONS
C -------------------
      REAL*8       DATMET(*)
C
C  LOCAL DECLARATIONS
C  ------------------
      CHARACTER    STRING*80
C
C RECORD 1        EPOCH/MET
200   READ(LFNMET,222,END=990,ERR=940) STRING
222   FORMAT(A80)
      IF(STRING.EQ.' ') GOTO 200
      IF(STRING(61:65).EQ.'RINEX') GOTO 950
      READ(STRING,1,ERR=940) IYEAR,MONTH,IDAY,IHOUR,MINUTE,ISEC,
     1                       (DATMET(K),K=1,NUMTYP)
1     FORMAT(6I3,8F7.1)
      DAY=IDAY+IHOUR/24.D0+MINUTE/1440.D0+ISEC/86400.D0
      IYEAR = IYEAR4(IYEAR)
      EPOCH=DJUL(IYEAR,MONTH,DAY)
C
900   IRCODE=0
      GOTO 999
C
C  ERROR DECODING DATA
940   IRCODE=4
      WRITE(LFNERR,941) STRING(1:79)
941   FORMAT(' SR RXRDMR: ERROR DECODING DATA ON THE FOLLOWING LINE:',
     1       /,1X,A)
      GOTO 999
C
C  START OF NEW HEADER FOUND
950   IRCODE=5
      BACKSPACE LFNMET
      WRITE(LFNERR,951) STRING(1:79)
951   FORMAT(' SR RXRDMR: START OF A NEW HEADER FOUND:',
     1       /,1X,A)
      GOTO 999
C
C  END OF FILE
990   IRCODE=9
      GOTO 999
C
999   RETURN
      END SUBROUTINE

      END MODULE
