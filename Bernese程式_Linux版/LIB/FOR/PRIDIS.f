      MODULE s_PRIDIS
      CONTAINS

C*
      SUBROUTINE PRIDIS(MAXSTA,TITLES,IPART,RMS,
     1                  DISOLD,DISNEW,QDD,NSTAT,LISTUS,STANUM,NFIX)
CC
CC NAME       :  PRIDIS
CC
CC PURPOSE    :  PRINT SLOPE DISTANCES AND THEIR RMS
CC
CC PARAMETERS :
CC         IN :  MAXSTA : MAXIMUM NUMBER OF STATIONS          I*4
CC               TITLES(I),I=1,2: TITLE LINES                 CH*132
CC               RMS    : RMS OF UNIT WEIGHT                  R*8
CC               DISOLD : DISTANCES (APPROX. COORDINATES)     R*8(1)
CC                        (UPPER TRIANGLE MATRIX, LINEARIZED)
CC               DISNEW : DISTANCES (ADJUST. COORDINATES)     R*8(1)
CC                        (UPPER TRIANGLE MATRIX, LINEARIZED)
CC               QDD    : VAR.MATRIX OF DISTANCES             R*8(1)
CC                        (UPPER TRIANGLE, LINEARIZED)
CC               NSTAT  : NUMBER OF UNKNOWN STATIONS          I*4
CC               LISTUS : LIST OF PARTICIPATING STATIONS      I*4(2,1)
CC               STANUM : EXTERNAL STATION NUMBERS            I*4(1)
CC               NFIX   : NUMBER OF FIXED STATIONS            I*4
CC
CC REMARKS    :  MAXIMUM NUMBER OF STATIONS : 100
CC
CC AUTHOR     :  W.GURTNER, M.ROTHACHER
CC
CC VERSION    :  3.4  (JAN 93)
CC
CC CREATED    :  87/11/03 10:26
CC
CC CHANGES    :  21-MAY-91 : ??: FORMAT FOR LARGE DISTANCES
CC               27-MAY-91 : ??: DON'T PRINT TRAILING BLANKS
CC               10-AUG-94 : MR: CALL EXITRC
CC               18-SEP-95 : JJ: MAXSTA TO 200 FROM 120
CC               05-MAR-03 : CU: REMOVE USE OF SKELETON FILE, USE IPART
CC               28-JUN-04 : RD: USE MAXSTA FROM P_GPSEST
CC               08-JUL-04 : RD: MAXSTA IS AN INPUT PARAMETER
CC               21-JUN-05 : MM: COMLFNUM.inc REMOVED, m_bern ADDED
CC               23-JUN-05 : MM: IMPLICIT NONE AND DECLARATIONS ADDED
CC               28-JUN-05 : MM: UNUSED VARIABLES REMOVED
CC
CC
CC COPYRIGHT  :  ASTRONOMICAL INSTITUTE
CC      1987     UNIVERSITY OF BERN
CC               SWITZERLAND
CC
C*
C
      USE m_bern
      USE f_lengt1
      IMPLICIT NONE
C
C DECLARATIONS INSTEAD OF IMPLICIT
C --------------------------------
      INTEGER*4 I     , IK    , J     , K     , MAXSTA, N     ,
     1          NACT  , NCHAR , NFIX  , NFROM , NMAT  , NMAXS , NSTAT ,
     2          NTO   , NTOT
C
      REAL*8    RMS
C
CCC       IMPLICIT REAL*8 (A-H,O-Z)
C
      CHARACTER*132 TITLES(2)
      CHARACTER*12  CHDOLD(MAXSTA),CHDNEW(MAXSTA),CHDRMS(MAXSTA),BLK12
      CHARACTER*1   OLDNEW(MAXSTA),VERTIC(MAXSTA),MINUS
      CHARACTER*14  TXTSTRG
      INTEGER*4     LISTUS(2,*),STANUM(*),IPART
      REAL*8        DISOLD(*),DISNEW(*),QDD(*)
C
      DATA MINUS/'-'/,BLK12/'            '/
C
C  TOTAL NUMBER OF PARTICIPATING STATIONS
      NTOT=NSTAT+NFIX
C
C  MARK 'NEW' AND 'FIXED' STATIONS
      DO 110 J=1,NSTAT
        OLDNEW(J)='N'
110   CONTINUE
      DO 120 J=NSTAT+1,NTOT
        OLDNEW(J)='F'
120   CONTINUE
C
C  NUMBER OF PARTIAL MATRICES: MAX. 'NMAXS' STATIONS PER LINE ALLOWED
      NMAXS=8
      NMAT=(NTOT-2)/NMAXS+1
C
      NFROM=2
C
      DO 10 N=1,NMAT
C
C  STATION NUMBERS TO BE PRINTED IN ONE LINE
        NTO  =NFROM+NMAXS-1
        IF(NTO.GT.NTOT) NTO=NTOT
C  ACTUAL # OF STATIONS
        NACT=NTO-NFROM+1
C  # OF CHARACTERS PER LINE (EXCEPT PRINTER CONTROL CHARACTER)
        NCHAR=14*NACT+11
C
        WRITE(LFNPRT,1) TITLES(1)(1:LENGT1(TITLES(1))),
     1                  TITLES(2)(1:LENGT1(TITLES(2)))
1       FORMAT(//,A,/,A,/,' ',131('-'),//)
C
        IF(N.EQ.1) TXTSTRG = ''
        IF(N.GT.1) TXTSTRG = '  CONTINUATION'
        WRITE(LFNPRT,"(
     1       ' SLOPE DISTANCES AND RMS ERRORS IN M  (PART ',I1,'):',A
     2    ,/,' ---------------------------------------------'
     3    ,/,1X)") IPART,TRIM(TXTSTRG)
C
        WRITE(LFNPRT,3) (MINUS,J=1,NCHAR)
3       FORMAT(' ',131A1)
C
C  WRITE STATION NUMBERS
C
        DO 20 K=NFROM,NTO
          VERTIC(K)=' '
C  SEPARATION BETWEEN NEW AND FIXED STATIONS
          IF(K.EQ.NSTAT) VERTIC(K)='|'
C  RIGHT HAND BOUNDARY OF TABLE
          IF(K.EQ.NTOT)  VERTIC(K)='|'
20      CONTINUE
        WRITE(LFNPRT,4)(STANUM(LISTUS(1,J)),OLDNEW(J),
     1                                      VERTIC(J),J=NFROM,NTO)
4       FORMAT(' | NUM |   |',8(I8,1X,A1,3X,A1))
        WRITE(LFNPRT,3) (MINUS,J=1,NCHAR)
C
        DO 30 I=1,NTOT-1
C
          DO 40 K=NFROM,NTO
C
C  DIAGONAL OR LOWER TRIANGLE OF MATRIX: SET VALUES BLANK
            IF(I.GE.K) THEN
              CHDOLD(K)='            '
              CHDNEW(K)='            '
              CHDRMS(K)='            '
            ELSE
C  INDEX IN LINEARIZED MATRICES
              IK=(K-1)*K/2+I
C
C  OUTPUT OLD DISTANCE TO INTERNAL FILE
              IF(DISOLD(IK).LT.10000000.D0) THEN
                WRITE(CHDOLD(K),6) DISOLD(IK)
6               FORMAT(F12.4)
              ELSE
                WRITE(CHDOLD(K),16) DISOLD(IK)
16              FORMAT(F12.3)
              END IF
C
C  OUTPUT NEW DISTANCE TO INTERNAL FILE
C
C  BOTH STATIONS HELD FIXED (NEW DISTANCE BLANK)
              IF(I.GT.NSTAT.AND.K.GT.NSTAT) THEN
                CHDNEW(K)='            '
              ELSE
C  AT LEAST ONE STATION IS NEW
                IF(DISNEW(IK).LT.10000000.D0) THEN
                  WRITE(CHDNEW(K),6) DISNEW(IK)
                ELSE
                  WRITE(CHDNEW(K),16) DISNEW(IK)
                END IF
C
C  OUTPUT RMS OF DISTANCE TO INTERNAL FILE
                WRITE(CHDRMS(K),6) RMS*DSQRT(QDD(IK))
              END IF
            END IF
40        CONTINUE
C
C  PRINT OLD AND NEW DISTANCES, RMS (1 LINE OF MATRIX EACH)
          WRITE(LFNPRT,7) STANUM(LISTUS(1,I)),
     1                    (CHDOLD(J),VERTIC(J),J=NFROM,NTO)
7         FORMAT(' | ',I3,' | O |',8(A12,1X,A1))
          WRITE(LFNPRT,8) OLDNEW(I),(CHDNEW(J),VERTIC(J),J=NFROM,NTO)
8         FORMAT(' |   ',A1,' | N |',8(A12,1X,A1))
C  RMS ONLY IF AT LEAST ONE STATION NEW
          IF(I.LE.NSTAT.OR.K.LE.NSTAT)
     1      WRITE(LFNPRT,9) (CHDRMS(J),VERTIC(J),J=NFROM,NTO)
9         FORMAT(' |     |RMS|',8(A12,1X,A1))
C
C  SEPARATE OLD STATIONS FROM NEW STATIONS BY '---'-LINE
C  BOTTOM LINE AFTER LAST STATION
          IF(I.EQ.NSTAT.OR.I.EQ.NTOT-1) THEN
            WRITE(LFNPRT,3) (MINUS,J=1,NCHAR)
          ELSE
            WRITE(LFNPRT,11) (BLK12,VERTIC(J),J=NFROM,NTO)
11          FORMAT(' |     |   |',8(A12,1X,A1))
          END IF
30      CONTINUE
C
        NFROM=NFROM+NMAXS
C
10    CONTINUE
C
      RETURN
      END SUBROUTINE

      END MODULE
