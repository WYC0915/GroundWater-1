      MODULE s_PRAE
      CONTAINS

C*
      SUBROUTINE PRAE(XMJD,PRAEZ)
CC
CC NAME       :  PRAE
CC
CC PURPOSE    :  COMPUTATION OF PRECESSION-MATRIX FROM 1950.0
CC               TO EPOCH XMJD
CC               SEE "EXPLANATORY SUPPLEMENT TO THE EPHEMERIS"
CC               EDITION 1961
CC
CC PARAMETERS :
CC         IN :  XMJD   : EPOCH IN MODIFIED JULIAN DATE   R*8
CC        OUT :  PRAEZ  : PRECESSION - MATRIX             R*8(3,3)
CC
CC REMARKS    :  ---
CC
CC AUTHOR     :  G.BEUTLER
CC
CC VERSION    :  3.3
CC
CC CREATED    :  31-MAY-92
CC
CC CHANGES    :  __-___-__ : __:
CC
CC COPYRIGHT  :  ASTRONOMICAL INSTITUTE
CC      1992     UNIVERSITY OF BERN
CC               SWITZERLAND
CC
C*
      IMPLICIT NONE
      REAL*8 PRAEZ(3,3),XMJD,T,KAPPA,OMEGA,NUE,CN,SN,CO,SO,SK,CK
C
      T=XMJD-33281.9234D0
      KAPPA=(.3059532D-6+.109D-14*T)*T
      OMEGA=(.3059532D-6+.396D-14*T)*T
      NUE  =(.2660400D-6-.156D-14*T)*T
C
      CN=DCOS(NUE)
      SN=DSIN(NUE)
      CO=DCOS(OMEGA)
      SO=DSIN(OMEGA)
      SK=DSIN(KAPPA)
      CK=DCOS(KAPPA)
C
      PRAEZ(1,1)=CK*CO*CN-SK*SO
      PRAEZ(2,1)=CK*SO*CN+SK*CO
      PRAEZ(3,1)=CK*SN
      PRAEZ(1,2)=-SK*CO*CN-CK*SO
      PRAEZ(2,2)=-SK*SO*CN+CK*CO
      PRAEZ(3,2)=-SK*SN
      PRAEZ(1,3)=-CO*SN
      PRAEZ(2,3)=-SO*SN
      PRAEZ(3,3)=CN
C
      RETURN
      END SUBROUTINE

      END MODULE
