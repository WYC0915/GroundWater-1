      MODULE s_STCPRP
      CONTAINS

C*
      SUBROUTINE STCPRP(NINT,TB12,NIMPT,NISD,FRCTYP,
     1                  NISEFF,STCINT,TIMSTC)
CC
CC NAME       :  STCPRP
CC
CC PURPOSE    :  PREPARE PROCESSING OF STOCHASTIC INFO PER ARC
CC
CC PARAMETERS :
CC        IN  :  NINT   : NUMBER OF INTERVALS IN INTEGRATION    I*4
CC               TB12   : INTERVAL BOUNDARIES                   R*8(*)
CC               NIMPT  : NUMBER OF IMPULSES PER EPOCH          I*4
CC               NISD   : NUMBER OF SETS PER DAY                I*4
CC               FRCTYP : CORRESPONDING FORCE TYPES             I*4(*)
CC        OUT :  NISEFF : TRUE NUMBER OF IMPULSE SETS PER ARC   I*4
CC               STCINT : DESCRIPTION OF EPOCHS AT WHICH STOCH. I*4(*)
CC                        IMPULSES TAKE PLACE
CC                        STCINT(K)=I, AT LEFT INTERVAL BOUNDARY
CC                        OF INTERVAL K IS IDENTICAL WITH I-TH
CC                        EPOCH FOR (BEGINNING IF ACCELERATIONS)
CC                        STOCHASTIC PERTURBATION
CC               TIMSTC : EPOCHS OF STOCHASTIC PERTURBATION     R*8(*,*)
CC                        (I) : EPOCH OF PERTURBATION I
CC
CC REMARKS    :  ---
CC
CC AUTHOR     :  G. BEUTLER
CC
CC VERSION    :  3.4 (JAN 93)
CC
CC CREATED    :  92/12/21
CC
CC CHANGES    :  10-AUG-94 : MR: CALL EXITRC
CC               10-DEC-03 : AJ: STOCHASTIC ACCELERATIONS ADDED
CC               14-APR-05 : AJ: SMALL CORRECTION FOR MODSTC=2
CC               21-JUN-05 : MM: COMLFNUM.inc REMOVED, m_bern ADDED
CC               23-JUN-05 : MM: IMPLICIT NONE AND DECLARATIONS ADDED
CC
CC COPYRIGHT  :  ASTRONOMICAL INSTITUTE
CC      1992     UNIVERSITY OF BERN
CC               SWITZERLAND
CC
C*
      USE m_bern
      USE s_exitrc
      IMPLICIT NONE
C
C DECLARATIONS INSTEAD OF IMPLICIT
C --------------------------------
      INTEGER*4 IEPRT , IMIN  , INT   , MXCSTC, NIMPT , NINT  , NISD  ,
     1          NISEFF, MODSTC, NTEST
C
      REAL*8    ARCLGT, DTEFF , TEST  , TMIN  , TPERT , XNEFF , TPERT1,
     1          TPERT2
C
CCC       IMPLICIT REAL*8 (A-H,O-Z)
C
      INTEGER*4    STCINT(*),FRCTYP(*)
      REAL*8       TB12(*),TIMSTC(3,*)
      CHARACTER*6  MXNSTC
C
      COMMON/MCMSTC/MXCSTC,MXNSTC
C
C RETURN IF EITHER NISD=0 OR NIMPT=0
C ----------------------------------
      IF (NISD.EQ.0.OR.NIMPT.EQ.0) RETURN
C
C INITIALIZE ARRAY STCINT:
C -----------------------
      DO 10 INT=1,NINT+1
        STCINT(INT)=0
10    CONTINUE
C
C CHECK TYPE OF STOCHASTIC PARAMETERS
C -----------------------------------
      IF (FRCTYP(1).LT.10) THEN
        MODSTC=0
      ELSEIF(FRCTYP(1).LT.20) THEN
        MODSTC=1
      ELSEIF(FRCTYP(1).LT.30) THEN
        MODSTC=2
      END IF
C
C NUMBER OF PERTURBATION EPOCHS PER ARC
C -------------------------------------
      ARCLGT=TB12(NINT+1)-TB12(1)
      XNEFF = ARCLGT*NISD
      NISEFF=DNINT(XNEFF)
C
      IF(MODSTC.EQ.0) NISEFF=NISEFF-1
      IF(MODSTC.EQ.2) NISEFF=NISEFF+1
C
      IF (NISEFF.EQ.0) RETURN
C
      IF (MODSTC.EQ.0.AND.NISEFF.GT.(NINT-1)) THEN
        WRITE(LFNERR,11) NISEFF,NINT-1
11      FORMAT(/,' ### SR STCPRP: TOO MANY STOCHASTIC ORBIT',

     1           ' PARAMETERS',
     2         /,16X,'NUMBER REDUCED TO THE NUMBER OF INTERVALS',
     3         /,16X,'(MINUS ONE) OF THE NUMERICAL INTEGRATION,'
     4         /,16X,'REQUESTED NUMBER:',I5,
     5         /,16X,'REDUCED NUMBER  :',I5,/)
        NISEFF=NINT-1
      END IF
      IF (MODSTC.EQ.1.AND.NISEFF.GT.NINT) THEN
        WRITE(LFNERR,12) NISEFF,NINT
12      FORMAT(/,' ### SR STCPRP: TOO MANY STOCHASTIC ORBIT',
     1           ' PARAMETERS',
     2         /,16X,'NUMBER REDUCED TO THE NUMBER OF INTERVALS',
     3         /,16X,'OF THE NUMERICAL INTEGRATION,'
     4         /,16X,'REQUESTED NUMBER:',I5,
     5         /,16X,'REDUCED NUMBER  :',I5,/)
        NISEFF=NINT
      END IF
      IF (MODSTC.EQ.2.AND.NISEFF.GT.(NINT+1)) THEN
        WRITE(LFNERR,13) NISEFF,NINT+1
13      FORMAT(/,' ### SR STCPRP: TOO MANY STOCHASTIC ORBIT',
     1           ' PARAMETERS',
     2         /,16X,'NUMBER REDUCED TO THE NUMBER OF INTERVALS',
     3         /,16X,'(PLUS ONE) OF THE NUMERICAL INTEGRATION,'
     4         /,16X,'REQUESTED NUMBER:',I5,
     5         /,16X,'REDUCED NUMBER  :',I5,/)
        NISEFF=NINT+1
      END IF
C
      IF(MODSTC.EQ.0) THEN
        DTEFF=ARCLGT/(NISEFF+1)
      ELSEIF(MODSTC.EQ.1) THEN
        DTEFF=ARCLGT/NISEFF
      ELSEIF(MODSTC.EQ.2) THEN
        DTEFF=ARCLGT/(NISEFF-1)
      END IF
C
      IF (NISEFF.GT.MXCSTC) THEN
        WRITE(LFNERR,14) NISEFF,MXCSTC
14      FORMAT(/,' *** SR STCPRP: TOO MANY STOCHASTIC ORBIT',
     1           ' PARAMETERS',
     2         /,16X,'REQUESTED NUMBER      :',I5,
     3         /,16X,'MAXIMUM NUMBER ALLOWED:',I5,/)
        CALL EXITRC(2)
      END IF
C
C DEFINE PERTURBATION EPOCHS
C --------------------------
      TPERT1=TB12(1)
      TPERT2=TB12(1)
      IF(MODSTC.GE.1)STCINT(1)=1
      DO 100 IEPRT=1,NISEFF
        IF(MODSTC.GE.1.AND.IEPRT.NE.1) THEN
          TPERT1=TIMSTC(2,IEPRT-1)
          IF(MODSTC.EQ.2) TPERT2=TIMSTC(3,IEPRT-1)
          STCINT(IMIN)=IEPRT
        END IF
        TPERT=TB12(1)+IEPRT*DTEFF
        TMIN=1000000.D0
        NTEST=NINT
        IF(MODSTC.GE.1) NTEST=NTEST+1
        DO 30 INT=1,NTEST
          TEST=DABS(TPERT-TB12(INT))
          IF (TEST.LT.TMIN) THEN
            IMIN=INT
            TMIN=TEST
          END IF
30      CONTINUE
        IF(MODSTC.EQ.0)STCINT(IMIN)=IEPRT
        TIMSTC(1,IEPRT)=TPERT1
        IF(MODSTC.EQ.2) THEN
          TIMSTC(2,IEPRT)=TPERT2
          TIMSTC(3,IEPRT)=TB12(IMIN)
        ELSE
          TIMSTC(2,IEPRT)=TB12(IMIN)
        END IF
        IF(MODSTC.EQ.0) THEN
          TIMSTC(1,IEPRT)=TIMSTC(2,IEPRT)
          TIMSTC(2,IEPRT)=TB12(NINT+1)
          TIMSTC(3,IEPRT)=TB12(NINT+1)
        ELSEIF(MODSTC.EQ.1) THEN
          TIMSTC(3,IEPRT)=TB12(NINT+1)
        END IF
100   CONTINUE
C
      RETURN
      END SUBROUTINE

      END MODULE
