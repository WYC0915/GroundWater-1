      MODULE s_POLDEF2
      CONTAINS

C*
      SUBROUTINE POLDEF2(TIME,ISUBFL,INTERP,XR,YR,DUTFRD,DUTGPS,DX,DY)
CC
CC NAME       :  POLDEF2
CC
CC PURPOSE    :  DEFINITION OF POLAR COORDINATES XR,YR (RADIAN),
CC               UT1-UTC (DAYFRACTION), GPS-UTC(DAYFRACTION)
CC               LAGRANGE INSTEAD OF LINEAR INTERPOLATION
CC
CC PARAMETERS :
CC         IN :  TIME   : TIME OF REQUEST IN MJD (IN UTC)     R*8
CC               ISUBFL : SUBDAILY MODEL INDICATION FLAG      I*4
CC                        =0 DO NOT APPLY SUBDAILY MODEL
CC                        =1 DO APPLY SUBDAILY MODEL
CC               INTERP : INTERPOLATION                       I*4
CC                        =1 LINEAR
CC                        =2 LAGRANGE
CC        OUT :  XR     : POLE X-COORDINATE (RADIAN)          R*8
CC               YR     : POLE Y-COORDINATE (RADIAN)          R*8
CC               DUTFRD : UT1-UTC FOR REQUESTED TIME (DAYS)   R*8
CC               DUTGPS : GPS-UTC (DAYS)                      R*8
CC
CC REMARKS    :
CC
CC AUTHOR     :  U. HUGENTOBLER
CC
CC CREATED    :  21-JAN-06
CC
CC CHANGES    :  21-JAN-06 HU: ADAPTED FROM POLDEF
CC               29-JAN-07 AG: EXTRAPOLATION TOLERANCE FOR LITPOL ADDED
CC
CC COPYRIGHT  :  ASTRONOMICAL INSTITUTE
CC      1987     UNIVERSITY OF BERN
CC               SWITZERLAND
CC
C*
      USE m_bern
      USE s_ut1red
      USE s_getpol2
      USE s_subpol
      USE s_litpol
      USE s_lagint
      USE s_exitrc
      IMPLICIT NONE
C
C DECLARATIONS INSTEAD OF IMPLICIT
C --------------------------------
      INTEGER*4 I1    , ISUBFL, INTERP
C
      REAL*8    DELTA , DUTFRD, DUTGPS, TIME, XR, YR, DX, DY
C
CCC       IMPLICIT REAL*8 (A-H,O-Z)
C
      REAL*8    T(4),XPOLE(4),YPOLE(4),UT1UTC(4),GPSUTC(4)
      REAL*8    UT1GPS(4),DEPS(4),DPSI(4)
      REAL*8 xr_hlp(1), yr_hlp(1), dutfrd_hlp(1), dutgps_hlp(1)
      REAL*8 dx_hlp(1), dy_hlp(1)
C
      INTEGER*4 POLTYP(2)
C
      DATA T/200000.D0,100000.D0,0.D0,-100000.D0/

C Variables for SUBPOL
      REAL*8       ERPSUB(3),ERPSUR(3)
      CHARACTER*16 SUBNAM


C
      IF(TIME.LT.T(2).OR.TIME.GE.T(3))THEN
        CALL GETPOL2(TIME,ISUBFL,T,XPOLE,YPOLE,UT1UTC,GPSUTC,
     1              DEPS,DPSI,POLTYP)
C
C REDUCE UT1-UTC TO UT1R-UTC FOR INTERPOLATION
        DO 10 I1=1,4
          CALL UT1RED(T(I1),DELTA)
          UT1UTC(I1)=UT1UTC(I1)-DELTA/86400000.D0
10      CONTINUE
C
      END IF

C
C INTERPOLATION OF POLE COORDINATES
C ---------------------------------
      IF (INTERP.EQ.1) THEN
        CALL LITPOL(2,1,T(2:3),XPOLE(2:3),TIME,0d0,XR_hlp)
        XR = XR_hlp(1)
        CALL LITPOL(2,1,T(2:3),YPOLE(2:3),TIME,0d0,YR_hlp)
        YR = YR_hlp(1)
C INTERPOLATE UT1GPS (NOT AFFECTED BY LEAP SECONDS)
        DUTGPS = GPSUTC(2)
        UT1GPS = UT1UTC-GPSUTC
        CALL LITPOL(2,1,T(2:3),UT1GPS(2:3),TIME,0d0,DUTFRD_hlp)
        DUTFRD = DUTFRD_hlp(1)+DUTGPS
        CALL LITPOL(2,1,T(2:3),DPSI(2:3),TIME,0d0,DX_hlp)
        DX = DX_hlp(1)
        CALL LITPOL(2,1,T(2:3),DEPS(2:3),TIME,0d0,DY_hlp)
        DY = DY_hlp(1)
      ELSEIF (INTERP.EQ.2) THEN
        CALL LAGINT (T,XPOLE,TIME,XR)
        CALL LAGINT (T,YPOLE,TIME,YR)
C INTERPOLATE UT1GPS (NOT AFFECTED BY LEAP SECONDS)
        DUTGPS = GPSUTC(2)
        UT1GPS = UT1UTC-GPSUTC
        CALL LAGINT (T,UT1GPS,TIME,DUTFRD)
        DUTFRD = DUTFRD+DUTGPS
        CALL LAGINT (T,DPSI,TIME,DX)
        CALL LAGINT (T,DEPS,TIME,DY)
      ELSE
        WRITE(lfnerr,
     1        "(/' *** SR POLDEF2: INTERPOLATION UNDEFINED',I6)") INTERP
        CALL EXITRC(2)
      ENDIF
C
C ADD SUBDAILY POLE MODEL TO POLE VALUES
C --------------------------------------
      IF (ISUBFL.EQ.1) THEN
        CALL SUBPOL(TIME,SUBNAM,ERPSUB,ERPSUR)
        XR=XR+ERPSUB(1)/206264.806D0
        YR=YR+ERPSUB(2)/206264.806D0
        DUTFRD=DUTFRD+ERPSUB(3)/86400.D0
      ENDIF
C
C CORRECT INTERPOLATED UT1R-UTC TO UT1-UTC
C ----------------------------------------
      CALL UT1RED(TIME,DELTA)
      DUTFRD=DUTFRD+DELTA/86400000.D0
C
      RETURN
      END SUBROUTINE

      END MODULE
