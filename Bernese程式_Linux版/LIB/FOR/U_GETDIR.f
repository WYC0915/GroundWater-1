      MODULE s_U_GETDIR
      CONTAINS

C*
      SUBROUTINE GETDIR(PTHDIR,FLSPEC,MAXFIL,ISORT,
     1             NRFILE,FILNAM,FILSIZ,FILDAT,FILTIM,SINDEX,IRCODE)
CC
CC NAME       :  GETDIR
CC
CC PURPOSE    :  - CREATE DIRECTORY, GET ALL FILENAMES OF DIRECTORY
CC
CC PARAMETERS :
CC         IN :  PTHDIR     : PATH TO THE DIRECTORY                 CH*(*)
CC               FLSPEC     : FILE SPECIFICATION TO CREATE DIR      CH*(*)
CC               MAXFIL     : DECLARED SIZE OF FILE LIST             I*4
CC               ISORT      : 1: SORT FILES ALPHABETICALLY           I*4
CC                            2: INCLUDE FILE SIZE
CC                            4: INCLUDE FILE DATE
CC                            8: INCLUDE FILE TIME
CC         OUT:  NRFILE     : NUMBER OF FILES IN LIST                I*4
CC               FILNAM     : LIST OF FILES FOUND                   CH*(*)(*)
CC                            (WITHOUT PATH)
CC               FILSIZ     : LIST OF FILE SIZES (BYTES)             I*4(*)
CC               FILDAT     : LIST OF FILE CREATION DATES           CH*9(*)
CC               FILTIM     : LIST OF FILE CREATION TIMES           CH*8(*)
CC               SINDEX     : SORT INDEX                             I*4(*)
CC               IRCODE     : RETURN CODE                            I*4
CC                            2: ERROR OR EXIT
CC
CC REMARKS    :  UNIX-VERSION
CC
CC AUTHOR     :  W. GURTNER, H. FRIEDHOFF
CC
CC CREATED    :  31-AUG-89
CC
CC CHANGES    :  22-MAY-91 : ??: INCLUDE SIZE/DATE/TIME OF FILES
CC               23-FEB-93 : ??: ONLY IF BIT 1 AND 2 OF ISORT IS SET
CC               29-AUG-94 : MR: NO PROMP1 FOR NON-INTERACTIVE MODE
CC               14-SEP-95 : LM: CORRECT CREATION DATE AND TIME
CC               05-JUN-96 : LM: CORRECTION FOR GERMAN HP
CC               11-JUN-96 : MR: USE LENGT1 FOR FLSPEC (MAY BE BLANK)
CC               08-SEP-98 : MR: USE "SYSCMD"
CC               24-AUG-00 : HU: FREE DECLARATIONS OF STRINGS
CC               17-FEB-03 : LM: NOINTR=1 HARD-WIRED
CC               12-MAR-12 : RD: USE SPLSTR AS MODULE NOW
CC               27-MAR-12 : RD: USE PROMP1 AS MODULE NOW
CC
CC COPYRIGHT  :  ASTRONOMICAL INSTITUTE
CC      1989     UNIVERSITY OF BERN
CC               SWITZERLAND
C*
      USE s_promp1
      USE f_lengt0
      USE s_upperc
      USE f_lengt1
      USE s_splstr
      USE s_dattim
      USE f_tstflg
      USE s_cordup
      IMPLICIT NONE
C
C DECLARATIONS INSTEAD OF IMPLICIT
C -------------------------------
      INTEGER*4 MAXSTR, IFIRST, NOINTR, LPATH, LSPEC, LOUTF, I,
     1          LFNLOC, IRCODE, IHLP2, NSUB, IHLP3, IDAY, IYEAR,
     2          K, ISORT
C
      PARAMETER(MAXSTR=20)
C  GLOBAL DECLARATIONS
      INTEGER*4    MAXFIL,NRFILE,SINDEX(*),FILSIZ(*)
      CHARACTER    PTHDIR*(*), FLSPEC*(*), FILNAM(*)*(*)
      CHARACTER    FILTIM(*)*8,FILDAT(*)*9
C
C  LOCAL DECLARATIONS
      CHARACTER    FILOUT*80,STRING*200,CHR1*1
      CHARACTER*80 TMPSTR,SUBSTR(MAXSTR),HLPSTR,STRG(1)
      CHARACTER    CDATE*9,CTIME*5
      CHARACTER    MNTH*3
      DATA FILOUT/'FILOUT.SCR'/
      DATA LFNLOC/99/
      DATA IFIRST/1/
C
C GET INTERACTION MODE
      IF (IFIRST.EQ.1) THEN
cccc        CALL GTINTM(NOINTR)
        nointr = 1
        IFIRST=0
      ENDIF
C
      LPATH=LENGT1(PTHDIR)
      LSPEC=LENGT1(FLSPEC)
      LOUTF=LENGT1(FILOUT)
C
C  REPLACE WRONG WILDCARDS
      DO 30 I=1,LSPEC
        IF(FLSPEC(I:I).EQ.'%') FLSPEC(I:I)='?'
30    CONTINUE
C
C  CREATE DIRECTORY COMMAND
      WRITE(STRING,1) 'ls -l ',
     1            PTHDIR(1:LPATH),FLSPEC(1:LSPEC),
     2            ' >',FILOUT(1:LOUTF)
1     FORMAT(5A)
C
C  EXECUTE COMMAND
      CALL SYSCMD(STRING,LEN(STRING))
C
C  GET CURRENT YEAR
      CALL DATTIM(CDATE,CTIME)
C
C  READ FILE WITH DIRECTORY
      OPEN(UNIT=LFNLOC,FILE=FILOUT,STATUS='OLD',ERR=920)
C
      DO 10 I=1,MAXFIL+1
11      READ(LFNLOC,'(A)',END=20) STRING
        IF(I.EQ.MAXFIL+1) THEN
          WRITE(*,211) MAXFIL
211       FORMAT(/,' *** SR GETDIR: TOO MANY FILES',
     1           /,16X,'INCREASE "MAXFIL" IN CALLING ROUTINE',
     2           /,16X,'MAX. NUMBER OF FILES ALLOWED:',I5,/)
          IF (NOINTR.NE.1) THEN
            STRG(1)='TYPE <ENTER> TO CONTINUE'
            CALL PROMP1(1,STRG)
            READ(*,'(A)') CHR1
          ENDIF
          IRCODE=2
          GOTO 999
        END IF
C
C  SPLIT INPUT STRING
        CALL SPLSTR(STRING,MAXSTR,' ',NSUB,SUBSTR,IRCODE)
        IF(IRCODE.NE.0) GOTO 999
C
        IHLP2=LENGT0(SUBSTR(NSUB-2))
        IHLP3=LENGT0(SUBSTR(NSUB-3))
        IF (SUBSTR(NSUB-3)(IHLP3:IHLP3) .EQ. '.') THEN
          HLPSTR         = SUBSTR(NSUB-3)(1:IHLP3-1)
          SUBSTR(NSUB-3) = SUBSTR(NSUB-2)(1:IHLP2-2)
          SUBSTR(NSUB-2) = HLPSTR
        END IF
C
C  GET FILESIZE
        IF(TSTFLG(CHAR(ISORT),1)) THEN
           READ(SUBSTR(NSUB-4),*) FILSIZ(I)
        END IF
C
C  GET DATE AND TIME OF FILE CREATION
        IF(TSTFLG(CHAR(ISORT),2)) THEN
c MONTH
          TMPSTR=SUBSTR(NSUB-3)
          MNTH=TMPSTR(1:3)
          CALL UPPERC(MNTH)
c DAY
          READ(SUBSTR(NSUB-2),*) IDAY
c TIME AND YEAR
          TMPSTR=SUBSTR(NSUB-1)
          IF(TMPSTR(3:3).NE.':') THEN
            FILTIM(I)='00:00:00'
            READ(TMPSTR,*) IYEAR
            IYEAR=MOD(IYEAR,100)
          ELSE
            FILTIM(I)=TMPSTR(1:5)//':00'
            READ(CDATE(8:9),*) IYEAR
          ENDIF
          WRITE(FILDAT(I),100) IDAY,MNTH,IYEAR
100       FORMAT(I2,'-',A3,'-',I2)
        END IF
C
C  GET FILENAME
        TMPSTR=SUBSTR(NSUB)
        CALL SPLSTR(TMPSTR,MAXSTR,'/',NSUB,SUBSTR,IRCODE)
        IF(IRCODE.NE.0) GOTO 999
        FILNAM(I)=SUBSTR(NSUB)
C
10    CONTINUE
20    CLOSE(UNIT=LFNLOC,STATUS='DELETE')
      NRFILE=I-1
C
C  SORT FILENAMES ALPHABETICALLY
C  (REPLACE '_' BY CHAR(1) TEMPORARILY BECAUSE OF ASCII VALUE)
      IF(TSTFLG(CHAR(ISORT),0)) THEN
        DO 40 I=1,NRFILE
          DO 40 K=1,12
            IF(FILNAM(I)(K:K).EQ.'_') FILNAM(I)(K:K)=CHAR(1)
40      CONTINUE
        CALL CORDUP(FILNAM,NRFILE,1,12,SINDEX)
        DO 50 I=1,NRFILE
          DO 50 K=1,12
            IF(FILNAM(I)(K:K).EQ.CHAR(1)) FILNAM(I)(K:K)='_'
50      CONTINUE
      END IF
C
      IRCODE=0
      GOTO 999
C
920   WRITE(*,921) FILOUT(1:LENGT1(FILOUT))
921   FORMAT(/,' *** SR GETDIR: DIRECTORY FILE NOT FOUND',
     1       /,16X,'FILENAME: ',A,/)
      IF (NOINTR.NE.1) THEN
        STRG(1)='PRESS <ENTER> TO CONTINUE'
        CALL PROMP1(1,STRG)
        READ(*,'(A)') CHR1
      ENDIF
      IRCODE=2
      GOTO 999
C
999   RETURN
      END SUBROUTINE

      END MODULE
