      MODULE s_ORBINT
      CONTAINS

C*
      SUBROUTINE ORBINT(EPH,TEPO,EPOEPH,LFNERR,X,DTSATC,IRCODE)
CC
CC NAME       :  ORBINT
CC
CC PURPOSE    :  COMPUTE GLONASS SATELLITE POSITION AND SATELLITE
CC               CLOCK CORRECTION FOR SPECIFIED EPOCH USING
CC               NUMERICAL INTEGRATION
CC
CC PARAMETERS :
CC         IN :  EPH    : NAVIGATION MESSAGE RECORD            R*8(16)
CC               TEPO   : EPOCH FOR NEW SATELLITE POSITION     R*8
CC               EPOEPH : EPOCH FOR GIVEN SATELLITE POSITION   R*8
CC               LFNERR : LFN FOR ERROR MESSAGES               I*4
CC        OUT :  X      : SATELITE POSITION FOR TEPO           R*8(3)
CC                        X(1)=X
CC                        X(2)=Y
CC                        X(3)=Z
CC               DTSATC : SATELLITE CLOCK CORRECTION FOR TEPO  R*8
CC                        (SEC)
CC               IRCODE : RETURN CODE                          I*4
CC                        0: OK
CC                        3: TOO MANY INTEGRATION STEPS
CC
CC
CC REMARKS    :
CC
CC AUTHOR     :  H. HABRICH
CC
CC VERSION    :  4.1(FEB-97)
CC
CC CREATED    :  04-MAR-97
CC
CC CHANGES    :  12-MAR-98 : MR: DTSATC IN SEC
CC               27-MAY-98 : JJ: MOVE DATA STATEMENT TO AFTER INCLUDE
CC               30-SEP-03 : HB: TAKE MAXINT FROM M_MAXDIM
CC               16-JUN-05 : MM: UNUSED COMCONST.inc REMOVED
CC               23-JUN-05 : MM: IMPLICIT NONE AND DECLARATIONS ADDED
CC               13-SEP-07 : RD: BRDC-MESSAGE W/O CLOCK INFORMATION
CC               04-MAY-12 : RD: USE DMOD FROM MODULE
CC
CC COPYRIGHT  :  ASTRONOMICAL INSTITUTE
CC      1997     UNIVERSITY OF BERN
CC               SWITZERLAND
CC
C*
      USE M_MAXDIM, ONLY : MAXINT
      USE l_basfun, ONLY: dmod
      USE f_gldvdt
      IMPLICIT NONE
C
C DECLARATIONS INSTEAD OF IMPLICIT
C --------------------------------
      INTEGER*4 I     , IRCODE, ISTEP , LFNERR, NRSTEP
C
      REAL*8    DTSATC, EPOEPH, TEPO  , X0    , X1    , X2    , XI    ,
     1          XIH   , XII   , XIII  , XIIII , XK1   , XK2   , XK3   ,
     2          XK4   , XKK   , XVI   , XVIH  , XVII  , XVIII , XVIIII,
     3          Y0    , Y1    , Y2    , YI    , YIH   , YII   , YIII  ,
     4          YIIII , YK1   , YK2   , YK3   , YK4   , YKK   , YVI   ,
     5          YVIH  , YVII  , YVIII , YVIIII, Z0    , Z1    , Z2    ,
     6          ZI    , ZIH   , ZII   , ZIII  , ZIIII , ZK1   , ZK2   ,
     7          ZK3   , ZK4   , ZKK   , ZVI   , ZVIH  , ZVII  , ZVIII ,
     8          ZVIIII
C
CCC       IMPLICIT REAL*8 (A-H,O-Z)
C
C PARAMETERS
C MAXINT : MAXIMUM NUMBER OF INTEGRATION STEPS
C
C DECLARATIONS
C ------------
      REAL*8       EPH(*),X(3)
      REAL*8       H,HSTEP(MAXINT),INTERV
C
C COMMON BLOCKS
C -------------
C
      DATA INTERV/10.D0/
C INTERV : INTERVAL FOR ONE INETRAGTION STEP (SEC)
C
C X COORDINATE
C ------------
      X0=EPH(5)
      X1=EPH(6)
      X2=EPH(7)
C
C Y COORDINATE
C ------------
      Y0=EPH(9)
      Y1=EPH(10)
      Y2=EPH(11)
C
C Z COORDINATE
C ------------
      Z0=EPH(13)
      Z1=EPH(14)
      Z2=EPH(15)
C
C SATELLITE CLOCK
C ---------------
      IF (EPH(2).NE.0D0.OR.EPH(3).NE.0D0) THEN
        DTSATC=EPH(2)+EPH(3)*(TEPO-EPOEPH)*86400.D0
      ENDIF
C
C NUMERICAL INTEGRATION
C ---------------------
C
C FORMULA ACCORDING GLONASS INTERFACE CONTROL DOCUMENT
C
C RUNGE-KUTTA-METHOD FOR INTEGRATION, NOTATION ACCORDING BRONSTEIN
C
C INITIALIZATION
C --------------
      XI=X0
      YI=Y0
      ZI=Z0
      XVI=X1
      YVI=Y1
      ZVI=Z1
C
C DEFINE INTEGRATION INTERVAL
C ---------------------------
C     NRSTEP = NUMBER OF ITERATION STEPS
C     HSTEP   = INTERVAL FOR EACH ITERATION STEP (SECONDS)
      NRSTEP=IABS(IDINT((TEPO-EPOEPH)*86400.D0/INTERV))+1
      IF(NRSTEP.GT.MAXINT)GOTO 930
      DO 100 I=1,NRSTEP-1
        HSTEP(I)=INTERV
        IF(TEPO-EPOEPH.LT.0.D0)HSTEP(I)=HSTEP(I)*(-1.D0)
100   CONTINUE
      HSTEP(NRSTEP)=DMOD((TEPO-EPOEPH)*86400.D0,INTERV)
C
C LOOP FOR ALL INTEGRATION STEPS
C ------------------------------
      DO 3000 ISTEP=1,NRSTEP
        H=HSTEP(ISTEP)
C
C INTEGRATION FOR VELOCITY
C ------------------------
C FORMULA TO COMPUTE NEW VELOCITY:
C V(I+1)=V(I)+DV/DT*DT
C
        XK1=GLDVDT(1,XI,YI,ZI,XVI,YVI)+X2
        YK1=GLDVDT(2,XI,YI,ZI,XVI,YVI)+Y2
        ZK1=GLDVDT(3,XI,YI,ZI,XVI,YVI)+Z2
        XVII=XVI+XK1*H/2
        YVII=YVI+YK1*H/2
        ZVII=ZVI+ZK1*H/2
        XII=XI+XVII*H/2
        YII=YI+YVII*H/2
        ZII=ZI+ZVII*H/2
C
        XK2=GLDVDT(1,XII,YII,ZII,XVII,YVII)+X2
        YK2=GLDVDT(2,XII,YII,ZII,XVII,YVII)+Y2
        ZK2=GLDVDT(3,XII,YII,ZII,XVII,YVII)+Z2
        XVIII=XVI+XK2*H/2
        YVIII=YVI+YK2*H/2
        ZVIII=ZVI+ZK2*H/2
        XIII=XI+XVIII*H/2
        YIII=YI+YVIII*H/2
        ZIII=ZI+ZVIII*H/2
C
        XK3=GLDVDT(1,XIII,YIII,ZIII,XVIII,YVIII)+X2
        YK3=GLDVDT(2,XIII,YIII,ZIII,XVIII,YVIII)+Y2
        ZK3=GLDVDT(3,XIII,YIII,ZIII,XVIII,YVIII)+Z2
        XVIIII=XVI+XK3*H
        YVIIII=YVI+YK3*H
        ZVIIII=ZVI+ZK3*H
        XIIII=XI+XVIIII*H
        YIIII=YI+YVIIII*H
        ZIIII=ZI+ZVIIII*H
C
        XK4=GLDVDT(1,XIIII,YIIII,ZIIII,XVIIII,YVIIII)+X2
        YK4=GLDVDT(2,XIIII,YIIII,ZIIII,XVIIII,YVIIII)+Y2
        ZK4=GLDVDT(3,XIIII,YIIII,ZIIII,XVIIII,YVIIII)+Z2
C
        XKK=(XK1+2*XK2+2*XK3+XK4)/6
        YKK=(YK1+2*YK2+2*YK3+YK4)/6
        ZKK=(ZK1+2*ZK2+2*ZK3+ZK4)/6
        XVIH=XVI+XKK*H
        YVIH=YVI+YKK*H
        ZVIH=ZVI+ZKK*H
C
C INTEGRATION FOR POSITION
C ------------------------
C FORMULA TO COMPUTE NEW POSITION:
C P(I+1)=P(I)+DP/DT*DT
C
        XK1=XVI
        YK1=YVI
        ZK1=ZVI
C
        XK2=XVII
        YK2=YVII
        ZK2=ZVII
C
        XK3=XVIII
        YK3=YVIII
        ZK3=ZVIII
C
        XK4=XVIIII
        YK4=YVIIII
        ZK4=ZVIIII
C
        XKK=(XK1+2*XK2+2*XK3+XK4)/6
        YKK=(YK1+2*YK2+2*YK3+YK4)/6
        ZKK=(ZK1+2*ZK2+2*ZK3+ZK4)/6
        XIH=XI+XKK*H
        YIH=YI+YKK*H
        ZIH=ZI+ZKK*H
C
C
C INITIALIZATION OF NEW INTEGRATION STEP
C --------------------------------------
        XVI=XVIH
        YVI=YVIH
        ZVI=ZVIH
        XI=XIH
        YI=YIH
        ZI=ZIH
C
C NEXT INTEGRATION STEP
3000  CONTINUE
C
C WRITE RESULTS
C -------------
      X(1)=XI
      X(2)=YI
      X(3)=ZI
C
      IRCODE=0
      GOTO 999
C
C TOO MANY INTEGRATION STEPS
930   IRCODE=3
      WRITE(LFNERR,931) NRSTEP,MAXINT
931   FORMAT(/,' *** SR ORBINT: TOO MANY INTEGRATION STEPS',
     1      /,16X,'NUMBER OF INTEGRATION STEPS: ',I6,
     2      /,16X,'INTEGRATION STEPS ALLOWED  : ',I6,/)
      GOTO 999
C
C
999   RETURN
      END SUBROUTINE

      END MODULE
