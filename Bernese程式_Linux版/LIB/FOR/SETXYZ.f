      MODULE s_SETXYZ
      CONTAINS

C*
      SUBROUTINE SETXYZ(NTFIL,TABFIL,ITFIL,INDTIM,TB1,TB2,DTABD,
     1                  NSAT,NAVNUM,IRCTOT,SOURCE,NSAMPL)
CC
CC NAME       :  SETXYZ
CC
CC PURPOSE    :  READ ALL POSITIONS FROM TABULAR ORBIT FILE FOR
CC               A SPECIFIC ARC. SR FOR PGM DEFSTD
CC
CC PARAMETERS :
CC         IN :  NTFIL  : NUMBER OF TABULAR ORBIT FILES       I*4
CC               TABFIL(I),I=1,2,..,NTFIL: TABULAR ORBIT FILE CH*32
CC               ITFIL  : FIRST FILE TO BE USED IN TABLE      I*4
CC               INDTIM : INDEX FOR TIME DEFINITION           I*4
CC                    =1: TIMES IN TAB FILES ARE GPS TIME
CC                    =2: TIMES IN TAB FILES ARE UTC
CC               TB1, TB2: ARC BOUNDARIES (MJD)               R*8
CC        OUT :  DTABD  : TABLE INTERVAL                      R*8
CC               NSAT   : TOTAL NUMBER OF SATELLITES FOR ARC  I*4
CC               NAVNUM(I),I=1,2,..,NSAT: SVN-NUMBERS ASSO-   I*4
CC                        CIATED WITH THE INDIVIDUAL SATELLITES
CC               IRCTOT : RETURN CODE                         I*4
CC               SOURCE(K),K=1,2,..,10: ORBIT SOURCE          CH*1
CC               NSAMPL : SAMPLING OF THE PRE-FILES           R*8(2)
CC                        1: MIN./2: MAX. VALUE FROM ALL FILES
CC
CC REMARKS    :  ---
CC
CC AUTHOR     :  G.BEUTLER, M.ROTHACHER
CC
CC VERSION    :  3.4  (JAN 93)
CC
CC CREATED    :  87/11/30 08:11
CC
CC CHANGES    :  23-JUN-92 : ??: SUBROUTINE "OPNFIL"
CC               07-JAN-93 : ??: ORDER TABULAR FILES IN TIME
CC               10-AUG-94 : MR: CALL EXITRC
CC               02-OCT-96 : TS: POSSIBILITY TO ENTER PRE AND ERP FILES
CC               04-NOV-96 : MR: ADD PARAMETER "IPOSOK" TO CALL RDTFIL
CC               10-MAY-99 : MR: ADD PARAMETER "IFIL" TO CALL RDTFIL
CC               07-NOV-00 : CU: SWITCH TO NEW MENU SYSTEM
CC               06-AUG-03 : HU: NEW TAB FORMAT, CHECK NUTATION MODEL
CC               21-JUN-05 : MM: COMLFNUM.inc REMOVED, m_bern ADDED
CC               23-JUN-05 : MM: IMPLICIT NONE AND DECLARATIONS ADDED
CC               28-MAR-07 : HB: CHANGE CALL OF CHKSYS (IERS2003 CONV)
CC               29-SEP-08 : RD: TAB-FILES ARE SORTED IN RDORBI
CC                               (MAXFIL IS NOW OBSOLETE)
CC               24-FEB-11 : RD: REPORT SAMPLING OF TAB FILE
CC
CC COPYRIGHT  :  ASTRONOMICAL INSTITUTE
CC      1987     UNIVERSITY OF BERN
CC               SWITZERLAND
CC
C*
      USE m_bern
      USE s_opnfil
      USE s_rdtfil
      USE s_opnerr
      USE s_chksys
      USE s_gtflna
      IMPLICIT NONE
C
C DECLARATIONS INSTEAD OF IMPLICIT
C --------------------------------
      INTEGER*4 IFIL  , INDTIM, IOSTAT, IPOSOK, IRC   ,
     1          IRCTOT, ITCORR, ITFIL , NSAT  , NTFIL
C
      REAL*8    DTAB  , DTABD , TB1   , TB2   , TEND  , TFLAST, TSTART
C
CCC       IMPLICIT REAL*8 (A-H,O-Z)
C
      INTEGER*4    NAVNUM(*)
      REAL*8       NSAMPL(2)
      CHARACTER*1  SOURCE(10)
      CHARACTER*4  BIAS
      CHARACTER*32 TABFIL(*),FILNAM
      CHARACTER*16 SUBNAM,NUTNAM
      CHARACTER*80 nutSubLin
      CHARACTER*100 STR100
C
C ORDER TABULAR FILES ACCORDING TO START TIME
C -------------------------------------------
      NSAMPL=0D0
      DO 10 IFIL=1,NTFIL
        CALL OPNFIL(LFNLOC,TABFIL(IFIL),'OLD','FORMATTED',
     1              'READONLY',' ',IOSTAT)
        CALL OPNERR(LFNERR,LFNLOC,IOSTAT,TABFIL(IFIL),'SETXYZ')
C
        READ(LFNLOC,*)
C CHECK CONSISTENCY OF NUTATION AND SUBDAILY MODELS IN TABFILES
        READ(LFNLOC,"(A)") STR100
        READ(STR100,"(23X,A16,A4,26X,A16)") NUTNAM,BIAS,SUBNAM
        WRITE(nutSubLin,"('NUTSUB: ',A16,1X,A16,1X,A4)")
     1       nutnam,subnam,BIAS
        CALL CHKSYS(1,nutSubLin,IRC)
        CLOSE(UNIT=LFNLOC)
10    CONTINUE
C
C CORRECT STARTING TABULAR FILE INDEX
      DO 20 ITCORR=1,NTFIL
        IF (ITFIL.EQ.ITCORR)  GOTO 30
20    CONTINUE
30    CONTINUE
C
C INITIALIZE ARC DEFINITION
C -------------------------
      IRCTOT=0
      TSTART=TB1
      TEND=TB2
      NSAT=0
C
C OPEN AUXILIARY FILE FOR SAT POS OF ARC
C --------------------------------------
      CALL GTFLNA(1,'TABAUX ',FILNAM,IRC)
      CLOSE(LFN001)
      CALL OPNFIL(LFN001,FILNAM,'UNKNOWN','UNFORMATTED',
     1            ' ',' ',IOSTAT)
      CALL OPNERR(LFNERR,LFN001,IOSTAT,FILNAM,'SETXYZ')
C
C READ TABULAR ORBIT FILES
C ------------------------
      IPOSOK=0
      DO 40 IFIL=ITCORR,NTFIL
        IF (TSTART.GE.TEND) GOTO 41
        CALL OPNFIL(LFNLOC,TABFIL(IFIL),'OLD','FORMATTED',
     1              'READONLY',' ',IOSTAT)
        CALL OPNERR(LFNERR,LFNLOC,IOSTAT,TABFIL(IFIL),'SETXYZ')
C
C READ THE SATELLITE POSITIONS IN REQUIRED TIME INTERVAL FROM
C TABULAR ORBIT FILES .
C IT IS ASSUMED THAT THE FILES ARE GIVEN IN A CHRONOLOGICAL
C ORDER.
C -----------------------------------------------------------
        CALL RDTFIL(LFNLOC,IFIL,INDTIM,TSTART,TEND,IPOSOK,DTAB,
     1              NSAT,NAVNUM,SOURCE,TFLAST,IRCTOT)
        IF (NSAMPL(1).EQ.0d0.OR.NSAMPL(1).GT.DTAB)NSAMPL(1)=DTAB
        IF (NSAMPL(2).EQ.0d0.OR.NSAMPL(2).LT.DTAB)NSAMPL(2)=DTAB
        TSTART=TFLAST
        DTABD=60.D0/86400.D0
C
        CLOSE(UNIT=LFNLOC)
40    CONTINUE
41    CONTINUE
C
      RETURN
      END SUBROUTINE

      END MODULE
