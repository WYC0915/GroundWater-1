      MODULE s_REDNEQ
      CONTAINS

C*
      SUBROUTINE REDNEQ(NOOBDL,MAXTYP,IELTYP,OPTELI,
     1                  NPAR,NAMB,NPARN,NPARMS,PARLST,RMS,
     2                  NPALCQ,ANOR,BNOR,XXX0,LOCQ,PARTYP,INDP,OBSCLS,
     3                  NOBSPA,PARFLG)
CC
CC NAME       :  REDNEQ
CC
CC PURPOSE    :  ELIMINATE AMBIGUITY PARAMETER FOR SATELLITES THAT
CC               HAVE NOT BEEN OBSERVED AND ELIMINATE ALL OTHER
CC               SPECIFIED PARAMETERS (BUT WITH COMPUTING THE EFFECT
CC               ON THE REST OF THE NORMAL EQUATION)
CC
CC REMARK     :  "INDP" IS NOT ANYMORE CORRECT AFTER A PREELIMINATION STEP
CC               IT DOES NOT MATTER (AT THE MOMENT) BECAUSE IT IS USED ONLY
CC               IN SR RESOUT AND SR RESEPO. THESE SUBROUTINES CANNOT WORK
CC               AFTER PREELIMINATING PARAMETERS.
CC
CC PARAMETERS :
CC          IN:  NOOBDL : 0: DELETE PARAMETERS WITH NO OBS.   I*4
CC                        1: NO DELETION AT ALL
CC               MAXTYP : MAX. NUMBER OF PARAMETER TYPES      I*4
CC               IELTYP : FLAG OF PARAMETER TYPES TO BE PRE-  I*4
CC                        ELIMINATED ACCORDING TO ARRAY
CC                        "OPTELI"
CC               OPTELI(I),I=1,..,MAXTYP: OPTION FOR PRE-     I*4
CC                        ELIMINATION OF PARAMETER TYPES:
CC                        =0 : NOT PRE-ELIMINATED
CC                        =1 : PRE-ELIMINATED BEFORE INVERSION
CC                        =2 : PRE-ELIMINATED AFTER  INVERSION
CC                        =3 : PRE-ELIMINATED EPOCH-WISE
CC      IN/OUT:  NPAR   : TOTAL NUMBER OF PARAMETERS          I*4
CC               NAMB   : NUMBER OF AMBIGUITIES               I*4
CC               NPARN  : NPAR-NAMB                           I*4
CC               NPARMS : NUMBER OF PARAMETERS TO COMPUTE RMS I*4
CC               PARLST(I,K), I=1,..,5,K=1,..,MAXTYP: NUMBER  I*4
CC                        OF PARAMETERS:
CC                        I=1: #PARAMETERS OF TYPE I (NPARMS)
CC                        I=2: #SET-UP
CC                        I=3: #NO-OBS
CC                        I=4: #REF. PARAMETERS
CC                        I=5: #SINGULAR
CC               RMS    : SUM OF RESIDUALS SQUARES            R*8
CC               NPALCQ : TOTAL NUMBER OF PARAMETERS IN LOCQ  I*4
CC                        INCLUDING ELIMINATED PARAMETERS
CC               ANOR(I),=1,2,..,NPAR(*(NPAR+1)/2             R*8
CC               BNOR(I),=1,2,..,NPAR                         R*8
CC               XXX0(I),=1,2,..,NPAR                         R*8
CC                        A PRIORI VALUES
CC               LOCQ(K,I),K=1,2,..,MAXLCQ, I=1,2,..,NPAR:    I*4
CC                       PARAMETER CHARACTERIZATION
CC               PARTYP: PARAMETER DESCRIPTION                t_partyp(*)
CC               INDP(I),I=1,..,NPAR: INDEX FOR PARAMETER     I*4
CC                       LOCATION IN REDUCED ANOR,BNOR,...
CC               OBSCLS(IAMB) : CLUSTER NUMBER FOR AMB.PAR.   I*4
CC               NOBSPA : NUM.OF OBSERV PER PARAMETER         I*4(*,*)
CC                        NOBSPA(MAXMEA*ISYS+IMEA,IPAR)
CC               IDEL(I),I=1,..,NPAR: AUXILIARY ARRAY         I*4
CC               NPAELI : NUMBER OF PARAM. TYPES TO BE ELI.   I*4
CC               IPAELI(I),I=1,..,NPAELI: PARAMETER TYPES TO  I*4
CC                        BE PRE-ELIMINATED
CC               PARFLG(K),K=1,..NPAR: FLAG FOR SINGULAR PAR. I*4
CC                        =0 : PARAMETER NOT SINGULAR
CC                        =1 : PARAMETER SINGULAR
CC
CC REMARKS    :  ---
CC
CC AUTHOR     :  M.ROTHACHER
CC
CC VERSION    :  3.4  (JAN 93)
CC
CC CREATED    :  87/10/28 08:37
CC
CC CHANGES    :  12-JUL-92 : ??: WRITE ONLY NUMBER OF ELIMINATED PARAM.
CC               07-AUG-92 : ??: REDUCE ARRAY OBSCLS
CC               19-FEB-93 : ??: KEEP LOCQ INFORMATION OF ELIMINATED
CC                              PARAMETERS. NEW PARAMETER "NPALCQ".
CC                              NEW COMMON "MCMLOC".
CC               29-APR-93 : ??: ELIMINATE PARAMETERS OF NORMAL EQUATION
CC                               WHICH ARE NOT YET ELIMINATED
CC                5-AUG-93 : ??: CORRECT ERROR MESSAGE 901
CC               10-MAR-94 : MR: WARNING, IF NO OBS. FOR A PARAMETER
CC               25-JUL-94 : MR: PRE-ELIMINATION STATISTICS SIMPLIFIED
CC               27-JUL-94 : MR: SYMING INSTEAD OF SYMIN8, "PARFLG"
CC               05-AUG-94 : MR: UPDATE "NPARMS" FOR SINGULAR PARAM.
CC               10-AUG-94 : MR: CALL EXITRC
CC               05-SEP-94 : EB: CORRECT USE OF ERROR MESSAGE 903
CC               18-OCT-94 : EB: CHECK A0I DIMENSION
CC               28-FEB-95 : EB: NO OBS STOCH. PARA IN COMB MODE NOT
CC                               ELIM.
CC               23-MAR-95 : MR: REMOVE TEST MESSAGE, ALL UPPERCASE
CC               21-APR-95 : GB: TURBO VERSION; NEW PARAMETER INDJ1
CC                               IN SR CALL
CC               25-APR-95 : EB: PREELIMINATION OF STATION COORDINATES
CC               04-MAY-95 : GB: NEW TURBO VERSION BASED ON SR REDTRB
CC                               (CHEAP SOLUTION WITH INDJ1 ELIMINATED)
CC               02-JUN-95 : EB: FORBIDDEN TO ELIMINATE SAT.ANTENNA OFFSETS
CC               31-JUL-95 : EB: NOOBDL=1 --> NO DELETION AT ALL
CC               14-AUG-95 : EB: NOOBDL=1 --> NO DELETION AT ALL BUT
CC                               NOT ORBITS
CC               06-JUN-96 : MR: SET I0MAX=1 IF NO PARAM.
CC               13-MAY-97 : MR: HANDLE TROPOSPHERE PARAM. OF STATIONS
CC                               EXCLUDED WITH STACRUX FILE
CC               08-JUL-97 : LM: DO NOT ELIMINATE TROP. PAR. WITH NO
CC                               OBSERVATIONS IF THEY ARE NOT PRE-ELIMINATED
CC               25-MAR-02 : SS: INITIALIZE "IDEL"
CC               11-APR-03 : RD: NEW CONDITION FOR ELIMINATION
CC               19-JUN-03 : RD: REMOVE EPOCH PARAMETER WITHOUT OBS.
CC               21-OCT-04 : RD: ALLOCATE A0I, AII, IDEL LOCALLY
CC               21-JUN-05 : MM: COMLFNUM.INC REMOVED
CC               23-JUN-05 : MM: IMPLICIT NONE AND DECLARATIONS ADDED
CC               29-JUN-06 : HB: ADD VECTOR WITH A PRIORI VALUES TO
CC                               PARAMETER LIST AND ALSO REDUCE THIS
CC               17-JUN-08 : RD: COUNTER FOR OBSERV. PER PARAMETER ADDED
CC               16-NOV-10 : RD: RED_PARTYP ADDED
CC               16-JAN-11 : RD: REMOVE STAELI-RELATED PART
CC               10-JUL-12 : RD: USE SYMINVG INSTEAD OF SYMING
CC               10-JUL-12 : RD: USE M_BERN WITH ONLY; REMOVE UNUSED VARIABLES
CC
CC COPYRIGHT  :  ASTRONOMICAL INSTITUTE
CC      1987     UNIVERSITY OF BERN
CC               SWITZERLAND
CC
C*
      USE M_BERN,   ONLY: r8b, LFNERR
      USE M_GLOBAL, ONLY: MAXSYS
      USE P_GPSEST, ONLY: MAXMEA, red_partyp, t_partyp
C
      USE f_ikf
      USE s_alcerr
      USE s_syminvg
      USE s_updpar
      USE s_redtrb
      USE s_exitrc
      USE s_imatrd
      USE s_dmatrd
      IMPLICIT NONE
C
C DECLARATIONS INSTEAD OF IMPLICIT
C --------------------------------
      INTEGER*4 I     , I0MAX , IAC   , IAMB  , IAMNEW, IELTYP, II    ,
     1          IK01  , IK11  , ILCQ  , IONE  , IPAR  , ISING ,
     2          ISIX  , ITYP  , ITYPE , K     , K1    , MAXLCQ,
     3          MAXTYP, MXCLCQ, MXCLOC, NAMB  , NDAMB , NDEL  , NDELA ,
     4          NDELPA, NDELR , NDIM  , NOOBDL, NPALCQ, NPAR  , NPARMS,
     5          NPARN , NPNEW
C
      REAL*8    RMS
C
CCC       IMPLICIT REAL*8 (A-H,O-Z)
C
      TYPE(t_partyp), DIMENSION(:) :: partyp
C
      CHARACTER*6 MXNLCQ,MXNLOC
      CHARACTER(LEN=6), PARAMETER :: SRNAME = 'REDNEQ'
C
      REAL*8      ANOR(*),BNOR(*),XXX0(*)
      REAL(r8b),  DIMENSION(:), ALLOCATABLE :: A0I,AII
C
      INTEGER*4   LOCQ(MXCLCQ,*),INDP(*),IDEL(2*NPAR),OBSCLS(*)
      INTEGER*4   OPTELI(*),PARLST(5,*),PARFLG(*)
      INTEGER*4   NOBSPA(:,:)
C
      LOGICAL     YESNO
C
      COMMON/MCMLCQ/MXCLCQ,MXNLCQ
      COMMON/MCMLOC/MXCLOC,MXNLOC
      MAXLCQ=MXCLCQ
C
C INITIALIZATION
C --------------
      DO IPAR=1,NPAR
        IDEL(IPAR)=0
      ENDDO
C
C ELIMINATE PARAMETER THAT COULD NOT BE ESTIMATED (NO OBSERVATIONS)
C -----------------------------------------------------------------
      NDEL=0
      NDAMB=0
      DO 20 IPAR=1,NPAR
ccc        if (ieltyp.eq.2 .and.
ccc     &    locq(1,ipar).eq.6 .and. opteli(locq(1,ipar)).eq.0)
ccc     &    goto 20
C
C REMOVED RD: 11-APR-2003
ccc        if (opteli(locq(1,ipar)).eq.0 .and. locq(1,ipar).ne.4) goto 20
ccc        II=IPAR*(IPAR+1)/2
cccC EXCEPTION FOR ORBIT PARAMETERS: AUTOMATICAL DELETION TO FORCE AN AUTOMATICAL
cccC SETTING UP OF A NEW ARC (ONLY IN CASE OF A GAP REALIZED)
ccc        IF(ANOR(II).NE.0.D0.OR.(LOCQ(1,IPAR).NE.3
ccc     1     .AND.NOOBDL.EQ.1))
cccCC        IF(ANOR(II).NE.0.D0.OR.NOOBDL.EQ.1)
ccc     1    THEN
C
C REPLACED RD: 11-APR-2003
        II=IPAR*(IPAR+1)/2
C
C EXCEPTION FOR ORBIT PARAMETERS: AUTOMATICAL DELETION TO FORCE AN AUTOMATICAL
C SETTING UP OF A NEW ARC (ONLY IN CASE OF A GAP REALIZED)
        IF ( ANOR(II).NE.0D0 .OR.
     1      (LOCQ(1,IPAR).NE.3.AND.NOOBDL.EQ.1).OR.
C     2      (OPTELI(LOCQ(1,IPAR)).EQ.0 .AND. LOCQ(1,IPAR).NE. 4)) THEN
     2      (OPTELI(LOCQ(1,IPAR)).EQ.0 .AND.
     3       LOCQ(1,IPAR).NE. 4 .AND. LOCQ(1,IPAR).NE.21 .AND.
     4       LOCQ(1,IPAR).NE.23 .AND. LOCQ(1,IPAR).NE.24)) THEN
C END OF REPLACEMENT RD: 11-APR-2003
          IDEL(IPAR)=0
          INDP(IPAR)=IPAR-NDEL
        ELSE
          NDEL=NDEL+1
          ITYPE=LOCQ(1,IPAR)
          IDEL(IPAR)=1
          INDP(IPAR)=0
          IF(IPAR.GT.NPARN) NDAMB=NDAMB+1
          PARLST(1,ITYPE)=PARLST(1,ITYPE)-1
          PARLST(3,ITYPE)=PARLST(3,ITYPE)+1
C
C COPY LOCQ INFORMATION TO END OF ARRAY
          IF (NPAR+NDEL .GT. MXCLOC) THEN
            WRITE(LFNERR,901) NPAR+NDEL,MXCLOC
901         FORMAT(/,' *** SR REDNEQ: TOO MANY PARAMETERS ',
     1             'TO BE CHARACTERIZED',/,
     2             16X,'NUMBER OF PARAMETERS >=',I5,/,
     3             16X,'MAXIMUM NUMBER        :',I5,/)
            CALL EXITRC(2)
          END IF
          IDEL(NPAR+NDEL)=0
          DO 10 ILCQ=1,MAXLCQ
            LOCQ(ILCQ,NPAR+NDEL) = LOCQ(ILCQ,IPAR)
10        CONTINUE
        END IF
20    CONTINUE
C
C REDUCTION OF ARRAYS
C -------------------
      CALL DMATRD('S',NPAR,NPAR,IDEL,ANOR,NPNEW,NPNEW)
      CALL DMATRD('R',NPAR,1,IDEL,BNOR,NPNEW,IONE)
      CALL DMATRD('R',NPAR,1,IDEL,XXX0,NPNEW,IONE)
      CALL IMATRD('C',MAXMEA*MAXSYS,NPAR,IDEL,NOBSPA,ISIX,NPNEW)
      CALL IMATRD('C',MAXLCQ,NPAR+NDEL,IDEL,LOCQ,ISIX,NPNEW)
      CALL red_partyp(npar,idel,partyp)
C
C THIS UPDATE IS NOT NECESSARY IN THE SECOND PART
      IF (IELTYP.EQ.1) THEN
        IAMNEW=0
        DO 30 IAMB=1,NAMB
          IF (IDEL(NPARN+IAMB).EQ.1) GO TO 30
          IAMNEW=IAMNEW+1
          OBSCLS(IAMNEW)=OBSCLS(IAMB)
30      CONTINUE
      ENDIF
C
      NPAR=NPAR-NDEL
      NAMB=NAMB-NDAMB
      NPARN=NPAR-NAMB
      NPARMS=NPARMS-NDEL
      NPALCQ=NPAR+NDEL
C
C PRE-ELIMINATION OF PARAMETERS
C -----------------------------
      DO 200 ITYP=1,MAXTYP
        IF (OPTELI(ITYP).EQ.IELTYP) GOTO 210
200   CONTINUE
      GOTO 999
C
210   CONTINUE
      NDEL=0
      NDELA=0
      NDELR=0
      DO 50 IPAR=1,NPAR
        IDEL(IPAR)=0
        INDP(IPAR)=IPAR-NDELA-NDELR
        ITYPE=LOCQ(1,IPAR)
C
        YESNO = .FALSE.
        IF (OPTELI(ITYPE).EQ.IELTYP.AND.ITYPE.NE.1) YESNO = .TRUE.
C
        IF (OPTELI(ITYPE).EQ.1.AND.ITYPE.EQ.1) YESNO = .TRUE.
C
C TROPOSPHERE PARAMETERS OF STATIONS EXCLUDED WITH STACRUX FILE
C ATTENTION: ONLY WORKS IF AT LEAST ONE PARAMETER TYPE IS PRE-ELIMIN.
C ELSE THIS PART OF THE SUBROUTINE IS NEVER RUN THROUGH.
C BE AWARE THAT A PRIORI WEIGHTS OF TROPOSPHERE PARAMETERS FROM
C EXCLUDED SITES ARE NOT CHANGED FROM WHAT THEY WERE IN THE RUN
C WHEN SAVING THE NORMAL EQUATIONS
C
        IF (ITYPE.EQ.6 .AND. LOCQ(3,IPAR).EQ.-1) YESNO=.TRUE.
C
        IF (YESNO) THEN
          IDEL(IPAR)=1
          NDEL=NDEL+1
          INDP(IPAR)=0
C AMBIGUITIES SHOULD BE PRE-ELIMINATED SESSIONWISE (!?)(NDELA=0)
          IF (ITYPE.EQ.4) THEN
            NDELA=NDELA+1
          ELSE
            NDELR=NDELR+1
          ENDIF
        ENDIF
50    CONTINUE
C
      DO 51 IPAR=NPAR+1,NPALCQ
        IDEL(IPAR)=0
51    CONTINUE
      NDELPA=NDELA+NDELR
      IF (NDELPA.EQ.0) GOTO 999
C
C REDUCE ANOR, BNOR, RMS, LOCQ (AEQUIVALENT TO SR REDPAR)
C -------------------------------------------------------
C GENERATE TWO AUXILIARY MATRICES A0I, AII
      ALLOCATE(AII(IKF(NDELPA,NDELPA)),STAT=IAC)
      CALL ALCERR(IAC,'AII',(/IKF(NDELPA,NDELPA)/),SRNAME)
      AII=0D0
C
      ALLOCATE(A0I(NDELPA*(NPAR-NDELPA)),STAT=IAC)
      CALL ALCERR(IAC,'A0I',(/NDELPA*(NPAR-NDELPA)/),SRNAME)
      A0I=0D0
C
      IK11=0
      IK01=0
      K1=0
      DO 400 K=1,NPAR
        IF (IDEL(K).EQ.1) THEN
          K1=K1+1
          DO 450 I=1,NPAR
            IF (IDEL(I).EQ.1 .AND. I.LE.K) THEN
              IK11=IK11+1
              AII(IK11)=ANOR(IKF(I,K))
            ELSE IF (IDEL(I).EQ.0) THEN
              IK01=IK01+1
              A0I(IK01)=ANOR(IKF(I,K))
            END IF
450       CONTINUE
        END IF
400   CONTINUE
      IF (K1.GT.0) THEN
        I0MAX=IK01/K1
      ELSE
        I0MAX=1
      ENDIF
C
C INVERT AII
C ----------
      NDIM=NDELA+NDELR
      CALL SYMINVG(NDIM,AII,1,ISING,PARFLG)
C
C UPDATE NUMBER OF SINGULAR PARAMETERS IN "PARLST"
      CALL UPDPAR(1,NPAR,LOCQ,IDEL,PARFLG,NPARMS,PARLST)
C
C REDUCE TERMS RMS, BNOR, ANOR USING SR REDTRB
C --------------------------------------------
      CALL REDTRB(NPAR,A0I,AII,IDEL,I0MAX,ANOR,BNOR,RMS)
C
C PHYSICALLY ELIMIATE PRE-REDUCED LINES AND COLUMNS IN NEQ-SYSTEM
C ---------------------------------------------------------------
      CALL DMATRD('S',NPAR,NPAR,IDEL,ANOR,NPNEW,NPNEW)
      CALL DMATRD('R',NPAR,1,IDEL,BNOR,NPNEW,IONE)
      CALL DMATRD('R',NPAR,1,IDEL,XXX0,NPNEW,IONE)
      CALL IMATRD('C',MAXMEA*MAXSYS,NPAR,IDEL,NOBSPA,ISIX,NPNEW)
      CALL IMATRD('C',MAXLCQ,NPALCQ,IDEL,LOCQ,ISIX,NPNEW)
      CALL red_partyp(npar,idel,partyp)
C
      NPAR=NPAR-NDELA-NDELR
      NAMB=NAMB-NDELA
      NPARN=NPAR-NAMB
      NPALCQ=NPALCQ-NDELA-NDELR
C
      DEALLOCATE(AII,STAT=IAC)
      DEALLOCATE(A0I,STAT=IAC)
C
999   RETURN
      END SUBROUTINE

      END MODULE
