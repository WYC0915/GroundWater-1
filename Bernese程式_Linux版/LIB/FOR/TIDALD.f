      MODULE s_TIDALD
      CONTAINS

C*
      SUBROUTINE TIDALD(XSTA,XSUN,XMON,DXTIDE)
CC
CC NAME       :  TIDALD
CC
CC PURPOSE    :  COMPUTATION OF VERTICAL DISPLACEMENTS OF STATIONS
CC               DUE TO EARTH TIDES
CC               CAUSED BY LUNAR AND SOLAR GRAVITATION
CC               (SEE IERS TECHNICAL NOTE 3, IERS STANDARDS 1989,
CC                P.27, STEP 1)
CC
CC PARAMETERS :
CC         IN :  XSTA(I),I=1,2,3:  GEOCENTRIC POSITION OF     R*8
CC                        THE STATION
CC               XSUN(I),I=1,2,3: GEOC. POSITION OF THE SUN   R*8
CC               XMON(I),I=1,2,3: GEOC. POSITION OF THE MOON  R*8
CC        OUT :  DXTIDE(I),I=1,2,3: DISPLACEMENT VECTOR       R*8
CC
CC REMARKS    :  ---
CC
CC AUTHOR     :  M.ROTHACHER
CC
CC VERSION    :  3.4  (JAN 93)
CC
CC CREATED    :  90/04/17
CC
CC CHANGES    :  23-JUN-05 : MM: IMPLICIT NONE AND DECLARATIONS ADDED
CC
CC COPYRIGHT  :  ASTRONOMICAL INSTITUTE
CC      1990     UNIVERSITY OF BERN
CC               SWITZERLAND
CC
C*
      USE d_const, ONLY: GM, GMM, GMS
      USE s_sprod
      IMPLICIT NONE
C
C DECLARATIONS INSTEAD OF IMPLICIT
C --------------------------------
      INTEGER*4 I
C
      REAL*8    FACMON, FACSUN, P2MON , P2SUN , RMON  , RSTA  , RSUN  ,
     1          SCM   , SCMON , SCS   , SCSUN , X2MON , X2SUN
C
CCC       IMPLICIT REAL*8 (A-H,O-Z)
C
      REAL*8 XSTA(*),XSUN(*),XMON(*),DXTIDE(*)
      REAL*8 H2,L2
C
C
C NOMINAL SECOND DEGREE LOVE NUMBER AND SHIDA NUMBER
C --------------------------------------------------
      DATA H2/0.6090D0/,L2/0.0852D0/
C
C SCALAR PRODUCT OF STATION VECTOR WITH SUN/MOON VECTOR
C -----------------------------------------------------
      CALL SPROD(XSTA,XSUN,SCS,RSTA,RSUN)
      CALL SPROD(XSTA,XMON,SCM,RSTA,RMON)
      SCSUN=SCS/RSTA/RSUN
      SCMON=SCM/RSTA/RMON
C
C P2-TERM
C -------
      P2SUN=3*(H2/2-L2)*SCSUN**2-H2/2
      P2MON=3*(H2/2-L2)*SCMON**2-H2/2
C
C TERM IN DIRECTION OF SUN/MOON VECTOR
C ------------------------------------
      X2SUN=3*L2*SCSUN
      X2MON=3*L2*SCMON
C
C FACTORS FOR SUN/MOON
C --------------------
      FACSUN=GMS/GM*RSTA**4/RSUN**3
      FACMON=GMM/GM*RSTA**4/RMON**3
C
C TOTAL DISPLACEMENT
C ------------------
      DO 10 I=1,3
        DXTIDE(I)=FACSUN*( X2SUN*XSUN(I)/RSUN + P2SUN*XSTA(I)/RSTA ) +
     1            FACMON*( X2MON*XMON(I)/RMON + P2MON*XSTA(I)/RSTA )
10    CONTINUE
C
      RETURN
      END SUBROUTINE

      END MODULE
