      MODULE s_SMPELV
      CONTAINS

C*
      SUBROUTINE SMPELV(NSAMPL,NFTOT ,IDELTT,ZENMAX,ELEVMM,NSMPNQ,
     1                  IELVNQ,INDSMP)
CC
CC NAME       :  SMPELV
CC
CC PURPOSE    :  RETURNS THE DATA SAMPLING INTERVAL (SECONDS) AND THE
CC               MINIMAL ELEVATION ANGLE (DEGREES).
CC
CC PARAMETERS :
CC         IN :  NSAMPL : CODED DATA SAMPLING INTERVAL,         I*4
CC                        REAL DATA IN SECONDS
CC               NFTOT  : TOTAL NUMBER OF FILES TO BE PRO-      I*4
CC                        CESSED IN CURRENT RUN
CC               IDELTT(I), I=(1,...,NFTOT) : TABLE INTERVAL    I*4
CC                        IN SECONDS
CC               ZENMAX : MAXIMUM ZENITH DISTANCE IN RADIANS    R*8
CC               ELEVMM(I,J), I=(1,2),J=(1,..,NFTOT) : MINIMAL  R*8
CC                        (I=1) AND MAXIMUM (I=2) ELEVATION
CC                        ANGLE IN DEGREES
CC
CC        OUT :  NSMPNQ : DATA SAMPLE RATE IN SECONDS           I*4
CC               IELVNQ : MINIMUM ELEVATION ANGLE IN DEGREES    I*4
CC      LOCAL :  INDSMP(I), I=(1,...,NFTOT) : INDEX FOR ORDER-  I*4
CC                        ING OF SAMPLING, PASSED ONLY BECAUSE
CC                        GLOBAL DIMENSIONING IS PREFERED
CC
CC INCLUDES   :  COMCONST
CC
CC REMARKS    :  IF THE MINIMAL ELEVATION ANGLE FOUND IN THE DATA IS
CC               SMALLER THAN THE CUT-OFF ELEVATION ANGLE SPECIFIED BY
CC               THE ZENITH DISTANCE 'ZENMAX', THE CUT-OFF ELEVATION
CC               ANGLE WILL BE RETURNED.
CC
CC AUTHOR     :  M.PROHASKA
CC
CC VERSION    :  4.1
CC
CC CREATED    :  18-FEB-97
CC
CC CHANGES    :  16-JUN-05 : MM: COMCONST.inc REPLACED BY d_const
CC               23-JUN-05 : MM: IMPLICIT NONE AND DECLARATIONS ADDED
CC
CC COPYRIGHT  :  ASTRONOMICAL INSTITUTE
CC      1997     UNIVERSITY OF BERN
CC               SWITZERLAND
CC
C*
      USE d_const, ONLY: PI
      USE s_iordup
      IMPLICIT NONE
C
C DECLARATIONS INSTEAD OF IMPLICIT
C --------------------------------
      INTEGER*4 IELEV1, IELTMP, IELVNQ, IFILE , NFTOT , NSAMPL, NSMPNQ
C
      REAL*8    ZENMAX
C
CCC       IMPLICIT REAL*8 (A-H,O-Z)
C
      REAL*8     ELEVMM(2,*)
      INTEGER*4  IDELTT(*),INDSMP(*)
C
C
C GET SAMPLING INTERVAL
C ---------------------
      CALL IORDUP(IDELTT,NFTOT,INDSMP)
      NSMPNQ=IDELTT(INDSMP(IDNINT(NFTOT/2.D0)))
      NSMPNQ=MAX0(NSMPNQ,NSAMPL)
C
C GET MIN. ELEVATION ANGLE
C ------------------------
      IELEV1=90
C
      DO IFILE=1,NFTOT
        IELTMP=IDNINT(ELEVMM(1,IFILE))
        IF (IELTMP.LT.IELEV1) IELEV1=IELTMP
      END DO
C
      IELTMP=90 - IDNINT(ZENMAX*1.8D2/PI)
      IELVNQ=MAX0(IELTMP,IELEV1)
C
      RETURN
      END SUBROUTINE

      END MODULE
