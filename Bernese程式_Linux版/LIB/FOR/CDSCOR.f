      MODULE s_CDSCOR
      CONTAINS

C*
      SUBROUTINE CDSCOR(FLGOUT,STITLE,SAVOPT,ICORFL,NSTAT,STNAME,
     1                  XSTAT,XSTECC,ICENTR,NFTOT,STFIL,XSTANW,TIMCRD)
CC
CC NAME       :  CDSCOR
CC
CC PURPOSE    :  SAVE COORDINATES ESTIMATED WITH PROGRAM CODSPP
CC               AND MAUPRP (ZD-CASE) IN COORDINATE OUTPUT FILE
CC
CC PARAMETERS :
CC         IN :  FLGOUT : FLAG FOR NEW STATIONS IN OUTPUT FILE CH*1
CC               STITLE : SHORT TITLE LINE                    CH*64
CC               SAVOPT : SAVE OPTION (=1: SAVE FILE)         I*4
CC               ICORFL : COORDINATES FIXED = 1               I*4
CC               NSTAT  : NUMBER OF STATIONS                  I*4
CC               STNAME(I),I=1,..,NSTAT: STATION NAMES        CH*16
CC               XSTAT(3,I),I=1,..,NSTAT: STATION COORDINATES R*8
CC               XSTECC(3,I),I=1,..,NSTAT: STATION ECCENTRI.  R*8
CC               ICENTR(I),I=1,..,NSTAT: NUMBER OF CENTER STA.I*4
CC               NFTOT  : NUMBER OF FILES PROCESSED           I*4
CC               STFIL(2,I),I=1,..,NFTOT: STATION NUMBER(S)   I*4
CC                        OF FILE I
CC               XSTANW(3,I),I=1,..,NFTOT: NEW STATION COORD. R*8
CC               TIMCRD : EPOCH OF COORDINATES (MJD)          R*8
CC
CC REMARKS    :  ---
CC
CC AUTHOR     :  M.ROTHACHER
CC
CC VERSION    :  3.4  (JAN 93)
CC
CC CREATED    :  87/12/11 12:02
CC
CC CHANGES    :  30-SEP-93 : ??: CALL TO SR WTSTAT CHANGED (STANUM,DATUM)
CC               10-AUG-94 : MR: CALL EXITRC
CC               18-SEP-95 : JJ: INCREASE MAXSTA TO 200
CC               04-AUG-99 : PF: USE SR WTSTAT
CC               26-JUN-02 : RD: OUTPUT FLAG IS AN INPUT PARAM.
CC               28-JUN-04 : RD: USE MAXSTA FROM M_MAXDIM
CC               16-JUN-05 : MM: COMCONST.inc REPLACED BY d_const
CC               23-JUN-05 : MM: IMPLICIT NONE AND DECLARATIONS ADDED
CC               08-FEB-11 : RD: NEW CALL OF WTSTAT
CC               14-FEB-11 : RD: REMOVE MAXSTA-COMMON (NOT NEEDED)
CC
CC COPYRIGHT  :  ASTRONOMICAL INSTITUTE
CC      1987     UNIVERSITY OF BERN
CC               SWITZERLAND
CC
C*
      USE d_const, ONLY: DATE, TIME
C
      USE s_wtstat
      USE s_maxtst
      USE s_exitrc
      IMPLICIT NONE
C
C DECLARATIONS INSTEAD OF IMPLICIT
C --------------------------------
      INTEGER*4 I     , ICORFL, IFIL  , IRC   , ISTA  , NFTOT ,
     1          NSTAT
C
CCC       IMPLICIT REAL*8 (A-H,O-Z)
C
      CHARACTER*80 TITLE
      CHARACTER*64 STITLE
      CHARACTER*32 FILCOR
      CHARACTER*16 STNAME(*),DATUM
      CHARACTER*1  STAFLG(NSTAT),FLGOUT
      REAL*8       XSTAT(3,*),XSTANW(3,*),XSTECC(3,*)
      REAL*8       TIMCRD
      INTEGER*4    SAVOPT,STFIL(2,*),ICENTR(*)
      INTEGER*4    STANUM(NSTAT)
C
C SAVE COORDINATES ?
C ------------------
      IF(SAVOPT.EQ.0.OR.ICORFL.EQ.0) GOTO 999
C
C TITLE
C -----
      TITLE=STITLE
      TITLE(66:74)=DATE
      TITLE(76:80)=TIME
C
C UPDATE COORDINATES
C ------------------
      DO 20 IFIL=1,NFTOT
        ISTA=STFIL(1,IFIL)
        DO 10 I=1,3
          XSTAT(I,ISTA)=XSTANW(I,IFIL)
          XSTAT(I,ICENTR(ISTA))=XSTANW(I,IFIL)-XSTECC(I,ISTA)
10      CONTINUE
20    CONTINUE
C
C SET FLAG
C --------
      DO 30 ISTA=1,NSTAT
        STAFLG(ISTA)='E'
        IF(ICENTR(ISTA).EQ.ISTA) STAFLG(ISTA)=FLGOUT
30    CONTINUE
C
C SAVE COORDINATES
C ----------------
      DATUM=' '
      FILCOR = ' '
      CALL WTSTAT(1,FILCOR,TITLE,DATUM,NSTAT,STNAME,XSTAT,
     1            STANUM,STAFLG,TIMCRD)
C
999   RETURN
      END SUBROUTINE

      END MODULE
