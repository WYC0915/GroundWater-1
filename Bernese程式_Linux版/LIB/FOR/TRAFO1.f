      MODULE s_TRAFO1
      CONTAINS

C*
      SUBROUTINE TRAFO1(NPAR,NPNEW,INDCOE,COETRA,ANOR,
     1                  BNOR,ANEW,BNEW)
CC
CC NAME       :  TRAFO1
CC
CC PURPOSE    : TRANSFORM NEQ-SYSTEM FOR CURRENT ARC TO CONTAIN
CC              ALL PARAMETERS FROM PREVIOUS ARCS (PSEUDO-STOCHASTIC)
CC
CC PARAMETERS :
CC        IN  : NPAR    : NUMBER OF PARAMETERS OF ARC I IN PREVIOUS I*4
CC                        COMPUTATION
CC              NPNEW   : NUMBER OF PARAMETERS AFTER ADDING         I*4
CC                        STOCHASTIC PARAMETERS
CC              INDCOE  : INDEX ARRAY FOR ARRAY COETRA              I*4(*,*)
CC              COETRA  : TRANSFORMATION MATRIX OLD --> NEW         R*8
CC     IN/OUT : ANOR    : NEQ-SYSTEM BEFORE/AFTER TRAFO             R*8(*)
CC                        (UPPER TRIANGULAR PART)
CC              BNOR    : RHS OF NEQ SYSTEM                         R*8(*)
CC              ANEW    : AUXILIARY MATRIX                          R*8(*)
CC              BNEW    : AUXILIARY MATRIX                          R*8(*)
CC
CC REMARKS    :
CC
CC AUTHOR     :  G.BEUTLER
CC
CC VERSION    :  3.5  (MAY 94)
CC
CC CREATED    :  94/05/11
CC
CC CHANGES    :  01-MAY-95 : GB: TURBO VERSION
CC               03-MAY-95 : MR: MAXPAR=1300 (OLD:1200)
CC               25-JUL-95 : TS: INCREASED MAXPAR FROM 1300 --> 1600
CC               20-FEB-96 : TS: INCREASED MAXPAR FROM 1600 --> 1700
CC               18-MAR-96 : EB: INCREASED MAXPAR/MAXLOC FROM 1700 --> 1800
CC               12-AUG-97 : GB: REDUCE ANEW TO UPPER TRIANGULAR MATRIX
CC                               "ABUSE" ARRAY BNEW FOR COMPUTATION OF
CC                               TRN(C)*A*C
CC               16-AUG-99 : RD: DIMENSIONS (SMALL/MEDIUM/LARGE) USING I:ADDNEQ
CC               17-FEB-03 : LM: USE P_ADDOLD
CC               15-MAR-03 : HU: REMOVE UNUSED MAXxxx
CC               23-JUN-05 : MM: IMPLICIT NONE AND DECLARATIONS ADDED
CC               16-Jul-08 : RD: USE P_ADDOLD->P_ADDNEQ
CC
CC COPYRIGHT  :  ASTRONOMICAL INSTITUTE
CC      1994     UNIVERSITY OF BERN
CC               SWITZERLAND
CC
C*
      USE p_addneq, ONLY: MAXLIN
      USE f_ikf
      USE s_trnhlp
      IMPLICIT NONE
C
C DECLARATIONS INSTEAD OF IMPLICIT
C --------------------------------
      INTEGER*4 IK   , IL   , INDEX, IP   , IPAR , KPAR , L    ,
     1          LPAR , NPAR , NPNEW
C
CCC       IMPLICIT REAL*8 (A-H,O-Z)
C
C MAXLIN : MAXIMUM NUMBER OF NON-ZERO ELEMENTS PER COL OF MATRIX COETRA
C MAXPAR : MAXIMUM NUMBER OF PARAMETERS
      INTEGER*4    INDCOE(2,*)
      REAL*8       ANOR(*),BNOR(*),COETRA(*)
      REAL*8       ANEW(*),BNEW(*)
      INTEGER*4    NLPCOL(NPNEW),IDXCOE(2,MAXLIN,NPNEW)
C
C DEFINE AUX. ARRAYS NLPCOL, IDXCOE
C ---------------------------------
      CALL TRNHLP(NPAR,NPNEW,MAXLIN,NPNEW,INDCOE,NLPCOL,IDXCOE)
C
C TRANSFORM ANEW USING AUX. ARRAY BNEW
C ------------------------------------
C
C COPY ANOR TO ANEW
      DO 10 IK=1,NPAR*(NPAR+1)/2
        ANEW(IK)=ANOR(IK)
10    CONTINUE
C
C LOOP OVER ALL COLUMNS OF RESULTING MATRIX
      DO 200 KPAR=1,NPNEW
C
C AUXILIARY ARRAY BNEW = ANEW * C
        DO 120 IP=1,NPAR
          BNEW(IP)=0.D0
          DO 110 L=1,NLPCOL(KPAR)
            LPAR =IDXCOE(1,L,KPAR)
            INDEX=IDXCOE(2,L,KPAR)
            IL=IKF(IP,LPAR)
            BNEW(IP)=BNEW(IP)+ANEW(IL)*COETRA(INDEX)
110       CONTINUE
120     CONTINUE
C
C ANEW = TRN(C) * BNEW
C
C LOOP OVER ALL LINES OF NEW MATRIX
        DO 190 IPAR=1,KPAR
          IK=IKF(IPAR,KPAR)
          ANOR(IK)=0.D0
          DO 180 L=1,NLPCOL(IPAR)
            LPAR =IDXCOE(1,L,IPAR)
            INDEX=IDXCOE(2,L,IPAR)
            ANOR(IK)=ANOR(IK)+COETRA(INDEX)*BNEW(LPAR)
180       CONTINUE
190     CONTINUE
200   CONTINUE
C
C TRANSFORM BNEW
C --------------
      DO 40 IPAR=1,NPAR
        BNEW(IPAR)=BNOR(IPAR)
40    CONTINUE
      DO 330 IPAR=1,NPNEW
        BNOR(IPAR)=0.D0
        DO 320 L=1,NLPCOL(IPAR)
          LPAR =IDXCOE(1,L,IPAR)
          INDEX=IDXCOE(2,L,IPAR)
          BNOR(IPAR)=BNOR(IPAR)+COETRA(INDEX)*BNEW(LPAR)
320     CONTINUE
330   CONTINUE
C
999   CONTINUE
      RETURN
      END SUBROUTINE

      END MODULE
