      MODULE s_JEOPEN
      CONTAINS

C*
      SUBROUTINE JEOPEN(RDF)
CC
CC NAME       :  JEOPEN
CC
CC PURPOSE    :  THIS SUBROUTINE OPENS THE JPL PLANETARY EPHEMERIS
CC               FILE. THE FILE NAME IS OBTAINED USING SR GTFLNA.
CC
CC PARAMETERS :
CC         IN :  RDF    : IF .TRUE. OPEN EPHEMERIS FILE WITH      L*1
CC                        STATUS='OLD' AND READ CONSTANTS....
CC
CC REMARKS    :  ---
CC
CC AUTHOR     :  JPL
CC
CC VERSION    :  3.4
CC
CC CREATED    :  22-OCT-92
CC
CC CHANGES    :  28-SEP-95 : JJ: DECLARE RDF AS L*4 INSTEAD OF L*1
CC               28-SEP-95 : JJ: DECLARE FIRST AS L*4 INSTEAD OF L*1
CC               14-SEP-96 : TS: ADDED READ ONLY TO OPEN STATEMENT
CC               07-MAY-98 : MR: ADD BEG_UNIX BEG_LAHEY ...
CC               13-AUG-99 : MR: REMOVE "READ,DENYWRITE" FROM OPEN
CC                               STATEMENT FOR LAHEY !!!
CC               25-AUG-00 : HU: REPLACE ENVIRONMENT VARIABLES
CC               29-JAN-01 : HU: ERROR HANDLING ON OPEN FILE
CC               17-FEB-03 : LM: USE M_COMJPL, PREPROC. COMMANDS
CC               19-FEB-03 : RD: STOP IF RPLENVAR FAILED
CC               27-FEB-03 : HU: M_COMJPL RENAMED TO D_COMJPL
CC               21-JUN-05 : MM: COMLFNUM.inc REMOVED, m_bern ADDED
CC               23-JUN-05 : MM: IMPLICIT NONE AND DECLARATIONS ADDED
CC               28-JUN-05 : MM: UNUSED VARIABLES REMOVED
CC               13-OCT-06 : HU: CHECK JPL EPHEMERIS TITLE
CC               29-OCT-12 : RD: REMOVE #ifdef OS_VMS
CC
CC COPYRIGHT  :  NONE
CC      1992
CC
C*
C     COMMON AREA FOR CHARACTER DATA IN RECORD 1
C
      USE m_bern,   ONLY: lfneph, lfnerr
      USE d_comjpl, ONLY: irecsz, jplnam
      USE s_opnerr
      USE s_exitrc
      USE s_rplenvar
      USE s_gtflna
      IMPLICIT NONE
C
C DECLARATIONS INSTEAD OF IMPLICIT
C --------------------------------
      INTEGER*4 IOSTAT, IRC, IOK, II
C

      CHARACTER*6 TTL(14,3)
      CHARACTER*6 CNAM(400)
      COMMON/CHRHDR/TTL,CNAM
C
C     COMMON AREA FOR CONSTANTS AND POINTERS IN RECORD 1
C
      REAL*8    SS(3),CVAL(400),AU,EMRAT
      INTEGER*4 NCON,IPT(36),DENUM,LPT(3)
      COMMON/EPHHDR/SS,CVAL,AU,EMRAT,NCON,IPT,DENUM,LPT
C
C
      LOGICAL*4 RDF
      LOGICAL*4 FIRST
C
      CHARACTER*255 FILNAM
      CHARACTER*84  TITLE
      CHARACTER*7  STAWD
C
      DATA FIRST/.TRUE./
C
C     OPEN FILE AND READ RECORD # 1 AND 2 ON FIRST ENTRY ONLY
C
      IF(FIRST) THEN
        FIRST=.FALSE.
C
C     GET EPHEMERIS FILE NAME
        CALL GTFLNA(1,'JPLEPH ',FILNAM,IRC)
C
C     REPLACE ENVIRONMENT VARIABLES
        CALL RPLENVAR(2,FILNAM)
C
        IF(RDF) THEN
          STAWD='OLD'
        ELSE
          STAWD='UNKNOWN'
        ENDIF
#ifdef OS_WIN32
        OPEN(UNIT=LFNEPH,
     *       FILE=FILNAM,
     *       ACCESS='DIRECT',
     *       FORM='UNFORMATTED',
     *       RECL=IRECSZ,
     *       STATUS=STAWD,
     *       IOSTAT=IOSTAT)
#endif
#ifdef OS_UNIX
        OPEN(UNIT=LFNEPH,
     *       FILE=FILNAM,
     *       ACCESS='DIRECT',
     *       FORM='UNFORMATTED',
     *       RECL=IRECSZ,
     *       STATUS=STAWD,
     *       IOSTAT=IOSTAT)
#endif
        CALL OPNERR(LFNERR,LFNEPH,IOSTAT,FILNAM,'JEOPEN')
        IF(RDF) THEN
          READ(LFNEPH,REC=1) TTL,CNAM,SS,NCON,AU,EMRAT,IPT,DENUM,LPT
          READ(LFNEPH,REC=2) CVAL
C CHECK TITLE FOR EPHEMERIS NAME
          DO II=0,13
            TITLE(II*6+1:II*6+6)=TTL(II+1,1)
          ENDDO
          IOK = INDEX(TITLE,TRIM(JPLNAM))
          IF (IOK == 0) THEN
            WRITE(LFNERR,"(/,
     1        ' *** SR JEOPEN: WRONG JPL PLANETARY EPHEMERIS FILE',
     2      /,'                EXPECTED  : ',A,
     3      /,'                FILE TITLE: ',A)") JPLNAM,TRIM(TITLE)
            CALL EXITRC(2)
          ENDIF
        ENDIF
      ENDIF
C
      RETURN
C
      END SUBROUTINE

      END MODULE
