      MODULE s_ARGLAT
      CONTAINS

C*
      SUBROUTINE ARGLAT(XV,U)
CC
CC NAME       :  ARGLAT
CC
CC PURPOSE    :  THIS SUBROUTINE CALCULATES THE ARGUMENT OF LATITUDE
CC               U FROM INSTANTANEOUS POSITION AND VELOCITY (IN ARRAY XV)
CC
CC PARAMETERS :
CC         IN :  XV     : POSITION AMND VELOCITY OF CELESTIAL R*8(3,2)
CC                        BODY AT TIME T
CC        OUT :  U      : ARGUMENT OF LATITUDE                R*8
CC
CC
CC REMARKS    :  ---
CC
CC AUTHOR     :  G.BEUTLER
CC
CC VERSION    :  3.4  (DEC 95)
CC
CC CREATED    :  87/11/03 12:22
CC
CC CHANGES    :  15-OCT-96 : TS: CORRECTED DECLARATION OF XV(3,2) TO XV(*)
CC               23-JUN-05 : MM: IMPLICIT NONE AND DECLARATIONS ADDED
CC
CC COPYRIGHT  :  ASTRONOMICAL INSTITUTE
CC      1987     UNIVERSITY OF BERN
CC               SWITZERLAND
CC
C*
      IMPLICIT NONE
C
C DECLARATIONS INSTEAD OF IMPLICIT
C --------------------------------
      REAL*8    CI, CK, H , SI, SK, U , XV, XX
C
CCC       IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION XV(*),XX(3),H(3)
      REAL*8 I,KN
C
      H(1)= XV(2)*XV(6)-XV(3)*XV(5)
      H(2)=-XV(3)*XV(4)+XV(1)*XV(6)
      H(3)= XV(1)*XV(5)-XV(2)*XV(4)
      KN=DATAN2(H(1),H(2))
      I=DATAN2(DSQRT(H(1)**2+H(2)**2),H(3))
      CK=DCOS(KN)
      SK=DSIN(KN)
      CI=DCOS(I)
      SI=DSIN(I)
      XX(1)=    CK*XV(1)   +SK*XV(2)
      XX(2)=-CI*SK*XV(1)+CI*CK*XV(2)+SI*XV(3)
      XX(3)= SI*SK*XV(1)-SI*CK*XV(2)+CI*XV(3)
      U=DATAN2(XX(2),XX(1))
      RETURN
      END SUBROUTINE

      END MODULE
