      MODULE s_SETAOB
      CONTAINS

C*
      SUBROUTINE SETAOB(IXY,IDEG,OMEGD,COSWT,SINWT,AOBS)
CC
CC NAME       :  SETAOB
CC
CC PURPOSE    : DEFINE ELEMENTS AOBS TO ENSURE THAT THE DERIVATIVE
CC              NUMBER IDEG-1 DOES NOT CONTAIN RETROGRADE DIURNAL TERMS.
CC              FOR THE THEORY SEE
CC              BEUTLER, G. (1995). "HANDLING THE DIURNAL RETROGRADE TERMS
CC              IN SUB-DAILY ESTIMATES OF POLAR WOBBLE COMPONENTS."
CC
CC PARAMETERS :
CC        IN  : IXY     : =1 CONDITION FOR X SERIES                 I*4
CC                        =2 CONDITION FOR Y SERIES
CC              IDEG    : IDEG-1 : DERIVATIVE NUMBER                I*4
CC              OMEGD   : ROTATION RATE (RAD/DAY)                   R*8
CC              COSWT, SINWT : COS, SIN (OMEGAD*T)                  R*8
CC        OUT : AOBS    : CONDITIONS FOR COS- AND SIN- TERMS        I*4
CC
CC REMARKS    :
CC
CC AUTHOR     :  G.BEUTLER
CC
CC VERSION    :  3.6  (JUNE 95)
CC
CC CREATED    :  95/06/12
CC
CC CHANGES    :  23-JUN-05 : MM: IMPLICIT NONE AND DECLARATIONS ADDED
CC
CC COPYRIGHT  :  ASTRONOMICAL INSTITUTE
CC      1995     UNIVERSITY OF BERN
CC               SWITZERLAND
CC
C*
      IMPLICIT NONE
C
C DECLARATIONS INSTEAD OF IMPLICIT
C --------------------------------
      INTEGER*4 I2    , IDEG  , ITEST1, IXY
C
      REAL*8    COSWT , OMEGD , SINWT
C
CCC       IMPLICIT REAL*8 (A-H,O-Z)
CCC       IMPLICIT INTEGER*4 (I-N)
      REAL*8 AOBS(*)
C
C DO WE HAVE AN EVEN OR ODD DERIVATIVE
C ------------------------------------
      ITEST1=MOD(IDEG+1,2)
      I2=IDEG/2
      IF(IXY.EQ.1)THEN
        IF(ITEST1.EQ.0)THEN
          AOBS(1)= OMEGD**(IDEG-1)*(-1)**I2*COSWT
          AOBS(2)=-OMEGD**(IDEG-1)*(-1)**I2*SINWT
        ELSE
          AOBS(1)= OMEGD**(IDEG-1)*(-1)**I2*SINWT
          AOBS(2)= OMEGD**(IDEG-1)*(-1)**I2*COSWT
        END IF
      ELSE
        IF(ITEST1.EQ.0)THEN
          AOBS(1)= OMEGD**(IDEG-1)*(-1)**I2*SINWT
          AOBS(2)= OMEGD**(IDEG-1)*(-1)**I2*COSWT
        ELSE
          AOBS(1)=-OMEGD**(IDEG-1)*(-1)**I2*COSWT
          AOBS(2)= OMEGD**(IDEG-1)*(-1)**I2*SINWT
        END IF
      END IF
999   CONTINUE
      RETURN
      END SUBROUTINE

      END MODULE
