      MODULE s_RXRDNH
      CONTAINS

C*
      SUBROUTINE RXRDNH(LFNNAV,LFNERR,PRGNAM,RUNBY,CRDATE,CRTIME,
     1                  NCOM,COMENT,IRCODE)
CC
CC NAME       :  RXRDNH
CC
CC PURPOSE    :  READ  THE ENTIRE HEADER INFORMATION OF A
CC               RINEX NAVIGATION FILE
CC
CC PARAMETERS :
CC         IN :  LFNNAV : LOGICAL FILE NUMBER                  I*4
CC               LFNERR : LFN FOR ERROR MESSAGES               I*4
CC        OUT :  PRGNAM : PROGRAM NAME                        CH*20
CC               RUNBY  : NAME OF AGENCY CREATING RINEX FILE  CH*20
CC               CRDATE : CREATION DATE                       CH*9
CC               CRTIME : CREATION TIME                       CH*5
CC               NCOM   : NUMBER OF COMMENT LINES              I*4
CC               COMENT : COMMENT LINES                       CH*60(*)
CC               IRCODE : RETURN CODE                          I*4
CC                        0: OK
CC                        1: WRONG FILE TYPE
CC                        2: NOT ANTICIPATED VERSION NUMBER
CC                        3: END OF FILE WITHIN HEADER
CC                        4: READ ERROR
CC                        9: END OF FILE AT BEGINNING OF HEADER
CC
CC REMARKS    :  ---
CC
CC AUTHOR     :  W. GURTNER
CC
CC VERSION    :  3.4  (JAN 93)
CC
CC CREATED    :  89/04/07 08:50
CC
CC CHANGES    :  23-JUN-05 : MM: IMPLICIT NONE AND DECLARATIONS ADDED
CC
CC COPYRIGHT  :  ASTRONOMICAL INSTITUTE
CC      1989     UNIVERSITY OF BERN
CC               SWITZERLAND
CC
C*
      IMPLICIT NONE
C
C DECLARATIONS INSTEAD OF IMPLICIT
C --------------------------------
      INTEGER*4 IRCODE, IRV   , IRVERS, LFNERR, LFNNAV, NCOM
C
CCC       IMPLICIT REAL*8 (A-H,O-Z)
C
C GLOBAL DECLARATIONS
C -------------------
      CHARACTER*20 PRGNAM,RUNBY
      CHARACTER    COMENT(*)*60,CRDATE*9,CRTIME*5
C
C  LOCAL DECLARATIONS
C  ------------------
      CHARACTER    HEAD*20,RXTYPE*1,STRING*60,RXT*1
C
C RINEX FILE TYPE
      DATA RXTYPE/'N'/
C
C MAXIMUM RINEX FORMAT VERSION
      DATA IRVERS/1/
C
      NCOM=0
C
C RECORD 1        RINEX VERSION / TYPE
101   READ(LFNNAV,222,END=990) STRING,HEAD
222   FORMAT(A60,A20)
      IF(HEAD(1:7).EQ.'COMMENT') THEN
        NCOM=NCOM+1
        COMENT(NCOM)=STRING
        GOTO 101
      END IF
      READ(STRING,1,ERR=940) IRV,RXT
1     FORMAT(I6,14X,A1)
      IF(RXT.NE.RXTYPE) GOTO 910
      IF(IRV.LE.0.OR.IRV.GT.IRVERS) GOTO 920
C
C RECORD 2        PGM / RUN BY / DATE
102   READ(LFNNAV,222,END=930) STRING,HEAD
      IF(HEAD(1:7).EQ.'COMMENT') THEN
        NCOM=NCOM+1
        COMENT(NCOM)=STRING
        GOTO 102
      END IF
      READ(STRING,2,ERR=940) PRGNAM,RUNBY,CRDATE,CRTIME
2     FORMAT(A20,A20,A9,1X,A5)
C
C  READ BLANK LINE
117   READ(LFNNAV,222,END=940) STRING,HEAD
      IF(HEAD(1:7).EQ.'COMMENT') THEN
        NCOM=NCOM+1
        COMENT(NCOM)=STRING
        GOTO 117
      END IF
      IF(STRING.NE.' ') GOTO 940
      IRCODE=0
      GOTO 999
C
C  WRONG FILE TYPE
910   IRCODE=1
      WRITE(LFNERR,911) RXT
911   FORMAT(' SR RXRDNH: WRONG FILE TYPE: "',A,'"')
      BACKSPACE LFNNAV
      GOTO 999
C
C  NOT ANTICIPATED VERSION NUMBER
920   IRCODE=2
      WRITE(LFNERR,921) IRV
921   FORMAT(' SR RXRDNH: NOT ANTICIPATED VERSION NUMBER:',I5)
      GOTO 999
C
C  END OF FILE WITHIN HEADER
930   IRCODE=3
      WRITE(LFNERR,931)
931   FORMAT(' SR RXRDNH: END OF FILE WITHIN HEADER')
      GOTO 999
C
C  ERROR DECODING DATA
940   IRCODE=4
      WRITE(LFNERR,941) STRING,HEAD(1:19)
941   FORMAT(' SR RXRDNH: ERROR DECODING DATA ON THE FOLLOWING LINE:',
     1       /,1X,2A)
      GOTO 999
C
C  END OF FILE AT FIRST RECORD
990   IRCODE=9
      GOTO 999
C
999   RETURN
      END SUBROUTINE

      END MODULE
