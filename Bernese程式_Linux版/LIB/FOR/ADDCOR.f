      MODULE s_ADDCOR
      CONTAINS

C*
      SUBROUTINE ADDCOR(IEQ0  ,IFREQ ,NSASES,NSFLEP,SATSAV,STFIL ,
     1    MELWUB,MEATYP,NDIFF ,TOBS  ,DTSIM ,STNAME,IZEROD,ICOELV,
     2    ELEVA ,COVELV,CIND  ,FIND  ,DIFF  )
CC
CC NAME       :  ADDCOR
CC
CC
CC PURPOSE    :  ADD LINES TO MATRIX C (SEE BELOW) OF ALL SIMULTANE-
CC               OUSLY OBSERVED DOUBLE DIFFERENCES. THE FIRST ROW ADDED
CC               TO THE MATRIX C IS ROW "IEQ0+1". ONLY THE
CC               POSITIONS OF THE FIRST FOUR NON-ZERO ELEMENTS OF THE
CC               C-MATRIX ARE STORED IN "CIND" AND ALWAYS AS
CC               IF L1 OBSERVATIONS WERE PRESENT. THE REMAINING NON-ZERO
CC               ELEMENTS (FOUR MORE IN THE CASE OF L3,L4, OR L5) OR THE
CC               SHIFT IN POSITION FOR L2 OBSERVATIONS IS THEN EASILY
CC               DERIVED FROM "NSASES" AND "NSTAT". THE ORDER TO DETER-
CC               MINE THE POSITIONS IN THE C-MATRIX ARE GIVEN BY
CC               THE ARRAYS "SATSES" AND "STNAME" (SEE SUBROUTINE
CC               "PRCEPO".  "FIND(I)" CONTAINS THE FREQUENCY OF THE
CC               OBSERVATIONS IN ROW I OF THE C-MATRIX.
CC
CC               STRUCTURE MATRIX C:
CC
CC                              |  P1| |
CC                  DDP|  = C * |      |
CC                              |  P2| |
CC
CC               DDP| : VECTOR OF THE ALL DOUBLE DIFFERENCES AT EPOCH
CC               P1|  : VECTOR OF ALL L1 ZERO DIFF. OBSERVATIONS
CC               P2|  : VECTOR OF ALL L2 ZERO DIFF. OBSERVATIONS
CC
CC               EACH LINE OF MATRIX C CONTAINS FOUR NONZERO ELEMENTS
CC               IF IFREQ=1,2 AND EIGHT IF IFREQ=3,4,5.
CC               FOR THE LINEAR COMBINATION K1*L1+K2*L2 OF L1 AND L2
CC               THE EIGHT NON-ZERO ELEMENTS HAVE THE FOLLOWING
CC               VALUES:  K1,-K1,-K1, K1, K2,-K2,-K2, K2
CC
CC               THE FULL WEIGHT MATRIX FOR ALL DOUBLE DIFF. PHASE OR
CC               CODE OBSERVATIONS OF ONE EPOCH IS THEN COMPUTED IN
CC               SUBROUTINE "CORREL".
CC
CC               THE ELEVATION-DEPENDENT VARIANCE IS SAVED IN ARRAY
CC               "COVELV" WHICH HAS THE SAME STRUCTURE AS "CIND".
CC
CC PARAMETERS :
CC         IN :  IEQ0   : FIRST ROW TO BE ADDED TO CORREATION I*4
CC                        MATRIX MINUS 1
CC               IFREQ  : FREQUENCY OF OBSERVATIONS           I*4
CC               NSASES : NUMBER OF SATELLITES IN THE SESSION I*4
CC               NSFLEP : NUMBER OF SATELLITES ACTUALLY OB-   I*4
CC                        OBSERVED AT THIS EPOCH, THIS FILE
CC               SATSAV(I),I=1,..,NSFLEP: INDICES OF THE      I*4
CC                        ACTUALLY OBSERVED SATELLITES IN THE
CC                        SESSION ARRAY "SATSES" (SEE PRCEPO)
CC               STFIL(I),I=1,2: INDICES OF THE STATIONS      I*4
CC                        INVOLVED IN THE STATION ARRAY
CC                        "STNAME" (SEE PRCEPO)
CC               MEATYP(I),I=1,..,NFTOT: MEASUREMENT TYPE     I*4
CC               MELWUB : MELBOURNE-WUEBBENA LC               I*4
CC                        =0: NO
CC                        =1: YES
CC                        =2: DTEC LC
CC               NDIFF  : DIFFERENCE TYPE                     I*4
CC                        NDIFF=0: ZERO DIFFERENCE
CC                        NDIFF=1: SINGLE DIFFERENCE
CC               TOBS   : OBSERVATION EPOCH (MJD)             R*8
CC               DTSIM  : TIME INTERVAL TO IDENTIFY OBSERV.   R*8
CC                        OF THE SAME EPOCH
CC               STNAME : LIST OF STATION NAMES               CH*16(*)
CC               ICOELV(1:2,J) : MODEL FOR ELEV.-DEP.         I*4(2,*)
CC                          OBS. WEIGHTING
CC                         I=1: STATION, AIRSPACE
CC                         I=2: LEO
CC                         J=MEATYP
CC                        =0: EQUAL WEIGHTING FOR ALL OBS.
CC                        >0: MODEL NUMBER (SEE SR WGTELV)
CC               ELEVA(2,I),I=1,..,NSASES: ELEVATIONS FOR     R*8
CC                        MAX. TWO SITES
CC               COVELV(4,I),I=1,..,NDIM: ELEVATION-DEPENDENT R*8
CC                        VARIANCES FOR ZERO-DIFFERENCE OBS.
CC               CIND(4,I),I=1,..,MAXEQN: POSITIONS OF NON-   I*4
CC                        ZERO ELEMENTS OF THE I-TH ROW OF THE
CC                        C-MATRIX (AS IF L1 OBSERVATIONS
CC                        PRESENT !). C IS USED TO COMPUTE THE
CC                        FULL CORRELATION MATRIX.
CC               FIND(I),I=1,..,MAXEQN: FREQUENCY OF OBSERV-  I*4
CC                        ATION CORRESPONDING TO ROW I
CC               DIFF(I),I=1,..,MAXEQN: DIFFERENCE TYPE OF    I*4
CC                        OBSERVATION CORRESPONDING TO ROW I
CC                        DIFF(I)=0: ZERO DIFFERENCE
CC                        DIFF(I)=1: SINGLE DIFFERENCE
CC               STNAME(I),I=1,..,NSTAT: STATION NAMES       CH*16
CC               TOBS   : ACTUAL OBSERVATION EPOCH            R*8
CC
CC REMARKS    :  ---
CC
CC AUTHOR     :  M.ROTHACHER
CC
CC VERSION    :  3.4  (JAN 93)
CC
CC CREATED    :  89/06/15 15:17
CC
CC CHANGES    :  26-MAR-96 : TS: ADDED NDIFF FOR ZERO/SINGLE DIFFERENCES
CC               19-JUL-96 : TS: ADDED DIFF ARRAY TO CORRECT "CORREL"
CC               29-JAN-97 : SS: ELEVATION-DEPENDENT OBS. WEIGHTING
CC               13-MAY-97 : MR: REMOVE UNUSED VARIABLE "COVI"
CC               30-MAR-98 : TS: SIMULTANEOUS CODE AND PHASE ZD PROCESSING
CC               28-AUG-01 : DS: ELEVATION DEPENDENT WEIGHTING FOR LEO
CC               09-APR-02 : DS: SET SLR WEIGHTS IN THE SAME WAY AS FOR CODE
CC               09-APR-02 : DS: NO ELEVATION DEPENDENT WEIGHTING FOR SLR
CC               24-JUN-02 : DS: LEO ELEV.DEP.WEIGHT. FLAG IN ICOELV(2)
CC               18-SEP-02 : DS: WEIGHTING FOR:LEO,AIRPLANE,
CC                               SHIP,GROUND,STATIC GROUND
CC               21-JAN-03 : RD: STATION WEIGHTING (ADDED TO COVELV)
CC               28-JAN-03 : RS: COVELV REAL*4 -> REAL*8
CC               08-MAR-03 : HU: INTERFACE FOR WGTSTA ADDED
CC               23-JUN-03 : HB: INTERFACE FOR SR STAFLG
CC               23-JUN-05 : MM: IMPLICIT NONE AND DECLARATIONS ADDED
CC               18-JUN-07 : RD: REMOVE "SNGL" FOR OBSWGT
CC               18-JUN-07 : RD: NEW MODEL ICOELV=2: 1/SIN(ELEVA)**4
CC
CC COPYRIGHT  :  ASTRONOMICAL INSTITUTE
CC      1989     UNIVERSITY OF BERN
CC               SWITZERLAND
CC
C*
      USE d_stacrx, ONLY : MTypeSPACE
C
      USE s_wgtelv
      USE s_wgtsta
      USE s_staflg
      IMPLICIT NONE
C
C DECLARATIONS INSTEAD OF IMPLICIT
C --------------------------------
      INTEGER*4 ICOEL2, IDIFF , IEQ0  , IEQN  , IFLAG , IFREQ , IST   ,
     1          ISTA  , IWGT  , IZEROD, MEATYP, MELWUB, NSASES, NSFLEP
C
      REAL*8    CODWGT, DTSIM , OBSWGT, PHAWGT, TOBS
C
CCC       IMPLICIT REAL*8 (A-H,O-Z)
C
      INTEGER*4 SATSAV(*),STFIL(2),ICOELV(2,*)
      INTEGER*4 CIND(4,*),FIND(*),DIFF(*),NDIFF
C
      REAL*8    COVELV(4,*)
      REAL*8    ELEVA(2,*)
C
      CHARACTER*20  MARTYP
      CHARACTER*(*) STNAME(*)
C
C ZERO-DIFF TRICK TO USE "DIFF" ARRAY FOR CODE/PHASE WEIGHTING IN CORREL
C ----------------------------------------------------------------------
      IF (IZEROD.EQ.1) THEN
        IWGT=-MEATYP
      ELSE
        IWGT=NDIFF
      ENDIF
C
C SLR CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C      IF (MEATYP.EQ.3) IWGT=-2
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C
C LOOP OVER ALL DOUBLE DIFFERENCES
C --------------------------------
      DO 20 IDIFF=1,NSFLEP-NDIFF
        IEQN=IEQ0+IDIFF
        DO 10 ISTA=1,NDIFF+1
          IST=(ISTA-1)*2+1
          CIND(IST,IEQN)=(STFIL(ISTA)-1)*NSASES+SATSAV(IDIFF)
C
C ELEVATION DEPENDENT WEIGHTING FOR MARTYP AND MEATYP
C ---------------------------------------------------
          CALL STAFLG(STNAME(STFIL(ISTA)),TOBS,IFLAG,MARTYP)
          IF (MARTYP.EQ. MTypeSPACE) THEN
            ICOEL2=ICOELV(2,MEATYP)
          ELSE
            ICOEL2=ICOELV(1,MEATYP)
          ENDIF
          IF (MEATYP.EQ.3) ICOEL2=0
C
          CALL WGTELV(ICOEL2,ELEVA(ISTA,SATSAV(IDIFF)),
     1                COVELV(IST,IEQN))
          IF (MELWUB.NE.1) THEN
            CALL WGTSTA(TOBS,DTSIM,MEATYP,STNAME(STFIL(ISTA)),OBSWGT)
            OBSWGT=OBSWGT**2
          ELSE
            CALL WGTSTA(TOBS,DTSIM,1,STNAME(STFIL(ISTA)),PHAWGT)
            CALL WGTSTA(TOBS,DTSIM,2,STNAME(STFIL(ISTA)),CODWGT)
            OBSWGT=(PHAWGT**2+CODWGT**2)/2d0
          ENDIF
          COVELV(IST,IEQN)=COVELV(IST,IEQN)*OBSWGT
C
          IF (NDIFF.EQ.1) THEN
            CIND(IST+1,IEQN)=(STFIL(ISTA)-1)*NSASES+SATSAV(IDIFF+1)
            CALL WGTELV(ICOEL2,ELEVA(ISTA,SATSAV(IDIFF+1)),
     1                  COVELV(IST+1,IEQN))
            COVELV(IST+1,IEQN)=COVELV(IST+1,IEQN)*OBSWGT
          ENDIF
10      CONTINUE
        FIND(IEQN)=IFREQ
        DIFF(IEQN)=IWGT
20    CONTINUE
C
      RETURN
      END SUBROUTINE

      END MODULE
