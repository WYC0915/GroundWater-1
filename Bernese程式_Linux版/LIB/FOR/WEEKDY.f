      MODULE s_WEEKDY
      CONTAINS

C*
        SUBROUTINE WEEKDY(TJUL,IWDAY,NRWEEK,IYEAR)
CC
CC NAME       :  WEEKDY
CC
CC PURPOSE    :  COMPUTE DAY OF THE WEEK AND
CC               WEEK NUMBER IN THE CURRENT YEAR
CC
CC PARAMETERS :
CC         IN :  TJUL  : MODIFIED JULIAN DATE                      R*8
CC        OUT :  IWDAY : DAY OF THE WEEK (1=MONDAY, 2=TUESDAY,...) I*4
CC               NRWEEK: WEEK NUMBER                               I*4
CC               IYEAR : YEAR COMPATIBLE WITH NRWEEK (2 DIGITS)    I*4
CC                       (SEE REMARKS)
CC
CC REMARKS     : THE FIRST DAYS OF THE YEAR MAY GET THE WEEK NUMBER
CC               OF THE LAST WEEK OF THE PREVIOUS YEAR. IN THIS CASE
CC               'IYEAR' WILL BE EQUAL TO THE PREVIOUS YEAR
CC
CC RESTRICTION : NONE
CC
CC AUTHOR      : W. GURTNER
CC               ASTRONOMICAL INSTITUTE, UNIVERSITY OF BERN
CC               SWITZERLAND
CC
CC CREATED     : 23-FEB-88
CC
CC CHANGES     : 31-DEC-96 : WG: 2-DIGIT YEAR WRONG AFTER 2000
CC               31-DEC-97 : WG: WEEK 53/1 PROBLEM ELIMINATED
CC               23-JUN-05 : MM: IMPLICIT NONE AND DECLARATIONS ADDED
CC               04-MAY-12 : RD: USE DMOD FROM MODULE
CC
C*
      USE l_basfun, ONLY: dmod
      USE f_djul
      USE s_jmt
      IMPLICIT NONE
C
C DECLARATIONS INSTEAD OF IMPLICIT
C --------------------------------
      INTEGER*4 IWDAY , IYEAR , MONTH , NRWEEK
C
      REAL*8    DAY   , TJUL  , TTHURS
C
CCC         IMPLICIT REAL*8(A-H,O-Z)
C
C  DAY OF THE WEEK
        IWDAY=DMOD(TJUL-5.D0,7.D0)+1.D0
C
C  MJD OF THIS WEEK'S THURSDAY
        TTHURS=TJUL-IWDAY+4
        CALL JMT(TTHURS,IYEAR,MONTH,DAY)
C
C  WEEK NUMBER
        NRWEEK=IDINT((TTHURS-DJUL(IYEAR,1,1.D0)+7.D0)/7.D0)
C
C  YEAR
        IYEAR=MOD(IYEAR,100)
C
        RETURN
        END SUBROUTINE

      END MODULE
