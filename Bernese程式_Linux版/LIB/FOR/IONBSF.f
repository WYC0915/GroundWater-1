      MODULE s_IONBSF
      CONTAINS

C*
      SUBROUTINE IONBSF(XST,XSAT,TOBS,SZ,ZEN,HION,ICARR,B0,S0,
     1                  DE0,DB0,DS0,FACTOR,DB,DS,ZP)
CC
CC NAME       :  IONBSF
CC
CC PURPOSE    :  IONBSF IS USED IN CONNECTION WITH THE SINGLE
CC               LAYER IONOSPHERE MODEL. FOR A RECEIVER WITH
CC               EARTH FIXED COORDINATES XST(K),K=1,2,3 AND A
CC               SATELLITE WITH COORDINATES XSAT(K),K=1,2,3 IN
CC               THE SYSTEM OF THE OBSERVATION EPOCH, THE COORDI-
CC               NATES (GEOCENTRIC LONGITUDE AND LATITUDE) OF THE
CC               INTERSECTION OF THE LINE RECEIVER-SATELLITE
CC               WITH THE IONOSPHERE LAYER IS COMPUTED. FINALLY
CC               THE DIFFERENCE IN LOCAL HOUR ANGLE OF THE SUN AND
CC               THE DIFFERENCE IN LATITUDE WITH RESPECT TO B0 AND
CC               S0, THE ZENITH DISTANCE AT THE INTERSECTION
CC               POINT AND THE FACTOR ARE COMPUTED.
CC
CC PARAMETERS :
CC         IN :  XST(K),K=1,2,3 : EARTH FIXED RECEIVER        R*8
CC                        COORDINATES
CC               XSAT(K),K=1,2,3: SATELLITE COORDINATES IN    R*8
CC                        SYSTEM OF EPOCH
CC               TOBS   : OBSERVATION TIME IN MJD             R*8
CC               SZ     : SIDERAL TIME (GREENWICH)            R*8
CC               ZEN    : ZENIT DISTANCE OF SATELLITE FROM    R*8
CC                        RECEIVER
CC               HION   : HEIGHT OF IONOSPHERE LAYER (M)      R*8
CC               ICARR  : FREQUENCY                           I*4
CC               B0,S0  : DEVELOPMENT POINT                   R*8
CC               DE0,DB0,DS0 : UNITS OF ELECTRON CONTENT,     R*8
CC                        OF LATITUDE, OF TIME
CC        OUT :  FACTOR : =  -41/FREQ**2                      R*8
CC               DB     : B-B0                                R*8
CC               DS     : S-S0                                R*8
CC               ZP     : ZENIT DISTANCE                      R*8
CC
CC REMARKS    :  ---
CC
CC AUTHOR     :  G.BEUTLER
CC
CC VERSION    :  3.4  (JAN 93)
CC
CC CREATED    :  87/11/02 12:20
CC
CC CHANGES    :  05-MAY-98 : ??: USE CONSTANTS "FACTEC" AND "CONRE"
CC               16-JUN-05 : MM: COMCONST.inc REPLACED BY d_const
CC               23-JUN-05 : MM: IMPLICIT NONE AND DECLARATIONS ADDED
CC               04-MAY-12 : RD: USE DMOD FROM MODULE
CC
CC COPYRIGHT  :  ASTRONOMICAL INSTITUTE
CC      1987     UNIVERSITY OF BERN
CC               SWITZERLAND
CC
C*
      USE d_const,  ONLY: CONRE, FACTEC, FREQ, PI
      USE l_basfun, ONLY: dmod
      IMPLICIT NONE
C
C DECLARATIONS INSTEAD OF IMPLICIT
C --------------------------------
      INTEGER*4 I     , ICARR
C
      REAL*8    B     , B0    , CSZ   , DB    , DB0   , DE0   , DIST  ,
     1          DS    , DS0   , FACTOR, HION  , R     , RPH   , S     ,
     2          S0    , SINZP , SSZ   , SZ    , TOBS  , XL    , XXX   ,
     3          ZEN   , ZP
C
CCC       IMPLICIT REAL*8 (A-H,O-Z)
      REAL*8 XST(3),XSAT(3),XSE(3),Y(3)
C
      IF(ICARR.LT.3)THEN
        FACTOR=-FACTEC/1.D16/FREQ(ICARR)**2*DE0
      ELSE IF(ICARR.EQ.4)THEN
        FACTOR=-(FACTEC/1.D16/FREQ(1)**2-FACTEC/1.D16/FREQ(2)**2)*DE0
      ELSE IF(ICARR.EQ.5)THEN
        FACTOR= FACTEC/1.D16/(FREQ(1)*FREQ(2))*DE0
      END IF
C
C TRANSFORM SATELLITE INTO EARTH-FIXED SYSTEM
      SSZ=DSIN(SZ)
      CSZ=DCOS(SZ)
      XSE(1)= CSZ*XSAT(1)+SSZ*XSAT(2)
      XSE(2)=-SSZ*XSAT(1)+CSZ*XSAT(2)
      XSE(3)=                         XSAT(3)
C
C RADIUS VECTOR OF RECEIVER
      R=DSQRT(XST(1)**2+XST(2)**2+XST(3)**2)
C
C GEOCENTRIC DISTANCE OF IONOSPHERE LAYER
      RPH=CONRE+HION
C
C ZENIT DISTANCE OF SATELLITE FROM INTERSECTION WITH IONOSPHERE
      SINZP=R/RPH*DSIN(ZEN)
      ZP=DATAN(SINZP/DSQRT(1.D0-SINZP**2))
C
C COMPUTATION OF COORDINATES OF INTERSECTION OF LINE RECEIVER-SATELLITE
C WITH IONOSPHERE-LAYER
      XXX=DSQRT(RPH**2+R**2-2.D0*RPH*R*DCOS(ZEN-ZP))
      DIST=0.D0
      DO 5 I=1,3
5     DIST=DIST+(XSE(I)-XST(I))**2
      DIST=DSQRT(DIST)
      DO 10 I=1,3
10    Y(I)=XST(I)+XXX*(XSE(I)-XST(I))/DIST
      XL=DATAN2(Y(2),Y(1))
      B =DATAN(Y(3)/DSQRT(Y(1)**2+Y(2)**2))
C
C DIFFERENCES
      DB=B-B0
      DB=DB*180/PI/DB0
      S=XL+(TOBS-DINT(TOBS))*2*PI-PI
      DS=DMOD(S-S0,2*PI)
      IF(DS.GT.PI)DS=DS-2*PI
      IF(DS.LT.-PI)DS=DS+2*PI
      DS=DS*12/PI/DS0
      RETURN
      END SUBROUTINE

      END MODULE
