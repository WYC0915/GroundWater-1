      MODULE s_SETSYN
      CONTAINS

C*
      SUBROUTINE SETSYN(NSING,INDSVN,NSASES,BSING,ISYNC,SYNC,
     1                  SYNCM,BOLD)
CC
CC NAME       :  SETSYN
CC
CC PURPOSE    :  COMPUTE SYNCHRONIZATION ERRORS FOR EACH SATELLITE AND
CC               A MEAN VALUE OF ALL SATELLITES OBSERVED
CC
CC PARAMETERS :
CC         IN :  NSING  : NUMBER OF SINGLE DIFF. OBSERVATIONS I*4
CC               INDSVN(I),I=1,..,NSING: ACTUALLY OBSERVED    I*4
CC                        SATELLITES FOR EPOCH
CC               NSASES : TOTAL NUMBER OF SATELLITES IN SESS. I*4
CC               BSING(I),I=1,..,NSING: OBS.-COMPUTED         R*8
CC      IN/OUT:  ISYNC(I),I=1,..,NSAT: SYNC.INITIALIZ. FLAG   I*4
CC               SYNC(I),I=1,..,NSAT: SYNCHRONIZATION ERRORS  R*8
CC               SYNCM  : MEAN SYNCHRONIZATION ERROR          R*8
CC                        ERRORS
CC               BOLD(I),I=1,..,NSING: LAST OBSERVED-COMPUTED R*8
CC
CC REMARKS    :  ---
CC
CC AUTHOR     :  G.BEUTLER, M.ROTHACHER
CC
CC VERSION    :  3.4  (JAN 93)
CC
CC CREATED    :  87/10/21 18:23
CC
CC CHANGES    :  23-JUN-05 : MM: IMPLICIT NONE AND DECLARATIONS ADDED
CC
CC COPYRIGHT  :  ASTRONOMICAL INSTITUTE
CC      1987     UNIVERSITY OF BERN
CC               SWITZERLAND
CC
C*
      USE d_const, ONLY: C
      IMPLICIT NONE
C
C DECLARATIONS INSTEAD OF IMPLICIT
C --------------------------------
      INTEGER*4 I     , ISAT  , K     , NMEAN , NSASES, NSING
C
      REAL*8    SYNCM , SYNCMO
C
CCC       IMPLICIT REAL*8 (A-H,O-Z)
      REAL*8 BSING(*),SYNC(*),BOLD(*)
      INTEGER*4 INDSVN(*),ISYNC(*)
C
C
      IF(NSING.EQ.0) RETURN
C
C OLD SYNCHRONIZATION VALUE FOR THE FILE
C --------------------------------------
      SYNCMO=SYNCM
C
C CHECK, IF AT LEAST ONE SATELLITE OF THE FILE HAS BEEN INITIALIZED
C -----------------------------------------------------------------
      DO 5 ISAT=1,NSASES
        IF(ISYNC(ISAT).NE.0) GOTO 8
5     CONTINUE
      SYNCMO=0.D0
C
C COMPUTE SYNC-TERM FOR ALL SATELLITES ACTIVE
C -------------------------------------------
8     NMEAN=0
      SYNCM=0.D0
      DO 10 I=1,NSING
        K=INDSVN(I)
        IF(ISYNC(K).EQ.0) GOTO 10
        SYNC(K)=SYNC(K)+(BSING(I)-BOLD(K))/C
        BOLD(K)=BSING(I)
        NMEAN=NMEAN+1
        SYNCM=SYNCM+SYNC(K)
10    CONTINUE
      IF(NMEAN.NE.0) THEN
        SYNCM=SYNCM/NMEAN
        SYNCMO=SYNCM
      ENDIF
C
C INITIALIZE NEW SATELLITES
C -------------------------
      DO 20 I=1,NSING
        K=INDSVN(I)
        IF(ISYNC(K).NE.0) GOTO 20
        ISYNC(K)=1
        BOLD(K)=BSING(I)
        SYNC(K)=SYNCMO
20    CONTINUE
      RETURN
      END SUBROUTINE

      END MODULE
