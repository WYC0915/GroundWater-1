      MODULE s_CHKPOL
      CONTAINS

C*
      SUBROUTINE CHKPOL(Q,ARG,FCT,SIGFCT,ANSWER,RATIO)
CC
CC NAME       :  CHKPOL
CC
CC PURPOSE    :  A FUNCTION, WHICH IS SUPPOSED TO BE A POLYNOMIAL
CC               OF DEGREE Q IS GIVEN BY Q+2 VALUES
CC               FCT(I), I=1,2,...,Q+2 AT THE ARGUMENT VALUES
CC               ARG(I), I=1,2,...,Q+2.
CC               THE FUNCTION VALUES FCT(I) MAY CONTAIN RANDOM
CC               ERRORS WITH EXPECTATION VALUE 0 AND VARIANCE
CC               SIGFCT**2.
CC               THE SUBROUTINE DECIDES WHETHER OR NOT THE ASSUMPTION
CC               THAT THE Q+2 VALUES ONLY CONTAIN RANDOM ERRORS IS
CC               CORRECT OR NOT. IT ALSO RETURNS THE RATIO FOR THE
CC               ABSOLUTE VALUE OF THE (Q+1)-TH DERIVATIVE OF FCT
CC               AND THE EXPECTED RMS FOR THIS QUANTITY. THIS RATIO
CC               IS RETURNED IN THE VARIABLE "RATIO". IT IS A MEASURE
CC               FOR THE CORRECTNESS OF THE ASSUMPTION THAT NO ERRORS
CC               ARE PRESENT.
CC
CC PARAMETERS :
CC         IN :  Q      : DEGREE OF POLYNOMIAL                I*4
CC               ARG(I),I=1,2,..,Q+2: ARGUMENT VALUES         R*8
CC               FCT(I),I=1,2,..,Q+2: FUNCTION VALUES         R*8
CC               SIGFCT : RMS OF FCT(I)                       R*8
CC        OUT :  ANSWER : =Y: NO ERROR IN THE Q+1 VALUES      CH*1
CC                            FCT(I),I=1,2,..,Q+2
CC                        =N: THERE IS AT LEAST ONE ERROR
CC                            IN FCT(I),I=1,2,..,Q+2
CC
CC REMARKS    :  ---
CC
CC AUTHOR     :  G.BEUTLER
CC
CC VERSION    :  3.4  (JAN 93)
CC
CC CREATED    :  88/02/27 12:23
CC
CC CHANGES    :  10-AUG-94 : MR: CALL EXITRC
CC               27-AUG-03 : HU: SHARED DO LABELS REMOVED
CC               21-JUN-05 : MM: COMLFNUM.inc REMOVED, m_bern ADDED
CC               23-JUN-05 : MM: IMPLICIT NONE AND DECLARATIONS ADDED
CC
CC COPYRIGHT  :  ASTRONOMICAL INSTITUTE
CC      1988     UNIVERSITY OF BERN
CC               SWITZERLAND
CC
C*
      USE m_bern
      USE s_exitrc
      IMPLICIT NONE
C
C DECLARATIONS INSTEAD OF IMPLICIT
C --------------------------------
      INTEGER*4 I     , ICOE  , K, MAXQ
C
      REAL*8    DERIV , RATIO , SIGFCT, SIGTST, TEST
C
CCC       IMPLICIT REAL*8 (A-H,O-Z)
CCC       IMPLICIT INTEGER*4 (I-N)
C
      PARAMETER(MAXQ=10)
C
      INTEGER*4 Q
C
      REAL*8 ARG(*),FCT(*)
      REAL*8 COEFF(MAXQ+2),COESIG(MAXQ+2)
C
      CHARACTER*1 ANSWER
C
C
C CHECK MAXIMUM POLYNOMIAL DEGREE
      IF(Q.GT.MAXQ) THEN
        WRITE(LFNERR,10) Q,MAXQ
10      FORMAT(/,' *** SR CHKPOL: POLYNOMIAL DEGREE TOO LARGE',/,
     1                       16X,'POLYNOMIAL DEGREE:',I3,/,
     2                       16X,'MAX. POLY. DEGREE:',I3,/)
        CALL EXITRC(2)
      END IF
C
C COMPUTE COEFFICIENTS OF LAGRANGE POLYNOMIAL OF DEGREE Q+1
      DO 20 ICOE=1,Q+2
        COEFF(ICOE)=FCT(ICOE)
        COESIG(ICOE)=1.D0
        DO K=1,Q+2
          IF(K.NE.ICOE)COEFF(ICOE)=COEFF(ICOE)/(ARG(ICOE)-ARG(K))
          IF(K.NE.ICOE)COESIG(ICOE)=COESIG(ICOE)/(ARG(ICOE)-ARG(K))
        ENDDO
20    CONTINUE
C                   (Q+1)
C COMPUTE (Q+1)! FCT      AND RMS ASSOCIATED WITH IT
      DERIV=0.D0
      SIGTST=0.D0
      DO 30 I=1,Q+2
        DERIV=DERIV+COEFF(I)
        SIGTST=SIGTST+COESIG(I)**2
30    CONTINUE
      SIGTST=SIGFCT*DSQRT(SIGTST)
C
C GIVE CORRECT ANSWER
      TEST=DABS(DERIV)
      RATIO=TEST/SIGTST
      IF(RATIO.LE.3)THEN
        ANSWER='Y'
      ELSE
        ANSWER='N'
      END IF
      RETURN
      END SUBROUTINE

      END MODULE
