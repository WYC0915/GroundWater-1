      MODULE s_PRCEFF
      CONTAINS

C*
      SUBROUTINE PRCEFF(IORSYS,TABINT,T,PREMAT)
CC
CC NAME       :  PRCEFF
CC
CC PURPOSE    :  EFFICIENT COMPUTATION OF PRECESSION MATRIX
CC               USING LINEAR INTERPOLATION
CC
CC PARAMETERS :
CC         IN :  IORSYS : INERTIAL SYSTEM                     I*4
CC                        =1: B1950.0
CC                        =2: J2000.0
CC               TABINT : TABULAR INTERVAL(IN HOURS)          R*8
CC               T      : TIME OF REQUEST                     R*8
CC        OUT :  PREMAT(I,K),I=1,2,3, K=1,2,3: PRECESSION     R*8
CC               MATRIX
CC
CC REMARKS    :  ---
CC
CC AUTHOR     :  G.BEUTLER, M.ROTHACHER
CC
CC VERSION    :  3.3
CC
CC CREATED    :  87/11/16 11:02
CC
CC CHANGES    :  31-MAY-92 : ??: OPTION J2000.0
CC               23-JUN-05 : MM: IMPLICIT NONE AND DECLARATIONS ADDED
CC               29-JAN-07 : AG: EXTRAPOLATION TOLERANCE FOR LITPOL ADDED
CC
CC COPYRIGHT  :  ASTRONOMICAL INSTITUTE
CC      1987     UNIVERSITY OF BERN
CC               SWITZERLAND
CC
C*
      USE s_prenew
      USE s_litpol
      USE s_pren20
      IMPLICIT NONE
C
C DECLARATIONS INSTEAD OF IMPLICIT
C --------------------------------
      INTEGER*4 IFIRST, IORSYS, K     , LEFT
C
      REAL*8    T     , T0    , TABEFF, TABINT
C
CCC       IMPLICIT REAL*8 (A-H,O-Z)
CCC       IMPLICIT INTEGER*4 (I-N)
      REAL*8 TABTIM(2),PREMAT(3,3),PRETAB(3,3,2)
      DATA IFIRST/1/
C
C INITIALIZATION
      IF(IFIRST.EQ.1)THEN
        IFIRST=0
        T0=T
        TABTIM(1)=0.D0
        TABTIM(2)=-1.D0
      END IF
C
C NEW TIME INTERVAL?
      IF(T.LT.TABTIM(1).OR.T.GT.TABTIM(2))THEN
        TABEFF=TABINT
        LEFT=DABS(T-T0)/(TABEFF/24.D0)
        IF(T.LT.T0)LEFT=-LEFT-1
        DO 10 K=1,2
          TABTIM(K)=T0+(LEFT+K-1)*TABEFF/24.D0
          IF (IORSYS.EQ.1) THEN
            CALL PRENEW(TABTIM(K),PRETAB(1,1,K))
          ELSE
            CALL PREN20(TABTIM(K),PRETAB(1,1,K))
          ENDIF
10      CONTINUE
      END IF
C
C COMPUTE RESULT BY LINEAR INTERPOLATION
      CALL LITPOL(2,9,TABTIM,PRETAB,T,0d0,PREMAT)
C
      RETURN
      END SUBROUTINE

      END MODULE
