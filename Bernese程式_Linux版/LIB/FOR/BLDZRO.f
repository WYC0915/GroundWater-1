      MODULE s_BLDZRO
      CONTAINS

C*
      SUBROUTINE BLDZRO(IEQ0  ,NEW   ,MAXEQN,MAXSNG,AAUX  ,INDAUX,
     1                  MELWUB,AOBS  ,INDA  ,BOBS  ,MXESNG)
CC
CC NAME       :  BLDZRO
CC
CC PURPOSE    :  FORM ZERO DIFFERENCE EQUATIONS
CC               SPECIALITY: ONLY NON-ZERO ELEMENTS IN AOBS, INDEX
CC               IN ORIGINAL EQUATIONS DEFINED BY INDA.
CC               AS IDENTICAL TO BLDDBL AS POSSIBLE
CC
CC PARAMETERS :
CC         IN :  IEQ0   : NUMBER OF FIRST EQUATION MINUS 1    I*4
CC               NEW    : NUMBER OF NEW ZERO DIFFERENCE       I*4
CC                        EQUATIONS
CC               MAXEQN : MAXIMUM NUMBER OF EQUATIONS
CC               MAXSNG : MAXIMUM NUMBER OF ROWS IN ARRAY AOBS I*4
CC               AAUX   : AUXILIARY ARRAY (DIM MAXSNG)        R*8
CC               INDAUX : AUXILIARY INDEX ARRAY (DIM MAXSNG)  I*2
CC               MELWUB : MELBOURNE-WUEBBENA LC               I*4
CC                        =0: NO
CC                        =1: YES
CC                        =2: DTEC LC
CC     IN/OUT :  AOBS   : LINEARIZED OBSERVATION EQN. MATRIX  R*8
CC               INDA   : LINEARIZED ARRAY OF INDICES FOR     I*2
CC                        MATRIX AOBS
CC               BOBS   : ARRAY WITH TERMS "OBSERVED-COMPUTED"R*8
CC               MXESNG : MAXIMUM ACTUAL NUMBER OF NON-ZERO   I*4
CC                        ELEMENTS IN ARRAY AOBS
CC
CC REMARKS    :  ---
CC
CC AUTHOR     :  T.A. SPRINGER
CC
CC VERSION    :  4.0  (JUL 95)
CC
CC CREATED    :  04-JUL-95
CC
CC CHANGES    :  19-JUL-96 : TS: SOME SMALL COSMETIC IMPROVEMENTS
CC               29-APR-98 : SS: DTEC LC
CC               23-JUN-04 : RD: CORRECT COMP. OF MXESNG
CC               21-JUN-05 : MM: COMLFNUM.inc REMOVED, m_bern ADDED
CC               23-JUN-05 : MM: IMPLICIT NONE AND DECLARATIONS ADDED
CC
CC COPYRIGHT  :  ASTRONOMICAL INSTITUTE
CC      1987     UNIVERSITY OF BERN
CC               SWITZERLAND
CC
C*
      USE m_bern
      USE s_exitrc
      IMPLICIT NONE
C
C DECLARATIONS INSTEAD OF IMPLICIT
C --------------------------------
      INTEGER*4 IDBL  , IEQ0  , IND   , IPAR  , KL    , KROW  , LINE  ,
     1          MAXEQN, MAXSNG, MELWUB, MXESNG, NEW
C
CCC       IMPLICIT REAL*8 (A-H,O-Z)
CCC       IMPLICIT INTEGER*4 (I-N)
C
      INTEGER*4 INDA(*),INDAUX(*)
      REAL*8    AOBS(*),BOBS(*),AAUX(*)
C
C
C LOOP OVER NEW ZERO DIFFERENCES
      DO 100 IDBL=1,NEW
C
C INITIALIZE INDEX ARRAYS OF CURRENT DOUBLE DIFFERENCE
        DO 10 KROW=1,MAXSNG
          INDAUX(KROW)=0
          AAUX(KROW)=0.D0
10      CONTINUE
C
C INDEX OF CURRENT LINE, LINE+1
        LINE=IEQ0+IDBL
C
C TERM "OBSERVED-COMPUTED"
C       BOBS(LINE)=BOBS(LINE)
C
C SQUARE O-C TERM, IF DTEC LC ANALYZED
        IF (MELWUB.EQ.2) BOBS(LINE)=BOBS(LINE)**2
C
        KL=1
C
C CURRENT LINE OF MATRIX AOBS, ARRAY INDAUX
        DO 20 KROW=1,2*MAXSNG
          IND=LINE+(KL-1)*MAXEQN
          IPAR=INDA(IND)
          IF(IPAR.EQ.0) THEN
            IF(KROW.GT.MXESNG) MXESNG=KROW
            GOTO 30
          ENDIF
C
          IF(KROW.GT.MAXSNG) THEN
            WRITE(LFNERR,901) MAXSNG
901         FORMAT(/,' *** SR BLDZRO: TOO MANY NON-ZERO PARAMETER FOR',
     1             /,16X,'ONE ZERO DIFF. OBSERVATION.',/,
     2               16X,'INCREASE MAX. DIMENSION "MAXSNG"',/,
     3               16X,'MAX. NUMBER OF NON-ZERO PARAM.:',I4,/)
            CALL EXITRC(2)
          ENDIF
C
          INDAUX(KROW)=IPAR
          AAUX(KROW)=AOBS(IND)
          KL=KL+1
20      CONTINUE
C
C COPY CURRENT LINE INTO RESULT MATRICES
30      CONTINUE
        DO 40 KROW=1,MAXSNG
          IND=LINE+(KROW-1)*MAXEQN
          AOBS(IND)=AAUX(KROW)
          INDA(IND)=INDAUX(KROW)
          IF(IDBL.EQ.NEW)THEN
            INDA(IND+1)=0
            AOBS(IND+1)=0.D0
          END IF
40      CONTINUE
100   CONTINUE
      RETURN
      END SUBROUTINE

      END MODULE
