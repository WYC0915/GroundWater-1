      MODULE s_EERESI
      CONTAINS

C*
      SUBROUTINE EERESI(MAXTYP,TITNEW,NOBS,NPAR,XXX,ANOR,
     1                  NUMOBS,RMSOBS,RMS,AMAT)
CC
CC NAME       :  EERESI
CC
CC PURPOSE    :  PRINT RESIDUALS INTO A RESIDUAL FILE. PRINT A PRIORI
CC               VALUE AND OBSERVATION AS WELL
CC
CC PARAMETERS :
CC        IN  :  MAXTYP : MAXIMUM NUMBER OF OBSERVATION TYPES    I*4
CC               TITNEW : GENERAL TITLE OF PROGRAM RUN          CH*80
CC               NOBS   : NUMBER OF OBSERVATIONS                 I*4
CC               NPAR   : MAXIMUM NUMBER OF PARAMETERS ALLOWED   I*4
CC               XXX(I),I=1,..,NPAR: SOLUTION VECTOR             R*8
CC               ANOR(I),I=1,..,NPAR*(NPAR+1)/2: NORMAL EQUATION R*8
CC                        MATRIX
CC               NUMOBS(I),I=1,..,MAXTYP: NUMBER OF OBSERVATIONS I*4
CC                        FOR EACH OBSERVATION TYPE
CC               RMSOBS(I),I=1,..,MAXTYP: RMS PER OBS. TYPE      R*8
CC               RMS    : A POSTERIORI RMS OF OBSERVATIONS       R*8
CC      LOCAL :  AMAT(I),I=1,..,NPAR: AUXILIARY MATRIX           R*8
CC
CC REMARKS    :  ---
CC
CC AUTHOR     :  M.ROTHACHER
CC
CC VERSION    :  4.1
CC
CC CREATED    :  07-NOV-97
CC
CC CHANGES    :  22-AUG-01 : MR: ADD PARAMETER "RMS", COMPUTE RMS
CC               12-AUG-03 : PS: CHANGED KEYWORD SCRATCH => AUXFIL
CC                               OPEN SCRATCHFILE WITH STATUS SCRATCH
CC               16-JUN-05 : MM: UNUSED COMCONST.inc REMOVED
CC               21-JUN-05 : MM: COMLFNUM.inc REMOVED, m_bern ADDED
CC               23-JUN-05 : MM: IMPLICIT NONE AND DECLARATIONS ADDED
CC               12-MAY-09 : PS/SL: OPEN SCRATCHFILE WITH STATUS UNKNOWN
CC
CC COPYRIGHT  :  ASTRONOMICAL INSTITUTE
CC      1997     UNIVERSITY OF BERN
CC               SWITZERLAND
CC
C*
      USE m_bern
      USE s_opnfil
      USE s_opnerr
      USE s_gtflna
      IMPLICIT NONE
C
C DECLARATIONS INSTEAD OF IMPLICIT
C --------------------------------
      INTEGER*4 II    , IOBS  , IOSTAT, IPAR  , IRCRES, IRCSCR, ITYP  ,
     1          MAXTYP, NOBS  , NPAR  , NUMOBT
C
      REAL*8    BMAT  , ERPEPO, ERPMOD, ERPOBS, ERPSIG, RESIDU, RMS   ,
     1          RMSHLP, WEIGHT
C
CCC       IMPLICIT REAL*8(A-H,O-Z)
C
      CHARACTER*80 TITNEW
      CHARACTER*32 FILRES,FILSCR
C
      REAL*8       XXX(*),ANOR(*),AMAT(*)
      REAL*8       RMSOBS(MAXTYP)
C
      INTEGER*4    NUMOBS(MAXTYP)
C
C
C INITIALIZE OBSERVATION STATISTICS
C ---------------------------------
      DO ITYP=1,MAXTYP
        NUMOBS(ITYP)=0
        RMSOBS(ITYP)=0.D0
      ENDDO
C
C GET RESIDUAL FILE NAME
C ----------------------
      CALL GTFLNA(0,'RESID  ',FILRES,IRCRES)
      IF (IRCRES.NE.0) RETURN
C
C OPEN RESIDUAL FILE, WRITE TITLE LINES
C -------------------------------------
      CALL OPNFIL(LFNRES,FILRES,'UNKNOWN','FORMATTED',
     1            ' ',' ',IOSTAT)
      CALL OPNERR(LFNERR,LFNRES,IOSTAT,FILRES,'EERESI')
C
      WRITE(LFNRES,1001) TITNEW
1001  FORMAT(A,/,80('-'),/,
     1       ' OBS.  TYP  EPOCH(MJD)   OBSERV.(mas)  MODEL(mas)   ',
     2       ' O-C(mas)    RESIDUAL(mas)   SIGMA(mas)',/)
C
C CLOSE AND RE-OPEN A-MATRIX FILE
C -------------------------------
      CALL GTFLNA(1,'AUXFIL',FILSCR,IRCSCR)
C
      CLOSE(UNIT=LFNPLT)
C
      CALL OPNFIL(LFNPLT,FILSCR,'UNKNOWN','UNFORMATTED',
     1            'READONLY',' ',IOSTAT)
      CALL OPNERR(LFNERR,LFNPLT,IOSTAT,FILSCR,'EERESI')
C
C LOOP OVER ALL OBSERVATIONS IN SCRATCH FILE
C ------------------------------------------
      DO IOBS=1,NOBS
C
C READ A-MATRIX LINE FROM SCRATCH FILE
        READ(LFNPLT) ITYP,ERPEPO,ERPOBS,ERPMOD,(AMAT(II),II=1,NPAR),
     1               BMAT,WEIGHT
C
C COMPUTE RESIDUAL
        RESIDU=BMAT
        DO IPAR=1,NPAR
          RESIDU=RESIDU-AMAT(IPAR)*XXX(IPAR)
        ENDDO
C
        RESIDU=RESIDU
C
        ERPSIG=1.D0/DSQRT(WEIGHT)
C
C UPDATE OBSERVATION STATISTICS
        NUMOBS(ITYP)=NUMOBS(ITYP)+1
        RMSOBS(ITYP)=RMSOBS(ITYP)+WEIGHT*RESIDU**2
C
C WRITE INTO RESIDUAL FILE
        WRITE(LFNRES,1002) IOBS,ITYP,ERPEPO,ERPOBS,ERPMOD,BMAT,RESIDU,
     1                     ERPSIG
1002    FORMAT(I6,I4,F13.5,5F13.7)
      ENDDO
C
C COMPUTE TOTAL RMS OF OBSERVAIONS
C --------------------------------
      RMSHLP=0.D0
      NUMOBT=0
      DO ITYP=1,MAXTYP
        NUMOBT=NUMOBT+NUMOBS(ITYP)
        RMSHLP=RMSHLP+RMSOBS(ITYP)
      ENDDO
C
      IF (NUMOBT-NPAR.GT.0) THEN
        RMS=DSQRT(RMSHLP/(NUMOBT-NPAR))
      ELSE
        RMS=999.D0
      ENDIF
C
C END
C ---
      RETURN
      END SUBROUTINE

      END MODULE
