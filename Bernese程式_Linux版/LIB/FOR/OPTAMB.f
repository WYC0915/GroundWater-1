      MODULE s_OPTAMB
      CONTAINS

C*
      SUBROUTINE OPTAMB(IOPT  ,NFREQ ,NSATEL,NUMSAT,NNEWAM,LSTAMB)
CC
CC NAME       :  OPTAMB
CC
CC PURPOSE    :  DISPLAY, ADD AND REMOVE MULTIPLE AMBIGUITIES
CC
CC PARAMETERS :
CC         IN :  IOPT   : OPTION                               I*4
CC                        =1: DISPLAY MULTIPLE AMBIGUITIES
CC                        =2: ADD     MULTIPLE AMBIGUITIES
CC                        =3: REMOVE  MULTIPLE AMBIGUITIES
CC               NFREQ  : NUMBER OF FREQUENCIES IN FILE        I*4
CC               NSATEL : TOTAL NUMBER OF SATELLITES           I*4
CC               NUMSAT(I),I=1,..,NSATEL: SATELLITE NUMBERS    I*4
CC   IN/OUT :    NNEWAM(ISATEL) : NUMBER OF AMBIGUITIES        I*4
CC               LSTAMB(I,ISATEL,IAMB)  LIST OF AMBIGUITIES    I*4
CC                        I=1 : THE FIRST EPOCH
CC                        I=2 : TYPE: 1 ... FILE
CC                                    2 ... CYCLE SLIP
CC                                    3 ... USER
CC                                    4 ... GAP
CC
CC REMARKS    :  ---
CC
CC AUTHOR     :  M.ROTHACHER, L.MERVART
CC
CC VERSION    :  3.4  (JAN 93)
CC
CC CREATED    :  89/08/04 22:21
CC
CC CHANGES    :  05-JUN-92 : ??: CHANGES FOR THE NEW MAUPRP VERSION
CC               11-JAN-94 : SF:INTERNAL READ WITH FORMAT "*" REPLACED
CC                              BY INPCI4,INPCR8
CC               05-MAR-96 : TS: HANDLING OF "MAXAMS" EXCEEDINGS
CC               28-JUL-98 : MR: MODIFICATIONS FOR GLONASS
CC               21-JUN-05 : MM: COMLFNUM.inc REMOVED, m_bern ADDED
CC               23-JUN-05 : MM: IMPLICIT NONE AND DECLARATIONS ADDED
CC               27-MAR-12 : RD: USE PROMP1 AS MODULE NOW
CC               28-MAR-12 : RD: USE INPCI4 AS MODULE NOW
CC
CC COPYRIGHT  :  ASTRONOMICAL INSTITUTE
CC      1989     UNIVERSITY OF BERN
CC               SWITZERLAND
CC
C*
      USE m_bern,  ONLY: LFNKBD, LFNPRT
      USE s_dspamb
      USE s_updamb
      USE s_promp1
      USE s_upperc
      USE f_inpci4
      IMPLICIT NONE
C
C DECLARATIONS INSTEAD OF IMPLICIT
C --------------------------------
      INTEGER*4 IAMSAT, IEPO1 , IEPO2 , IEPOCH, IOPT  ,
     1          IRC   , IRCODE, ISATEL, IVAL  , MXCAMB, MXCSAT, NFREQ ,
     2          NSATEL
C
CCC       IMPLICIT REAL*8 (A-H,O-Z)
C
      CHARACTER*72 INPUT,STRING(2)
      CHARACTER*6  MXNSAT,MXNAMB
      CHARACTER*1  CDUMMY,AMSFLG
C
      INTEGER*4    NUMSAT(*),NNEWAM(MXCSAT),LSTAMB(2,MXCSAT,*)
      INTEGER*4    IDUMMY(1),IHLP1(1)
C
C
      COMMON/MCMSAT/MXCSAT,MXNSAT
      COMMON/MCMAMB/MXCAMB,MXNAMB
C
      STRING(1)=' '
      AMSFLG='G'
C
C BRANCH ACCORDING TO OPTION
C --------------------------
      GOTO (100,200,300) IOPT
C
C OPTION 41: DISPLAY MULTIPLE AMBIGUITIES
C ---------------------------------------
100   CALL DSPAMB(NSATEL,NUMSAT,NNEWAM,LSTAMB,IRC)
      GOTO 999
C
C OPTION 42: ADD MULTIPLE AMBIGUITIES
C -----------------------------------
C
C ENTER SATELLITE
200   WRITE(STRING(2),201)
201   FORMAT('SATELLITE (END=E)')
      CALL PROMP1(2,STRING)
      READ(LFNKBD,202) INPUT
202   FORMAT(A)
      CALL UPPERC(INPUT)
      IF(INPUT.EQ.'E') GOTO 999
      IHLP1(1)=IAMSAT
      IVAL=INPCI4(-72,1,0,INPUT,IHLP1,CDUMMY,IDUMMY,IRCODE)
      IAMSAT=IHLP1(1)
      IF (IRCODE.NE.0) GOTO 200
C
C CHECK SATELLITE
      DO 210 ISATEL=1,NSATEL
        IF(NUMSAT(ISATEL).EQ.IAMSAT) GOTO 220
210   CONTINUE
      WRITE(LFNPRT,211) IAMSAT
211   FORMAT(/,' SATELLITE NOT FOUND:',I4)
      GOTO 200
C
C CHECK MAXIMUM NUMBER OF AMBIGUITIES PER SATELLITE
220   IF(NNEWAM(ISATEL)+1.GT.MXCAMB) THEN
        WRITE(LFNPRT,221) MXCAMB
221     FORMAT(/,' MAX. NUMBER OF AMBIGUITIES PER SATELLITE EXCEEDED',
     1         /,' MAXIMUM NUMBER ALLOWED:',I5,/)
        GOTO 200
      ENDIF
C
C ENTER EPOCH
230   WRITE(STRING(2),231)
231   FORMAT('EPOCH NUMBER')
      CALL PROMP1(2,STRING)
      READ(LFNKBD,202) INPUT
      IHLP1(1)=IEPOCH
      IVAL=INPCI4(-72,1,0,INPUT,IHLP1,CDUMMY,IDUMMY,IRCODE)
      IEPOCH=IHLP1(1)
      IF (IRCODE.NE.0) GOTO 230
C
C DEFINE NEW MULTIPLE AMBIGUITY
      CALL UPDAMB(ISATEL,IEPOCH,IEPOCH,3,NNEWAM,LSTAMB,
     1            NSATEL,NUMSAT,2,AMSFLG,IRC)
      GO TO 200
C
C OPTION 43: REMOVE MULTIPLE AMBIGUITY
C ------------------------------------
C
C DISPLAY MULTIPLE AMBIGUITIES
300   CALL DSPAMB(NSATEL,NUMSAT,NNEWAM,LSTAMB,IRC)
      IF(IRC.EQ.1) GOTO 999
C
C ENTER SATELLITE
      WRITE(STRING(2),301)
301   FORMAT('SATELLITE (END=E,ALL=A)')
      CALL PROMP1(2,STRING)
      READ(LFNKBD,302) INPUT
302   FORMAT(A)
      CALL UPPERC(INPUT)
      IF(INPUT.EQ.'E') GOTO 999
      IF(INPUT.EQ.'A') THEN
        DO 350 ISATEL=1,NSATEL
          CALL UPDAMB(ISATEL,2,9999,0,NNEWAM,LSTAMB,
     1                NSATEL,NUMSAT,2,AMSFLG,IRC)
350     CONTINUE
      END IF
      IHLP1(1)=IAMSAT
      IVAL=INPCI4(-72,1,0,INPUT,IHLP1,CDUMMY,IDUMMY,IRCODE)
      IAMSAT=IHLP1(1)
      IF (IRCODE.NE.0) GOTO 300
C
C CHECK SATELLITE
      DO 310 ISATEL=1,NSATEL
        IF(NUMSAT(ISATEL).EQ.IAMSAT) GOTO 330
310   CONTINUE
      WRITE(LFNPRT,311) IAMSAT
311   FORMAT(/,' SATELLITE NOT FOUND:',I4)
      GOTO 300
C
C ENTER EPOCH
330   WRITE(STRING(2),331)
331   FORMAT('EPOCH NUMBER (ALL=A)')
      CALL PROMP1(2,STRING)
      READ(LFNKBD,302) INPUT
      CALL UPPERC(INPUT)
      IF(INPUT .EQ. 'A') THEN
        IEPO1 = 2
        IEPO2 = 9999
      ELSE
        IHLP1(1)=IEPOCH
        IVAL=INPCI4(-72,1,0,INPUT,IHLP1,CDUMMY,IDUMMY,IRCODE)
        IEPOCH=IHLP1(1)
        IF (IRCODE.NE.0) GOTO 330
        IEPO1 = IEPOCH
        IEPO2 = IEPOCH
      END IF
C
C REMOVE AMBIGUITY
      CALL UPDAMB(ISATEL,IEPO1,IEPO2,0,NNEWAM,LSTAMB,
     1            NSATEL,NUMSAT,2,AMSFLG,IRC)
      IF (IRC .EQ. 0) WRITE(LFNPRT,340)
340     FORMAT(/,' NO SUCH AMBIGUITY ')
      GO TO 300
C
C RETURN
C ------
999   RETURN
      END SUBROUTINE

      END MODULE
