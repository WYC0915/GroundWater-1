      MODULE s_WGTDIP
      CONTAINS

C*
      SUBROUTINE WGTDIP(OPTDIP,SIGDIP,NPARN ,LOCQ  ,SIGAPR,IDELTT,
     1                  ANOR  )
CC
CC NAME       :  WGTDIP
CC
CC PURPOSE    :  COMPUTE A PRIORI OCCUPATION OF NORMAL EQUATION MATRIX
CC               FOR DIFFERENTIAL IONOSPHERE PARAMETERS
CC
CC PARAMETERS :
CC         IN :  OPTDIP : OPTIONS FOR DIFF. ION. PARAMETERS   I*4(3)
CC                        (1): =0: NO DIFF. ION. PARAMETERS
CC                             =1: ONE PAR. PER EPOCH AND SAT.
CC                             =2: PARAMETERS EPOCH-WISE PRE-
CC                                 ELIMINATED
CC                        (2): ELIMINATION OF REF. ION. PAR.
CC                        (3): ELEV.-DEP. PAR. CONSTRAINING
CC               SIGDIP : A PRIORI SIGMAS FOR DIFF. ION. PAR. R*8(2)
CC                        (1): ABSOLUTE SIGMA
CC                        (2): RELATIVE SIGMA IN M/MIN**1/2
CC               NPARN  : NUMBER OF NON-AMBIGUITY PARAMETERS  I*4
CC               LOCQ   : PARAMETER DESCRIPTION               I*4(*,*)
CC               SIGAPR : A PRIORI SIGMA OF WEIGHT UNIT       R*8
CC               IDELTT : TABLE INTERVAL IN SEC               I*4(*)
CC    IN/OUT  :  ANOR   : UPDATED NORMAL EQUATION MATRIX      R*8(*)
CC
CC REMARKS    :  ---
CC
CC AUTHOR     :  S.SCHAER
CC
CC VERSION    :  3.5
CC
CC CREATED    :  13-OCT-93
CC
CC CHANGES    :  05-JAN-94 : SS: RELATIVE CONSTRAINTS INTRODUCED
CC               04-SEP-95 : SS: "SIGDIP(1)" GIVEN ON SINGLE DIFF. LEVEL
CC               05-AUG-97 : SS: ELEV.-DEP. SIP CONSTRAINING
CC               08-SEP-97 : SS: ESTIMATE ZENITH SIP PARAMETERS
CC               16-JUN-05 : MM: UNUSED COMCONST.inc REMOVED
CC               21-JUN-05 : MM: COMLFNUM.inc REMOVED
CC               23-JUN-05 : MM: IMPLICIT NONE AND DECLARATIONS ADDED
CC
CC COPYRIGHT  :  ASTRONOMICAL INSTITUTE
CC      1993     UNIVERSITY OF BERN
CC               SWITZERLAND
CC
C*
      USE f_ikf
      IMPLICIT NONE
C
C DECLARATIONS INSTEAD OF IMPLICIT
C --------------------------------
      INTEGER*4 IP    , IP1   , IP1IP1, IP1IP2, IP2   , IP2IP2,
     1          IPIP  , MXCLCQ, NPARN
C
      REAL*8    SIGAPR, TIMMIN, WGTABS, WGTIRW, WGTREL
C
CCC       IMPLICIT REAL*8 (A-H,O-Z)
C
C GLOBAL DECLARATION
C ------------------
      CHARACTER*6  MXNLCQ
      REAL*8       SIGDIP(*),ANOR(*)
      INTEGER*4    OPTDIP(*),LOCQ(MXCLCQ,*),IDELTT(*)
C
      COMMON/MCMLCQ/MXCLCQ,MXNLCQ
C
C
C RETURN, IF DIFFERENTIAL IONOSPHERE PARAMETERS ARE NOT APPLIED OR
C PARAMETERS ARE EPOCH-WISE PRE-ELIMINATED
C ----------------------------------------------------------------
      IF(OPTDIP(1).NE.1) GOTO 999
C
C ADD ABSOLUTE A PRIORI WEIGHTS TO NEQ-MATRIX
C -------------------------------------------
      IF(SIGDIP(1).NE.0.D0)THEN
        WGTABS=(SIGAPR/SIGDIP(1))**2
C
C COMPUTE ABSOLUTE WEIGHT ON DOUBLE DIFFERENCE LEVEL
        IF (OPTDIP(2).EQ.1) WGTABS=WGTABS/2.D0
C
        DO 110 IP=1,NPARN
          IF(LOCQ(1,IP).EQ.17)THEN
            IPIP=IKF(IP,IP)
            ANOR(IPIP)=ANOR(IPIP)+WGTABS
          ENDIF
110     CONTINUE
      ENDIF
C
C ADD RELATIVE A PRIORI WEIGHTS OF IONOSPHERIC RANDOM WALK
C TO NEQ-MATRIX
C --------------------------------------------------------
      IF(SIGDIP(2).NE.0.D0)THEN
        WGTREL=(SIGAPR/SIGDIP(2))**2
        DO 210 IP2=2,NPARN
          IP1=IP2-1
          IF(LOCQ(1,IP1).EQ.17.AND.
     1       LOCQ(1,IP2).EQ.17.AND.
     2       LOCQ(2,IP1).EQ.LOCQ(2,IP2).AND.
     3       LOCQ(3,IP1).EQ.LOCQ(3,IP2))THEN
            TIMMIN=IDELTT(LOCQ(2,IP2))*(LOCQ(4,IP2)-LOCQ(4,IP1))/60.D0
            WGTIRW=WGTREL/TIMMIN
            IP1IP1=IP1*(IP1+1)/2
            ANOR(IP1IP1)=ANOR(IP1IP1)+WGTIRW
            IP2IP2=IP2*(IP2+1)/2
            ANOR(IP2IP2)=ANOR(IP2IP2)+WGTIRW
            IP1IP2=IKF(IP1,IP2)
            ANOR(IP1IP2)=ANOR(IP1IP2)-WGTIRW
          ENDIF
210     CONTINUE
      ENDIF
C
999   RETURN
      END SUBROUTINE

      END MODULE
